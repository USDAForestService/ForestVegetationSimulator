      SUBROUTINE MPBGO(L)
      IMPLICIT NONE
C----------
C  **MPBGO         DATE OF LAST REVISION:  07/02/10
C----------
C
C
C     L     = TRUE WHEN THE MOUNTAIN PINE BEETLE MODEL IS TO BE CALLED
C
C     EVENTS CONTROLED BY THIS ROUTINE:
C
C     1) DECIDES IF AN OUTBREAK WILL TAKE PLACE DURING THIS CYCLE
C     2) SETS NUMBER OF CLASSES TO NUMBER 'MIN0(POSSABLE,REQUESTED)'
C
C Revision History:
C   02/26/88 Last noted revision date.
C   08/06/01 Lance R. David (FHTET)
C     Initialization of logical variable L.
C   07/02/10 Lance R. David (FMSC)
C     Added IMPLICIT NONE.
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'MPBCOM.F77'
C
C
COMMONS
C
      LOGICAL L,NOERR
      INTEGER I, MYACTS(1), NTODO
      REAL  PROTBK, X
      
      DATA MYACTS /555/

      L = .FALSE.
      
      IF (.NOT.LMPB1) RETURN
      NACLAS = 0
      PRBMPB(ICYC) = 0.0
      IF (LEPI) PRBMPB(ICYC) = EPIPRB
C
      IF (DEBUIN) THEN
         WRITE (JOMPB,10) ICYC,MPBON,MPBYR,WORKIN
   10    FORMAT (/,'IN MPBGO:  ICYC= ',I5,' MPBON= ',I4,' MPBYR= ',I5/
     >           '  WORKIN= ',40L2)
         WRITE (JOMPB,15) (IY(I),I=1,NCYC)
   15    FORMAT (' IY =',T10,20I5,/,T10,20I5)
      ENDIF
C
C     CALL THE EVENT MONITOR TO 'UNSET' THE PREVIOUS MPB MORTALITY.
C
      CALL EVUST4 (4)
C
C     CALL MPBER TO SEE IF MINIMUM CONDITIONS MET FOR OUTBREAK.
C
      CALL MPBER (NOERR)
      L=NOERR
C
C     CALL OPFIND TO DETERMINE IF OUTBREAK SCHEDULED BY USER THIS CYCLE
C
      CALL OPFIND (1,MYACTS,NTODO)
C
C     IF MINIMUM CONDITIONS MET BRANCH TO STMT 100 FOR POSSIBLE
C     OUTBREAK.
C
      IF (L) GO TO 100
C
C     CONDITIONS NOT MET FOR OUTBREAK. NONE WILL OCCUR. IF NO OUTBREAK
C     OCCURS BUT USER ASKED FOR ONE WRITE ERROR MESSAGE.
C
      IF ( NTODO .GT. 0) WRITE (JOMPB,95)
   95 FORMAT (/,' ***** WARNING:  A REQUESTED MOUNTAIN',
     >        ' PINE BEETLE OUTBREAK CAN NOT BE SIMULATED.')
C
C     WRITE REQUESTED GRAPHIC OUTPUT IF A OUTBREAK WAS IN PROGRESS.
C
      IF ( MPBYR .GT. 0 .AND. MPBGRF) CALL MPLOTW
      RETURN
  100 CONTINUE
C
C     AN OUTBREAK CAN  BE CONTINUED OR SIMULATED.
C     SET NUMBER OF ACTUAL CLASSES. ALSO CALL OPDONE TO SIGNAL OPTION
C     DONE.
C
      IF (NTODO .GT. 0) CALL OPDONE (1,IY(ICYC))
      NACLAS = MIN0 (ILP,NCLASS)
      IF ( LEPI ) GO TO 110
C
C     CALCULATE THE PROBABILITY OF AN OUTBREAK
C
      CALL MPOTPR ( PROTBK )
C
C     SCALE THE PROBABILITY AND SAVE IT FOR FUTURE PRINTING
C
      PRBMPB(ICYC) = PROTBK*PRBSCL
  110 CONTINUE
      PROTBK=PRBMPB(ICYC)
C
C     STORE THE PROBABILITY OF OUTBREAK IN THE EVENT MONITOR.
C
      CALL EVSET4 (6,PROTBK)
C
C     MINIMUM CONDITIONS MET FOR OUTBREAK. NOW OUTBREAK WILL
C     OCCUR IF THIS IS THE YEAR THE OUTBREAK HAS BEEN SCHEDULED
C     OR IF IT IS ALREADY IN PROGRESS.
C
      IF (NTODO .GT. 0 .OR. MPBYR .GT. 0) THEN
         L=.TRUE.
         WORKIN(ICYC)=.TRUE.
      ELSE
C
C        AT THIS POINT, NTODO AND MPBYR ARE BOTH ZERO.
C        NO OUTBREAK WILL OCCUR UNLSSS THE RANSTART KEYWORD WAS
C        WAS SPECIFIED.
C
         IF (LRANST) THEN
            CALL MPRANN (X)
            IF (X.LT.PROTBK) THEN
C
C              AN OUTBREAK IS OCCURING.  SCHEDULE THE ACTIVITY,
C              AND RETURN L AS TRUE.
C
               L=.TRUE.
               WORKIN(ICYC)=.TRUE.

               CALL OPADD (IY(ICYC),MYACTS(1),IY(ICYC),0,WK6,I)
            ELSE
               L=.FALSE.
               WORKIN(ICYC) = .FALSE.
            ENDIF
         ELSE
            L=.FALSE.
            WORKIN(ICYC) = .FALSE.
         ENDIF
      ENDIF
C
      IF (DEBUIN) THEN
         WRITE (JOMPB,225) CNTLP,ILP,NACLAS,NCLASS
  225    FORMAT (/,' DUMP OF CONTROLING VARIABLES:',/
     >           T15,'TREES/ACRE  NUMBER OF TREE  NUMBER OF CLASSES',
     >           /,T30,'RECORDS      ACTUAL    REQUESTED'/
     >           ' LODGEPOLE:',T14,F10.3,I11,2I13)
C
         WRITE (JOMPB,250) L
  250    FORMAT (/,' T=SIMULATION WILL GO; F=NO GO   " ',L1,' "')
      ENDIF
C
C     IF AN OUTBREAK IS TO BE SIMULATED;
C     THEN: SAVE PAST DIAMETER GROWTH ESTIMATES.
C
      IF (L) CALL MPSVDG
      RETURN
      END
