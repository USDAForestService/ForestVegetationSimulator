      SUBROUTINE MPGR
      IMPLICIT NONE
C----------
C  **MPGR          DATE OF LAST REVISION:  06/14/13
C----------
C
C     PART OF THE MOUNTAIN PINE BEETLE EXTENSION OF PROGNOSIS SYSTEM.
C     NICK CROOKSTON; INT-MOSCOW                      NOV 1980.
C
C     MPGR COMPUTES THE PERIODIC GROWTH RATIO FOR LODGEPOLE PINE.
C     ENTRY MPSVDG IS CALLED FROM MPBGO TO SAVE THE PAST DG'S.
C
C Revision History
C   05/31/00 Last noted revision date.
C   07/02/10 Lance R. David (FMSC)
C     Added IMPLICIT NONE.
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'MPBCOM.F77'
C
C
COMMONS

      INTEGER I, I2, J

      REAL AVRAT, BRATIO, D1, D2, FDG, ODDS, ODG, PRB,
     &  SCALE, SUMP, X

C
C     NOTE: THIS ROUTINE ASSUMES THAT ENTRY MPSVDG IS CALLED BEFORE
C           DGDRIV IS CALL DURING THE CURRENT CYCLE AND THEREFORE
C           DG IS ON A IFINT YEAR BASIS FOR THE PREVIOUS CYCLE OR
C           AS IT WAS INVENTORIED.
C
C           ENTRY MPGR MUST BE CALLED AFTER DGDRIV FOR THE CURRENT
C           CYCLE AND DG IS THEN ASSUMED TO CONTAIN DG'S SCALED TO
C           A YR BASIS...(USUALLY 10 YEARS).
C
C     ********************     EXECUTION     ********************
C
C     IF THERE ARE LE 2 LPP SELECTED; THEN: SET PGR=.9 AND RETURN.
C
      IF (NPGR .GT. 2) GO TO 10
      PGR=.9
      RETURN
   10 CONTINUE
C
C     INITIALIZE THE SCALARS
C
      SCALE=YR/NPYR
      SUMP=0.
      AVRAT=0.
C
C     LOOP THRU SELECTED LPP.
C
      ILP=ILP-1
      DO 20 J=1,NPGR
      I=IPT(J+ILP)
      PRB=PROB(I)
      SUMP=SUMP+PRB
      FDG=DG(I)
      D2=DBH(I)*BRATIO(7,DBH(I),HT(I))
      ODG=XPT(I)
      D1=D2-ODG
C
C     COMPUTE OLD DDS ON A YR-PERIOD BASIS.
C
      ODDS=ODG*(2*D2-ODG)*SCALE
C
C     COMPUTE ODG ON A YR-PERIOD BASIS.
C
      ODG=0.
      X=D2*D2-ODDS
      IF (X .GT. .000001) ODG=D2-SQRT(X)
C
C     THE FUTURE DG IS CURRENTLY ON A YR-PERIOD BASIS. COMPUTE RATIO.
C
      AVRAT=AVRAT+(FDG/ODG*PRB)
C
      IF (DEBUIN) WRITE (JOMPB,15) J,I,D1,D2,FDG,ODG,PRB
   15 FORMAT ('J=',I4,' I=',I4,' D1=',F5.2,' D2=',F5.2,' FDG=',
     >        F4.2,' ODG=',F4.2,' PRB=',F6.3)
   20 CONTINUE
C
C     COMPUTE THE AVERAGE PGR.
C
      PGR=AVRAT/SUMP
C
C     WRITE DEBUG, IF REQUESTED.
C
      IF(DEBUIN) WRITE(JOMPB,50) ICYC,PGR,SUMP
   50 FORMAT (/,'IN MPGR - ICYC=',I3,'; PGR=',F8.3,'; SUMP=',F8.3)
      RETURN
C
C
      ENTRY MPSVDG
      NPGR=0
C
C     IF THIS IS CYCLE 1 AND IDG GT 1 (THE DG MEASURMENT PERIOD IS
C     CONCURRENT WITH THE PAST GROWTH PERIOD) THEN WE CAN NOT
C     ESTIMATE PGR...THEREFORE RETURN.
C
      IF(ICYC.EQ.1 .AND. IDG.GT.1) RETURN
C
C     SAVE THE VALUES OF DG WHICH WILL BE NEEDED TO COMPUTE PGR.
C
C     Changed pointer array ISCT subscript from 7 to IDXLP to correspond
C     with new species mapping and new location for LP (RNH Dec98, GEB May2000)
C
      ILP=ISCT(IDXLP,1)
      I2=ISCT(IDXLP,2)
C
C      ILP=ISCT(7,1)
C      I2=ISCT(7,2)
      DO 100 J=ILP,I2
      I=IND1(J)
      IF (PCT(I).LT.PCTCO) GOTO 100
      IPT(NPGR+ILP)=I
      NPGR=NPGR+1
      XPT(I)=DG(I)
  100 CONTINUE
C
C     INSURE NPYR CONTAINS THE MEASUREMENT PERIOD OF THE LAST CYCLE.
C     IF ICYC=1; THEN: NPYR WAS SET IN MPBIN (ENTRY MPBOPS).
C     ELSE:
C
      IF (ICYC .GT. 1) NPYR=IY(ICYC)-IY(ICYC-1)
C
      IF (DEBUIN) WRITE (JOMPB,150) NPGR,PCTCO,NPYR
  150 FORMAT (/,'IN PGR - ',I4,' TREE RECS SELECTED; PCTCO=',F8.3,
     >        ' NPYR=',I3)
      RETURN
      END
