      SUBROUTINE LBSTRD (IREAD,LENSET,SET,IRECNT,RECORD,KODE,WRK1,WRK2)
      IMPLICIT NONE
C----------
C BASE $Id$
C----------
C
C     READ RECORDS FORM IREAD AND FORM A SET IN STRING SET WITH
C     A TOTAL STRING LENGTH OF LENSET.  (FOR STAND AND ACTIVITY LABELS).
C
C     PART OF THE LABEL PROCESSING COMPONENT OF THE PROGNOSIS SYSTEM
C     N.L. CROOKSTON -- INTERMOUNTAIN RESEARCH STATION -- JAN 1987
C
C     IREAD = DATA SET REFERENCE NUMBER
C     LENSET= THE LENGTH OF THE STRING HOLDING THE SET
C     SET   = THE STRING HOLDING THE SET
C     IRECNT= NUMBER OF RECORDS READ ON IREAD.
C     RECROD= INPUT RECORD
C     KODE  = 0, OK,
C             1, INFORMATION OVERFILLS STRING HOLDING THE SET,
C             2, END-OF-DATA ON IREAD.
C
C     THE FOLLOWING RULES ARE FOLLOWED:
C
C     1.  LEADING BLANKS ARE DELETED, STRINGS OF OVER 1 BLANK ARE
C         CONVERTED TO ONE BLANK, BLANKS FOUND BETWEEN A NON-BLANK
C         CHAR. AND A COMMA ARE DELETED.
C     2.  ONE BLANK IS ADDED, IF NEEDED, FOLLOWING A COMMA.
C     3.  THE LAST MEMBER OF THE SET IS NOT FOLLOWED BY A COMMA.
C     4.  NULL-STRING AND ONE-BLANK SET MEMBERS ARE DELETED
C     5.  DUPLICATE SET MEMBERS ARE DELETED (A SINGLE MEMBER CAN
C         NOT OCCUR TWICE).
C     6.  PERIODS ARE DELETED.
C     7.  CONTINUATION RECORDS FOLLOW RECORDS ENDING WITH AN '&'.
C
      CHARACTER*250 SET,WRK1,WRK2
      CHARACTER*(*) RECORD
      INTEGER KODE,IRECNT,LENSET,IREAD,IRPS,KGET,LNWRK1,LNWRK2,KINT
      INTEGER I,ISTLNB
C
C     INITIALIZE RETURN CODE, LENSET, AND SET.
C
      KODE=0
      LENSET=0
      SET=' '
      IRPS=0
C
C     LOOP OVER ALL INPUT MEMBERS.
C
   10 CONTINUE
C
C     GET ONE MEMBER FROM THE INPUT FILE.
C
      CALL LBGET1 (IREAD,IRECNT,RECORD,IRPS,LNWRK2,WRK2,0,KGET)
      KODE=KGET
      IF (KODE.GT.0) GOTO 100
C
C     IF THE LENGTH OF THE MEMBER IS ZERO, THEN NO MEMBER WAS FOUND.
C
      IF (LNWRK2.EQ.0) GOTO 100
C
C     FIND THE UNION OF THE MEMBER WITH THE CURRENT SET.
C
      LNWRK1=LENSET
      WRK1=SET
      CALL LBUNIN (LNWRK1,WRK1,LNWRK2,WRK2,LENSET,SET,KINT)
      IF (KINT.GT.0) THEN
C
C        IF THE LABEL SET IS FULL, DROP RECORDS AS NECESSARY
C        TO PASS BY THE END OF THE LABEL SET DEFINITION.
C
   20    CONTINUE
         I=ISTLNB(RECORD)
         IF (I.GT.0) THEN
            IF (RECORD(I:I).EQ.'&') THEN
               READ (IREAD,'(A)',END=200) RECORD
               IRECNT=IRECNT+1
               GOTO 20
            ENDIF
         ENDIF
      ENDIF
      KODE=KINT
      IF (KODE.GT.0) GOTO 100
C
C     LOOP TO GET ANOTHER MEMBER, UNLESS SIGNALED THAT NO MORE EXIST.
C
      GOTO 10
C
  100 CONTINUE
      RETURN
  200 CONTINUE
      KODE=2
      RETURN
      END
