      SUBROUTINE R6CRWD(ISPC,D,H,CRWDTH)
      IMPLICIT NONE
C----------
C  $Id$
C----------
C  THIS ROUTINE COMPUTES CROWN WIDTH FOR INDIVIDUAL TREES.
C  CALLED FROM CCFCAL IN ALL REGION 6 VARIANTS, AND FOR THE
C  NI, KT, AND IE VARIANTS.
C
C  EQUATIONS FOR TREES LARGER THAN 4.5 FEET TALL WERE FIT BY
C  DENNIS DONNELLY (FOREST MANAGEMENT SERVICE CENTER, FORT COLLINS,
C  COLORADO) FROM INVENTORY DATA PROVIDED SPECIFICALLY FOR THIS
C  PURPOSE BY TOMMY GREGG IN REGION 6.  EQUATIONS ARE OF THE FORM:
C  CW = A * (DBH)**B     CW IN FEET, DBH IN INCHES
C
C  EQUATIONS FOR TREES LESS THAN 4.5 FEET TALL ARE OF THE FORM
C  CW = CONSTANT * HEIGHT, WHERE THE CONSTANT IS THE SLOPE OF A
C  STRAIGHT LINE THROUGH THE POINTS (H=0, CW=0) AND
C  (H=4.5, CW=VALUE OF DONNELLY EQUATION AT BUD WIDTH)
C
C----------
C  DEFINITION OF VARIABLES:
C
C     CROWN WIDTH COEFFICIENTS FOR TREES WITH HEIGHT > 4.5 FEET
C      CRWDTH = BG1() * D**BG2()
C      BG1 -- CONSTANT TERM OR INTERCEPT, SUBSCRIPTED BY SPECIES.
C      BG1 -- EXPONENT, SUBSCRIPTED BY SPECIES.
C
C     CROWN WIDTH COEFFICIENTS FOR TREE WITH HEIGHT <= 4.5 FEET
C      CRWDTH = SM * H
C      SM  -- SLOPE, SUBSCRIPTED BY SPECIES
C
C     CRWDTH = CROWN WIDTH FOR A TREE (FEET)
C     D      = DIAMETER AT BREAST HEIGHT (INCHES)
C     H      = TOTAL TREE HEIGHT (FEET)
C     ISPC   = NUMERIC SPECIES CODE
C
C  ORDER OF COEFFICIENTS IN THIS SUBROUTINE:
C
C   1 = DOUGLAS FIR                     PSEUDOTSUGA MENZIESII
C   2 = PACIFIC SILVER FIR              ABIES AMABILIS
C   3 = WHITE FIR                       ABIES CONCOLOR
C   4 = GRAND FIR                       ABIES GRANDIS
C   5 = SUBALPINE FIR                   ABIES LASIOCARPA
C   6 = RED FIR                         ABIES MAGNIFICA
C   7 = NOBLE FIR                       ABIES PROCERA
C
C   8 = INCENSE CEDAR                   CALOCEFRUS DECURRENS
C   9 = PORT ORFORD CEDAR               CHAMAECYPARIS LAWSONIA
C  10 = ALASKA CEDAR                    CHAMAECYPARIS NOOTKATENSIS
C  11 = WESTERN RED CEDAR               THUJA PLICATA
C
C  12 = SUBALPINE LARCH                 LARIX LYALLII
C  13 = WESTERN LARCH                   LARIX OCCIDENTALIS
C
C  14 = WESTERN HEMLOCK                 TSUGA HETEROPHYLLA
C  15 = MOUNTAIN HEMLOCK                TSUGA MERTENSIANA
C
C  16 = PACIFIC YEW                     TAXUS BREVIFOLIA
C
C  17 = WHITEBARK PINE                  PINUS ALBICAULIS
C  18 = KNOBCONE PINE                   PINUS ATTENUATA
C  19 = LODGEPOLE PINE                  PINUS CONTORTA
C  20 = JEFFREY PINE                    PINUS JEFFREYI
C  21 = SUGAR PINE                      PINUS LAMBERTIANA
C  22 = WESTERN WHITE PINE              PINUS MONTICOLA
C  23 = PONDEROSA PINE                  PINUS PONDEROSA
C
C  24 = ENGELMANN SPRUCE                PICEA ENGELMANNII
C  25 = SITKA SPRUCE                    PICEA SITCHENSIS
C
C  26 = BIG LEAF MAPLE                  ACER MACROPHYLLUM
C  27 = RED ALDER                       ALNUS RUBRA
C  28 = PAPER BIRCH                     BETULA PAPYRIFERA
C  29 = QUAKING ASPEN                   POPULUS TREMULOIDES
C  30 = OREGON WHITE OAK                QUERCUS GARRYANA
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
COMMONS
C----------
      INTEGER   ISPC, INDX
      INTEGER   MAPBC(15)
      REAL      D, H, CRWDTH
      REAL      BG1(30),BG2(30),SM(30)
C
      DATA BG1/
     & 4.4215, 3.9723, 3.8166, 4.1870, 3.2348, 3.1146, 3.0614, 4.0920,
     & 5.3864, 3.5341, 6.2318, 2.1039, 2.9571, 5.4864, 2.9372, 4.5859,
     & 2.1606, 2.1451, 2.4132, 3.2367, 3.0610, 3.4447, 2.8541, 3.6802,
     & 4.2857, 7.5183, 7.0806, 5.8980, 4.0910, 2.4922/
      DATA BG2/
     & 0.5329, 0.5177, 0.5229, 0.5341, 0.5179, 0.5780, 0.6276, 0.4912,
     & 0.4213, 0.5374, 0.4259, 0.6758, 0.6081, 0.5144, 0.5878, 0.4841,
     & 0.6897, 0.7132, 0.6403, 0.6247, 0.6201, 0.5185, 0.6400, 0.4940,
     & 0.5940, 0.4461, 0.4771, 0.4841, 0.5907, 0.8544/
      DATA SM/
     &  0.517,  0.473,  0.452,  0.489,  0.385,  0.345,  0.320,  0.412,
     &  0.608,  0.331,  0.698,  0.207,  0.316,  0.533,  0.253,  0.468,
     &  0.255,  0.248,  0.298,  0.406,  0.385,  0.476,  0.407,  0.451,
     &  0.466,  0.815,  0.730,  0.601,  0.351,  0.140/
C----------
C MAPPING FUNCTION FOR SPECIES IN THE --SEI-- VARIANT
C  1=pw,  2=lw,  3=fd,  4=bg, 5=hw, 6=cw, 7=pl, 8=se, 9=bl, 10=pp,
C 11=ep, 12=at, 13=ac, 14=oc(fd), 15=oh(ep)
C
C ONLY ISPC=11,12,13,15 ARE USED
C SURROGATES: RA(27) FOR AC - COTTONWOOD
C
C
      DATA MAPBC/
     &  22, 13,  1,  4, 14,
     &  11, 19, 24,  5, 23,
     &  28, 29, 27,  1, 28 /

C----------
C IF SPECIES IS ZERO, RETURN
C----------
      IF(ISPC .EQ. 0) THEN
        CRWDTH=0.
        GO TO 100
      ENDIF
C----------
C ASSIGN THE APPROPRIATE EQUATION NUMBER.
C----------
      IF (VVER(:2).EQ.'BC') THEN
        INDX=MAPBC(ISPC)
       ELSE
        INDX=99
      ENDIF
C----------
C COMPUTE CROWN WIDTH
C----------
      IF(INDX .EQ. 99) THEN
        CRWDTH=0.
        GO TO 100
      ENDIF
C
      IF(H .GT. 4.5) THEN
        CRWDTH = BG1(INDX) * D**BG2(INDX)
      ELSE
        CRWDTH = SM(INDX) * H
      ENDIF
C
  100 CONTINUE
      RETURN
      END
