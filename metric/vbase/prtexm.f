      SUBROUTINE PRTEXM (INPUT,IPRINT,ITITLE)
      IMPLICIT NONE
C----------
C METRIC-VBASE $Id: prtexm.f 2438 2018-07-05 16:54:21Z gedixon $
C----------
C
C     READS THE DATA THAT COMPRISES THE EXAMPLE TREE AND STAND
C     ATTRIBUTE TABLE FROM AN UNFORMATTED DATA SET REFERENCED BY
C     INPUT, AND PRINTS THE TABLE IN LINE PRINTER FORMAT USING THE
C     DATA SET REFERENCE NUMBER IPRINT. IF EITHER INPUT OR IPRINT
C     ARE ZERO, THE ROUTINE EXITS.
C
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--JUNE 1980
C
C     IRT   = THE UNFORMATTED RECORD TYPE IDENTIFER:
C
C             0= STAND IDENTIFIER RECORD; FIRST RECORD IN FILE.
C             1= STAND ATTRIBUTE RECORD WITHOUT A RESIDUAL RECORD
C             2= STAND ATTRIBUTE RECORD; A RESIDUAL RECORD FOLLOWS
C             3= THE RESIDUAL RECORD WHICH SHOULD ONLY FOLLOW A TYPE
C                2 RECORD.
C             4= YEAR AND PERIOD LENGTH RECORD.
C             5= EXAMPLE TREE RECORDS.
C
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
COMMONS
C
C----------
C  VARIABLE DECLARATIONS:
C----------
C
      CHARACTER*4 MGTID
      CHARACTER*26 NPLT
      CHARACTER*72 ITITLE
C
      CHARACTER*3 IONSP(6)
C
      INTEGER I,I1,INPUT,IPRINT,IRT,IRTNCD,IS
C
      INTEGER I2(2),IFRAC(6),IOICR(6)
C
      REAL A5(5),DBHIO(6),DGIO(6),HTIO(6),PCTIO(6),PRBIO(6)
C
C----------
C  DATA STATEMENTS:
C----------
      DATA IFRAC/10,30,50,70,90,100/
C----------
C
      IF (INPUT*IPRINT .EQ. 0) RETURN
      REWIND INPUT
C----------
C  SET FLAG INDICATING THAT SAMPLE TREES HAVE NOT BEEN RESELECTED.
C----------
      IS=0
C----------
C  READ THE FIRST RECORD, THEN WRITE HEADINGS
C----------
      READ (INPUT,END=160) IRT,NPLT,MGTID
      IF (IRT .NE. 0) CALL ERRGRO(.FALSE.,19)
      CALL fvsGetRtnCode(IRTNCD)
      IF (IRTNCD.NE.0) RETURN
C
      CALL GHEADS (NPLT,MGTID,0,IPRINT,ITITLE)
      GO TO 125
   90 CONTINUE
      READ (INPUT,END=155) IRT,I1,A5
      IF (IRT .NE. 1 .AND. IRT .NE. 2) CALL ERRGRO(.FALSE.,19)
      CALL fvsGetRtnCode(IRTNCD)
      IF (IRTNCD.NE.0) RETURN
C
      SELECT CASE (VARACD)
C----------
C  EASTERN TWIGS AND CANADIAN ONTARIO VARIANTS
C----------
      CASE ('CS','LS','NE','ON')
        WRITE (IPRINT,100) I1,A5(1),A5(2),A5(3),A5(4)
  100   FORMAT(73X,I3,5X,F5.1,4X,F6.0,4X,F5.0,6X,F5.1)
        IF (IRT .EQ. 1) GO TO 125
        READ (INPUT,END=155) IRT,A5
        IF (IRT .NE. 3) CALL ERRGRO(.FALSE.,19)
        CALL fvsGetRtnCode(IRTNCD)
        IF (IRTNCD.NE.0) RETURN
        WRITE (IPRINT,110) A5(1),A5(2),A5(3),A5(4)
  110   FORMAT(69X,'RESIDUAL:',4X,F4.1,4X,F6.0,4X,F5.0,6X,F5.1)
C----------
C  WESTERN, SOUTHERN, AND CANADIAN BRITISH COLUMBIA VARIANTS
C----------
      CASE DEFAULT
        WRITE (IPRINT,115) I1,A5
  115   FORMAT(73X,I3,6X,F4.1,4X,F6.0,4X,F5.0,6X,F5.1,3X,F6.1)
        IF (IRT .EQ. 1) GO TO 125
        READ (INPUT,END=155) IRT,A5
        IF (IRT .NE. 3) CALL ERRGRO(.FALSE.,19)
        CALL fvsGetRtnCode(IRTNCD)
        IF (IRTNCD.NE.0) RETURN
        WRITE (IPRINT,120) A5
  120   FORMAT(69X,'RESIDUAL:',4X,F4.1,4X,F6.0,4X,F5.0,6X,F5.1,3X,F6.1)
C
      END SELECT
C
  125 CONTINUE
      READ (INPUT,END=165) IRT,I2
      IF (IRT .NE. 4) CALL ERRGRO(.FALSE.,19)
      CALL fvsGetRtnCode(IRTNCD)
      IF (IRTNCD.NE.0) RETURN
C----------
C  IF DATE IS NEGETIVE, THE SAMPLE TREES WERE RESELECTED.  WRITE
C  **, SET FLAG FOR MSG, AND SET DATE POSITIVE.
C----------
      IF (I2(1).LT.0) GOTO 135
      WRITE (IPRINT,130) I2
  130 FORMAT(/I4,42X,'(',I3,' YRS)'/)
      GOTO 145
  135 CONTINUE
      I2(1)=-I2(1)
      IS=1
      WRITE (IPRINT,140) I2
  140 FORMAT(/I4,' **',39X,'(',I3,' YRS)'/)
  145 CONTINUE
      READ (INPUT,END=155) IRT,IONSP,DBHIO,HTIO,IOICR,DGIO,PCTIO,PRBIO
      IF (IRT .NE. 5) CALL ERRGRO(.FALSE.,19)
      CALL fvsGetRtnCode(IRTNCD)
      IF (IRTNCD.NE.0) RETURN
C
      WRITE (IPRINT,150) (IFRAC(I),IONSP(I),DBHIO(I),HTIO(I),IOICR(I),
     >                    DGIO(I),PCTIO(I),PRBIO(I),  I=1,6)
  150 FORMAT(7X,I3,5X,A3,5X,F6.2,3X,F6.2,3X,I3,3X,F6.2,4X,F5.1,
     &       1X,F7.1)
      GO TO 90
C----------
C  END OF DATA TARGETS:
C----------
  155 CALL ERRGRO(.FALSE.,19)
      CALL fvsGetRtnCode(IRTNCD)
      IF (IRTNCD.NE.0) RETURN
C
  160 CALL ERRGRO (.TRUE.,20)
  165 CONTINUE
      REWIND INPUT
      IF (IS.EQ.1) WRITE (IPRINT,170)
  170 FORMAT (/'** NOTE:  DUE TO HARVEST, COMPRESSION, OR REGENER',
     >        'ATION ESTABLISHMENT, NEW SAMPLE TREES WERE SELECTED.')
C
      RETURN
      END
