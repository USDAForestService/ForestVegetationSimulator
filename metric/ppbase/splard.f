      SUBROUTINE SPLARD (SCALOC,SCAREA,INPUT,NRECS,IRC)
      IMPLICIT NONE
C----------
C  **SPLARD--PPBASE   DATE OF LAST REVISION:  06/03/10
C----------
C     READS THE LOCATION AND NEIGHBORS DATA.
C
C     SCALOC= SCALING OF THE LOCATION DATA.
C     SCAREA= SCALING OF THE AREA DATA.
C     INPUT = INPUT DATA SET REFERENCE NUMBER
C     NRECS = INPUT RECORD COUNT
C     IRC   = RETURN CODE: 0=OK, 1=ACTUAL END-OF-FILE WAS REACHED,
C             2=TOO MANY INPUT RECORDS.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPSPLA.F77'
C
C
      INCLUDE 'METRIC.F77'
C
C
COMMONS
C
      CHARACTER ID*26,CXL*10,CYL*10,CA*10
      INTEGER IRC,NRECS,INPUT
      REAL SCAREA,SCALOC,SCLOC,SCARE,A,XL,YL
C
C     CHECK THE SCALING VALUES
C
      SCLOC=SCALOC
      IF (SCALOC.LE.0.0) SCLOC=1.0
      SCARE=SCAREA*HAtoACR
      IF (SCAREA.LE.0.0) SCARE=HAtoACR
C
C     READ THE DATA...COUNT THE INPUT RECORDS.
C
      NLAS=0
   10 CONTINUE
      READ (INPUT,'(A26,T29,3F10.0,T29,3A10)',END=30)
     >      ID,XL,YL,A,CXL,CYL,CA
      IF (INDEX(ID,'-999').GT.0) GOTO 20
      IF (ID.EQ.' ') GOTO 10
      NLAS=NLAS+1
      IF (NLAS.GT.MXSTND) THEN
         IRC=2
         GOTO 40
      ENDIF
      CLAID(NLAS)=ID
      IF (CXL.NE.' '.AND. CYL.NE.' ') THEN
         XLOC(NLAS)=XL*SCLOC
         YLOC(NLAS)=YL*SCLOC
      ELSE
         XLOC(NLAS)=-99999.0
         YLOC(NLAS)=-99999.0
      ENDIF
      IF (CA.NE.' ') THEN
         AREA(NLAS)=A*SCARE
      ELSE
         AREA(NLAS)=-99999.0
      ENDIF
      GOTO 10
   20 CONTINUE
      IRC=0
      GOTO 40
   30 CONTINUE
      IRC=1
   40 CONTINUE
      NRECS=NLAS
      RETURN
      END
