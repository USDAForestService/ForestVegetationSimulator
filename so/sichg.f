      SUBROUTINE SICHG(ISISP,SSITE,SIAGE)
      IMPLICIT NONE
C----------
C  **SICHG--SO  DATE OF LAST REVISION:  04/24/08
C----------
COMMONS
C
      INCLUDE 'PRGPRM.F77'
C
COMMONS
C  THIS ROUTINE TAKES THE SITE FROM THE STDINFO CARD AND/OR
C  THE DEFAULT VALUE FROM SITSET, AND DETERMINES IF IT IS A
C  BHA OR TTA SITE CURVE.  THE RETURN VARIABLE SIAGE REPRESENTS THE
C  SPECIES BASE AGE + AGE2BH IF THE SITE SPECIES SITE CURVE
C  IS A TTA CURVE AND SPECIES CURVE A BHA CURVE, AND SO ON.
C  THEN, THE SITE FOR EACH SPECIES MAY BE ESTIMATED USING
C  THE SITE SPECIES CURVE IN HTCALC.
C  DATA STATEMENTS FOR A AND B CONTAIN COEFFICIENTS FOR A LINEAR
C  RELATION OF AGE TO BREAST HEIGHT AS A FUNCTION OF SITE INDEX.
C  THE LINEAR RELATION IS AN ESTIMATE FROM THE LOWER END POINT OF
C  A HEIGHT GROWTH CURVE TO THE ORIGIN (0 AGE; 0 TOT TREE HT.)
C  THESE COEFFICIENTS SHOULD BE COMPATIBLE WITH SIMILAR VALUES USED
C  IN THE SMHTGF SUBROUTINE.
C
C  CALLED FROM SITSET
C  SPECIES ORDER:
C  1=WP,  2=SP,  3=DF,  4=WF,  5=MH,  6=IC,  7=LP,  8=ES,  9=SH,  10=PP,
C 11=JU, 12=GF, 13=AF, 14=SF, 15=NF, 16=WB, 17=WL, 18=RC, 19=WH,  20=PY,
C 21=WA, 22=RA, 23=BM, 24=AS, 25=CW, 26=CH, 27=WO, 28=WI, 29=GC,  30=MC,
C 31=MB, 32=OS, 33=OH
C----------
      INTEGER DIFF,REFAGE(MAXSP)
      CHARACTER*1 ISILOC,REFLOC(MAXSP)
      REAL SSITE,AGE2BH
      INTEGER ISISP,I
      REAL A(MAXSP),B(MAXSP),SIAGE(MAXSP)
C
      DATA A /
     &  18.43316,  8.00000,  8.50000,  9.01840, 45.24969,
     &   9.01840, 10.65724, 33.72545,  0.00000,  8.00000,
     &   34.0000,  9.01840, 13.88200,  9.01840, 7.938059,
     &   34.0000,  8.11680, 24.62857,  6.15767, 11.56252,
     &  11.56252,  3.28241, 11.56252,  34.0000, 11.56252,
     &  11.56252,  00.0000, 11.56252, 11.56252, 11.56252,
     &  11.56252,  8.50000, 11.56252/
C
      DATA B /
     &  -0.13399, -0.04286, -0.05000, -0.05700, -1.28885,
     &  -0.05700, -0.10667,-0.274509, -0.00000, -0.04286,
     &  -0.20000, -0.05700, -0.06588, -0.05700, -0.02873,
     &  -0.20000, -0.05661, -0.20465, -0.03596, -0.05586,
     &  -0.05586, -0.02987, -0.05586, -0.20000, -0.05586,
     &  -0.05586, -0.00000, -0.05586, -0.05586, -0.05586,
     &  -0.05586, -0.05000, -0.05586/
C
      DATA REFLOC/
     & 'T', 'B', 'B', 'B', 'B', 'B', 'T', 'B', 'B', 'B',
     & 'T', 'B', 'B', 'B', 'B', 'T', 'B', 'T', 'B', 'B',
     & 'B', 'T', 'B', 'B', 'B', 'B', 'B', 'B', 'B', 'B',
     & 'B', 'B', 'B'/
C
      DATA REFAGE/
     &  50, 100,  50,  50, 100,  50,  50, 100,  50, 100,
     & 100,  50, 100,  50, 100, 100,  50, 100,  50, 100,
     & 100,  20, 100,  80, 100, 100,  50, 100, 100, 100,
     & 100,  50, 100/
C----------
C ISILOC IS THE PLACE THE AGE FOR THE SITE SPECIES.
C----------
      ISILOC = REFLOC(ISISP)
      DO 100 I = 1,MAXSP
C----------
C  SET UP THE ARRAY TO TELL WHETHER YOU NEED TO SLIDE UP OR DOWN THE SIT
C  LINE TO ADJUST FOR TOTAL AGE OR BREAST HIGH AGE
C----------
      IF(ISILOC .EQ. 'T' .AND. REFLOC(I) .EQ. 'B')DIFF=-1
      IF(ISILOC .EQ. REFLOC(I))DIFF=0
      IF(ISILOC .EQ. 'B' .AND. REFLOC(I) .EQ. 'T')DIFF=1
      AGE2BH=0.0
      IF(DIFF .LT. 0 .OR. DIFF .GT. 0)THEN
        AGE2BH=A(I) + B(I)*SSITE
        IF((ISISP .NE. 5) .AND. (I .EQ. 5))
     &  AGE2BH=A(I) + B(I)*(SSITE/3.281)
        IF((ISISP .EQ. 5) .AND. (I .NE. 5))
     &  AGE2BH=A(I) + B(I)*(SSITE*3.281)
      END IF
      IF(I.EQ.9)AGE2BH=18.
      IF(I.EQ.27)AGE2BH=2.
      SIAGE(I) = REFAGE(I) + AGE2BH*DIFF
  100 CONTINUE
      RETURN
      END
