      SUBROUTINE HTDBH (IFOR,ISPC,D,H,MODE)
      IMPLICIT NONE
C----------
C CS $Id: htdbh.f 000 2018-03-27 00:00:00Z gedixon $
C----------
C  THIS SUBROUTINE CONTAINS THE DEFAULT HEIGHT-DIAMETER RELATIONSHIPS
C  FROM THE INVENTORY DATA.  IT IS CALLED FROM CRATET TO DUB MISSING
C  HEIGHTS, AND FROM REGENT TO ESTIMATE DIAMETERS (PROVIDED IN BOTH
C  CASES THAT LHTDRG IS SET TO .TRUE.).
C
C  COEFFICIENTS DEVELOPED FOR THE SN VARIANT USING SOUTHERN TREE DATA.
C
C  DEFINITION OF VARIABLES:
C         D = DIAMETER AT BREAST HEIGHT
C         H = TOTAL TREE HEIGHT (STUMP TO TIP)
C         DB= BUDWIDTH(INCHES) AT HEIGHT = 5.51 FT.
C
C  LOCAL ARRAYS
C         SNALL = CURTIS-ARNEY COEFFICIENTS FOR ALL SPECIES.  ALL
C                 SOUTHERN TREE DATA WERE USED IN DEVELOPMENT OF
C                 THESE COEFFS.
C        SNDBAL = BUDWITH, TRANSITION DIAMETER, ESTIMATES FOR ALL
C                 SPECIES
C        IWYKCA = 0 IF USING WKYOFF EQNS; 1 IF USING CURTIS-ARNEY
C
C      MODE = MODE OF OPERATING THIS SUBROUTINE
C             0 IF DIAMETER IS PROVIDED AND HEIGHT IS DESIRED
C             1 IF HEIGHT IS PROVIDED AND DIAMETER IS DESIRED
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
COMMONS
C
      REAL SNALL(3,96), SNDBAL(96)
      INTEGER IWYKCA(96)
      INTEGER MODE,ISPC,IFOR,J,I
      REAL H,D,P2,P3,P4,DB,HAT3
      LOGICAL DEBUG
      REAL DANUW
C
C=====================================
C     SPECIES LIST FOR CENTRAL STATES
C=====================================
C     1 = EASTERN REDCEDAR (RC)
C     2 = JUNIPER SPECIES (JU)
C     3 = SHORTLEAF PINE (SP)
C     4 = VIRGINIA PINE (VP)
C     5 = LOBLOLLY PINE (LP)
C     6 = OTHER SOFTWOOD SPECIES (OS)
C     7 = EASTERN WHITE PINE (WP)
C     8 = BLACK WALNUT (WN)
C     9 = BUTTERNUT (BN)
C    10 = TUPELO SPECIES(TL)
C    11 = SWAMP TUPELO (TS)
C    12 = WATER TUPELO (WT)
C    13 = BLACKGUM / BLACK TUPELO (BG)
C    14 = SELECT HICKORY (HS)
C    15 = SHAGBARK HICKORY (SH)
C    16 = SHELLBARK HICKORY (SL)
C    17 = MOCKERNUT HICKORY (MH)
C    18 = PIGNUT HICKORY (PH)
C    19 = HICKORY SPECIES (HI)
C    20 = WATER HICKORY (WH)
C    21 = BITTERNUT HICKORY (BH)
C    22 = PECAN (PE)
C    23 = BLACK HICKORY (BI)
C    24 = AMERICAN BEECH (AB)
C    25 = BLACK ASH (BA)
C    26 = PUMPKIN ASH (PA)
C    27 = BLUE ASH (UA)
C    28 = EASTERN COTTONWOOD (EC)
C    29 = RED MAPLE (RM)
C    30 = BOXELDER (BE)
C    31 = SILVER MAPLE (SV)
C    32 = BLACK CHERRY (BC)
C    33 = AMERICAN ELM (AE)
C    34 = SUGARBERRY (SG)
C    35 = HACKBERRY (HK)
C    36 = WINGED ELM (WE)
C    37 = ELM SPECIES (EL)
C    38 = SIBERIAN ELM (SI)
C    39 = SLIPPERY (RED) ELM (RL)
C    40 = ROCK ELM (RE)
C    41 = YELLOW-POPLAR (YP)
C    42 = AMERICAN BASSWOOD (BW)
C    43 = SUGAR MAPLE (SM)
C    44 = ASH SPECIES(AS)
C    45 = WHITE ASH (WA)
C    46 = GREEN ASH (GA)
C    47 = WHITE OAK (WO)
C    48 = NORTHERN RED OAK (RO)
C    49 = SOUTHERN RED OAK (SK)
C    50 = BLACK OAK (BO)
C    51 = SCARLET OAK (SO)
C    52 = BLACKJACK OAK (BJ)
C    53 = CHINKAPIN OAK (CK)
C    54 = SWAMP WHITE OAK (SW)
C    55 = BUR OAK (BR)
C    56 = SWAMP CHESTNUT OAK (SN)
C    57 = POST OAK (PO)
C    58 = DELTA POST OAK (DO)
C    59 = CHESTNUT OAK (CO)
C    60 = PIN OAK (PN)
C    61 = CHERRYBARK OAK (CB)
C    62 = SHINGLE OAK (QI)
C    63 = OVERCUP OAK (OV)
C    64 = WATER OAK (WK)
C    65 = NUTTALL OAK (NK)
C    66 = WILLOW OAK (WL)
C    67 = SHUMARD OAK (QS)
C    68 = OTHER UPLAND HARDWOOD SPECIES (UH)
C    69 = SASSAFRAS (SS)
C    70 = OHIO BUCKEYE (OB)
C    71 = CATALPA (CA)
C    72 = COMMON PERSIMMON (PS)
C    73 = HONEYLOCUST (HL)
C    74 = BALSAM POPLAR (BP)
C    75 = BIGTOOTH ASPEN (BT)
C    76 = QUAKING ASPEN (QA)
C    77 = BLACK LOCUST (BK)
C    78 = OTHER LOWLAND SPECIES (OL)
C    79 = SYCAMORE (SY)
C    80 = BALDCYPRESS (BY)
C    81 = RIVER BIRCH (RB)
C    82 = SWEETGUM (SU)
C    83 = WILLOW SPECIES(WI)
C    84 = BLACK WILLOW (BL)
C    85 = NON-COMMERCIAL HARDWOOD SPECIES (NC)
C    86 = AMERICAN HORNBEAM (AH)
C    87 = EASTERN REDBUD (RD)
C    88 = FLOWERING DOGWOOD (DW)
C    89 = HAWTHORN SPECIES(HT)
C    90 = KENTUCKY COFFEETREE (KC)
C    91 = OSAGE-ORANGE (OO)
C    92 = CUCUMBERTREE (CT)
C    93 = SWEETBAY (MV)
C    94 = MULBERRY SPECIES (MB)
C    95 = EASTERN HOPHORNBEAM (HH)
C    96 = SOURWOOD (SD)
C----------
C
C----------
C     CURTIS-ARNEY COEFFICIENTS
C     ALL SOUTHERN DATA SNALL(I,J) I1=>P2, I2=>P3, I3=>P4, J= SPECIES
C----------
C
      DATA ((SNALL(I,J),I=1,3), J= 1,15)/
C 1=EASTERN REDCEDAR       USE SN VIRGINIA PINE (14)
     & 926.1802712,     4.46209203,     -0.20053974 ,
C 2=JUNIPER (SP.)          USE SN JUNIPER (SP.) (2)
     & 212.7932729,     3.47154903,     -0.3258523  ,
C 3=SHORTLEAF PINE         USE SN SHORTLEAF PINE (5)
     & 444.0921666,     4.11876312,     -0.30617043 ,
C 4=VIRGINIA PINE          USE SN VIRGINIA PINE (14)
     & 926.1802712,     4.46209203,     -0.20053974 ,
C 5=LOBLOLLY PINE          USE SN LOBLOLLY PINE (13)
     & 243.860648,      4.28460566,     -0.47130185 ,
C 6=OTHER SOFTWOODS        USE SN JUNIPER (SP.) (2)
     & 212.7932729,     3.47154903,     -0.32585230 ,
C 7=EASTERN WHITE PINE     USE SN EASTERN WHITE PINE (12)
     & 2108.844224,     5.65948135,     -0.18563136 ,
C 8=BLACK WALNUT           USE SN BLACK WALNUT (43)
     & 93.71042027,     3.6575094,      -0.88246833 ,
C 9=BUTTERNUT              USE SN BUTTERNUT (42)
     & 285.8797853,     3.52138815,     -0.3193688  ,
C 10=TUPELO (SP.)          USE SN BLACKGUM/BLACK TUPELO (54)
     & 319.9788466,     3.67313408,     -0.30651323 ,
C 11=SWAMP TUPELO          USE SN SWAMP TUPELO (55)
     & 252.3566527,     3.24398683,     -0.33343129 ,
C 12=WATER TUPELO          USE SN WATER TUPELO (53)
     & 163.9728054,     2.76819717,     -0.44098009 ,
C 13=BLACKGUM/BLACK TUPELO USE SN BLACKGUM/BLACK TUPELO (54)
     & 319.9788466,     3.67313408,     -0.30651323 ,
C 14=SELECT HICKORY        USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 15=SHAGBARK HICKORY      USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 /
C
      DATA ((SNALL(I,J),I=1,3), J= 16,30)/
C 16=SHELLBARK HICKORY     USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 17=MOCKERNUT HICKORY     USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 18=PIGNUT HICKORY        USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 19=HICKORY (SP.)         USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 20=WATER HICKORY         USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 21=BITTERNUT HICKORY     USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 22=PECAN                 USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 23=BLACK HICKORY         USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 24=AMERICAN BEECH        USE SN AMERICAN BEECH (33)
     & 526.1392688,     3.89232121,     -0.22587084 ,
C 25=BLACK ASH             USE SN BLACK ASH (36)
     & 178.9307637,     4.92861465,     -0.63777014 ,
C 26=PUMPKIN ASH           USE SN ASH (34) 
     & 251.4042514,     3.26919806,     -0.35905996 ,
C 27=BLUE ASH              USE SN ASH (34)
     & 251.4042514,     3.26919806,     -0.35905996 ,
C 28=EASTERN COTTONWOOD    USE SN COTTONWOOD (60)
     & 190.9797059,     3.69278884,     -0.52730469 ,
C 29=RED MAPLE             USE SN RED MAPLE (20)
     & 268.5564351,     3.11432843,     -0.29411156 ,
C 30=BOXELDER              USE SN BUTTERNUT (42)
     & 285.8797853,     3.52138815,     -0.3193688  /
C
      DATA ((SNALL(I,J),I=1,3), J= 31,45)/
C 31=SILVER MAPLE          USE SN SILVER MAPLE (21)
     & 80.51179925,     26.98331005,     -2.02202808,
C 32=BLACK CHERRY          USE SN BLACK CHERRY (62)
     & 364.0247807,     3.55987361,     -0.27263121 ,
C 33=AMERICAN ELM          USE SN AMERICAN ELM (86)
     & 418.5941897,     3.17038578,     -0.18964025 ,
C 34=SUGARBERRY            USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 35=HACKBERRY             USE SN HACKBERRY (SP.) (29)
     & 484.7529797,     3.93933286,     -0.25998833 ,
C 36=WINGED ELM            USE SN WINGED ELM (85)
     & 1001.672885,     4.57310438,     -0.18898217 ,
C 37=ELM (SP.)             USE SN ELM (84)
     & 1005.80672,      4.6473994,      -0.20336143 ,
C 38=SIBERIAN ELM          USE SN ELM (84)
     & 1005.80672,      4.6473994,      -0.20336143 ,
C 39=SLIPPERY (RED) ELM    USE SN SLIPPERY ELM (87)
     & 1337.547184,     4.48953501,     -0.14749529 ,
C 40=ROCK ELM              USE SN ELM (84)
     & 1005.80672,      4.6473994,      -0.20336143 ,
C 41=YELLOW-POPLAR         USE SN YELLOW-POPLAR (45)
     & 625.7696614,     3.87320571,     -0.23349496 ,
C 42=AMERICAN BASSWOOD     USE SN BASSWOOD (83)
     & 293.5715132,     3.52261899,     -0.35122247 ,
C 43=SUGAR MAPLE           USE SN SUGAR MAPLE (22)
     & 209.8555358,     2.95281334,     -0.36787496 ,
C 44=ASH (SP.)             USE SN ASH (34)
     & 251.4042514,     3.26919806,     -0.35905996 ,
C 45=WHITE ASH             USE SN WHITE ASH (35)
     & 91.35276617,     6.99605268,     -1.22937669 /
C
      DATA ((SNALL(I,J),I=1,3), J= 46,60)/
C 46=GREEN ASH             USE SN GREEN ASH (37)
     & 404.9692122,     3.39019741,     -0.255096   ,
C 47=WHITE OAK             USE SN WHITE OAK (63)
     & 170.1330787,     3.27815866,     -0.48744214 ,
C 48=NORTHERN RED OAK      USE SN NORTHERN RED OAK (75)
     & 700.0636452,     4.10607389,     -0.21392785 ,
C 49=SOUTHERN RED OAK      USE SN SOUTHERN RED OAK (65)
     & 150.4300023,     3.13270999,     -0.49925872 ,
C 50=BLACK OAK             USE SN BLACK OAK (78)
     & 224.716279,      3.11648501,     -0.35982064 ,
C 51=SCARLET OAK           USE SN SCARLET OAK (64)
     & 196.0564703,     3.0067167,      -0.38499624 ,
C 52=BLACKJACK OAK         USE SN BLACKJACK OAK (70)
     & 157.4828626,     3.38919504,     -0.39151499 ,
C 53=CHINKAPIN OAK         USE SN CHINKAPIN OAK (72)
     & 72.7907469,      3.67065539,     -1.09878979 ,
C 54=SWAMP WHITE OAK       USE SN CHERRYBARK OAK (66)
     & 182.6306309,     3.12897883,     -0.46391125 ,
C 55=BUR OAK               USE SN SCARLET OAK (64)
     & 196.0564703,     3.0067167,      -0.38499624 ,
C 56=SWAMP CHESTNUT OAK    USE SN SWAMP CHESTNUT OAK (71)
     & 281.3413276,     3.51695826,     -0.3336282  ,
C 57=POST OAK              USE SN POST OAK (77)
     & 765.2907525,     4.22375114,     -0.18974706 ,
C 58=DELTA POST OAK        USE SN POST OAK (77)
     & 765.2907525,     4.22375114,     -0.18974706 ,
C 59=CHESTNUT OAK          USE SN CHESTNUT OAK (74)
     & 94.54465221,     3.42034111,     -0.818759   ,
C 60=PIN OAK               USE SN SCARLET OAK (64)
     & 196.0564703,     3.0067167,      -0.38499624 /
C
      DATA ((SNALL(I,J),I=1,3), J= 61,75)/
C 61=CHERRYBARK OAK        USE SN CHERRYBARK OAK (66)
     & 182.6306309,     3.12897883,     -0.46391125 ,
C 62=SHINGLE OAK           USE SN CHESTNUT OAK (74)
     & 94.54465221,     3.42034111,     -0.818759   ,
C 63=OVERCUP OAK           USE SN OVERCUP OAK (69)
     & 184.0856396,     3.49535241,     -0.46211544 ,
C 64=WATER OAK             USE SN WATER OAK (73)
     & 470.0617193,     3.78892643,     -0.25123824 ,
C 65=NUTTALL OAK           USE SN SCARLET OAK (64)
     & 196.0564703,     3.0067167,      -0.38499624 ,
C 66=WILLOW OAK            USE SN COTTONWOOD (60)
     & 190.9797059,     3.69278884,     -0.52730469 ,
C 67=SHUMARD OAK           USE SN SHUMARD OAK (76)
     & 215.0009406,     3.14204012,     -0.39067352 ,
C 68=OTHER UPLAND HDWDS    USE SN WHITE OAK (63)
     & 170.1330787,     3.27815866,     -0.48744214 ,
C 69=SASSAFRAS             USE SN SASSAFRAS (82)
     & 755.1038099,     4.39496421,     -0.21778831 ,
C 70=OHIO BUCKEYE          USE SN BASSWOOD (83)     
     & 293.5715132,     3.52261899,     -0.35122247 ,
C 71=CATALPA               USE SN CATALPA (28)
     & 190.9797059,     3.69278884,     -0.52730469 ,
C 72=COMMON PERSIMMON      USE SN HACKBERRY (SP.) (29)
     & 484.7529797,     3.93933286,     -0.25998833 ,
C 73=HONEYLOCUST           USE SN HONEYLOCUST (38)
     & 778.9356784,     4.20756452,     -0.18734197 ,
C 74=BALSAM POPLAR         USE SN WHITE ASH (35)
     & 91.35276617,     6.99605268,     -1.22937669 ,
C 75=BIGTOOTH ASPEN        USE SN WHITE ASH (35)
     & 91.35276617,     6.99605268,     -1.22937669 /
C
      DATA ((SNALL(I,J),I=1,3), J= 76,90)/
C 76=QUAKING ASPEN         USE SN HICKORY (SP.) (27)
     & 337.6684758,     3.62726466,     -0.32083172 ,
C 77=BLACK LOCUST          USE SN BLACK LOCUST (80)
     & 880.2844971,     4.59642097,     -0.21824277 ,
C 78=OTHER LOWLAND SPECIES USE SN RED MAPLE (20)
     & 268.5564351,     3.11432843,     -0.29411156 ,
C 79=SYCAMORE              USE SN SYCAMORE (59)
     & 644.3567687,     3.92045786,     -0.21444786 ,
C 80=BALDCYPRESS           USE SN BALDCYPRESS (15)
     & 119.5749091,     4.13535453,     -0.79625456 ,
C 81=RIVER BIRCH           USE SN BIRCH (SP.) (24)
     & 170.5253403,     2.68833651,     -0.40080716 ,
C 82=SWEETGUM              USE SN SWEETGUM (44)
     & 290.90548,       3.6239536,      -0.3720123  ,
C 83=WILLOW (SP.)          USE SN WILLOW (81)
     & 408.2772475,     3.81808285,     -0.27210505 ,
C 84=BLACK WILLOW          USE SN WILLOW (81)
     & 408.2772475,     3.81808285,     -0.27210505 ,
C 85=NON-COMM HARDWOODS    USE SN HACKBERRY (SP.) (29)
     & 484.7529797,     3.93933286,     -0.25998833 ,
C 86=AMERICAN HORNBEAM     USE SN EASTERN HOPHORNBEAM (56)
     & 109.7324294,     2.25025802,     -0.41297463 ,
C 87=EASTERN REDBUD        USE SN EASTERN REDBUD (30)
     & 103.1767713,     2.21695491,     -0.3596216  ,
C 88=FLOWERING DOGWOOD     USE SN FLOWERING DOGWOOD (31)
     & 863.0501053,     4.38560239,     -0.14812185 ,
C 89=HAWTHORN (SP.)        USE SN HACKBERRY (SP.) (29)
     & 484.7529797,     3.93933286,     -0.25998833 ,
C 90=KENTUCKY COFFEETREE   USE SN AMERICAN BEECH (33)
     & 526.1392688,     3.89232121,     -0.22587084 /
C
      DATA ((SNALL(I,J),I=1,3), J= 91,96)/
C 91=OSAGE-ORANGE          USE SN MISC HARDWOODS (89)
     & 109.7324294,     2.25025802,     -0.41297463 ,
C 92=CUCUMBER TREE         USE SN CUCUMBERTREE (47)
     & 660.1996521,     3.92077102,     -0.21124354 ,
C 93=SWEETBAY              USE SN SWEETBAY (49)
     & 184.1931837,     2.84569124,     -0.36952511 ,
C 94=MULBERRY (SP.)        USE SN MULBERRY (SP.) (52)
     & 750.1823388,     4.14262749,     -0.15940723 ,
C 95=EASTERN HOPHORNBEAM   USE SN EASTERN HOPHORNBEAM (56)
     & 109.7324294,     2.25025802,     -0.41297463 ,
C 96=SOURWOOD              USE SN SOURWOOD (57)
     & 690.4917743,     4.15983216,     -0.18613455 /
C----------
C  TRANSITION VALUES (INCHES) IN EXTRAPOLATION TO SHORTER HEIGHTS.
C  THESE CORRESPOND TO THE BUDWIDTH PHYSICAL ATTRIBUTE.  IF THESE
C  ARE MODIFIED, ALSO MODIFY THE DIAM ARRAY IN THE REGCON ENTRY
C  IN **REGENT.
C----------
      DATA (SNDBAL(I), I= 1,96)/
     &   0.5, 0.3, 0.5, 0.5, 0.5, 0.3, 0.4, 0.4, 0.3, 0.2,
     &   0.2, 0.2, 0.2, 0.3, 0.3, 0.3, 0.3, 0.3, 0.3, 0.3,
     &   0.3, 0.3, 0.3, 0.1, 0.2, 0.2, 0.2, 0.1, 0.2, 0.3,
     &   0.2, 0.1, 0.1, 0.3, 0.1, 0.1, 0.1, 0.1, 0.1, 0.1,
     &   0.2, 0.1, 0.2, 0.2, 0.2, 0.2, 0.2, 0.2, 0.1, 0.2,
     &   0.2, 0.2, 0.1, 0.1, 0.2, 0.2, 0.1, 0.1, 0.2, 0.2,
     &   0.1, 0.2, 0.2, 0.1, 0.2, 0.1, 0.1, 0.2, 0.1, 0.1,
     &   0.3, 0.1, 0.1, 0.2, 0.2, 0.3, 0.1, 0.2, 0.1, 0.2,
     &   0.1, 0.2, 0.1, 0.1, 0.1, 0.2, 0.2, 0.1, 0.1, 0.1,
     &   0.2, 0.2, 0.2, 0.2, 0.2, 0.2/
C----------
C  FLAGS WHETHER TO USE WYKOFF EQUATIONS (0), OR CURTIS-ARNEY 
C  EQUATIONS (1).
C----------
      DATA IWYKCA/
     & 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0,
     & 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0, 0, 0,
     & 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0,
     & 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0,
     & 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0/
C----------
C  DUMMY ARGUMENT NOT USED WARNING SUPPRESSION SECTION
C----------
      DANUW = REAL(IFOR)
C
C-----------
C  SEE IF WE NEED TO DO SOME DEBUG.
C-----------
      CALL DBCHK (DEBUG,'HTDBH',5,ICYC)
      IF(DEBUG)WRITE(JOSTND,2) ICYC
    2 FORMAT(' ENTERING SUBROUTINE HTDBH CYCLE =',I4)
C----------
C  SET EQUATION PARAMETERS ACCORDING TO FOREST AND SPECIES.
C----------
      P2 = SNALL(1,ISPC)
      P3 = SNALL(2,ISPC)
      P4 = SNALL(3,ISPC)
      DB = SNDBAL(ISPC)
C
      IF(MODE .EQ. 0) H=0.
      IF(MODE .EQ. 1) D=0.
C----------
C  PROCESS ACCORDING TO MODE AND WHETHER THE SPECIES IS USING
C  A WYKOFF EQUATION OR CURTIS-ARNEY EQUATION.
C----------
      IF(MODE .EQ. 0) THEN
        IF(IWYKCA(ISPC) .EQ. 0) THEN
          H=EXP(HT1(ISPC) + HT2(ISPC)/(D+1.0))+4.5
        ELSE
          IF(D .GE. 3.) THEN
            H=4.5+P2*EXP(-1.*P3*D**P4)
          ELSE
            H=((4.5+P2*EXP(-1.*P3*(3.**P4))-4.51)*(D-DB)/(3.-DB))+4.51
          ENDIF
        ENDIF
      ELSE
        IF(IWYKCA(ISPC) .EQ. 0) THEN
          D=(HT2(ISPC)/(ALOG(H-4.5)-HT1(ISPC)))-1.0
        ELSE
          HAT3 = 4.5 + P2 * EXP(-1.*P3*3.0**P4)
          IF(H .GE. HAT3) THEN
            IF(DEBUG) WRITE(JOSTND,*)' H= ',H,' HAT3= ',HAT3,' P2= ',P2
            D=EXP(ALOG((ALOG(H-4.5)-ALOG(P2))/(-1.*P3)) * 1./P4)
          ELSE
            D=(((H-4.51)*(3.-DB))/(4.5+P2*EXP(-1.*P3*(3.**P4))-4.51))+DB
          ENDIF
        ENDIF
      ENDIF
C
      IF(MODE.NE.0 .AND. D.LT.DB)D=DB
C
      IF (DEBUG) THEN
        WRITE(JOSTND,*) ' IN HTDBH MODE,ISPC,IWYKCA,H,D= ',
     &                    MODE,ISPC,IWYKCA(ISPC),H,D
        WRITE(JOSTND,*) ' IN HTDBH  P2, P3, P4, DB= ', P2, P3, P4, DB
        WRITE(JOSTND,*) ' IN HTDBH  HT1,HT2 = ',HT1(ISPC),HT2(ISPC)
      ENDIF
      RETURN
      END
