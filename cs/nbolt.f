      SUBROUTINE NBOLT(ISPC,H,D,DBHMIN,BFMIND,SINDX,TOPDOB,BFTDOB,
     &           JOSTND,DEBUG,IHT1,IHT2)
      IMPLICIT NONE
C----------
C CS $Id$
C----------
C THIS ROUTINE CALCULATES THE NUMBER OF 8' BOLTS TO SPECIFIED SAWTIMBER
C TOP DIAMETER AND THE NUMBER OF 8' BOLTS TO A SPECIFIED PULPWOOD TOP
C DIAMETER.
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
COMMONS
C
      LOGICAL DEBUG
C
      REAL B1(MAXSP),B2(MAXSP),B3(MAXSP),B4(MAXSP),B5(MAXSP),
     &          B6(MAXSP),DBHMIN(MAXSP),BFMIND(MAXSP)
      INTEGER IHT1,IHT2,JOSTND,ISPC
      REAL BFTDOB,TOPDOB,SINDX,D,H,FACTOR,ESTTHT,ESTCHT,PULBOL
      REAL ESTSHT,SAWBOL
C
      IF (DEBUG) WRITE(JOSTND,10)
   10 FORMAT(' ENTERING NBOLT ')
C
C----------
C  LOADING SPECIES SPECIFIC COEFFICIENTS FOR HEIGHT EQUATIONS.
C----------
      DATA B1/2*8.2079,3*16.934,2*16.281,2*13.625,4*6.86,10*6.1034,
     &        5.3416,3*11.291,13.625,3*6.86,7.2773,8*8.458,2*6.3628,
     &        5.3416,3*8.1782,9.2078,2*6.6844,3*3.8011,4*9.2078,
     &        3*3.8011,8*9.2078,10*6.4301,7*6.9572,12*13.6200/
      DATA B2/2*.19672,3*.12972,2*.08621,2*.28668,4*.27725,10*.17368,
     &        .23044,3*.2525,.28668,3*.27725,.22721,8*.27527,2*.27859,
     &        .23044,3*.27316,.22208,2*.19049,3*.39213,4*.22208,
     &        3*.39213,8*.22208,10*.23545,7*.26564,12*.24255/
      DATA B3/2*1.3112,5*1.0,2*1.6124,4*1.4287,10*1.0,1.1529,3*1.5466,
     &         1.6124,3*1.4287,1.0,8*1.9602,2*1.8677,1.1529,3*1.7250,
     &         3*1.0,3*2.9053,4*1.0,3*2.9053,8*1.0,10*1.3380,7*1.0000,
     &         12*1.2885/
      DATA B4/2*.33978,3*.20854,2*.16220,2*.30651,4*.40115,10*.44725,
     &        .54194,3*.35711,.30651,3*.40115,.41179,8*.34894,2*.49589,
     &        .54194,3*.38694,.31723,2*.43972,3*.55634,4*.31723,
     &        3*.55634,8*.31723,10*.47370,7*.48660,12*.25831/
      DATA B5/2*.76173,3*.77792,2*.86833,2*1.0292,4*.85299,10*1.0237,
     &        .83440,3*.7506,1.0292,3*.85299,.76498,8*.89213,2*.76169,
     &        .83440,3*.75822,.8356,2*.82962,3*.84317,4*.8356,
     &        3*.84317,8*.83560,10*.73385,7*.76954,12*.68128/
      DATA B6/2*.11666,3*.12902,2*.23316,2*.0746,4*.12403,10*.14610,
     &        .06372,3*.06859,.0746,3*.12403,.11046,8*.12594,2*.05841,
     &        .06372,3*.10847,.13465,2*.10806,3*.09593,4*.13465,
     &        3*.09593,8*.13465,10*.08228,7*.01618,12*.10771/
C----------
C  COMPUTE ESTIMATED TOTAL TREE HEIGHT.
C----------
       IF(D .LT. DBHMIN(ISPC)) THEN
         IHT1=0
         IHT2=0
         GO TO 100
       ELSE
         FACTOR = 0.0
         ESTTHT = 4.5+B1(ISPC)*(1.0-EXP(-1.0*B2(ISPC)*D))
     &         **B3(ISPC)*SINDX**B4(ISPC)*(1.00001-FACTOR)
     &         **B5(ISPC)*BA**B6(ISPC)
         IF(DEBUG)WRITE(JOSTND,*)' ESTTHT=',ESTTHT,' ISPC=',ISPC,
     &      ' B1=',B1(ISPC),' B2=',B2(ISPC),' B3=',B3(ISPC),' B4=',
     &      B4(ISPC),' B5=',B5(ISPC),' B6=',B6(ISPC),' D=',D,' H=',H,
     &      ' SINDX=',SINDX,' TOP/D=',TOPDOB/D,' BA=',BA,
     &      ' TOPDOB=',TOPDOB
       ENDIF
C----------
C  COMPUTE THE NUMBER OF 8' BOLTS TO SPECIFIED PULPWOOD TOP DIAMETER.
C----------
       IF(D .LT. DBHMIN(ISPC)) THEN
         IHT1=0
         IHT2=0
         GO TO 100
       ELSE
         FACTOR = TOPDOB/D
         IF(FACTOR .GT. 1.0) FACTOR=1.0
         ESTCHT = 4.5+B1(ISPC)*(1.0-EXP(-1.0*B2(ISPC)*D))
     &         **B3(ISPC)*SINDX**B4(ISPC)*(1.00001-FACTOR)
     &         **B5(ISPC)*BA**B6(ISPC)
         IF(DEBUG)WRITE(JOSTND,*)' ESTCHT=',ESTCHT,' ISPC=',ISPC,
     &      ' B1=',B1(ISPC),' B2=',B2(ISPC),' B3=',B3(ISPC),' B4=',
     &      B4(ISPC),' B5=',B5(ISPC),' B6=',B6(ISPC),' D=',D,' H=',H,
     &      ' SINDX=',SINDX,' TOP/D=',TOPDOB/D,' BA=',BA,
     &      ' TOPDOB=',TOPDOB
         PULBOL=ESTCHT*(H/ESTTHT)
         IHT2=INT(PULBOL/8.333333)
         IF(DEBUG)WRITE(JOSTND,*)' No. PULPWOOD BOLTS (IHT2)= ',IHT2
       ENDIF
C----------
C  COMPUTE THE NUMBER OF 8' BOLTS TO A SPECIFIED SAWTIMBER TOP
C  DIAMETER.
C----------
       IF(D .LT. BFMIND(ISPC)) THEN
         IHT1=0
         GO TO 100
       ELSE
         FACTOR = BFTDOB/D
         IF(FACTOR .GT. 1.0) FACTOR=1.0
         ESTSHT = 4.5+B1(ISPC)*(1.0-EXP(-1.0*B2(ISPC)*D))
     &         **B3(ISPC)*SINDX**B4(ISPC)*(1.00001-FACTOR)
     &         **B5(ISPC)*BA**B6(ISPC)
         IF(DEBUG)WRITE(JOSTND,*)' B1=',B1(ISPC),' B2=',B2(ISPC),
     &     ' B3=',B3(ISPC),' B4=',B4(ISPC),' B5=',B5(ISPC),' B6=',
     &     B6(ISPC),' ISPC=',ISPC,' BA=',BA,' SINDX=',SINDX,
     &     ' BFT/D=',BFTDOB/D,' D=',D,' H=',H,' ESTSHT=',ESTSHT
         SAWBOL=ESTSHT*(H/ESTTHT)
         IHT1=INT(SAWBOL/8.333333)
         IF(DEBUG)WRITE(JOSTND,*)' No. SAWWOOD BOLTS (IHT1)= ',IHT1
      ENDIF
100   CONTINUE
      RETURN
      END
