      SUBROUTINE ESTUMP (JSSP,DBH,PREM,JPLOT,ISHAG)
      IMPLICIT NONE
C----------
C ESTB $Id$
C----------
C  STORE INFORMATION FOR STUMP SPROUT SUBROUTINE.
C
C  STRP AND ESTB VERSIONS ARE IDENTICAL; IF YOU UPDATE ONE,
C  UPDATE BOTH.
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'ESPARM.F77'
C
C
      INCLUDE 'ESHOOT.F77'
C
C
      INCLUDE 'ESHAP.F77'
C
C
      INCLUDE 'ESCOMN.F77'
C
C
COMMONS
C----------
      INTEGER JPLOT,JSSP,ISSP,IDBH,ISHAG,INDXAS
      INTEGER*4 MDBH,IPLT,MSP,ISHI,I,MNAGDF,IBEST
      REAL PREM,DBH,DBHEND
C----------
C  DATA STATEMENTS
C----------
      DATA MDBH/10000000/,MSP/10000/
C----------
C  DETERMINE IF THIS IS A SPROUTING SPECIES
C----------
      DO 10 I=1,NSPSPE
      IF(JSSP.EQ.ISPSPE(I)) GO TO 11
   10 CONTINUE
      GO TO 900
   11 CONTINUE
      ISSP=I
C----------
C  DETERMINE DIAMETER CLASS OF THE TREE BEING CUT.
C----------
      DO 100 I=1,NDBHCL-1
      DBHEND=(DBHMID(I)+DBHMID(I+1))/2.0
      IF(DBH.GT.DBHEND) GO TO 100
      IDBH=I
      GO TO 110
  100 CONTINUE
      IDBH=NDBHCL
  110 CONTINUE
      IPLT=JPLOT
      IF(IPLT.GT.9999) IPLT=9999
      ISHI = IDBH*MDBH + ISSP*MSP + IPLT
C----------
C  IF THERE IS ROOM IN THE LIST, THEN CREATE A NEW STUMP RECORD.
C----------
      ITRNRM=ITRNRM+1
C----------
C  ISHOOT() IS A NUMBER COMPOSED OF DDDSSSPPPP, WHERE DDD = DBH CLASS,
C  SSS = SPECIES, AND PPPP = PLOT.  DECODED IN **ESUCKR**.
C  PRBREM() CONTAINS AMOUNT OF PROB REMOVED IN CUTTING TREE RECORD.
C----------
      IF (ITRNRM.LE.MAXTRE) THEN
         DSTUMP(ITRNRM) = DBH
         ISHOOT(ITRNRM) = ISHI
         PRBREM(ITRNRM) = PREM
         JSHAGE(ITRNRM) = ISHAG
      ELSE
         ITRNRM = MAXTRE
C----------
C  THE LIST IS FULL. FIND AN EXISTING RECORD THAT MATCHES AS BEST
C  IT CAN AND ADD THE PROB TO THAT RECORD. IF NO MATCH IS FOUND,
C  THEN IGNORE THE STUMP.
C----------
         MNAGDF = 9999
         IBEST  = 0
C
         DO I=1,ITRNRM
            IF (ISHI.EQ.ISHOOT(I)) THEN
               IF (ABS(ISHAG-JSHAGE(I)).LT.MNAGDF) THEN
                  IBEST = I
                  MNAGDF = ABS(ISHAG-JSHAGE(I))
               ENDIF
            ENDIF
            IF (MNAGDF.EQ.0) EXIT
         ENDDO
C
         IF (IBEST.GT.0) PRBREM(IBEST) = PRBREM(IBEST)+PREM
      ENDIF
C      
  900 CONTINUE
C----------
C  DETERMINE IF QUAKING ASPEN WAS CUT; IF SO, ACCUMULATE BA AND TPA
C  OF ASPEN THAT WAS REMOVED FOR ESTIMATION OF NUMBER OF ASPEN SPROUTS
C  IN SUBROUTINE **ESUCKR**.
C----------
      CALL ESASID(VARACD,INDXAS)
      IF(JSSP .EQ. INDXAS)THEN
        ASTPAR = ASTPAR + PREM
        ASBAR = ASBAR + 0.0054542*PREM*DBH**2.
      ENDIF
C
      RETURN
      END
