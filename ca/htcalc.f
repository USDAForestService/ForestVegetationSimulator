      SUBROUTINE HTCALC(JFOR,SINDX,KSPEC,AG,HGUESS,JOSTND,DEBUG)
      IMPLICIT NONE
C----------
C CA $Id$
C----------
C THIS ROUTINE CALCULATES A POTENTIAL HT GIVEN A SITE INDEX, SPECIES, 
C AND AGE. IT IS USED TO CALCULATE POTENTIAL HEIGHT GROWTH AND SITE 
C EQUIVALENTS.
C----------
      LOGICAL DEBUG
      INTEGER INDX,KSPEC,JFOR,JOSTND
      REAL HGUESS,AG,SINDX
      REAL TOPTRM,BOTTRM,TERM,TERM2,B,B50
      REAL DUNL1(6),DUNL2(6),DUNL3(6)
C----------
C SPECIES ORDER IN CA VARIANT:
C  1=PC  2=IC  3=RC  4=WF  5=RF  6=SH  7=DF  8=WH  9=MH 10=WB
C 11=KP 12=LP 13=CP 14=LM 15=JP 16=SP 17=WP 18=PP 19=MP 20=GP
C 21=JU 22=BR 23=GS 24=PY 25=OS 26=LO 27=CY 28=BL 29=EO 30=WO
C 31=BO 32=VO 33=IO 34=BM 35=BU 36=RA 37=MA 38=GC 39=DG 40=FL
C 41=WN 42=TO 43=SY 44=AS 45=CW 46=WI 47=CN 48=CL 49=OH
C----------
C
      DATA DUNL1/ -88.9, -82.2, -78.3, -82.1, -56.0, -33.8 /
      DATA DUNL2/ 49.7067, 44.1147, 39.1441,
     &            35.4160, 26.7173, 18.6400 /
      DATA DUNL3/ 2.375, 2.025, 1.650, 1.225, 1.075, 0.875 /
C
      IF(DEBUG)WRITE(JOSTND,15)
   15 FORMAT(' ENTERING HTCALC')
      IF(DEBUG)WRITE(JOSTND,30)AG,SINDX,KSPEC
   30 FORMAT(' IN HTCALC 30F AG,SINDX,KSPEC=',2F10.4,I10)
C
      SELECT CASE (JFOR)
C----------
C REGION 5 LOGIC
C R5 USE DUNNING/LEVITAN SITE CURVE.
C----------
      CASE(1:5)
        IF(JFOR .LE. 5) THEN
C----------
C SET UP MAPPING TO THE CORRECT DUNNING-LEVITAN SITE CURVE
C----------
          IF(SINDX .LE. 44.) THEN
            INDX=6
          ELSEIF (SINDX.GT.44. .AND. SINDX.LE.52.) THEN
            INDX=5
          ELSEIF (SINDX.GT.52. .AND. SINDX.LE.65.) THEN
            INDX=4
          ELSEIF (SINDX.GT.65. .AND. SINDX.LE.82.) THEN
            INDX=3
          ELSEIF (SINDX.GT.82. .AND. SINDX.LE.98.) THEN
            INDX=2
          ELSE
            INDX=1
          ENDIF
          IF(AG .LE. 40.) THEN
            HGUESS = DUNL3(INDX) * AG
          ELSE
            HGUESS = DUNL1(INDX) + DUNL2(INDX)*ALOG(AG)
          ENDIF
        ENDIF
C
      CASE DEFAULT
C----------
C REGION 6 LOGIC
C
C GO TO A DIFFERENT POTENTIAL HEIGHT CURVE DEPENDING ON THE SPECIES.
C SCALE FACTORS ADDED TO SOME SPECIES SO THEY WOULD
C HIT THE SITE HEIGHT.
C----------
        SELECT CASE (KSPEC)
C----------
C HANN-SCRIVANI OSU RES BULL 59, FEB 1987
C SPECIES: PC, IC, RC, WF, DF, WH, SP, BR, GS, OS
C----------
        CASE(1:4,7,8,16,22,23,25)
          TOPTRM = 1.0-EXP(-EXP(-6.21693+0.281176*ALOG(SINDX-4.5)
     &           + 1.14354*ALOG(AG)))
          BOTTRM = 1.0-EXP(-EXP(-6.21693+0.281176*ALOG(SINDX-4.5)
     &           + 1.14354*ALOG(50.)))
          HGUESS = ((SINDX-4.5)*TOPTRM/BOTTRM) + 4.5
          HGUESS = HGUESS*1.05
          IF(DEBUG)WRITE(JOSTND,*)' SINDX,AG,TOPTRM,BOTTRM,HGUESS= ',
     &    SINDX,AG,TOPTRM,BOTTRM,HGUESS
C----------
C HANN-SCRIVANI OSU RES BULL 59, FEB 1987
C SPECIES: JP, WP, PP, MP, GP
C----------
        CASE(15,17:20)
          TOPTRM = 1.0-EXP(-EXP(-6.54707+0.288169*ALOG(SINDX-4.5)
     &           + 1.21297*ALOG(AG)))
          BOTTRM = 1.0-EXP(-EXP(-6.54707+0.288169*ALOG(SINDX-4.5)
     &           + 1.21297*ALOG(50.)))
          HGUESS = ((SINDX-4.5)*TOPTRM/BOTTRM) + 4.5
          HGUESS = HGUESS*1.05
          IF(DEBUG)WRITE(JOSTND,*)' SINDX,AG,TOPTRM,BOTTRM,HGUESS= ',
     &    SINDX,AG,TOPTRM,BOTTRM,HGUESS
C----------
C DOLPH RED FIR CURVES, RES PAP PSW 206
C SPECIES: RF, SH, MH
C----------
        CASE(5,6,9)
          TERM=AG*EXP(AG*(-0.0440853))*1.41512E-6
          B = SINDX*TERM - 3.04951E6*TERM*TERM + 5.72474E-4
          TERM2 = 50.0 * EXP(50.0*(-0.0440853)) * 1.41512E-6
          B50 = SINDX*TERM2 - 3.04951E6*TERM2*TERM2 + 5.72474E-4
          HGUESS = ((SINDX-4.5) * (1.0-EXP(-B*(AG**1.51744)))) /
     &             (1.0-EXP(-B50*(50.0**1.51744)))
          HGUESS = HGUESS + 4.5
          IF(DEBUG)WRITE(JOSTND,*)' SINDX,AG,TERM,B,TERM2,B50,HGUESS= ',
     &    SINDX,AG,TERM,B,TERM2,B50,HGUESS
C----------
C DAHMS LODGEPOLE PINE, RES PAP PNW-8
C SPECIES: WB, KP, LP, CP, LM, WJ
C----------
        CASE(10:14,21)
          HGUESS = SINDX * (-0.0968 + 0.02679*AG - 0.00009309*AG*AG)
          HGUESS = HGUESS*1.10
          IF(DEBUG)WRITE(JOSTND,*)' SINDX,AG,HGUESS= ',SINDX,AG,HGUESS
C----------
C POWERS BLACK OAK RES NOTE PSW-262
C SPECIES: PY, WO, BO, VO, BU, DG, FL
C----------
        CASE(24,30:32,35,39,40)
          TERM = SQRT(AG)-SQRT(50.)
          HGUESS = (SINDX * (1 + 0.322*TERM)) - 6.413*TERM
          HGUESS = HGUESS * 0.70
          IF(DEBUG)WRITE(JOSTND,*)' SINDX,AG,TERM,HGUESS= ',
     &    SINDX,AG,TERM,HGUESS
C----------
C PORTER & WIANT TANOAK, JOF 4/1965 286-287p
C SPECIES: TO
C----------
        CASE(42)
          HGUESS = SINDX / (0.204 + 39.787/AG)
          HGUESS = HGUESS * 0.80
          IF(DEBUG)WRITE(JOSTND,*)' SINDX,AG,HGUESS= ',SINDX,AG,HGUESS
C----------
C PORTER & WIANT MADRONE, JOF 4/1965 286-287p
C SPECIES: LO, CY, BL, EO, IO, MA, GC
C----------
        CASE(26:29,33,37,38)
          HGUESS = SINDX / (0.375 + 31.233/AG)
          HGUESS = HGUESS * 0.80
          IF(DEBUG)WRITE(JOSTND,*)' SINDX,AG,HGUESS= ',SINDX,AG,HGUESS
C----------
C PORTER & WIANT RED ALDER, JOF 4/1965 286-287p
C SPECIES: BM, RA, WN, SY, AS, CW, WI, CN, CL, OH
C----------
        CASE(34,36,41,43:49)
          HGUESS = SINDX / (0.649 + 17.556/AG)
          HGUESS = HGUESS * 0.80
          IF(DEBUG)WRITE(JOSTND,*)' SINDX,AG,HGUESS= ',SINDX,AG,HGUESS
C----------
C KING DOUGLAS-FIR, WEYERHAUSER FOR PAP 8, 1966.
C **NOT USED ANY MORE IN THIS VARIANT**
C----------
C       CASE(none)
C         Z = 2500./(SINDX - 4.5)
C         A = -0.954038  + 0.109757*Z
C         B =  0.055818  + 0.0079224*Z
C         C = -0.0007338 + 0.0001977*Z
C         HGUESS = ((AG*AG)/(A + B*AG + C*AG*AG)) + 4.5
C----------
C DOLPH WHITE FIR, RES PAP PSW-185, FEB 1987
C **NOT USED ANY MORE IN THIS VARIANT**
C----------
C       CASE(none)
C         X1 = (38.0202*AG**(-1.05213)*EXP(0.009557*AG))
C         X2 = 101.842894*(1.0 - EXP(-0.001442*AG**1.679259))
C         HGUESS = (SINDX - 69.91 + X1*X2)/X1 + 4.5
C         IF(DEBUG)WRITE(JOSTND,*)' X1,X2,SINDX,AG,HGUESS= ',
C    &    X1,X2,SINDX,AG,HGUESS
C----------
C FUTURE SPECIES
C----------
        CASE DEFAULT
          HGUESS = 0.
C
        END SELECT
C
      END SELECT
C      
      RETURN
      END
