      SUBROUTINE SMHTGF(NSPC,D,CR,BA,BAL,SI,HTGR,RELHT,DEBUG,JOSTND)
      IMPLICIT NONE
C----------
C CA $Id: smhtgf.f 0000 2018-02-14 00:00:00Z gedixon $
C----------
C THIS SUBROUTINE COMPUTES THE HEIGHT GROWTH FOR SMALL TREES (HTGR)
C IN REAL SCALE.  THIS ROUTINE IS CALLED FROM REGENT.
C
C THE FOUR SMALL TREE HEIGHT GROWTH EQUATIONS ARE THOSE FIT FOR THE
C WESSIN FVS VARIANT FOR PINES, FIRS, BLACK OAK, AND TANOAK. A MAPPING
C FUNCTION IS USED TO MAP SPECIES IN THIS VARIANT TO ONE OF THE FOUR
C EQUATIONS.  THE EQUATIONS UNDERESTIMATED HEIGHT GROWTH IN THE WESSIN
C VARIANT, SO AN ADJUSTMENT FACTOR IS USED TO MAKE THE ESTIMATES MORE
C REALISTIC AND MORE INDIVIDUALISTIC.
C
C SPECIES ORDER IN CA VARIANT:
C  1=PC  2=IC  3=RC  4=WF  5=RF  6=SH  7=DF  8=WH  9=MH 10=WB
C 11=KP 12=LP 13=CP 14=LM 15=JP 16=SP 17=WP 18=PP 19=MP 20=GP
C 21=JU 22=BR 23=GS 24=PY 25=OS 26=LO 27=CY 28=BL 29=EO 30=WO
C 31=BO 32=VO 33=IO 34=BM 35=BU 36=RA 37=MA 38=GC 39=DG 40=FL
C 41=WN 42=TO 43=SY 44=AS 45=CW 46=WI 47=CN 48=CL 49=OH
C----------
C    1 = CHLA     PORT ORFORD CEDAR         CHAMAECYPARIS LAWSONIANA
C    2 = LIDE     INCENSE CEDAR             LIBOCEDRUS DECURRENS
C    3 = THPL     WESTERN REDCEDAR          THUJA PLICATA
C
C    4 = ABCO     WHITE FIR                 ABIES CONCOLOR
C    5 = ABMA     CALIFORNIA RED FIR        ABIES MAGNIFICA (MAGNIFICA)
C    6 = ABSH     SHASTA RED FIR            ABIES MAGNIFICA (SHASTENSIS)
C    7 = PSME     DOUGLAS-FIR               PSEUDOTSUGA MENZIESII
C
C    8 = TSHE     WESTERN HEMLOCK           TSUGA HETEROPHYLLA
C    9 = TSME     MOUNTAIN HEMLOCK          TSUGA MERTENSIANA
C
C   10 = PIAL     WHITEBARK PINE            PINUS ALBICAULIS
C   11 = PIAT     KNOBCONE PINE             PINUS ATTENUATA
C   12 = PICO     LODGEPOLE PINE            PINUS CONTORTA
C   13 = PICO3    COULTER PINE              PINUS COULTERI
C   14 = PIFL2    LIMBER PINE               PINUS FLEXILIS (FLEXILIS)
C   15 = PIJE     JEFFREY PINE              PINUS JEFFREYI
C   16 = PILA     SUGAR PINE                PINUS LAMBERTIANA
C   17 = PIMO3    WESTERN WHITE PINE        PINUS MONTICOLA
C   18 = PIPO     PONDEROSA PINE            PINUS PONDEROSA
C   19 = PIRA2    MONTEREY PINE             PINUS RADIATA
C   20 = PISA2    GRAY PINE                 PINUS SABINIANA
C
C   21 = JUOC     WESTERN JUNIPER           JUNIPERUS OCCIDENTALIS
C   22 = PIBR     BREWER SPRUCE             PICEA BREWERIANA
C   23 = SEGI2    GIANT SEQUOIA             SEQUOIADENDRON GIGANTIUM
C   24 = TABR2    PACIFIC YEW               TAXUS BREVIFOLIA
C
C   25 =          OTHER SOFTWOODS
C
C   26 = QUAG     COAST LIVE OAK            QUERCUS AGRIFOLIA
C   27 = QUCH2    CANYON LIVE OAK           QUERCUS CHRYSOLEPSIS
C   28 = QUDO     BLUE OAK                  QUERCUS DOUGLASII
C   29 = QUEN     ENGELMANN OAK             QUERCUS ENGELMANNI
C   30 = QUGA4    OREGON WHITE OAK          QUERCUS GARRYANA
C   31 = QUKE     CALIFORNIA BLACK OAK      QUERCUS KELLOGGII
C   32 = QULO     VALLEY WHITE OAK          QUERCUS LOBATA
C   33 = QUWI2    INTERIOR LIVE OAK         QUERCUS WISLIZENII
C
C   34 = ACMA3    BIGLEAF MAPLE             ACER MACROPHYLLUM
C   35 = AECA     CALIFORNIA BUCKEYE        AESCULUS CALIFORNICA
C   36 = ALRU2    RED ALDER                 ALNUS RUBRA
C   37 = ARME     PACIFIC MADRONE           ARBUTUS MENZIESII
C   38 = CHCHC4   GIANT CHINQUAPIN          CHRYSOLEPIS CHRYSOPHYLLA
C   39 = CONU4    PACIFIC DOGWOOD           CORNUS NUTTALLII
C   40 = FRLA     OREGON ASH                FRAXINUS LATIFOLIA
C   41 = JU__     WALNUT                    JUGLANS sp.
C   42 = LIDE3    TANOAK                    LITHOCARPUS DENSIFLORUS
C   43 = PLRA     CALIFORNIA SYCAMORE       PLATANUS RACEMOSA
C   44 = POTR5    QUAKING ASPEN             POPULUS TREMULOIDES
C   45 = POBAT    BLACK COTTONWOOD          POPULUS TRICHOCARPA
C   46 = SA__     WILLOW                    SALIX sp.
C   47 = TOCA     CALIFORNIA NUTMEG         TORREYA CALIFORNICA
C   48 = UMCA     CALIFORNIA LAUREL         UMBELLULARIA CALIFORNICA
C
C   49 =          OTHER HARDWOODS
C----------
      LOGICAL DEBUG
      REAL SPADJF(49),RELHT,HTGR,SI,BAL,BA,CR,D,FACTOR,TEMBAL
      REAL DOMHTGR,CRMOD,RHMOD,SMHMOD
      INTEGER MAPSP(49),JOSTND,NSPC
      REAL RDANUW
C----------
C  DATA STATEMENTS
C----------
      DATA SPADJF /
     & 1.0, 1.0, 0.9, 1.1, 1.1, 1.1, 1.1, 0.8, 0.9, 0.9,
     & 1.0, 1.0, 1.0, 1.0, 1.0, 1.1, 1.1, 1.0, 1.1, 0.9,
     & 1.0, 0.9, 1.0, 0.8, 1.0, 1.1, 0.9, 1.1, 1.1, 1.0,
     & 1.1, 1.0, 1.1, 1.0, 1.0, 1.0, 1.0, 1.0, 1.0, 1.0,
     & 1.1, 1.0, 1.1, 1.2, 1.2, 1.1, 0.8, 1.0, 1.0 /
C
      DATA MAPSP /
     & 2, 2, 2, 2, 2, 2, 2, 2, 2, 1,
     & 1, 1, 1, 1, 1, 1, 1, 1, 1, 1,
     & 1, 2, 1, 2, 1, 3, 3, 3, 3, 3,
     & 3, 3, 3, 4, 3, 3, 4, 3, 4, 3,
     & 3, 4, 3, 3, 3, 3, 2, 4, 3 /
C----------
C  DUMMY ARGUMENT NOT USED WARNING SUPPRESSION SECTION
C----------
      RDANUW = BA
      RDANUW = D
C----------
C  COMPUTE ADJUSTMENT FACTOR BASED ON SPECIES AND SITE INDEX
C----------
      FACTOR = SPADJF(NSPC) * (.80+0.004*(SI-50.))
C
      TEMBAL=BAL
      IF(TEMBAL .LT. 5.0) TEMBAL=5.0
      GO TO (100,200,300,400) MAPSP(NSPC)
C
C----------
C   PINES
C----------
  100 CONTINUE
      HTGR = EXP(0.7452 - 0.003271*BAL - 0.1632*CR + 0.0217*CR*CR
     &     + 0.00536*SI) * FACTOR * 1.75
      GO TO 500
C
C----------
C  FIRS - INCLUDING DOUGLAS FIR
C  EQUATION FOR DOMINANT HEIGHT GROWTH (DOMHTGR) DERIVED FROM THE
C  FOLLOWING:  CURVES FOR DOMINANT-HEIGHT-GROWTH FOR DOUGLAS-FIR IN
C  SOUTHWEST OREGON (FIG.2, P.9; RES. BULL. 59, FEB 1987; BY HANN AND
C  SCRIVANI;  DOMINANT-HEIGHT-GROWTH AND SITE-INDEX EQUATIONS....IN
C  SOUTHWEST OREGON.)  BASE RELATIONSHIP FIT FROM THESE CURVES IS
C  POT'L. HT. GR. = CHANGE IN HT. / CHANGE IN AGE (TIME).  THE CURVES
C  IN FIG. 2 ARE STRAIGHT LINES FROM AGEBH=0 TO AGEBH=20 AND SO CAN BE
C  DESCRIBED AS SIMPLE LINEAR EQUATIONS WHOSE SLOPE DEPENDS ON SITE
C  INDEX.  THE ADDITIONAL COMPLEXITY RESULTS FROM CHANGING THE CURVES 
C  FROM AGE, BREAST HT. TO AGE, TOTAL TREE, USED HERE IN FVS.  WHEN
C  HEIGHT GROWTH IS SOLVED FOR AS THE DEPENDENT VARIABLE, THE RESULTING
C  EQUATION IS THAT FOR DOMHTGR BELOW.
C  THE SMALL HEIGHT GROWTH MODIFIER (SMHMOD) IS BASED ON THE RITCHIE &
C  HANN WORK (FOR.ECOL.&MGMT. 1986. 15:135-145).  THE ORIGINAL COEFF.
C  (1.117148) IS CHANGED TO 1.016605 TO MAKE THE SMALL TREE HEIGHTS
C  CLOSE TO THE SITE INDEX CURVE.  THE MODIFIER HAS TWO PARTS, ONE
C  (CRMOD) FOR TREE VIGOR USING CROWN RATIO AS A SURROGATE; OTHER
C  (RHMOD) FOR COMPETITION FROM NEIGHBORING TREES USING RELATIVE TREE
C  HEIGHT AS A SURROGATE.
C----------
  200 CONTINUE
      DOMHTGR= 5*(2.2227 + 0.4314*SI)/(29.0 - 0.05*SI)
      CR= CR/10.
      CRMOD=(1.0-EXP(-4.26558*CR))
      RHMOD=(EXP(2.54119*(RELHT**0.250537-1.0)))
      SMHMOD= 1.016605*CRMOD*RHMOD
      HTGR= DOMHTGR*SMHMOD
      IF (DEBUG) WRITE (JOSTND,9900) DOMHTGR, SMHMOD, HTGR, SI,CR,RELHT
 9900 FORMAT(' DEBUG-SMHTGF--9900--',3F10.4, 2F6.1,F7.4)
      GO TO 500
C
C----------
C  BLACKOAK
C----------
  300 CONTINUE
      HTGR = EXP(3.817 - 0.7829*ALOG(TEMBAL)) * FACTOR
      GO TO 500

CC----------
C  TANOAK
C----------
  400 CONTINUE
      HTGR = EXP(3.385 - 0.5898*ALOG(TEMBAL)) * FACTOR
C
C----------
C  SET LOWER BOUND ON HEIGHT INCREMENT
C----------
  500 CONTINUE
      IF(HTGR .LT. 0.1) HTGR = 0.1
      RETURN
      END
