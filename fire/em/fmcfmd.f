      SUBROUTINE FMCFMD (IYR, FMD)
      IMPLICIT NONE
C----------
C   **FMCFMD FIRE-EM-DATE OF LAST REVISION: 03/13/09
C----------
C     SINGLE-STAND VERSION
C     CALLED FROM: FMBURN
C  PURPOSE:
C     THIS SUBROUTINE RETURNS TWO TYPES OF INFORMATION: THE FUEL MODEL
C     THAT WOULD BE USED IF THE STATIC FUEL MODEL OPTION IS SELECTED
C     (STORED AS IFMD(1), WITH A WEIGTH OF FWT(1)=1.0 AND THE CLOSEST
C     THE CLOSEST FUEL MODELS (UP TO 4) AND THEIR WEIGHTINGS FOR USE
C     BY THE DYNAMIC FUEL MODEL
C----------
C
C  CALL LIST DEFINITIONS:
C     FMD:     FUEL MODEL NUMBER
C
C  COMMON BLOCK VARIABLES AND PARAMETERS:
C     SMALL:   SMALL FUELS FROM DYNAMIC FUEL MODEL
C     LARGE:   LARGE FUELS FROM DYNAMIC FUEL MODEL
C
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'FMPARM.F77'
C
C
      INCLUDE 'FMFCOM.F77'
C
C
      INCLUDE 'FMCOM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
COMMONS
C----------
C  VARIABLE DECLARATIONS
C----------
      INTEGER ICLSS
      PARAMETER(ICLSS = 14)
C
      INTEGER  FMD
      INTEGER  M1, M2
      INTEGER  IPTR(ICLSS), ITYP(ICLSS)
      REAL     WT1(4), X(2), Y(2), ALGSLP, AFWT
      REAL     XPTS(ICLSS,2)
      REAL     EQWT(ICLSS)
      LOGICAL  DEBUG
      INTEGER  IYR,I,J
C----------
C     FIXED VALUES FOR INTERPOLATION FUNCTION
C----------
      DATA     Y / 0.0, 1.0 /
C----------
C     THESE ARE THE INTEGER TAGS ASSOCIATED WITH EACH FIRE MODEL
C     CLASS. THEY ARE RETURNED WITH THE WEIGHT
C----------
      DATA IPTR / 1,2,3,4,5,6,7,8,9,10,11,12,13,14 /
C----------
C     THESE ARE 0 FOR REGULAR LINES, -1 FOR HORIZONTAL AND 1 FOR
C     VERTICAL LINES. IF ANY OF THE LINES DEFINED BY XPTS() ARE OF
C     AN UNUSUAL VARIETY, THIS MUST BE ENTERED HERE SO THAT
C     SPECIAL LOGIC CAN BE INVOKED.  IN THIS CASE, ALL THE LINE
C     SEGMENTS HAVE A |SLOPE| THAT IS > 0 AND LESS THAN INIF.
C----------
      DATA ITYP / ICLSS * 0 /
C----------
C     XPTS: FIRST COLUMN ARE THE SMALL FUEL VALUES FOR EACH FIRE MODEL
C     WHEN LARGE FUEL= 0 (I.E. THE X-INTERCEPT OF THE LINE). SECOND
C     COLUMN CONTAINS THE LARGE FUEL VALUE FOR EACH FIRE MODEL WHEN
C     SMALL FUEL=0 (I.E. THE Y-INTERCEPT OF THE LINE).
C----------
      DATA ((XPTS(I,J), J=1,2), I=1,ICLSS) /
     >   5., 15.,   ! FMD   1
     >   5., 15.,   ! FMD   2
     >   5., 15.,   ! FMD   3
     >   5., 15.,   ! FMD   4
     >   5., 15.,   ! FMD   5
     >   5., 15.,   ! FMD   6
     >   5., 15.,   ! FMD   7
     >   5., 15.,   ! FMD   8
     >   5., 15.,   ! FMD   9
     >  15., 30.,   ! FMD  10 ! shares with 11
     >  15., 30.,   ! FMD  11
     >  30., 60.,   ! FMD  12 ! shares with 14
     >  45.,100.,   ! FMD  13
     >  30., 60./   ! FMD  14
C----------
C     INITIALLY SET ALL MODELS OFF; NO TWO CANDIDATE MODELS ARE COLINEAR (EXCEPT FOR
C     FM1 AND FM2) AND COLINEARITY WEIGHTS ARE ZERO. IF TWO CANDIDATE MODELS ARE
C     COLINEAR, THE WEIGHTS MUST BE SET, AND MUST SUM TO 1, WRT EACH OTHER
C----------
      DO I = 1,ICLSS
        EQWT(I) = 0.0
      ENDDO
C----------
C     BEGIN ROUTINE
C----------
      CALL DBCHK (DEBUG,'FMCFMD',6,ICYC)

      IF (DEBUG) WRITE(JOSTND,1) ICYC,IYR,LUSRFM
    1 FORMAT(' FMCFMD CYCLE= ',I2,' IYR=',I5,' LUSRFM=',L5)
C----------
C     IF USER-SPECIFIED FM DEFINITIONS, THEN WE ARE DONE.
C----------
      IF (LUSRFM) RETURN
C
      IF (DEBUG) WRITE(JOSTND,7) ICYC,IYR,HARVYR,LDYNFM,PERCOV,FMKOD,
     >           SMALL,LARGE
    7 FORMAT(' FMCFMD CYCLE= ',I2,' IYR=',I5,' HARVYR=',I5,
     >       ' LDYNFM=',L2,' PERCOV=',F7.2,' FMKOD=',I4,
     >       ' SMALL=',F7.2,' LARGE=',F7.2)
C----------
C     FIRST, WE NEED TO SEE WHICH FUEL MODELS ARE ACTIVE AND TO IDENTIFY THE
C     ONE THAT WOULD BE USED IN THE STATIC SITUATION (FOR OUTPUT PURPOSES).
C     THE AMOUNT OF SMALL AND LARGE FUEL PRESENT CAN MODIFY THE CHOICE OF
C     MODEL AS CAN THE HABITAT TYPE OR WHEN THE LAST HARVEST ACTIVITY WAS
C     DONE. FROM FAX FROM ELIZABETH REINHARDT
C----------
      DO I = 1,4
        WT1(I) = 0.0
      ENDDO
C
      CALL EMMD(M1,M2)  ! GET FIRE MODELS **FMCBA**
C
      X(1) = 30.0
      X(2) = 50.0
      J = 2
C
      WT1(J)   = ALGSLP(PERCOV,X,Y,2)
      WT1(J-1) = 1.0 - WT1(J)
C
      EQWT(M1) = EQWT(M1) + WT1(1)
      EQWT(M2) = EQWT(M2) + WT1(2)
C----------
C     END OF DETAILED LOW FUEL MODEL SELECTION
C
C     DURING THE 5 YEARS AFTER AN ENTRY, AND ASSUMING THAT SMALL+LARGE
C     ACTIVIVITY FUELS HAVE JUMPED BY 10%, THEN MODEL 11 AND 14 ARE
C     CANDIDATE MODELS, SHARING WITH 10 AND 12 RESPECTIVELY. THE
C     WEIGHT OF THE SHARED RELATIONSHIP DECLINES FROM PURE 11 INITIALLY,
C     TO PURE 10 AFTER THE PERIOD EXPIRES. SIMILARLY, COMPUTE WEIGHT FOR
C     MODEL 14 ACTIVITY FUELS, SHARED WITH CURRENT MODEL 12. THE
C     RELATIONSHIP CHANGES IN THE SAME WAS AS THE 10/11 FUELS.
C----------
      AFWT = MAX(0.0, 1.0 - (IYR - HARVYR) / 5.0)
      IF (SLCHNG .GE. SLCRIT .OR. LATFUEL) THEN
        LATFUEL  = .TRUE.
        EQWT(11) = AFWT
        EQWT(14) = AFWT
        IF (AFWT .LE. 0.0) LATFUEL = .FALSE.
      ENDIF
      IF (.NOT. LATFUEL) AFWT = 0.0
C----------
C     MODELS 10,12,13 ARE ALWAYS CANDIDATE MODELS FOR NATURAL FUELS
C     OTHER MODELS ARE ALSO CANDIDATES, DEPENDING ON COVER TYPE, ETC
C----------
      EQWT(10) = 1.0 - AFWT
      EQWT(12) = 1.0 - AFWT
      EQWT(13) = 1.0
C----------
C     CALL FMDYN TO RESOLVE WEIGHTS, SORT THE WEIGHTED FUEL MODELS
C     FROM THE HIGHEST TO LOWEST, SET FMD (USING THE HIGHEST WEIGHT)
C----------
      CALL FMDYN(SMALL,LARGE,ITYP,XPTS,EQWT,IPTR,ICLSS,LDYNFM,FMD)
C
      IF (DEBUG) WRITE (JOSTND,8) FMD
    8 FORMAT (' FMCFMD, FMD=',I4)
C
      RETURN
      END
