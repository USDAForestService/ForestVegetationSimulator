      SUBROUTINE DFBOUT
      IMPLICIT NONE
C----------
C DFB $Id$
C----------
C
C  WRITES DFB OUTBREAK SUMMARY AND COPIES THE DFBB OUTPUT TABLES TO
C  THE MAIN PROGNOSIS OUTPUT FILE.
C
C  CALLED BY :
C     MAIN   [PROGNOSIS]
C
C  CALLS :
C     DFBHED  (SUBROUTINE)  [DFB]
C
C  LOCAL VARIABLES :
C     DSTRNG - CHARACTER STRING USED TO READ THE DFB OUTPUT FILE AND
C              WRITE IT TO THE PROGNOSIS OUTPUT FILE.
C     I      - COUNTER INDEX.
C     NO     - THE WORD 'NO'.
C     OUTBRK - SET TO 'YES' OR 'NO' DEPENDING ON IF THERE WAS AN
C              OUTBREAK OR NOT.
C     YES    - THE WORD 'YES'.
C
C  COMMON BLOCK VARIABLES USED :
C     IDFBYR - (DFBCOM)  INPUT
C     IY     - (CONTRL)  INPUT
C     JODFB  - (DFBCOM)  INPUT
C     JOSTND - (CONTRL)  INPUT
C     LDFBON - (DFBCOM)  INPUT
C     NCYC   - (CONTRL)  INPUT
C     PRBDFB - (DFBCOM)  INPUT
C     WORKIN - (DFBCOM)  INPUT
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'DFBCOM.F77'
C
C
COMMONS
C

      INTEGER I

      CHARACTER*133 DSTRNG
      CHARACTER*3   OUTBRK
      CHARACTER*3   YES
      CHARACTER*2   NO

      DATA YES /'YES'/, NO /'NO'/

      IF (.NOT. LDFBON) RETURN

C
C     WRITE OUT TABLE HEADER.
C
      CALL DFBHED
      WRITE (JODFB,100)
  100 FORMAT (22('-'),' DOUGLAS-FIR BEETLE ACTIVITY SUMMARY ',
     &        23('-'),//,'-------- CYCLE --------',8X,'YEAR OF',11X,
     &        'CONDITIONAL',10X,'WAS THERE AN',/,'NUMBER',12X,'YEARS',
     &        5X,'REGIONAL DFB',8X,'PROBABILITY',10X,'OUTBREAK IN',/,
     &        31X,'OUTBREAK',7X,'OF STAND OUTBREAK',7X,'THIS STAND',/,
     &        82('-'),/)

C
C     PRINT OUT SUMMARY OUTBREAK TABLE.
C
      DO 200 I=1,NCYC
         OUTBRK = NO
         IF (WORKIN(I)) OUTBRK = YES
         IF (IDFBYR(I) .EQ. 0) WRITE (JODFB,300) I, IY(I), IY(I+1),
     &                        PRBDFB(I), OUTBRK
         IF (IDFBYR(I) .NE. 0) WRITE (JODFB,400) I, IY(I), IY(I+1),
     &                        IDFBYR(I), PRBDFB(I), OUTBRK
  200 CONTINUE

  300 FORMAT(I3,9X,I4,' - ',I4,T48,F10.3,T75,A3)
  400 FORMAT(I3,9X,I4,' - ',I4,10X,I4,T48,F10.3,T75,A3)

C
C     THE REST OF THE SUBROUTINE COPIES THE DOUGLAS-FIR BEETLE
C     OUTPUT TO THE MAIN PROGNOSIS PRINT FILE (JOSTND).
C
      IF (JODFB .EQ. JOSTND) RETURN
      REWIND JODFB

  500 CONTINUE
      READ (JODFB,600,END=1000) DSTRNG
      WRITE (JOSTND,600) DSTRNG
      GOTO 500

  600 FORMAT(A133)

 1000 CONTINUE
      REWIND JODFB

      RETURN
      END
