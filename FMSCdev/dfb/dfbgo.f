      SUBROUTINE DFBGO(L)
      IMPLICIT NONE
C----------
C DFB $Id$
C----------
C
C  THIS ROUTINE DECIDES IF A STAND OUTBREAK WILL TAKE PLACE DURING THIS
C  CYCLE.
C
C  CALLED BY :
C     GRINCR  [PROGNOSIS]
C
C  CALLS :
C     DFBER   (SUBROUTINE)   [DFB]
C     OPFIND  (SUBROUTINE)   [PROGNOSIS]
C     OPDONE  (SUBROUTINE)   [PROGNOSIS]
C     DFBPRB  (SUBROUTINE)   [DFB]
C     DFBRAN  (SUBROUTINE)   [DFB]
C
C  PARAMETERS :
C     L      - SET TO .TRUE. WHEN THE DOUGLAS-FIR BEETLE MODEL IS
C              TO BE CALLED.
C
C  LOCAL VARIABLES :
C     LMIN   - SET TO .TRUE. IN SUBROUTINE DFBER IF MINIMUM CONDITIONS
C              FOR OUTBREAK ARE MET.
C     IACTK  - HOLDS THE OPTION PROCESSOR CODE FOR A REGIONAL DFB
C              OUTBREAK (SET TO 2208 OR 2209).
C     NTODO  - HOLDS NUMBER OF ACTIVITIES TO DO THIS CYCLE.  RETURNED
C              FROM THE SUBROUTINE OPFIND.
C     PROTBK - HOLDS THE PROBABILITY OF AN OUTBREAK.
C     RAND   - RANDOM NUMBER FROM RANDOM NUMBER GENERATOR.
C
C  COMMON BLOCK VARIABLES USED :
C     DEBUIN - (DFBCOM)  INPUT
C     EPIPRB - (DFBCOM)  INPUT
C     ICYC   - (CONTRL)  INPUT
C     IDFBYR - (DFBCOM)  OUTPUT
C     ISMETH - (DFBCOM)  INPUT
C     IY     - (CONTRL)  INPUT
C     JODFB  - (DFBCOM)  INPUT
C     LDFBON - (DFBCOM)  INPUT
C     LEPI   - (DFBCOM)  INPUT
C     PRBDFB - (DFBCOM)  OUTPUT
C     WORKIN - (DFBCOM)  OUTPUT
C
C REVISION HISTORY:
C   02-AUG-01 Lance R. David (FHTET)
C     (no previous revision date was listed)
C     Changed scalar variable IACTK to array with dimension of (1) due to
C     error detected by LF95.
C-------------------------------------------------------------------------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'DFBCOM.F77'
C
C
COMMONS
C
      LOGICAL L, LMIN

      INTEGER IACTK(1), NTODO

      REAL    PROTBK, RAND

      IF (DEBUIN) WRITE (JODFB,*) ' ** ENTERING SUBROUTINE DFBGO'

C.... INITIALIZE L AND WORKIN(ICYC)

      L = .FALSE.
      WORKIN(ICYC) = .FALSE.

C.... IF THE DFB MODEL IS NOT BEING USED THEN GO TO THE END OF THIS
C.... SUBROUTINE AND RETURN.

      IF (.NOT. LDFBON) GOTO 1000

C.... INITIALIZE THE PROBABILITY OF THE STAND BEING INCLUDED IN THE
C.... REGIONAL OUTBREAK TO 0.0.

      PRBDFB(ICYC) = 0.0

C.... CALL DFBER TO CALCULATE STAND STATISTICS AND TO CHECK FOR MINIMUM
C.... CONDITIONS FOR OUTBREAK.

      CALL DFBER (LMIN)

C.... SET THE PROBABILITY OF THE STAND BEING INCLUDED IN THE REGIONAL
C.... OUTBREAK TO EPIPRB IF THE USER USED THE STOPROB KEYWORD.

      IF (LEPI) THEN
         PRBDFB(ICYC) = EPIPRB

      ELSE

C....    IF STOPROB KEYWORD NOT USED THEN CALCULATE PROBABILITY OF A
C....    STAND OUTBREAK.

         CALL DFBPRB (PROTBK)

C....    SAVE THE PROBABILITY FOR FUTURE PRINTING.

         PRBDFB(ICYC) = PROTBK
      ENDIF

C.... SET PROTBK TO THE PROBABILITY OF THIS STAND BEING INCLUDED IN
C.... THE REGIONAL OUTBREAK.

      PROTBK = PRBDFB(ICYC)

C.... CALL OPFIND TO SEE IF A REGIONAL OUTBREAK IS OCCURING.

      IACTK(1) = 2208
      CALL OPFIND (1,IACTK(1),NTODO)
      IF (NTODO .EQ. 0) THEN
         IACTK(1) = 2209
         CALL OPFIND (1,IACTK(1),NTODO)
      ENDIF

C.... IF REGIONAL OUTBREAK OCCURED THEN TEST TO SEE IF THIS STAND
C.... IS INCLUDED IN THE OUTBREAK.

      IF (NTODO .GT. 0) THEN

C....    SET IDFBYR TO SHOW THAT A REGIONAL OUTBREAK OCCURED.

         IDFBYR(ICYC) = IY(ICYC)

C....    CHECK LMIN TO SEE IF MINIMUM CONDITIONS MET FOR OUTBREAK.

         IF (LMIN) THEN

C....       MINIMUM CONDITIONS FOR OUTBREAK WERE MET.  TEST TO SEE IF
C....       STAND IS INCLUDED IN REGIONAL OUTBREAK.

            IF (ISMETH .EQ. 1) THEN

C....          MANSTART; A STAND OUTBREAK WILL HAPPEN.

               L = .TRUE.
               WORKIN(ICYC) = .TRUE.
               CALL OPDONE (NTODO,IY(ICYC))

            ELSEIF (ISMETH .EQ. 2) THEN

C....          RANSTART USED AND STAND OUTBREAK WILL HAPPEN
C....          STOCASTICALLY.

               CALL DFBRAN (RAND)

               IF (RAND .LT. PROTBK) THEN

C....             A STAND OUTBREAK WILL OCCUR. SET L TO .TRUE. AND
C....             SIGNAL THAT OUTBREAK HAS OCCURED.

                  L = .TRUE.
                  WORKIN(ICYC) = .TRUE.
                  CALL OPDONE (NTODO,IY(ICYC))
               ENDIF
            ENDIF

         ENDIF
      ENDIF

 1000 CONTINUE
      IF (DEBUIN) WRITE (JODFB,*) ' ** LEAVING SUBROUTINE DFBGO'

      RETURN
      END
