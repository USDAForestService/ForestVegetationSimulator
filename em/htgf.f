      SUBROUTINE HTGF
      IMPLICIT NONE
C----------
C EM $Id: htgf.f 0000 2018-02-14 00:00:00Z gedixon $
C-----------
C  THIS SUBROUTINE COMPUTES THE PREDICTED PERIODIC HEIGHT INCREMENT FOR
C  EACH CYCLE AND LOADS IT INTO THE ARRAY HTG. HEIGHT INCREMENT IS 
C  PREDICTED FROM SPECIES, HABITAT TYPE, HEIGHT, DBH, AND PREDICTED DBH
C  INCREMENT.  THIS ROUTINE IS CALLED FROM **TREGRO** DURING REGULAR
C  CYCLING.  ENTRY **HTCONS** IS CALLED FROM **RCON** TO LOAD SITE
C  DEPENDENT CONSTANTS THAT NEED ONLY BE RESOLVED ONCE.
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CALCOM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'MULTCM.F77'
C
C
      INCLUDE 'HTCAL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PDEN.F77'
C
COMMONS
C----------
C   MODEL COEFFICIENTS AND CONSTANTS:
C
C   HTCON -- AN ARRAY CONTAINING HABITAT TYPE CONSTANTS FOR
C            HEIGHT GROWTH MODEL (SUBSCRIPTED BY SPECIES)
C
C  HDGCOF -- COEFFICIENT FOR DIAMETER GROWTH TERMS.
C
C    HGLD -- AN ARRAY, SUBSCRIPTED BY SPECIES, OF THE
C             COEFFICIENTS FOR THE DIAMETER TERM IN THE HEIGHT
C             GROWTH MODEL.
C
C   H2COF -- COEFFICIENT FOR HEIGHT SQUARED TERMS.
C    IND2 -- ARRAY OF POINTERS TO SMALL TREES.
C
C   SCALE -- TIME FACTOR DERIVED BY DIVIDING FIXED POINT CYCLE
C            LENGTH BY GROWTH PERIOD LENGTH FOR DATA FROM
C            WHICH MODELS WERE DEVELOPED.
C----------
      LOGICAL DEBUG
      INTEGER ISPC,I1,I2,I3,I,ITFN,MAPHAB(30),IICR,K,IXAGE,IHT
      REAL SCALE,C1MOD,C2MOD,C3MOD,C4MOD,XHT,SI100,SI50,H,BAL,D,CON
      REAL CR,PHTG,CRMOD,RLHTMD,HTMOD,TEMHTG,COF1,COF2,COF3,COF4,COF5
      REAL COF6,COF7,COF8,COF9,TEMD,Y1,Y2,FBY1,FBY2,Z,ZADJ,CLOSUR
      REAL HGHC(8),HGLDD(8),HGH2(8),DIA,BRATIO,PSI,HTNEW
      REAL COFLM(9,3),COFAS(9,3)
      REAL MISHGF
C----------
C  SPECIES ORDER:
C   1=WB,  2=WL,  3=DF,  4=LM,  5=LL,  6=RM,  7=LP,  8=ES,
C   9=AF, 10=PP, 11=GA, 12=AS, 13=CW, 14=BA, 15=PW, 16=NC,
C  17=PB, 18=OS, 19=OH
C
C  SPECIES EXPANSION
C  LM USES IE LM (ORIGINALLY FROM TT VARIANT)
C  LL USES IE AF (ORIGINALLY FROM NI VARIANT)
C  RM USES IE JU (ORIGINALLY FROM UT VARIANT)
C  AS,PB USE IE AS (ORIGINALLY FROM UT VARIANT)
C  GA,CW,BA,PW,NC,OH USE IE CO (ORIGINALLY FROM CR VARIANT)
C----------
C
      DATA (COFLM(I,1),I=1,9)/
     +37.0,85.0,1.77836,-0.51147,1.88795,1.20654,0.57697,
     +3.57635,0.90283/
      DATA (COFLM(I,2),I=1,9)/
     +45.0,100.0,1.66674,0.25626,1.45477,1.11251,0.67375,
     +2.17942,0.88103/
      DATA (COFLM(I,3),I=1,9)/
     +45.0,90.0,1.64770,0.30546,1.35015,0.94823,0.70453,
     +2.46480,1.00316/
C
       DATA (COFAS(I,1),I=1,9)/
     +30.0,85.0,2.00995,0.03288,1.81059,1.28612,0.72051,
     +3.00551,1.01433/
      DATA (COFAS(I,2),I=1,9)/
     +30.0,85.0,2.00995,0.03288,1.81059,1.28612,0.72051,
     +3.00551,1.01433/
      DATA (COFAS(I,3),I=1,9)/
     +35.0,85.0,1.80388,-0.07682,1.70032,1.29148,0.72343,
     +2.91519,0.95244/
C
C-----------
C  CHECK FOR DEBUG.
C-----------
      CALL DBCHK (DEBUG,'HTGF',4,ICYC)
C
      IF(DEBUG) WRITE(JOSTND,3)ICYC
    3 FORMAT(' ENTERING SUBROUTINE HTGF  CYCLE =',I5)
C
      IF(DEBUG)WRITE(JOSTND,*) 'IN HTGF AT BEGINNING,HTCON=',
     *HTCON,'RMAI=',RMAI,'ELEV=',ELEV
      SCALE=FINT/YR
      ISMALL=0
C----------
C  GET THE HEIGHT GROWTH MULTIPLIERS.
C----------
      CALL MULTS (2,IY(ICYC),XHMULT)
C----------
C   BEGIN SPECIES LOOP:
C----------
C MODIFIERS ADOPTED FROM RITCHIE AND HANN 1986
C
      C1MOD = 1.117148
      C2MOD = -4.26558
      C3MOD = 2.54119
      C4MOD = 0.250537
C----------
      DO 40 ISPC=1,MAXSP
      I1 = ISCT(ISPC,1)
      IF (I1 .EQ. 0) GO TO 40
      I2 = ISCT(ISPC,2)
      XHT=1.0
      XHT=XHMULT(ISPC)
C----------
C  AT THIS POINT PRODUCE A SITE INDEX FOR THE SPECIES
C  SITE INDEX WAS SET IN SITSET.F TO APPROPRIATE BASE YEAR
C   SPECIES   BASE YR   REF
C     WB       100      ALEXANDER TACKLE AND DAHMS REDUCED TO .75
C     DF        50      MONSERUD
C     LP       100      ALEXANDER TACKLE AND DAHMS
C     AF       100      ALEXANDER
C     ES       100      ALEXANDER
C     PP       100      MEYER  BULL 630
C      ALL OTHERS PRODUCE A HTG OF 0.0
C----------
      SI100 = 0.0
      SI50  = 0.0
      IF(ISPC .EQ. 3) THEN
      	SI50 = SITEAR(ISPC)
      ENDIF
      IF(ISPC .EQ. 1 .OR. ISPC .EQ. 2 .OR. 
     &  (ISPC.GE.7 .AND. ISPC.LE.10) .OR. ISPC .EQ. 18) THEN
        SI100 = SITEAR(ISPC)
      ENDIF
C-----------
C   BEGIN TREE LOOP WITHIN SPECIES LOOP
C-----------
      DO 30 I3 = I1,I2
      I=IND1(I3)
      H=HT(I)
      HTG(I)=0.
      D=DBH(I)
C 
C  NOTE: ORIGINAL EM WOULD LOAD 0.1 FOR HTG IF PROB WAS ZERO.
C
      IF (PROB(I).LE.0.0)THEN 
        IF(LTRIP)THEN
          ITFN=ITRN+2*I-1
          HTG(ITFN)=0.
          HTG(ITFN+1)=0.
        ENDIF
        GO TO 30
      ENDIF
C----------
C  CALCULATE HEIGHT GROWTH -- DIFFERENT LOGIC DEPENDING ON WHICH VARIANT
C  THE SPECIES CAME FROM.
C
C  FIRST IS THE ORIGINAL EM SECTION
C----------
      IF(ISPC.LE.3 .OR. (ISPC.GE.7 .AND. ISPC.LE.10) .OR. ISPC.EQ.18)
     &THEN 
        IF(H .LE. 4.5)GO TO 60
        BAL=((100.0-PCT(I))/100.0)*BA
        IF(BAL .LE. 0.0)BAL=.001
        CR=ICR(I)/100.0
        CALL POTHTG(I,ISPC,H,SI50,SI100,PHTG,DEBUG)
        IF(DEBUG)WRITE(JOSTND,49)ISPC,H,SI50,SI100,PHTG,ABIRTH(I)
   49   FORMAT(' IN HTGF CALLING POTHTG ISPC,H,SI50,SI100,PHTG,ABIRTH=',
     &  I5,5F10.0)
        IF (DEBUG) WRITE(JOSTND,777) I,ICR(I),DG(I),HT(I),PHTG
  777   FORMAT(' I,ICR,DG,HT,PHTG',2I6,3E15.5)
C
C       RALPHS CORRECTION TO POTENTIAL HEIGHT
C
        PHTG = PHTG * 0.706 * (1.0-EXP(-10.19*CR)) *
     &   (1.0-EXP(-0.1*18.158*DG(I)))**0.944
     &   + 0.0265 * H
C
C       NOW REDUCE THE POTENTIAL FOR SOME MODIFIERS
C
        IF(DEBUG)WRITE(JOSTND,50)PHTG,C1MOD,C2MOD,CR,C3MOD,H,AVH,
     &  C4MOD
   50   FORMAT(' IN HTGF 50F PHTG,C1MOD,C2MOD,CR,C3MOD,H,AVH,C4MOD =',
     &  8F10.5)
        CRMOD=1.0
        RLHTMD = EXP(C3MOD*((H/AVH)**C4MOD-1.0))
        IF (DEBUG)WRITE(JOSTND,901)SI50,SI100,H,AVH,CRMOD,RLHTMD
     &  ,PHTG,ISPC
  901   FORMAT(' HTG SI50,100,H,AVH,CRMD,RLMD,PHTG,ISPC',7F10.2,I5)
        HTMOD = CRMOD * RLHTMD
        IF(HTMOD .GT. 1.0) HTMOD = 1.0
        HTG(I) = PHTG*HTMOD
        IF(HTG(I).LT.0.1)HTG(I)=0.1
C----------
C ORIGINAL NI SECTION
C----------
      ELSEIF(ISPC.EQ.5)THEN
        CON = HTCON(ISPC) + H2COF*H*H - 0.1997*ALOG(D) +
     &        0.23315*ALOG(H)
        HTG(I)=EXP(CON+HDGCOF*ALOG(DG(I))) + 0.4809
        IF(HTG(I).LT.0.1)HTG(I)=0.1
C----------
C  SKIP THIS SECTION FOR JUNIPER BECAUSE JUNIPER GETS ALL HEIGHT GROWTH
C  CALCULATED IN REGENT.
C----------
      ELSEIF(ISPC.EQ.6)THEN
        GO TO 30
C----------
C  EQUATIONS FROM UT AND TT VARIANTS, USED FOR EM SPECIES 4, 11-17, & 19
C----------
      ELSE
        IICR= INT(ICR(I)/10.0 + 0.5)
        IF(IICR .GT. 9) IICR=9
        GO TO(101,101,102,102,102,102,102,103,103),IICR
  101   K=1
        GO TO 110
  102   K=2
        GO TO 110
  103   K=3
  110   CONTINUE
        IF(ISPC .EQ. 4)THEN
          COF1=COFLM(1,K)
          COF2=COFLM(2,K)
          COF3=COFLM(3,K)
          COF4=COFLM(4,K)
          COF5=COFLM(5,K)
          COF6=COFLM(6,K)
          COF7=COFLM(7,K)
          COF8=COFLM(8,K)
          COF9=COFLM(9,K)       
        ELSE
          COF1=COFAS(1,K)
          COF2=COFAS(2,K)
          COF3=COFAS(3,K)
          COF4=COFAS(4,K)
          COF5=COFAS(5,K)
          COF6=COFAS(6,K)
          COF7=COFAS(7,K)
          COF8=COFAS(8,K)
          COF9=COFAS(9,K)         
        ENDIF
C-----------
C  CHECK IF HEIGHT OR DBH EXCEED PARAMETERS
C-----------
        IF (H .LE. 4.5) GO TO 180
        IF((0.1 + COF1) .LE. D) GO TO 180
        IF((4.5 + COF2) .LE. H) GO TO 180
C----------
C  TRAP TO AVOID LOG(0) ERRORS WITH XI1 AND XI2; HTG WILL COME FROM
C  REGENT FOR THESE SMALL OF TREES ANYWAY.  GED 05/07/09
C
      IF(DBH(I) .LE. 0.1 .OR. HT(I).LE. 4.5)GO TO 180
C----------
        GO TO 490
  180   CONTINUE
C------------
C    THE SBB IS UNDEFINED IF CERTAIN INPUT VALUES EXCEED PARAMETERS IN
C    THE FITTED DISTRIBUTION.  IN INPUT VALUES ARE EXCESSIVE THE HEIGHT
C    GROWTH IS TAKEN TO BE ZERO.
C------------
        HTG(I) = 0.1
        GO TO 60
  490   CONTINUE
C------------
C CALCULATE ALPHA FOR THE TREE USING SCHREUDER + HAFLEY
C------------
        TEMD=D
        IF(TEMD .LE. 0.2)TEMD=0.2
        Y1=(TEMD - 0.1)/COF1
        Y2=(H - 4.5)/COF2
        FBY1=ALOG(Y1/(1.0 - Y1))
        FBY2= ALOG(Y2/(1.0 - Y2))
        Z=( COF4 + COF6*FBY2 - COF7*( COF3 +
     +   COF5*FBY1))*(1.0 - COF7**2)**(-0.5)
C------------
C THE HT DIA MODEL NEEDS MODIFICATION TO CORRECT KNOWN BIAS
C------------
        IF(ISPC .NE. 4)THEN
          ZADJ = .1 - .10273*Z + .00273*Z*Z
          IF(ZADJ .LT. 0.0)ZADJ=0.0
          Z=Z+ZADJ
        ENDIF
C-----------
C YOUNG SMALL LODGEPOLE HTG ACCELLERATOR BASED ON TARGHEE HTG
C-----------
        IF(IAGE .EQ. 0 .OR. ICYC .GT. 1)GO TO 184
        IXAGE=IAGE + IY(ICYC) -IY(1)
        IF(IXAGE .LT. 40. .AND. IXAGE .GT. 10. .AND. D
     &     .LT. 9.0)THEN
          IF(Z .GT. 2.0) GO TO 184
          ZADJ=.3564*DG(I)*FINT/YR
          CLOSUR=PCT(I)/100.0
          IF(RELDEN .LT. 100.0)CLOSUR=1.0
          IF(DEBUG)WRITE(JOSTND,9650)ELEV,IXAGE,ZADJ,FINT,YR,
     &     DG(I),CLOSUR
 9650     FORMAT(' ELEV',F6.1,'IXAGE',F5.0,'ZADJ',
     &    F10.4,'FINT',F6.0,'YR',F6.0,'DG',F10.3,'CLOSUR',F10.1)
          ZADJ=ZADJ*CLOSUR
C-----------
C ADJUSTMENT IS HIGHER FOR LONG CROWNED TREES
C-----------
          IF(IICR .EQ. 9 .OR. IICR .EQ. 8)ZADJ=ZADJ*1.1
          Z=Z + ZADJ
          IF(Z .GT. 2.0)Z=2.0
        ENDIF
  184   CONTINUE
C-----------
C CALCULATE DIAMETER AFTER 10 YEARS
C-----------
        DIA= D + DG(I)/BRATIO(ISPC,D,H)
        IF((0.1 + COF1) .GT. DIA) GO TO 185
        HTG(I) = 0.1
        GO TO 60
  185   CONTINUE
C-----------
C  CALCULATE HEIGHT AFTER 10 YEARS
C-----------
        PSI= COF8*((DIA-0.1)/(0.1 + COF1 - DIA))**COF9
     +     * (EXP(Z*((1.0 - COF7**2  ))**0.5/COF6))
C
        H= ((PSI/(1.0 + PSI))* COF2) + 4.5
C 
        IF(.NOT. DEBUG)GO TO 191
        WRITE(JOSTND,9631)D,DIA,H,DG(I),Z ,H
 9631   FORMAT(1X,'IN HTGF DIA=',F7.3,'DIA+10=',F7.3,'H=',F7.1,
     &  'DIA GR=',F8.3,'Z=',E15.8,'NEW H=',F8.1)
  191   CONTINUE
C------------
C  CALCULATE HEIGHT GROWTH
C   NEGATIVE HEIGHT GROWTH IS NOT ALLOWED
C------------
        IF(H .LT. HT(I)) H=HT(I)
        HTG(I)= (H - HT(I))
      ENDIF
C-----------
C   HEIGHT GROWTH EQUATION, EVALUATED FOR EACH TREE EACH CYCLE
C    MULTIPLIED BY SCALE TO CHANGE FROM A YR. PERIOD TO FINT AND
C    MULTIPLIED BY XHT TO APPLY USER SUPPLIED GROWTH MULTIPLIERS.
C----------
   60 CONTINUE
      HTG(I)=SCALE*XHT*HTG(I)
C
      IF(DEBUG)THEN
        HTNEW=HT(I)+HTG(I)
        WRITE (JOSTND,9000) HTG(I),HTCON(ISPC),H2COF,D,
     &  WK1(I),HTNEW,HDGCOF,I,ISPC
 9000   FORMAT(' 9000 HTGF, HTG=',F8.4,' HTCON=',F8.4,
     &  ' H2COF=',F12.8,' D =',F8.4/' WK1=',F8.4,
     &  ' HTNEW=',F8.4,' HDGCOF=',F8.4,' I=',I4,' ISPC=',I2)
      ENDIF     
C----------
C    APPLY DWARF MISTLETOE HEIGHT GROWTH IMPACT HERE,
C    INSTEAD OF AT EACH FUNCTION IF SPECIAL CASES EXIST.
C----------
      HTG(I)=HTG(I)*MISHGF(I,ISPC)
C
      TEMHTG=HTG(I)
C----------
C CHECK FOR SIZE CAP COMPLIANCE.
C----------
      IF((HT(I)+HTG(I)).GT.SIZCAP(ISPC,4))THEN
        HTG(I)=SIZCAP(ISPC,4)-HT(I)
        IF(HTG(I) .LT. 0.1) HTG(I)=0.1
      ENDIF
C
      IF(.NOT.LTRIP) GO TO 30
      ITFN=ITRN+2*I-1
      IF(ISPC.EQ.5)THEN
        HTG(ITFN)=EXP(CON+HDGCOF*ALOG(DG(ITFN)))+ 0.4809
        IF(HTG(ITFN).LT.0.1)HTG(ITFN)=0.1
        HTG(ITFN)=HTG(ITFN)*SCALE*XHT
      ELSE
        HTG(ITFN)=TEMHTG
      ENDIF
C----------
C CHECK FOR SIZE CAP COMPLIANCE.
C----------
      IF((HT(ITFN)+HTG(ITFN)).GT.SIZCAP(ISPC,4))THEN
        HTG(ITFN)=SIZCAP(ISPC,4)-HT(ITFN)
        IF(HTG(ITFN) .LT. 0.1) HTG(ITFN)=0.1
      ENDIF
C
      IF(ISPC.EQ.5)THEN
        HTG(ITFN+1)=EXP(CON+HDGCOF*ALOG(DG(ITFN+1)))+ 0.4809
        HTG(ITFN+1)=HTG(ITFN+1)*SCALE*XHT
        IF(HTG(ITFN+1).LT.0.1)HTG(ITFN+1)=0.1
      ELSE
        HTG(ITFN+1)=TEMHTG
      ENDIF
C----------
C CHECK FOR SIZE CAP COMPLIANCE.
C----------
      IF((HT(ITFN+1)+HTG(ITFN+1)).GT.SIZCAP(ISPC,4))THEN
        HTG(ITFN+1)=SIZCAP(ISPC,4)-HT(ITFN+1)
        IF(HTG(ITFN+1) .LT. 0.1) HTG(ITFN+1)=0.1
      ENDIF
C
      IF(DEBUG) WRITE(JOSTND,9001) HTG(ITFN),HTG(ITFN+1)
 9001 FORMAT( ' UPPER HTG =',F8.4,' LOWER HTG =',F8.4)
C----------
C   END OF TREE LOOP
C----------
   30 CONTINUE
C----------
C   END OF SPECIES LOOP
C----------
   40 CONTINUE
      IF(DEBUG)WRITE(JOSTND,70)ICYC
   70 FORMAT(' LEAVING SUBROUTINE HTGF  CYCLE =',I5)
      RETURN
C
      ENTRY HTCONS
C----------
C  ENTRY POINT FOR LOADING HEIGHT INCREMENT MODEL COEFFICIENTS THAT ARE
C  SITE DEPENDENT AND REQUIRE ONE-TIME RESOLUTION.  HGHC CONTAINS
C  HABITAT TYPE INTERCEPTS, HGLDD CONTAINS HABITAT DEPENDENT
C  COEFFICIENTS FOR THE DIAMETER INCREMENT TERM, HGH2 CONTAINS HABITAT
C  DEPENDENT COEFFICIENTS FOR THE HEIGHT-SQUARED TERM, AND HGHC CONTAINS
C  SPECIES DEPENDENT INTERCEPTS.  HABITAT TYPE IS INDEXED BY ITYPE (SEE
C  /PLOT/ COMMON AREA).
C----------
      DATA MAPHAB/
     & 1,1, 7*2, 3,3,4,5,6, 4*7, 4,4,1,4,4, 3*8, 4*1/
      DATA  HGHC /
     & 2.03035, 1.72222, 1.19728, 1.81759, 2.14781, 1.76998, 2.21104,
     & 1.74090/
      DATA  HGLDD /
     & 0.62144, 1.02372, 0.85493, 0.75756, 0.46238, 0.49643, 0.37042,
     & 0.34003/
      DATA  HGH2 /
     & -13.358E-05, -3.809E-05, -3.715E-05, -2.607E-05, -5.200E-05,
     & -1.605E-05, -3.631E-05, -4.460E-05/
C----------
C  ASSIGN HABITAT DEPENDENT COEFFICIENTS.
C----------
      IHT=MAPHAB(ITYPE)
      HGHCH=HGHC(IHT)
      H2COF=HGH2(IHT)
      HDGCOF=HGLDD(IHT)
C----------
C  LOAD OVERALL INTERCEPT FOR EACH SPECIES.
C----------
      DO 150 ISPC=1,MAXSP
      IF(ISPC .EQ. 5)THEN
        HTCON(ISPC)=HGHCH - 0.5478
      ELSE
        HTCON(ISPC) = 0.0
      ENDIF
      IF(LHCOR2 .AND. HCOR2(ISPC).GT.0.0) HTCON(ISPC)=
     &    HTCON(ISPC)+ALOG(HCOR2(ISPC))
  150 CONTINUE
C
      RETURN
      END
