      SUBROUTINE SMHTGF(ISPC,I,HTGRTH,CR,TPCCF)
      IMPLICIT NONE
C----------
C  **SMHTGF--EM   DATE OF LAST REVISION:  03/26/09
C----------
C THIS SUBROUTINE COMPUTES THE HEIGHT GROWTH FOR SMALL TREES (HTGRTH)
C IN REAL SCALE.  THIS ROUTINE IS CALLED FROM REGENT.
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'CALCOM.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
C
COMMONS
      EXTERNAL RANN
C
      INTEGER I,ISPC
      REAL TPCCF,CR,HTGRTH,BACHLO,D,BETA1,BETA2,HTG1,STDDEV
C----------
C  SPECIES ORDER:
C   1=WB,  2=WL,  3=DF,  4=LM,  5=LL,  6=RM,  7=LP,  8=ES,
C   9=AF, 10=PP, 11=GA, 12=AS, 13=CW, 14=BA, 15=PW, 16=NC,
C  17=PB, 18=OS, 19=OH
C
C  SPECIES EXPANSION
C  LM USES IE LM (ORIGINALLY FROM TT VARIANT)
C  LL USES IE AF (ORIGINALLY FROM NI VARIANT)
C  RM USES IE JU (ORIGINALLY FROM UT VARIANT)
C  AS,PB USE IE AS (ORIGINALLY FROM UT VARIANT)
C  GA,CW,BA,PW,NC,OH USE IE CO (ORIGINALLY FROM CR VARIANT)
C----------
C  RESET ZRAND IF THIS IS THE START OF THE PROJECTION, THIS IS THE FIRST 
C  PREDICTION FOR A NEW TRIPLE, OR IF GROWTH IN THE PREVIOUS CYCLE WAS 
C  LESS THAN 0.1 FT.
C----------
      IF(ZRAND(I).NE.-999.) GO TO 20
   10 CONTINUE
      ZRAND(I)=BACHLO(0.0,1.0,RANN)
      IF(ZRAND(I).LT.-2.0 .OR. ZRAND(I).GT.2.0) GO TO 10
   20 CONTINUE
      D = DBH(I)
      IF (D.LE.0.0) THEN
        HTGRTH=0.0
        RETURN
      ENDIF
      BETA1=EXP(B0ACCF(ISPC) + B1ACCF(ISPC)*ALOG(TPCCF))
      BETA2=EXP(B0BCCF(ISPC) + B1BCCF(ISPC)*ALOG(TPCCF))
      HTG1=BETA1 + BETA2*CR
      STDDEV=HTG1*(B0ASTD(ISPC) + B1BSTD(ISPC)*CR)
      HTGRTH=HTG1 + ZRAND(I)*STDDEV
C----------
C  IF ESTIMATED HEIGHT INCREMENT IS CLOSE TO ZERO, SET FLAG TO DRAW 
C  A NEW RANDOM ERROR ON NEXT PASS THROUGH.
C----------
      IF(HTGRTH .GT. 0.1) RETURN
      HTGRTH = 0.1
      ZRAND(I)=-999.
      RETURN
      END
