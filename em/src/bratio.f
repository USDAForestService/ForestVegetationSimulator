      FUNCTION BRATIO(IS,D,H)
      IMPLICIT NONE
C----------
C  **BRATIO--EM   DATE OF LAST REVISION:  03/26/09
C----------
C FUNCTION TO COMPUTE BARK RATIOS AS A FUNCTION OF DIAMETER AND SPECIES.
C REPLACES ARRAY BKRAT IN BLKDAT. 
C----------
C  COMMONS
C
      INCLUDE 'PRGPRM.F77'
C
C  COMMONS
C----------
C  SPECIES ORDER:
C   1=WB,  2=WL,  3=DF,  4=LM,  5=LL,  6=RM,  7=LP,  8=ES,
C   9=AF, 10=PP, 11=GA, 12=AS, 13=CW, 14=BA, 15=PW, 16=NC,
C  17=PB, 18=OS, 19=OH
C
C  OS USES EM WB BARK COEFFICIENT
C
C  SPECIES EXPANSION
C  LM USES IE LM (ORIGINALLY FROM TT VARIANT)
C  LL USES IE AF (ORIGINALLY FROM NI VARIANT)
C  RM USES IE JU (ORIGINALLY FROM UT VARIANT; REALLY PP FROM CR VARIANT)
C  AS,PB USE IE AS (ORIGINALLY FROM UT VARIANT)
C  GA,CW,BA,PW,NC,OH USE IE CO (ORIGINALLY FROM CR VARIANT)
C----------
      REAL BARK1(MAXSP),BARK2(MAXSP),H,D,BRATIO,TEMD
      INTEGER IMAP(MAXSP),IS,IEQN

      DATA BARK1/0.934, 0.934, 0.867, 0.969, 0.937, 0.000, 0.969,
     &    0.956, 0.937, 0.890, 0.892, 0.950, 0.892, 0.892, 0.892,
     &    0.892, 0.950, 0.934, 0.892/
      DATA BARK2/0.000, 0.000, 0.000, 0.000, 0.000, 0.000, 0.000,
     &    0.000, 0.000, 0.000, -.086, 0.000, -.086, -.086, -.086,
     &    -.086, 0.000, 0.000, -.086/
      DATA IMAP/     2,     2,     2,     2,     2,     1,     2,
     &        2,     2,     2,     3,     2,     3,     3,     3,
     &        3,     2,     2,     3/
C----------
      IEQN=IMAP(IS)
      TEMD=D
      IF(TEMD.LT.1.)TEMD=1.
C
      IF(IEQN .EQ. 1) THEN
        IF(BARK1(IS).EQ.0.0 .AND. BARK2(IS).EQ.0.0)THEN
          IF(TEMD.GT.19.)TEMD=19.
          BRATIO = 0.9002 - 0.3089*(1/TEMD)
        ELSE
          BRATIO=BARK1(IS)+BARK2(IS)*(1/TEMD)
        ENDIF
C
      ELSEIF(IEQN .EQ. 2)THEN
        BRATIO=BARK1(IS)
C
      ELSEIF(IEQN .EQ. 3)THEN
        BRATIO=BARK1(IS)+BARK2(IS)*(1.0/TEMD)
      ENDIF
C
      IF(BRATIO .GT. 0.99) BRATIO=0.99
      IF(BRATIO .LT. 0.80) BRATIO=0.80
C
      RETURN
      END

