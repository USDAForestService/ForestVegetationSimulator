      SUBROUTINE SITSET
      IMPLICIT NONE
C----------
C  **SITSET--NC   DATE OF LAST REVISION:  05/11/11
C----------
C
C THIS SUBROUTINE LOADS THE SITEAR ARRAY WITH A SITE INDEX FOR EACH
C SPECIES THAT WAS NOT ASSIGNED BY KEYWORD, AND LOADS THE SDIDEF
C ARRAY FOR EACH SPECIES WHICH WAS NOT ASSIGNED AN SDI MAXIMUM
C BY KEYWORD.
C
C THIS SUBROUTINE ALSO SETS VARIABLES WHICH ARE FOREST SPECIFIC.
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
C
      INCLUDE 'VOLSTD.F77'
C
C
COMMONS
C----------
      LOGICAL DEBUG
      CHARACTER FORST*2,FORDUM*2,DIST*2,PROD*2,VAR*2,VOLEQ*10
      CHARACTER*4 ASPEC
      INTEGER IFIASP, ERRFLAG
      INTEGER NTOHI,NSISET,NSDSET,I,JSISP,INDEX,KNTECO,ISEQ,NUM
      INTEGER ISFLAG,ITOHI,ISPC,K,INTFOR,IREGN,IRDUM,J,JJ
      REAL SIAGE(MAXSP),SI(MAXSP),C5(MAXSP)
      REAL FORMAX,RSI,RSDI,AG,SINDX
C----------
C  DATA STATEMENTS
C----------
      DATA C5/624.,647.,547.,759.,588.,706.,382.,759.,800.,571.,759./
      DATA FORMAX/850./
C-----------
C  SEE IF WE NEED TO DO SOME DEBUG.
C-----------
      CALL DBCHK (DEBUG,'SITSET',6,ICYC)
C----------
C  DETERMINE HOW MANY SITE VALUES AND SDI VALUES WERE SET VIA KEYWORD.
C----------
      NTOHI=0
      NSISET=0
      NSDSET=0
      DO 5 I=1,MAXSP
      IF(SITEAR(I) .GT. 0.) NSISET=NSISET+1
      IF(SDIDEF(I) .GT. 0.) NSDSET=NSDSET+1
    5 CONTINUE
      IF(DEBUG)WRITE(JOSTND,*)' ENTERING SITSET, SITE VALUES SET = ',
     &NSISET,'  SDI VALUES SET = ',NSDSET
C----------
C  SET SITE SPECIES AND SITE INDEX IF NOT SET BY KEYWORD.
C  FOR REGION 6 FORESTS SET SDI DEFAULTS HERE ALSO.
C----------
      IF(IFOR.EQ.4 .OR. IFOR.EQ.7) THEN
C----------
C       REGION 6 FOREST --- CALL **ECOCLS** WITH THE ECOCLASS CODE,
C       AND GET BACK THE DEFAULT SITE SPECIES, ALL SITE INDICIES,
C       AND DEFAULT SDI MAXIMUMS ASSOCIATED WITH THE ECOCLASS
C----------
        JSISP=0
        INDEX=0
        KNTECO=0
   10   CALL ECOCLS (PCOM,ASPEC,RSDI,RSI,ISFLAG,NUM,INDEX,ISEQ)
        KNTECO=KNTECO+1
        IF(DEBUG)WRITE(JOSTND,*)' AFTER ECOCLS,PCOM,ASPEC,RSDI,RSI,',
     &  'ISFLAG,NUM,INDEX,ISEQ = ',PCOM,ASPEC,RSDI,RSI,ISFLAG,
     &  NUM,INDEX,ISEQ
C----------
C IF DEFAULT SDI IS OUT OF BOUNDS, RESET IT.
C----------
        ITOHI=0
        IF(RSDI .GT. FORMAX)THEN
          RSDI=FORMAX
          ITOHI=1
        ENDIF
C
C       LOOP THROUGH JSP ARRAY AND DETERMINE FVS SEQUENCE NUMBER FOR
C       THIS SPECIES
C
        IF(ISEQ .EQ. 0) GO TO 25
        IF(JSISP.EQ.0 .AND. ISFLAG.EQ.1) JSISP=ISEQ
        IF(ISISP.LE.0 .AND. ISFLAG.EQ.1) ISISP=ISEQ
        IF(SITEAR(ISEQ).LE.0. .AND. NSISET.EQ.0) SITEAR(ISEQ)=RSI
        IF(SDIDEF(ISEQ) .LE. 0.) THEN
          SDIDEF(ISEQ)=RSDI
          IF(ITOHI .GT. 0) NTOHI=NTOHI+1
        ENDIF
        IF(ISISP.GT.0 .AND. ISFLAG.EQ.1) THEN
          IF(SDIDEF(ISISP) .LE. 0.) THEN
            SDIDEF(ISISP)=RSDI
            IF(ITOHI .GT. 0) NTOHI=NTOHI+1
          ENDIF
        ENDIF
   25   CONTINUE
        IF(NUM.GT.1 .AND. KNTECO.LT.NUM) THEN
          INDEX=INDEX+1
          GO TO 10
        ENDIF
      ELSE
        IF(ISISP .LE. 0) ISISP = 3
        IF(SITEAR(ISISP) .LE. 0.0) SITEAR(ISISP) = 90.
        JSISP= ISISP
      ENDIF
C---------
C  ON THE CHANCE THAT A SITE SPECIES WAS NOT ENCOUNTERED IN **ECOCLS**
C  PROVIDE A REGION 6 GLOBAL DEFAULT.
C----------
      IF(IFOR.EQ.4 .OR. IFOR.EQ.7) THEN
        IF(ISISP .LE. 0) ISISP = 3
        IF(SITEAR(ISISP) .LE. 0.0) SITEAR(ISISP) = 90.
      ENDIF
C----------
C TRANSLATE THE SITES AND KEEP
C----------
      CALL SICHG(ISISP,SITEAR(ISISP),SIAGE,JOSTND)
C----------
C SI IS A VECTOR X MAXSP GIVING EQUIVALENT SITES FOR EACH SPECIES
C----------
      IF (DEBUG) WRITE(JOSTND,9003)ISISP,SITEAR(ISISP),SIAGE
 9003 FORMAT(' MAIN REF SPECIES ',I5,' SITE INDEX ',F10.1/
     &  5X,11F6.1)
   20 CONTINUE
      DO 30 ISPC=1,MAXSP
      AG=SIAGE(ISPC)
      SINDX=SITEAR(ISISP)
C
      CALL HTCALC(SINDX,ISISP,AG,SI(ISPC),JOSTND,DEBUG)
C
   30 CONTINUE
C----------
C IF SITEAR() HAS NOT BEEN SET WITH SITECODE THEN CALCULATE VALUE HERE.
C----------
      DO 35 I=1,MAXSP
      IF(SITEAR(I) .EQ. 0.0) SITEAR(I)=SI(I)
   35 CONTINUE
C----------
C LOAD THE SDIDEF ARRAY
C----------
      K=ISISP
      IF(SDIDEF(K) .LE. 0.) K=JSISP
      DO 40 I=1,MAXSP
        IF(SDIDEF(I) .GT. 0.0) GO TO 40
        IF(BAMAX .GT. 0.) THEN
          SDIDEF(I)= BAMAX/(0.5454154*(PMSDIU/100.))
        ELSEIF(IFOR.EQ.4 .OR. IFOR.EQ.7) THEN
          SDIDEF(I) = SDIDEF(K) * (C5(I)/C5(K))
          IF(SDIDEF(I) .GT. FORMAX)THEN
            SDIDEF(I)=FORMAX
            NTOHI=NTOHI+1
          ENDIF
        ELSE
          SDIDEF(I) = C5(I)
        ENDIF
   40 CONTINUE
C
      DO 92 I=1,15
      J=(I-1)*10 + 1
      JJ=J+9
      IF(JJ.GT.MAXSP)JJ=MAXSP
      WRITE(JOSTND,90)(NSP(K,1)(1:2),K=J,JJ)
   90 FORMAT(/' SPECIES ',5X,10(A2,6X))
      WRITE(JOSTND,91)(SDIDEF(K),K=J,JJ )
   91 FORMAT(' SDI MAX ',   10F8.0)
      IF(JJ .EQ. MAXSP)GO TO 93
   92 CONTINUE
   93 CONTINUE
      IF(NTOHI .GT. 0)WRITE(JOSTND,102)FORMAX
  102 FORMAT(/' *NOTE -- AT LEAST ONE DEFAULT MAXIMUM SDI EXCEEDED THE F
     &OREST DEFAULT MAXIMUM. FOREST DEFAULT MAXIMUM OF ',F5.0,' USED.',/
     &,'          YOU MAY NEED TO SPECIFICALLY RESET VALUES FOR THESE SP
     &ECIES USING THE SDIMAX KEYWORD.')
C----------
C  LOAD VOLUME DEFAULT MERCH. SPECS.
C----------
      SELECT CASE(IFOR)
      CASE(4)
        DO I=1,MAXSP
        IF(TOPD(I) .LE. 0.) TOPD(I) = 4.5
        IF(BFTOPD(I) .LE. 0.) BFTOPD(I) = 4.5
        END DO
      CASE(5,7)
        DO I=1,MAXSP
        IF(DBHMIN(I) .LE. 0.) DBHMIN(I) = 9.0
        IF(TOPD(I) .LE. 0.) TOPD(I) = 5.0
        IF(BFTOPD(I) .LE. 0.0) BFTOPD(I) = 5.0
        IF(BFMIND(I) .LE. 0.0) BFMIND(I) = 9.0
        END DO
      CASE DEFAULT
        DO I=1,MAXSP
        IF(DBHMIN(I) .LE. 0.) DBHMIN(I) = 9.0
        IF(TOPD(I) .LE. 0.) TOPD(I) = 6.0
        IF(BFTOPD(I) .LE. 0.0) BFTOPD(I) = 6.0
        IF(BFMIND(I) .LE. 0.0) BFMIND(I) = 9.0
        END DO
      END SELECT
C----------
C  SET METHB & METHC DEFAULTS.  DEFAULTS ARE INITIALIZED TO 999 IN
C  **GRINIT**.  IF THEY HAVE A DIFFERENT VALUE NOW, THEY WERE CHANGED
C  BY KEYWORD IN INITRE. ONLY CHANGE THOSE NOT SET BY KEYWORD.
C
C  SISKIYOU (IFOR=4) IS A REGION 6 FOREST. ALL OTHERS FORESTS IN THIS
C  VARIANT ARE IN REGION 5. SIMPSON & HOOPA USE R5 EQUATIONS.
C  BLM (IFOR=7) USE NATCRS WITH BLM EQNS
C----------
      DO 50 ISPC=1,MAXSP
      IF(IFOR.EQ.4 .OR. IFOR.EQ.7)THEN
        IF(METHB(ISPC).EQ.999)METHB(ISPC)=6
        IF(METHC(ISPC).EQ.999)METHC(ISPC)=6
      ELSE
        IF(METHB(ISPC).EQ.999)METHB(ISPC)=6
        IF(METHC(ISPC).EQ.999)METHC(ISPC)=6
      ENDIF
   50 CONTINUE
C----------
C  LOAD VOLUME EQUATION ARRAYS FOR ALL SPECIES
C----------
      INTFOR = KODFOR - (KODFOR/100)*100
      WRITE(FORST,'(I2)')INTFOR
      IF(INTFOR.LT.10)FORST(1:1)='0'
      IREGN = KODFOR/100
      DIST='  '
      PROD='  '
      VAR='NC'
C
C  USE SPECIAL FORESTS FOR PRIVATE VOLUME EQS.
C  NC SIMPSOM TIMBER (FC=800 USES R5 SAY 510)
C
      IF(IREGN.EQ.8)THEN
        IRDUM=5
        FORDUM='10'
      ELSE
        IRDUM=IREGN
        FORDUM=FORST
      ENDIF
C
      DO ISPC=1,MAXSP
      READ(FIAJSP(ISPC),'(I4)')IFIASP
      IF(((METHC(ISPC).EQ.6).OR.(METHC(ISPC).EQ.9)).AND.
     &     (VEQNNC(ISPC).EQ.'          '))THEN
        CALL VOLEQDEF(VAR,IRDUM,FORDUM,DIST,IFIASP,PROD,VOLEQ,ERRFLAG)
        VEQNNC(ISPC)=VOLEQ
      ENDIF
      IF(((METHB(ISPC).EQ.6).OR.(METHB(ISPC).EQ.9)).AND.
     &     (VEQNNB(ISPC).EQ.'          '))THEN
        CALL VOLEQDEF(VAR,IRDUM,FORDUM,DIST,IFIASP,PROD,VOLEQ,ERRFLAG)
        VEQNNB(ISPC)=VOLEQ
      ENDIF
      ENDDO
C----------
C  IF FIA CODES WERE IN INPUT DATA, WRITE TRANSLATION TABLE
C---------
      IF(LFIA) THEN
        CALL FIAHEAD(JOSTND)
        WRITE(JOSTND,211) (NSP(I,1)(1:2),FIAJSP(I),I=1,MAXSP)
 211    FORMAT ((T13,8(A3,'=',A6,:,'; '),A,'=',A6))
      ENDIF
C----------
C  WRITE VOLUME EQUATION NUMBER TABLE
C----------
      CALL VOLEQHEAD(JOSTND)
      WRITE(JOSTND,230)(NSP(J,1)(1:2),VEQNNC(J),VEQNNB(J),J=1,MAXSP)
 230  FORMAT(4(3X,A2,4X,A10,1X,A10))
C
      RETURN
      END
