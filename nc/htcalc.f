      SUBROUTINE HTCALC(SINDX,ISPC,AG,HGUESS,JOSTND,DEBUG)
      IMPLICIT NONE
C----------
C  **HTCALC--NC   DATE OF LAST REVISION:  01/17/11
C----------
C THIS ROUTINE CALCULATES A POTENTIAL HT GIVEN AN SPECIES SITE AND AGE
C IT IS USED TO CAL POTHTG AND SITE
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
C
COMMONS
C----------
      LOGICAL DEBUG
      INTEGER ISPC,JOSTND
      REAL B0,B1,B2,B3,B4,B5,B6,B7,B8,B9,B10,B11,B12,B13
      REAL HGUESS,AG,SINDX,Z,A,B,C,X1,X2
C----------
      IF(DEBUG)WRITE(JOSTND,15)AG,SINDX
   15 FORMAT(' ENTERING HTCALC AG,SINDX= ',2F10.4)
C----------
C   LOAD COEFFICIENTS
C----------
      B0=BB0(ISPC)
      B1=BB1(ISPC)
      B2=BB2(ISPC)
      B3=BB3(ISPC)
      B4=BB4(ISPC)
      B5=BB5(ISPC)
      B6=BB6(ISPC)
      B7=BB7(ISPC)
      B8=BB8(ISPC)
      B9=BB9(ISPC)
      B10=BB10(ISPC)
      B11=BB11(ISPC)
      B12=BB12(ISPC)
      B13=BB13(ISPC)
      IF(DEBUG) WRITE(JOSTND,9045)ISPC,B0,B1,B2,B3,B4,B5,B6,B7,B8,B9,
     &          B10,B11,B12,B13
 9045 FORMAT(' IN HTGF SPEC ',I4,' COEFS:'/10E12.4/4E12.4)
C----------
C GO TO A DIFFERENT POTENTIAL HEIGHT CURVE DEPENDING ON THE SPECIES
C----------
      SELECT CASE (ISPC)
C----------
C DOUGLAS FIR USE KING
C OTHER CONIFER USE DOUGLAS-FIR
C----------
      CASE(1,3)
        Z = B0/(SINDX - 4.5)
        A = B1 + B2*Z
        B = B3 + B4*Z
        C = B5 + B6*Z
        HGUESS = ((AG*AG)/(A + B*AG + C*AG*AG)) + 4.5
C----------
C WHITE FIR USE DOLPH
C INCENSE CEDAR USE WF
C RED FIR USE WF
C----------
      CASE(4,6,9)
        X1 = (B1*AG**B2*EXP(B3*AG))
        X2 = B4*(1.0 - EXP(B5*AG**B6))
        HGUESS = (SINDX - B0 + X1*X2)/X1 + 4.5
        IF(DEBUG)WRITE(JOSTND,9500)X1,X2,SINDX,AG,HGUESS
 9500   FORMAT(' X1,X2,SINDX,AG',12F8.3)
C----------
C MADRONE USE PORTER AND WIANT
C----------
      CASE(5)
        HGUESS=SINDX/(B0 + B1/AG)
C----------
C BLACK OAK   POWERS
C SCALE FACTOR ADDED SO THAT TREES WOULD HIT SITE HEIGHT
C----------
      CASE(7)
        A = SQRT(AG) - SQRT(50.0)
        HGUESS = SINDX*(1.0+B2*(A))-B1*A
        HGUESS = HGUESS*.80
C----------
C TANOAK USE WIANT
C SCALE FACTOR ADDED SO THAT TREES WOULD HIT SITE HEIGHT
C MISC SPECIES USE TANOAK
C----------
      CASE(8,11)
        HGUESS = SINDX /(B0 + B1/AG)
        HGUESS = HGUESS*0.85
C----------
C PONDEROSA PINE USE POWERS AND OLIVER
C SUGAR PINE USE POWERS  PONDEROSA PINE
C----------
      CASE(2,10)
        IF(DEBUG)WRITE(JOSTND,20)B0,B1,AG,B2,B3,B4,B5,B6,B7,SINDX
   20   FORMAT(' HTCALC 20F B0-1,AG,B2-7,SINDX = ',/,1H ,10F13.8)
        HGUESS =(B1*SINDX - B2) * (1.0-EXP(B3*AG))**(0.001*SINDX+B4)
C
      END SELECT
C
      IF(DEBUG)WRITE(JOSTND,*)' LEAVING HTCALC HGUESS= ',HGUESS
C
      RETURN
      END
