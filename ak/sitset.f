      SUBROUTINE SITSET
      IMPLICIT NONE
C----------
C AK $Id$
C----------
C  THIS SUBROUTINE LOADS THE SITEAR ARRAY WITH A SITE INDEX FOR EACH
C  SPECIES WHICH WAS NOT ASSIGNED A SITE INDEX BY KEYWORD, AND LOADS
C  THE SDIDEF ARRAY WITH SDI MAXIMUMS FOR EACH SPECIES WHICH WAS NOT
C  ASSIGNED AN SDI MAXIMUM USING THE SDIMAX KEYWORD.
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'VOLSTD.F77'
C
C
COMMONS
C
C----------
C  VARIABLE DECLARATIONS:
C----------
C
      CHARACTER DIST*2,FORST*2,PROD*2,VAR*2,VOLEQ*10
C
      INTEGER ERRFLAG,I,IFIASP,INTFOR,IREGN,ISPC,J,JJ,K
C
      REAL TEM
C
      REAL SDICON(MAXSP),SLO(MAXSP),SHI(MAXSP)
C
C----------
C  DATA STATEMENTS:
C
C  SPECIES ORDER
C  1  2  3  4  5  6  7  8  9 10 
C SF AF YC TA WS LS BE SS LP RC 
C
C 11 12 13 14 15 16 17 18 19 20 21 22 23
C WH MH OS AD RA PB AB BA AS CW WI SU OH
C
C DELETE THESE NEXT TWO COMMENT LINES WHEN NON-SDI STUFF 
C IN THIS SUBROUTINE IS UPDATED! 
C  1    2    3    4    5    6    7    8    9   10   11  12  13
C WS  WRC  PSF   MH   WH  AYC   LP   SS   SAF  RA   CW  OH  OS
C----------
      DATA SDICON /790.,602.,592.,387.,412.,412.,500.,654.,679.,762.,
     & 682.,687.,412.,441.,441.,466.,466.,384.,562.,452.,447.,447.,452./
      DATA SLO /20.,20.,20.,20.,20.,20.,20.,20.,20.,20.,
     & 20.,20.,20.,20.,20.,20.,20.,20.,20.,20.,20.,20.,20./
      DATA SHI /138., 131., 138., 131., 131., 131., 131., 164., 131., 
     & 138., 138., 131., 131., 138., 138., 131., 131., 138., 131., 138.,
     & 138., 138., 138./
C----------
C IF SITEAR(I) HAS NOT BEEN SET WITH SITECODE KEYWORD, LOAD IT
C WITH DEFAULT SITE VALUES. USERS SHOULD USE HEGYI (1981) FOR
C RC, YC, LP SPECIES AND WH, MH AND SS GREATER THAN OR EQUAL TO 
C 200 YEARS OF AGE. WH, MH, AND SS LESS THAN 200 YEARS OF AGE
C SHOULD USE PAYANDEH (1974)
C----------
      TEM = 70.
      IF(ISISP .GT. 0) THEN
        IF (SITEAR(ISISP) .GT. 0.0) TEM=SITEAR(ISISP)
      ENDIF
      IF(ISISP .EQ. 0) ISISP = 11
      DO 10 I=1,MAXSP
      SELECT CASE(I)
        CASE(3,8,9,10,11,12) 
          IF(TEM .LT. SLO(ISISP))TEM=SLO(ISISP)
          IF(SITEAR(I) .LE. 0.0) SITEAR(I) = SLO(I) +
     &    (TEM-SLO(ISISP))/(SHI(ISISP)-SLO(ISISP))
     &    *(SHI(I)-SLO(I))
        CASE DEFAULT
          SITEAR(I)= 0.
      END SELECT
   10 CONTINUE    	
C----------
C LOAD THE SDIDEF ARRAY.
C----------
      DO 40 I=1,MAXSP
        IF(SDIDEF(I) .GT. 0.0) GO TO 40
        IF(BAMAX .GT. 0.) THEN
          SDIDEF(I)=BAMAX/(0.5454154*(PMSDIU/100.))
        ELSE
          SDIDEF(I) = SDICON(I)
        ENDIF
   40 CONTINUE
C
      DO 92 I=1,15
      J=(I-1)*10 + 1
      JJ=J+9
      IF(JJ.GT.MAXSP)JJ=MAXSP
      WRITE(JOSTND,90)(NSP(K,1)(1:2),K=J,JJ)
   90 FORMAT(/'SPECIES ',5X,10(A2,6X))
      WRITE(JOSTND,91)(SDIDEF(K),K=J,JJ )
   91 FORMAT('SDI MAX ',   10F8.0)
      IF(JJ .EQ. MAXSP)GO TO 93
   92 CONTINUE
   93 CONTINUE
C----------
C  LOAD VOLUME EQUATION ARRAYS FOR ALL SPECIES
C----------
      INTFOR = KODFOR - (KODFOR/100)*100
      WRITE(FORST,'(I2)')INTFOR
C     CHANGED CONDITION FROM .LT. TO .NE. IN ORDER TO DEAL WITH NEW 
C     LOCATION CODES FOR STATE, BLM, TCC, AND TRIBAL LANDS      
      IF(INTFOR.NE.10)FORST(1:1)='0'
      IREGN = KODFOR/100
      DIST='  '
      PROD='  '
      VAR='AK'
C----------
C    R7 (CODE=701) BRITISH COLUMBIA / MAKAH INDIAN RESERVATION
C    USES THE TONGAS VOLUME EQUATIONS
C    TEMPORARY FIX: MAP ALL NON-R10 CODES TO TONGASS, CHANGED
C    FORST='03' TO '02' TO REPRESENT CHANGE IN FOREST CODE ORDER.
C----------
      IF(IREGN.EQ.7).OR.(IREGN.EQ.74).OR.(IREGN.EQ.81)THEN
        IREGN=10
        FORST='02'
      ENDIF
C
      DO ISPC=1,MAXSP
      READ(FIAJSP(ISPC),'(I4)')IFIASP
      IF(((METHC(ISPC).EQ.6).OR.(METHC(ISPC).EQ.9)).AND.
     &     (VEQNNC(ISPC).EQ.'          '))THEN
        CALL VOLEQDEF(VAR,IREGN,FORST,DIST,IFIASP,PROD,VOLEQ,ERRFLAG)
        VEQNNC(ISPC)=VOLEQ
      ENDIF
      IF(((METHB(ISPC).EQ.6).OR.(METHB(ISPC).EQ.9)).AND.
     &     (VEQNNB(ISPC).EQ.'          '))THEN
        CALL VOLEQDEF(VAR,IREGN,FORST,DIST,IFIASP,PROD,VOLEQ,ERRFLAG)
        VEQNNB(ISPC)=VOLEQ
      ENDIF
      ENDDO
C----------
C  IF FIA CODES WERE IN INPUT DATA, WRITE TRANSLATION TABLE
C---------
      IF(LFIA) THEN
        CALL FIAHEAD(JOSTND)
        WRITE(JOSTND,211) (NSP(I,1)(1:2),FIAJSP(I),I=1,MAXSP)
 211    FORMAT ((T12,8(A3,'=',A6,:,'; '),A,'=',A6))
      ENDIF
C----------
C  WRITE VOLUME EQUATION NUMBER TABLE
C----------
      CALL VOLEQHEAD(JOSTND)
      WRITE(JOSTND,230)(NSP(J,1)(1:2),VEQNNC(J),VEQNNB(J),J=1,MAXSP)
 230  FORMAT(4(2X,A2,4X,A10,1X,A10,1X))
C
      RETURN
      END
