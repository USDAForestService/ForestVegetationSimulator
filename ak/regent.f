      SUBROUTINE REGENT(LESTB,ITRNIN)
      IMPLICIT NONE
C----------
C AK $Id$
C----------
C  THIS SUBROUTINE COMPUTES HEIGHT AND DIAMETER INCREMENTS FOR
C  SMALL TREES.  THE HEIGHT INCREMENT MODEL IS APPLIED TO TREES
C  THAT ARE LESS THAN 5 INCHES DBH
C  AND THE DBH INCREMENT MODEL IS APPLIED TO TREES THAT ARE LESS
C  THAN 3 INCHES DBH.  FOR TREES THAT ARE GREATER THAN 2 INCHES
C  DBH, HEIGHT INCREMENT PREDICTIONS
C  ARE AVERAGED WITH THE PREDICTIONS FROM THE LARGE TREE MODEL.
C  DIAMETER IS ASSIGNED FROM A HEIGHT-DIAMETER FUNCTION
C  INCREMENT IS COMPUTED BY SUBTRACTION.
C  THIS ROUTINE IS CALLED FROM **CRATET** DURING CALIBRATION AND
C  FROM **TREGRO** DURING CYCLING.  ENTRY **REGCON** IS CALLED FROM
C  **RCON** TO LOAD MODEL PARAMETERS THAT NEED ONLY BE RESOLVED ONCE.
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CALCOM.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'ESTCOR.F77'
C
C
      INCLUDE 'HTCAL.F77'
C
C
      INCLUDE 'MULTCM.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
C
COMMONS
C
C----------
C  VARIABLE DEFINITIONS:
C
C   CORTEM -- A TEMPORARY ARRAY FOR PRINTING CORRECTION TERMS.
C   NUMCAL -- A TEMPORARY ARRAY FOR PRINTING NUMBER OF HEIGHT
C             INCREMENT OBSERVATIONS BY SPECIES.
C    RHCON -- CONSTANT FOR THE HEIGHT INCREMENT MODEL.
C     XMAX -- UPPER END OF THE RANGE OF DIAMETERS OVER WHICH HEIGHT
C             INCREMENT PREDICTIONS FROM SMALL AND LARGE TREE MODELS
C             ARE AVERAGED.
C     XMIN -- LOWER END OF THE RANGE OF DIAMETERS OVER WHICH HEIGHT
C             INCREMENT PREDICTIONS FROM THE SMALL AND LARGE TREE
C             ARE AVERAGED.
C----------
C  VARIABLE DECLARATIONS:
C----------

      EXTERNAL RANN

      LOGICAL DEBUG,LESTB,LSKIPH

      CHARACTER SPEC*2

      INTEGER I,I1,I2,I3,IICR,IP,IREFI,ISPC,ISPEC,ITRNIN
      INTEGER K,KK,KOUT,L,N,I6,IWHO,PI2,RDZ1
      INTEGER NUMCAL(MAXSP)

      REAL BACHLO,BARK,BRATIO,CON,CORNEW,D,DDS,DK,EDH
      REAL FNT,H,HK,HTGR,HTNEW,P,RANC,RCR
      REAL REGYR,RSI,SCALE1,SCALE2,SCALE3,SNP,SNX,SNY,TBAL,TCR
      REAL TERM,XMN,XMX,XRDGRO,XRHGRO,XWT,ZZRAN,A,B,AX,BX,CX,DX
      REAL EX,FX,GX,HX,IX,AX1,BX1,CX1,DX1,EX1,FX1,GX1,HX1,IX1,CRAT,DLO
      REAL DHI,DTEMP,ELEVATN,PBAL,RDZ,SDIC,SDIC2,XSDI,XSITE
     
      REAL CORTEM(MAXSP),DIAM(MAXSP),XMIN(MAXSP),XMAX(MAXSP),ZRD(MAXPLT)

C----------
C SPECIES LIST FOR ALASKA VARIANT.
C
C Number Code  Common Name         FIA  PLANTS Scientific Name
C   1     SF   Pacific silver fir  011  ABAM   Abies amabilis
C   2     AF   subalpine fir       019  ABLA   Abies lasiocarpa
C   3     YC   Alaska cedar        042  CANO9  Callitropsis nootkatensis
C   4     TA   tamarack            071  LALA   Larix laricina
C   5     WS   white spruce        094  PIGL   Picea glauca
C   6     LS   Lutz’s spruce            PILU   Picea lutzii
C   7     BE   black spruce        095  PIMA   Picea mariana
C   8     SS   Sitka spruce        098  PISI   Picea sitchensis
C   9     LP   lodgepole pine      108  PICO   Pinus contorta
C  10     RC   western redcedar    242  THPL   Thuja plicata
C  11     WH   western hemlock     263  TSHE   Tsuga heterophylla
C  12     MH   mountain hemlock    264  TSME   Tsuga mertensiana
C  13     OS   other softwoods     298  2TE
C  14     AD   alder species       350  ALNUS  Alnus species
C  15     RA   red alder           351  ALRU2  Alnus rubra
C  16     PB   paper birch         375  BEPA   Betula papyrifera
C  17     AB   Alaska birch        376  BENE4  Betula neoalaskana
C  18     BA   balsam poplar       741  POBA2  Populus balsamifera
C  19     AS   quaking aspen       746  POTR5  Populus tremuloides
C  20     CW   black cottonwood    747  POBAT  Populus trichocarpa
C  21     WI   willow species      920  SALIX  Salix species
C  22     SU   Scouler’s willow    928  SASC   Salix scouleriana
C  23     OH   other hardwoods     998  2TD
C
C----------
C  DIMENSIONS FOR INTERNAL VARIABLES:
C
C   CORTEM -- A TEMPORARY ARRAY FOR PRINTING CORRECTION TERMS.
C   NUMCAL -- A TEMPORARY ARRAY FOR PRINTING NUMBER OF HEIGHT
C             INCREMENT OBSERVATIONS BY SPECIES.
C    RHCON -- CONSTANT FOR THE HEIGHT INCREMENT MODEL.  ZERO FOR ALL
C             SPECIES IN THIS VARIANT
C     XMAX -- UPPER END OF THE RANGE OF DIAMETERS OVER WHICH HEIGHT
C             INCREMENT PREDICTIONS FROM SMALL AND LARGE TREE MODELS
C             ARE AVERAGED.
C     XMIN -- LOWER END OF THE RANGE OF DIAMETERS OVER WHICH HEIGHT
C             INCREMENT PREDICTIONS FROM THE SMALL AND LARGE TREE
C             ARE AVERAGED.
C     DIAM -- BUD WIDTH (I.E. MINIMUM DIAMETER)
C----------
C  DATA STATEMENTS:
C----------
      DATA REGYR / 5.0 /
      DATA XMAX / 23*3.0 /
      DATA XMIN / 23*1.0 /

      DATA DIAM / 0.3, 0.3, 0.3, 0.2, 0.4,
     &            0.4, 0.2, 0.4, 0.4, 0.3,
     &            0.3, 0.3, 0.4, 0.2, 0.2,
     &            0.1, 0.1, 0.2, 0.3, 0.2,
     &            0.1, 0.1, 0.2 /

C-----------
C  CHECK FOR DEBUG.
C-----------
      LSKIPH=.FALSE.
      CALL DBCHK (DEBUG,'REGENT',6,ICYC)
      IF(DEBUG) WRITE(JOSTND,9980)ICYC
 9980 FORMAT('ENTERING SUBROUTINE REGENT  CYCLE =',I5)
C----------
C  IF THIS IS THE FIRST CALL TO REGENT, BRANCH TO STATEMENT 40 FOR
C  MODEL CALIBRATION.
C----------
      IF(LSTART) GOTO 40
      CALL MULTS (3,IY(ICYC),XRHMLT)
      CALL MULTS(6,IY(ICYC),XRDMLT)
      IF (ITRN.LE.0) GO TO 91
C----------
C  HEIGHT INCREMENT IS DERIVED FROM A HEIGHT-AGE CURVE AND IS NOMINALLY
C  BASED ON A 1-YEAR GROWTH PERIOD.  SCALE IS USED TO CONVERT
C  HEIGHT INCREMENT PREDICTIONS TO A FINT-YEAR PERIOD.  DIAMETER
C  INCREMENT IS PREDICTED FROM CHANGE IN HEIGHT, AND IS SCALED TO A 1-
C  YEAR PERIOD BY APPLICATION OF SCALE2.  DIAMETER INCREMENT
C  IS CONVERTED TO A FINT-YEAR BASIS IN **UPDATE**.
C----------
      FNT=FINT
      IF(LESTB) THEN
        IF(FINT.LE.5.0) THEN
          LSKIPH=.TRUE.
        ELSE
          FNT=FNT-5.0
        ENDIF
      ENDIF
      SCALE1=FNT/REGYR
      SCALE2=YR/FNT
C----------
C  ENTER GROWTH PREDICTION LOOP.  PROCESS EACH SPECIES AS A GROUP;
C  LOAD CONSTANTS FOR NEXT SPECIES.
C----------
      DO 30 ISPC=1,MAXSP
      I1=ISCT(ISPC,1)
      IF(I1.EQ.0) GO TO 30
      I2=ISCT(ISPC,2)
      XRHGRO=XRHMLT(ISPC)
      XRDGRO=XRDMLT(ISPC)
      CON=RHCON(ISPC) * EXP(HCOR(ISPC))
      XMX=XMAX(ISPC)
      XMN=XMIN(ISPC)
C----------
C  PROCESS NEXT TREE RECORD.
C----------
      DO 25 I3=I1,I2
      I=IND1(I3)
      IP=ITRE(I)
      D=DBH(I)
      IF(D .GE. XMX)GO TO 25
      H=HT(I)
      BARK=BRATIO(ISPC,D,H)
      TBAL=(1.0 - (PCT(I)/100.)) * BA
C----------
C  BYPASS INCREMENT CALCULATIONS IF CALLED FROM ESTAB AND THIS IS NOT A
C  NEWLY CREATED TREE.
C----------
      IF(LESTB) THEN
        IF(I.LT.ITRNIN) GO TO 25
C----------
C  ASSIGN CROWN RATIO FOR NEWLY ESTABLISHED TREES.
C----------
        TCR = 0.89722 - 0.0000461*PCCF(IP)
    1   CONTINUE
        RANC = BACHLO(0.0,1.0,RANN)
        IF(RANC .LT. -1.0 .OR. RANC .GT. 1.0) GO TO 1
        TCR = TCR + 0.07985 * RANC
        IF(TCR .GT. .90) TCR = .90
        IF(TCR .LT. .20) TCR = .20
        ICR(I) = INT((TCR*100.0) + 0.5)
      ENDIF
      IICR=((ICR(I)-1)/10)+1
      IF(IICR.GT.9) IICR=9
      RCR=REAL(IICR)
      K=I
      L=0
      IF(LSKIPH) THEN
        HTG(K)=0.0
        GO TO 5
      ENDIF
C----------
C     RETURN HERE TO PROCESS NEXT TRIPLE.
C----------
    2 CONTINUE

C  CALL SDICAL TO LOAD THE POINT MAX SDI WEIGHTED BY SPECIES ARRAY XMAXPT()
C  RECENT DEAD ARE NOT INCLUDED. IF THEY NEED TO BE, SDICAL.F NEEDS MODIFIED
C  TO DO SO. IWHO PARAMETER HAS NO AFFECT ON XMAXPT ARRAY VALUES.
C
      IWHO = 2
      CALL SDICAL (IWHO, XSDI)
C
C  COMPUTE RELATIVE DENSITY (ZEIDI) FOR INDIVIDUAL POINTS.
C  ALL SPECIES AND ALL SIZES INCLUDED FOR THIS CALCULATION.
C 
      DLO = 0.0
      DHI = 500.0
      ISPEC = 0
      IWHO = 1
      PI2 = INT(PI)
      DO I6 = I, PI2
         CALL SDICLS (ISPEC,DLO,DHI,IWHO,SDIC,SDIC2,A,B,I1)
         ZRD(I1) = SDIC2
      END DO 

      EDH = 0.
      SELECT CASE(ISPC)
C----------
C  CALCULATE HIEGHT GROWTH FOR BOREAL SPECIES 
C----------
      CASE(4:7,13,16:23)
        IF(.NOT.LPERM) THEN
        XSITE=SITEAR(ISPC)
        PBAL=PTBALT(I)
        RDZ1=ITRE(I)
        RDZ=(ZRD(RDZ1))/XMAXPT(RDZ1)
        CRAT=ICR(I)
        ELEVATN=ELEV*100
        AX=NOPERMH1(I)
        BX=NOPERMH2(I)
        CX=NOPERMH3(I)
        DX=NOPERMH4(I)
        EX=NOPERMH5(I)
        FX=NOPERMH6(I)
        GX=NOPERMH7(I)
        HX=NOPERMH8(I)
        IX=NOPERMH9(I)
        HTGR=EXP(AX + BX*(HT(I))**2 + CX*LOG(HT(I)) + DX*PBAL + 
     $         EX*LOG(RDZ) + FX*LOG(CRAT) + GX*ELEVATN +
     $         HX*RDZ + IX*LOG(XSITE))*FINT
        ENDIF
        IF(LPERM) THEN
        XSITE=SITEAR(ISPC)
        PBAL=PTBALT(I)
        RDZ1=ITRE(I)
        RDZ=(ZRD(RDZ1))/XMAXPT(RDZ1)
        CRAT=ICR(I)
        ELEVATN=ELEV*100
        AX1=PERMH1(I)
        BX1=PERMH2(I)
        CX1=PERMH3(I)
        DX1=PERMH4(I)
        EX1=PERMH5(I)
        FX1=PERMH6(I)
        GX1=PERMH7(I)
        HX1=PERMH8(I)
        IX1=PERMH9(I)
        HTGR=EXP(AX1 + BX1 +CX1*(HT(I))**2 + DX1*LOG(HT(I)) + EX1*PBAL + 
     $         FX1*LOG(RDZ) + GX1*LOG(CRAT) + HX1*ELEVATN +
     $         IX1*RDZ)*FINT
        ENDIF
C----------
C  CALCULATE HIEGHT GROWTH FOR COASTAL SPECIES 
C----------     
      CASE(1:3,8:12,14,15)
        XSITE=SITEAR(ISPC)
        PBAL=PTBALT(I)
        RDZ1=ITRE(I)
        RDZ=(ZRD(RDZ1))/XMAXPT(RDZ1)
        CRAT=ICR(I)
        ELEVATN=ELEV*100
        AX=NOPERMH1(I)
        BX=NOPERMH2(I)
        CX=NOPERMH3(I)
        DX=NOPERMH4(I)
        EX=NOPERMH5(I)
        FX=NOPERMH6(I)
        GX=NOPERMH7(I)
        HX=NOPERMH8(I)
        IX=NOPERMH9(I)
        HTGR=EXP(AX + BX*(HT(I))**2 + CX*LOG(HT(I)) + DX*PBAL + 
     $         EX*LOG(RDZ) + FX*LOG(CRAT) + GX*ELEVATN +
     $         HX*RDZ + LOG(XSITE))*FINT
        CASE DEFAULT
          HTGR = 0.
        END SELECT
C       
        ZZRAN = 0.0
        IF(DGSD.GE.1.0) ZZRAN=BACHLO(0.0,1.0,RANN)
        IF((ZZRAN .GT. 0.5) .OR. (ZZRAN .LT. -2.0)) GOTO 5
        IF(DEBUG)WRITE(JOSTND,9984) HTGR,ZZRAN,XRHGRO,SCALE1
 9984   FORMAT('IN REGENT 9984 FORMAT',4(F10.4,2X))
        HTGR = (HTGR +ZZRAN*0.1)*XRHGRO * SCALE1 * CON
C-----------
C     COMPUTE WEIGHTS FOR THE LARGE AND SMALL TREE HEIGHT INCREMENT
C     ESTIMATES.  IF DBH IS LESS THAN OR EQUAL TO XMN, THE LARGE TREE
C     PREDICTION IS IGNORED (XWT=0.0).
C-----------
      XWT=(D-XMN)/(XMX-XMN)
      IF(D.LE.XMN.OR.LESTB) XWT = 0.0
C----------
C     COMPUTE WEIGHTED HEIGHT INCREMENT FOR NEXT TRIPLE.
C----------
      IF(DEBUG)WRITE(JOSTND,9985)XWT,HTGR,HTG(K),I,K
 9985 FORMAT('IN REGENT 9985 FORMAT',3(F10.4,2X),2I7)
      HTG(K)=HTGR*(1.0-XWT) + XWT*HTG(K)
      IF(HTG(K) .LT. .1) HTG(K) = .1
C----------
C CHECK FOR SIZE CAP COMPLIANCE.
C----------
      IF((H+HTG(K)).GT.SIZCAP(ISPC,4))THEN
        HTG(K)=SIZCAP(ISPC,4)-H
        IF(HTG(K) .LT. 0.1) HTG(K)=0.1
      ENDIF
C
    5 CONTINUE
C----------
C     ASSIGN DBH AND COMPUTE DBH INCREMENT FOR TREES WITH DBH LESS
C     THAN 3 INCHES (COMPUTE 10-YEAR DBH INCREMENT REGARDLESS OF
C     PROJECTION PERIOD LENGTH).
C----------
      IF(D.GE.3.0) GO TO 23
      HK=H + HTG(K)
      IF(DEBUG)WRITE(JOSTND,*)'ISPC,H,HTG,HK,D= ',ISPC,H,HTG(K),HK,D
      IF(HK .LE. 4.5) THEN
        DG(K)=0.0
        DBH(K)=D+0.001*HK
      ELSE           
      DTEMP=D
      CALL DGF (DTEMP)
      DK=DTEMP
C----------
C         IF CALLED FROM **ESTAB** ASSIGN DIAMETER
C----------
        IF(LESTB) THEN
          DBH(K)=DK
          IF(DBH(K).LT.0.1) DBH(K)=0.1
          DBH(K)=DBH(K)+0.001*HK
          DG(K)=DBH(K)
        ELSE
C----------
C         COMPUTE DIAMETER INCREMENT BY SUBTRACTION, APPLY USER
C         SUPPLIED MULTIPLIERS, AND CHECK TO SEE IF COMPUTED VALUE
C         IS WITHIN BOUNDS.
C----------
          IF(DK.LT.0.0)THEN
            DG(K)=HTG(K)*0.2*BARK*XRDGRO
            DK=D+DG(K)
          ELSE
            DG(K)=DK*BARK*XRDGRO
          ENDIF
          IF(DG(K) .LT. 0.0) DG(K)=0.0
          IF(DEBUG)WRITE(JOSTND,*)'K,D,DG(K),BARK,SCALE2,YR,FNT = ',
     &    K,D,DG(K),BARK,SCALE2,YR,FNT
C----------
C    SCALE DIAMETER INCREMENT TO 10-YR ESTIMATE.
C    TO BE CONSISTENT WITH GRADD, THE SCALE ADJUSTMENT IS
C    ON GROWTH IN DDS TERMS RATHER THAN INCHES OF DG
C----------     
          DDS=(DG(K)*(2.0*BARK*D+DG(K))) * SCALE2
          DG(K)=SQRT((D*BARK)**2.0+DDS)-BARK*D
        ENDIF
        IF((DBH(K)+DG(K)).LT.DIAM(ISPC))THEN
          DG(K)=DIAM(ISPC)-DBH(K)
        ENDIF
      ENDIF
C----------
C  CHECK FOR TREE SIZE CAP COMPLIANCE
C----------
      CALL DGBND(ISPC,DBH(K),DG(K))
C
   23 CONTINUE
C----------
C  RETURN TO PROCESS NEXT TRIPLE IF TRIPLING.  OTHERWISE,
C  PRINT DEBUG AND RETURN TO PROCESS NEXT TREE.
C----------
      IF(LESTB .OR. .NOT.LTRIP .OR. L.GE.2) GO TO 22
      L=L+1
      K=ITRN+2*I-2+L
      GO TO 2
C----------
C  END OF GROWTH PREDICTION LOOP.  PRINT DEBUG INFO IF DESIRED.
C----------
   22 CONTINUE
      IF(DEBUG)THEN
      HTNEW=HT(I)+HTG(I)
      WRITE(JOSTND,9987) I,ISPC,HT(I),HTG(I),HTNEW,DBH(I),DG(I)
 9987 FORMAT('IN REGENT, I=',I4,',  ISPC=',I3,'  CUR HT=',F7.2,
     &       ',  HT INC=',F7.4,',  NEW HT=',F7.2,',  CUR DBH=',F10.5,
     &       ',  DBH INC=',F7.4)
      ENDIF
   25 CONTINUE
   30 CONTINUE
      GO TO 91
C----------
C  SMALL TREE HEIGHT CALIBRATION SECTION.
C----------
   40 CONTINUE
      DO 45 ISPC=1,MAXSP
      HCOR(ISPC)=0.0
      CORTEM(ISPC)=1.0
      NUMCAL(ISPC)=0
   45 CONTINUE
      IF (ITRN.LE.0) GO TO 91
      IF(IFINTH .EQ. 0)  GOTO 95
      SCALE3 = REGYR / FINTH
C----------
C  BEGIN PROCESSING TREE LIST IN SPECIES ORDER.  DO NOT CALCULATE
C  CORRECTION TERMS IF THERE ARE NO TREES FOR THIS SPECIES.
C----------
      DO 100 ISPC=1,MAXSP
      CORNEW=1.0
      I1=ISCT(ISPC,1)
      IF(I1.EQ.0 .OR. .NOT. LHTCAL(ISPC)) GO TO 100
      N=0
      SNP=0.0
      SNX=0.0
      SNY=0.0
      I2=ISCT(ISPC,2)
      IREFI=IREF(ISPC)
      RSI=SITEAR(ISPC)
      IF(RSI.LT.40.)RSI=40.
C----------
C  BEGIN TREE LOOP WITHIN SPECIES.  IF MEASURED HEIGHT INCREMENT IS
C  LESS THAN OR EQUAL TO ZERO, OR DBH IS LESS THAN 3.0, THE RECORD
C  WILL BE EXCLUDED FROM THE CALIBRATION.
C----------
      DO 60 I3=I1,I2
      I=IND1(I3)
      H=HT(I)
      TBAL=(1.0 - (PCT(I)/100.)) * BA
      IICR=((ICR(I)-1)/10)+1
      IF(IICR.GT.9) IICR=9
      RCR=REAL(IICR)
C----------
C  DIA GT 3 INCHES INCLUDED IN OVERALL MEAN
C----------
      IF(IHTG.LT.2) H=H-HTG(I)
      IF(DBH(I).GE.3.0.OR.H.LT.0.01) GO TO 60
C
C  COMPUTE RELATIVE DENSITY (ZEIDI) FOR INDIVIDUAL POINTS.
C  ALL SPECIES AND ALL SIZES INCLUDED FOR THIS CALCULATION.
C 
      DLO = 0.0
      DHI = 500.0
      ISPEC = 0
      IWHO = 1
      PI2=INT(PI)
      DO I6 = I, PI2
         CALL SDICLS (ISPEC,DLO,DHI,IWHO,SDIC,SDIC2,A,B,I1)
         ZRD(I1) = SDIC2
      END DO 

      EDH = 0.
      SELECT CASE(ISPC)
C----------
C  CALCULATE HIEGHT GROWTH FOR BOREAL SPECIES 
C----------
      CASE(4:7,13,16:23)
        IF(LPERM) GO TO 10
        XSITE=SITEAR(ISPC)
        PBAL=PTBALT(I)
        RDZ1=ITRE(I)
        RDZ=(ZRD(RDZ1))/XMAXPT(RDZ1)
        CRAT=ICR(I)
        ELEVATN=ELEV*100
        AX=NOPERMH1(I)
        BX=NOPERMH2(I)
        CX=NOPERMH3(I)
        DX=NOPERMH4(I)
        EX=NOPERMH5(I)
        FX=NOPERMH6(I)
        GX=NOPERMH7(I)
        HX=NOPERMH8(I)
        IX=NOPERMH9(I)
        EDH=EXP(AX + BX*(HT(I))**2 + CX*LOG(HT(I)) + DX*PBAL + 
     $         EX*LOG(RDZ) + FX*LOG(CRAT) + GX*ELEVATN +
     $         HX*RDZ + IX*LOG(XSITE))*FINT
  10  CONTINUE
        XSITE=SITEAR(ISPC)
        PBAL=PTBALT(I)
        RDZ1=ITRE(I)
        RDZ=(ZRD(RDZ1))/XMAXPT(RDZ1)
        CRAT=ICR(I)
        ELEVATN=ELEV*100
        AX1=PERMH1(I)
        BX1=PERMH2(I)
        CX1=PERMH3(I)
        DX1=PERMH4(I)
        EX1=PERMH5(I)
        FX1=PERMH6(I)
        GX1=PERMH7(I)
        HX1=PERMH8(I)
        IX1=PERMH9(I)
        EDH=EXP(AX1 + BX1 +CX1*(HT(I))**2 + DX1*LOG(HT(I)) + EX1*PBAL + 
     $         FX1*LOG(RDZ) + GX1*LOG(CRAT) + HX1*ELEVATN +
     $         IX1*RDZ)*FINT
C----------
C  CALCULATE HIEGHT GROWTH FOR COASTAL SPECIES 
C----------     
      CASE(1:3,8:12,14,15)
        XSITE=SITEAR(ISPC)
        PBAL=PTBALT(I)
        RDZ1=ITRE(I)
        RDZ=(ZRD(RDZ1))/XMAXPT(RDZ1)
        CRAT=ICR(I)
        ELEVATN=ELEV*100
        AX=NOPERMH1(I)
        BX=NOPERMH2(I)
        CX=NOPERMH3(I)
        DX=NOPERMH4(I)
        EX=NOPERMH5(I)
        FX=NOPERMH6(I)
        GX=NOPERMH7(I)
        HX=NOPERMH8(I)
        IX=NOPERMH9(I)
        EDH=EXP(AX + BX*(HT(I))**2 + CX*LOG(HT(I)) + DX*PBAL + 
     $         EX*LOG(RDZ) + FX*LOG(CRAT) + GX*ELEVATN +
     $         HX*RDZ + LOG(XSITE))*FINT
C----------
C  SPACE FOR OTHER SPECIES
C---------
        CASE DEFAULT
          EDH = 0.
C
      END SELECT

      P=PROB(I)
      IF(HTG(I).LT.0.001) GO TO 60
      TERM=HTG(I) * SCALE3
      SNP=SNP+P
      SNX=SNX+EDH*P
      SNY=SNY+TERM*P
      N=N+1
C----------
C  PRINT DEBUG INFO IF DESIRED.
C----------
      IF(DEBUG)WRITE(JOSTND,9991) NPLT,I,ISPC,H,DBH(I),ICR(I),
     & PCT(I),RELDM1,RHCON(ISPC),EDH,TERM
 9991 FORMAT('NPLT=',A26,',  I=',I5,',  ISPC=',I3,',  H=',F6.1,
     & ',  DBH=',F5.1,',  ICR',I5,',  PCT=',F6.1,',  RELDEN=',
     & F6.1 / 12X,'RHCON=',F10.3,',  EDH=',F10.3,', TERM=',F10.3)
C----------
C  END OF TREE LOOP WITHIN SPECIES.
C----------
   60 CONTINUE
      IF(DEBUG) WRITE(JOSTND,9992) ISPC,SNP,SNX,SNY
 9992 FORMAT(/'SUMS FOR SPECIES ',I2,':  SNP=',F10.2,
     & ';  SNX=',F10.2,';  SNY=',F10.2)
C----------
C  COMPUTE CALIBRATION TERMS.  CALIBRATION TERMS ARE NOT COMPUTED
C  IF THERE WERE FEWER THAN NCALHT (DEFAULT=5) HEIGHT INCREMENT
C  OBSERVATIONS FOR A SPECIES.
C----------
      IF(N.LT.NCALHT) GO TO 80
C----------
C  CALCULATE MEANS FOR THE POPULATION AND FOR THE SAMPLE ON THE
C  NATURAL SCALE.
C----------
      SNX=SNX/SNP
      SNY=SNY/SNP
C----------
C  CALCULATE RATIO ESTIMATOR.
C----------
      CORNEW = SNY/SNX
      IF(CORNEW.LE.0.0) CORNEW=1.0E-4
      HCOR(ISPC)=ALOG(CORNEW)
C----------
C  TRAP CALIBRATION VALUES OUTSIDE 2.5 STANDARD DEVIATIONS FROM THE 
C  MEAN. IF C IS THE CALIBRATION TERM, WITH A DEFAULT OF 1.0, THEN
C  LN(C) HAS A MEAN OF 0.  -2.5 < LN(C) < 2.5 IMPLIES 
C  0.0821 < C < 12.1825
C----------
      IF(CORNEW.LT.0.0821 .OR. CORNEW.GT.12.1825) THEN
        CALL ERRGRO(.TRUE.,27)
        WRITE(JOSTND,9194)ISPC,JSP(ISPC),CORNEW
 9194   FORMAT(T28,'SMALL TREE HTG: SPECIES = ',I2,' (',A3,
     &  ') CALCULATED CALIBRATION VALUE = ',F8.2)
        CORNEW=1.0
        HCOR(ISPC)=0.0
      ENDIF
   80 CONTINUE
      CORTEM(IREFI) = CORNEW
      NUMCAL(IREFI) = N
  100 CONTINUE
C----------
C  END OF CALIBRATION LOOP.  PRINT CALIBRATION STATISTICS AND RETURN
C----------
      WRITE(JOSTND,9993) (NUMCAL(I),I=1,NUMSP)
 9993 FORMAT(/'NUMBER OF RECORDS AVAILABLE FOR SCALING'/
     >       'THE SMALL TREE HEIGHT INCREMENT MODEL',
     >        ((T48,11(I4,2X)/)))
   95 CONTINUE
      WRITE(JOSTND,9994) (CORTEM(I),I=1,NUMSP)
 9994 FORMAT(/'INITIAL SCALE FACTORS FOR THE SMALL TREE'/
     >      'HEIGHT INCREMENT MODEL',
     >       ((T48,11(F5.2,1X)/)))
C----------
C OUTPUT CALIBRATION TERMS IF CALBSTAT KEYWORD WAS PRESENT.
C----------
      IF(JOCALB .GT. 0) THEN
        KOUT=0
        DO 207 K=1,MAXSP
        IF(CORTEM(K).NE.1.0 .OR. NUMCAL(K).GE.NCALHT) THEN
          SPEC=NSP(MAXSP,1)(1:2)
          ISPEC=MAXSP
          DO 203 KK=1,MAXSP
          IF(K .NE. IREF(KK)) GO TO 203
          ISPEC=KK
          SPEC=NSP(KK,1)(1:2)
          GO TO 2031
  203     CONTINUE
 2031     WRITE(JOCALB,204)ISPEC,SPEC,NUMCAL(K),CORTEM(K)
  204     FORMAT(' CAL: SH',1X,I2,1X,A2,1X,I4,1X,F6.3)
          KOUT = KOUT + 1
        ENDIF
  207   CONTINUE
        IF(KOUT .EQ. 0)WRITE(JOCALB,209)
  209   FORMAT(' NO SH VALUES COMPUTED')
        WRITE(JOCALB,210)
  210   FORMAT(' CALBSTAT END')
      ENDIF
   91 IF(DEBUG)WRITE(JOSTND,9995)ICYC
 9995 FORMAT('LEAVING SUBROUTINE REGENT  CYCLE =',I5)
      RETURN
      ENTRY REGCON
C----------
C  ENTRY POINT FOR LOADING OF REGENERATION GROWTH MODEL
C  CONSTANTS  THAT REQUIRE ONE-TIME RESOLUTION.
C---------
      DO 90 ISPC=1,MAXSP
      RHCON(ISPC) = 1.0
      IF(LRCOR2.AND.RCOR2(ISPC).GT.0.0)
     &RHCON(ISPC)=RCOR2(ISPC)
   90 CONTINUE
      RETURN
      END
