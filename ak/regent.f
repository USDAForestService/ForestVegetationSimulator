      SUBROUTINE REGENT(LESTB,ITRNIN)
      IMPLICIT NONE
C----------
C AK $Id$
C----------
C  THIS SUBROUTINE COMPUTES HEIGHT AND DIAMETER INCREMENTS FOR
C  SMALL TREES. THE HEIGHT INCREMENT MODELS ARE APPLIED 
C  TO TREES THAT ARE LESS THAN 3 INCH DBH AND THE DIAMETER INCREMENT
C  MODELS ARE APPLIED TO TREES LESS THAN 1 INCH DBH. FOR TREES THAT ARE 
C  GREATER THAN 1 INCH DBH, HEIGHT INCREMENT PREDICTIONS ARE AVERAGED WITH
C  THE PREDICTIONS FROM THE LARGE TREE MODEL.
C  THIS ROUTINE IS CALLED FROM **CRATET** DURING CALIBRATION AND
C  FROM **TREGRO** DURING CYCLING.  ENTRY **REGCON** IS CALLED FROM
C  **RCON** TO LOAD MODEL PARAMETERS THAT NEED ONLY BE RESOLVED ONCE.
C----------
C
C----------
C SPECIES LIST FOR ALASKA VARIANT.
C Number  V  Code  Common Name         FIA  PLANTS Scientific Name
C   1        SF   Pacific silver fir  011  ABAM   Abies amabilis
C   2        AF   subalpine fir       019  ABLA   Abies lasiocarpa
C   3        YC   Alaska cedar        042  CANO9  Callitropsis nootkatensis
C   4        TA   tamarack            071  LALA   Larix laricina
C   5     P  WS   white spruce        094  PIGL   Picea glauca
C   6     P  LS   Lutz’s spruce            PILU   Picea lutzii
C   7     P  BE   black spruce        095  PIMA   Picea mariana
C   8        SS   Sitka spruce        098  PISI   Picea sitchensis
C   9        LP   lodgepole pine      108  PICO   Pinus contorta
C  10        RC   western redcedar    242  THPL   Thuja plicata
C  11        WH   western hemlock     263  TSHE   Tsuga heterophylla
C  12        MH   mountain hemlock    264  TSME   Tsuga mertensiana
C  13     P  OS   other softwoods     298  2TE
C  14        AD   alder species       350  ALNUS  Alnus species
C  15        RA   red alder           351  ALRU2  Alnus rubra
C  16     P  PB   paper birch         375  BEPA   Betula papyrifera
C  17     P  AB   Alaska birch        376  BENE4  Betula neoalaskana
C  18     P  BA   balsam poplar       741  POBA2  Populus balsamifera
C  19     P  AS   quaking aspen       746  POTR5  Populus tremuloides
C  20     P  CW   black cottonwood    747  POBAT  Populus trichocarpa
C  21     P  WI   willow species      920  SALIX  Salix species
C  22     P  SU   Scouler’s willow    928  SASC   Salix scouleriana
C  23     P  OH   other hardwoods     998  2TD
C----------

COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CALCOM.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'ESTCOR.F77'
C
C
      INCLUDE 'HTCAL.F77'
C
C
      INCLUDE 'MULTCM.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
C
COMMONS
C
C----------
C  VARIABLE DEFINITIONS:
C
C   CORTEM -- A TEMPORARY ARRAY FOR PRINTING CORRECTION TERMS.
C   NUMCAL -- A TEMPORARY ARRAY FOR PRINTING NUMBER OF HEIGHT
C             INCREMENT OBSERVATIONS BY SPECIES.
C    RHCON -- CONSTANT FOR THE HEIGHT INCREMENT MODEL.
C     XMAX -- UPPER END OF THE RANGE OF DIAMETERS OVER WHICH HEIGHT
C             INCREMENT PREDICTIONS FROM SMALL AND LARGE TREE MODELS
C             ARE AVERAGED.
C     XMIN -- LOWER END OF THE RANGE OF DIAMETERS OVER WHICH HEIGHT
C             INCREMENT PREDICTIONS FROM THE SMALL AND LARGE TREE
C             ARE AVERAGED.
C----------
C  VARIABLE DECLARATIONS:
C----------

      EXTERNAL RANN

      LOGICAL DEBUG,LESTB,LSKIPH

      CHARACTER SPEC*2

      INTEGER I,I1,I2,I3,IICR,IP,IREFI,ISPC,ISPEC,ITRNIN
      INTEGER K,KK,KOUT,L,N,MODE0,MODE2
      INTEGER NUMCAL(MAXSP)

      REAL BACHLO,BARK,BRATIO,CON,CORNEW,D,DDS,DK,DKK,EDH
      REAL FNT,H,HK,HTGR,HTNEW,P,RANC,RCR
      REAL REGYR,RSI,SCALE1,SCALE2,SCALE3,SNP,SNX,SNY,TBAL
      REAL TERM,XMN,XMX,XRDGRO,XRHGRO,ZZRAN
      REAL ELEVATN,PBAL,XSITE,CR
      REAL DGSM,DGLT,LTHG,XWT,XDWT,AGET,HTMAX
      REAL CORTEM(MAXSP),DIAM(MAXSP),XMIN(MAXSP),XMAX(MAXSP)
      REAL DGMAX(MAXSP)

C----------
C SPECIES LIST FOR ALASKA VARIANT.
C
C Number Code  Common Name         FIA  PLANTS Scientific Name
C   1     SF   Pacific silver fir  011  ABAM   Abies amabilis
C   2     AF   subalpine fir       019  ABLA   Abies lasiocarpa
C   3     YC   Alaska cedar        042  CANO9  Callitropsis nootkatensis
C   4     TA   tamarack            071  LALA   Larix laricina
C   5     WS   white spruce        094  PIGL   Picea glauca
C   6     LS   Lutz’s spruce            PILU   Picea lutzii
C   7     BE   black spruce        095  PIMA   Picea mariana
C   8     SS   Sitka spruce        098  PISI   Picea sitchensis
C   9     LP   lodgepole pine      108  PICO   Pinus contorta
C  10     RC   western redcedar    242  THPL   Thuja plicata
C  11     WH   western hemlock     263  TSHE   Tsuga heterophylla
C  12     MH   mountain hemlock    264  TSME   Tsuga mertensiana
C  13     OS   other softwoods     298  2TE
C  14     AD   alder species       350  ALNUS  Alnus species
C  15     RA   red alder           351  ALRU2  Alnus rubra
C  16     PB   paper birch         375  BEPA   Betula papyrifera
C  17     AB   Alaska birch        376  BENE4  Betula neoalaskana
C  18     BA   balsam poplar       741  POBA2  Populus balsamifera
C  19     AS   quaking aspen       746  POTR5  Populus tremuloides
C  20     CW   black cottonwood    747  POBAT  Populus trichocarpa
C  21     WI   willow species      920  SALIX  Salix species
C  22     SU   Scouler’s willow    928  SASC   Salix scouleriana
C  23     OH   other hardwoods     998  2TD
C
C----------
C  DIMENSIONS FOR INTERNAL VARIABLES:
C
C   CORTEM -- A TEMPORARY ARRAY FOR PRINTING CORRECTION TERMS.
C   NUMCAL -- A TEMPORARY ARRAY FOR PRINTING NUMBER OF HEIGHT
C             INCREMENT OBSERVATIONS BY SPECIES.
C    RHCON -- CONSTANT FOR THE HEIGHT INCREMENT MODEL.  ZERO FOR ALL
C             SPECIES IN THIS VARIANT
C     XMAX -- UPPER END OF THE DIAMETER RANGE USED FOR WEIGHTING
C             SMALL AND LARGE TREE HEIGHT GROWTH.
C     XMIN -- LOWER END OF THE DIAMETER RANGE USED FOR WEIGHTING
C             SMALL AND LARGE TREE HEIGHT GROWTH.
C     DIAM -- BUD WIDTH (I.E. MINIMUM DIAMETER)
C  STHGMLT -- SMALL TREE HEIGHT GROWTH MULTIPLIERS BY SPECIES. THESE
C             MULTIPLIERS ARE USED TO HELP GET TREES OUT OF THE SMALL
C             SIZE CLASSE.
C----------
C  DATA STATEMENTS:
C----------
      DATA REGYR / 10.0 /

      DATA XMIN / 1.0, 1.0, 1.0, 1.0, 1.0,
     &            1.0, 1.0, 1.0, 1.0, 1.0,
     &            1.0, 1.0, 1.0, 1.0, 1.0,
     &            1.0, 1.0, 1.0, 1.0, 1.0,
     &            1.0, 1.0, 1.0/

      DATA XMAX / 5.0, 5.0, 8.0, 3.0, 3.0,
     &            3.0, 3.0, 5.0, 8.0, 8.0,
     &            7.0, 8.0, 3.0, 3.0, 3.0,
     &            3.0, 3.0, 3.0, 3.0, 3.0,
     &            3.0, 3.0, 3.0/

      DATA DGMAX /5.0, 5.0, 5.0, 3.0, 3.0,
     &            3.0, 3.0, 5.0, 5.0, 5.0,
     &            5.0, 5.0, 3.0, 3.0, 3.0,
     &            3.0, 3.0, 3.0, 3.0, 3.0,
     &            3.0, 3.0, 3.0/

      DATA DIAM / 0.3, 0.3, 0.3, 0.2, 0.4,
     &            0.4, 0.2, 0.4, 0.4, 0.3,
     &            0.3, 0.3, 0.4, 0.2, 0.2,
     &            0.1, 0.1, 0.2, 0.3, 0.2,
     &            0.1, 0.1, 0.2 /

C-----------
C  CHECK FOR DEBUG.
C-----------
      LSKIPH=.FALSE.
      CALL DBCHK (DEBUG,'REGENT',6,ICYC)
      IF(DEBUG) WRITE(JOSTND,9980)ICYC
 9980 FORMAT('ENTERING SUBROUTINE REGENT  CYCLE =',I5)
C----------
C  IF THIS IS THE FIRST CALL TO REGENT, BRANCH TO STATEMENT 40 FOR
C  MODEL CALIBRATION.
C----------
      IF(LSTART) GOTO 40
      CALL MULTS (3,IY(ICYC),XRHMLT)
      CALL MULTS(6,IY(ICYC),XRDMLT)
      IF (ITRN.LE.0) GO TO 91
C----------
C  HEIGHT INCREMENT IS DERIVED FROM A HEIGHT-AGE CURVE AND IS NOMINALLY
C  BASED ON A 1-YEAR GROWTH PERIOD.  SCALE IS USED TO CONVERT
C  HEIGHT INCREMENT PREDICTIONS TO A FINT-YEAR PERIOD.  DIAMETER
C  INCREMENT IS PREDICTED FROM CHANGE IN HEIGHT, AND IS SCALED TO A 1-
C  YEAR PERIOD BY APPLICATION OF SCALE2.  DIAMETER INCREMENT
C  IS CONVERTED TO A FINT-YEAR BASIS IN **UPDATE**.
C----------
      FNT=FINT
      IF(LESTB) THEN
        IF(FINT.LE.5.0) THEN
          LSKIPH=.TRUE.
        ELSE
          FNT=FNT-5.0
        ENDIF
      ENDIF
      
      SCALE1=FNT/REGYR
      SCALE2=YR/FNT
      IF(DEBUG)WRITE(JOSTND,*)'SCALE DEBUG',' FNT=',FNT, 'REGYR ', 
     & REGYR, 'SCALE1 ', SCALE1, 'SCALE2 ', SCALE2
C----------
C  ENTER GROWTH PREDICTION LOOP.  PROCESS EACH SPECIES AS A GROUP;
C  LOAD CONSTANTS FOR NEXT SPECIES.
C----------
      DO 30 ISPC=1,MAXSP
      I1=ISCT(ISPC,1)
      IF(I1.EQ.0) GO TO 30
      I2=ISCT(ISPC,2)
      XRHGRO=XRHMLT(ISPC)
      XRDGRO=XRDMLT(ISPC)
      CON=RHCON(ISPC) * EXP(HCOR(ISPC))
      XMX=XMAX(ISPC)
      XMN=XMIN(ISPC)
C----------
C  PROCESS NEXT TREE RECORD.
C----------
      DO 25 I3=I1,I2
      I=IND1(I3)
      IP=ITRE(I)
      D=DBH(I)
      IF(D .GE. XMX)GO TO 25
      H=HT(I)
      BARK=BRATIO(ISPC,D,H)
      TBAL=(1.0 - (PCT(I)/100.)) * BA
      XSITE=SITEAR(ISPC)
      PBAL=PTBALT(I)
      CR=ICR(I)
      ELEVATN=ELEV*100
C----------
C  BYPASS INCREMENT CALCULATIONS IF CALLED FROM ESTAB AND THIS IS NOT A
C  NEWLY CREATED TREE.
C----------
      IF(LESTB) THEN
        IF(I.LT.ITRNIN) GO TO 25
C----------
C  ASSIGN CROWN RATIO FOR NEWLY ESTABLISHED TREES.
C----------
        CR = 0.89722 - 0.0000461*PCCF(IP)
    1   CONTINUE
        RANC = BACHLO(0.0,1.0,RANN)
        IF(RANC .LT. -1.0 .OR. RANC .GT. 1.0) GO TO 1
        CR = CR + 0.07985 * RANC
        IF(CR .GT. .90) CR = .90
        IF(CR .LT. .20) CR = .20
        ICR(I) = INT((CR*100.0) + 0.5)
        CR = CR * 100
      ENDIF
      IICR=((ICR(I)-1)/10)+1
      IF(IICR.GT.9) IICR=9
      RCR=REAL(IICR)
      K=I
      L=0
      IF(LSKIPH) THEN
        HTG(K)=0.0
        GO TO 5
      ENDIF
C----------
C     RETURN HERE TO PROCESS NEXT TRIPLE.
C----------
    2 CONTINUE
C----------
C  CALCULATE SMALL TREE HEIGHT GROWTH
C  SET MODE ARGUMENT TO 0
C----------

C  INITIALIZE VARIABLES AND CALL HTCALC
      MODE0= 0
      AGET= 0.0
      HTGR= 0.0
      HTMAX=0.0
      CALL HTCALC(MODE0,ISPC,AGET,H,HTMAX,HTGR,JOSTND,DEBUG)

C----------
C  CALL HTCALC TO DETERMINE AN AGE
C  SET MODE ARGUMENT TO 2
C----------

C  IF INITIAL TREE HEIGHT IS >= MAX HEIGHT FOR SITE (HTMAX).
C  IF TRUE, THEN SET HTG= 0.1 FT.
      IF(HTMAX-H.LE.1.) THEN
        IF(DEBUG) WRITE(JOSTND,*) 'HTI>=HTMAX , HTMAX ',HTMAX
        HTGR = 0.10
      ELSE
C ELSE FIND HTGR
        MODE2= 2
        CALL HTCALC (MODE2,ISPC,AGET,H,HTMAX,HTGR,JOSTND,DEBUG)
      ENDIF

    4 CONTINUE
      ZZRAN = 0.0
      IF(DGSD.GE.1.0) ZZRAN=BACHLO(0.0,1.0,RANN)
      IF((ZZRAN .GT. 0.5) .OR. (ZZRAN .LT. -2.0)) GOTO 4
      IF(DEBUG)WRITE(JOSTND,9984) HTGR,ZZRAN,XRHGRO,SCALE1
 9984 FORMAT('IN REGENT 9984 FORMAT',4(F10.4,2X))
      HTGR = (HTGR +ZZRAN*0.1)*XRHGRO * SCALE1 * CON
      IF(DEBUG)WRITE(JOSTND,*)'IN REGENT AFTER 9984',' HTGR=',HTGR
C-----------
C COMPUTE WEIGHTS FOR SMALL TREE GROWTH ESTIMATES.
C IF DBH IS LESS THAN OR EQUAL TO XMN, THE LARGE TREE
C PREDICTION IS IGNORED (XWT 0.0).
C-----------
      XWT=(D-XMN)/(XMX-XMN)
      XDWT=(D-XMN)/(DGMAX(ISPC)-XMN)
      IF(D.LE.XMN.OR.LESTB) THEN
         XWT = 0.0
         XDWT = 0.0
      ENDIF
C----------
C     COMPUTE WEIGHTED HEIGHT INCREMENT FOR NEXT TRIPLE.
C----------
      LTHG = HTG(K)

C     IF TREE IS COMING OUT OF ESTAB THEN DO NOT AVERAGE HTGR WITH LTHG
c     SINCE THERE IS NO LARGE TREE GROWTH ESTIMATE IN THIS SCENARIO.
      IF(LESTB) THEN 
        HTGR= HTGR
      ELSE
        HTGR= (HTGR + LTHG)/2.0
      ENDIF

C     CALCULATE SMALL TREE GROWTH WHICH IS WEIGHTED AVERAGE OF HTGR 
C     AND LTHG.
      HTG(K)= HTGR*(1.0-XWT) + XWT*LTHG
      IF(DEBUG)WRITE(JOSTND,9985)ISPC,D,XWT,HTGR,LTHG,HTG(K),I,K
 9985 FORMAT(' IN REGENT 9985 FORMAT','  ISPC=',I3,
     &       '  D=',F7.2,'  XWT=',F7.2,'  HTGR=',F7.4,'  LTHG=',F7.4,
     &       '  HTG=',F7.4,'  I=',I4, '  K=',I4)
      IF(HTG(K) .LT. .1) HTG(K) = .1
C----------
C CHECK FOR SIZE CAP COMPLIANCE.
C----------
      IF((H+HTG(K)).GT.SIZCAP(ISPC,4))THEN
        HTG(K)=SIZCAP(ISPC,4)-H
        IF(HTG(K) .LT. 0.1) HTG(K)=0.1
      ENDIF
C
    5 CONTINUE
C----------
C     ASSIGN DBH AND COMPUTE DBH INCREMENT FOR TREES WITH DBH LESS
C     THAN DGMAX (COMPUTE 10-YEAR DBH INCREMENT REGARDLESS OF
C     PROJECTION PERIOD LENGTH).
C----------
C IF DBH IS GREATER THAN DGMAX, BYPASS DG INCREMENT CALCULATION
      IF(D.GE.DGMAX(ISPC)) GO TO 23
      HK=H + HTG(K)
      IF(DEBUG)WRITE(JOSTND,*)'ISPC,H,HTG,HK,D= ',ISPC,H,HTG(K),HK,D

C IF HEIGHT IS LESS THAN 4.5' DG IS 0 AND DBH IS ADJUSTED
      IF(HK .LE. 4.5) THEN
        DG(K)=0.0
        DBH(K)=D+0.001*HK
      ELSE           
C----------
C  USE INVENTORY EQUATIONS IF CALIBRATION OF THE HT-DBH FUNCTION IS TURNED
C  OFF, OR IF WYKOFF CALIBRATION DID NOT OCCUR.
C  
C  NOTE: THIS SIMPLIFIES TO IF(IABFLB(ISPC).EQ.1) BUT IS SHOWN IN IT'S
C  ENTIRITY FOR CLARITY.
C----------

C  TEST WHICH HT-DBH RELATIONSHIP SHOULD BE USED FOR DBH IMPUTATION
        IF(.NOT.LHTDRG(ISPC) .OR. 
     &     (LHTDRG(ISPC) .AND. IABFLG(ISPC).EQ.1))THEN

C----------
C  USE INVERTED CHAPMAN RICHARDS FUNCTION (INVENTORY EQUATION) TO CALCULATE
C  DIAMETERS (DK,DKK)
C----------
          DK = 1/HTT12(ISPC) * LOG(1-((HK-4.5)/HTT11(ISPC))**
     &         (1/HTT13(ISPC)))
          IF(H .LE. 4.5) THEN
            DKK=D
          ELSE
            DKK = 1/HTT12(ISPC) * LOG(1-((H-4.5)/HTT11(ISPC))**
     &         (1/HTT13(ISPC)))
         ENDIF

          IF(DEBUG)WRITE(JOSTND,*)'INV EQN DUB,ISPC,H,HK,DK,'
     &      ,'DKK= ',ISPC,H,HK,DK,DKK

          IF(DEBUG)WRITE(JOSTND,*)'ISPC,LHTDRG,IABFLG= ',
     &      ISPC,LHTDRG(ISPC),IABFLG(ISPC)
C----------
C  ELSE USE INVERTED WYKOFF FUNCTION TO CALCULATE DIAMETERS (DK,DKK)
C----------
        ELSE
          DK=HT2(ISPC)/(ALOG(HK-4.5)-HT1(ISPC))-1.0
          IF(H .LE. 4.5) THEN
            DKK=D
          ELSE
            DKK=HT2(ISPC)/(ALOG(H-4.5)-HT1(ISPC))-1.0
          ENDIF
          IF(DEBUG)WRITE(JOSTND,*)'I,ISPC,H,HK,DK,DKK,XRDGRO= ',
     &    I,ISPC,H,HK,DK,DKK,XRDGRO
          ENDIF
C----------
C  IF CALLED FROM **ESTAB** ASSIGN DIAMETER
C----------
        IF(LESTB) THEN
          DBH(K)=DK
          IF(DBH(K).LT.0.1) DBH(K)=0.1
          DBH(K)=DBH(K)+0.001*HK
          DG(K)=DBH(K)
        ELSE
C----------
C  COMPUTE DIAMETER INCREMENT BY SUBTRACTION, APPLY USER
C  SUPPLIED MULTIPLIERS, AND CHECK TO SEE IF COMPUTED VALUE
C  IS WITHIN BOUNDS.
C----------
          IF(DK.LT.0.0 .OR. DKK.LT.0.0)THEN
            DG(K)=HTG(K)*0.2*BARK*XRDGRO
            DK=D+DG(K)
          ELSE
            DGSM = (DK-DKK)*BARK*XRDGRO
          ENDIF
          IF(DGSM .LT. 0.0) DGSM=0.0
C----------
C    SCALE SMALL TREE DIAMETER INCREMENT TO 10-YR ESTIMATE.
C    TO BE CONSISTENT WITH GRADD, THE SCALE ADJUSTMENT IS
C    ON GROWTH IN DDS TERMS RATHER THAN INCHES OF DG.
C
C    NOTE: SMALL TREE DIAMETER GROWTH IS SCALTED TO 10-YEAR
C    ESTIMATE BEFORE WEIGHTING.
C----------
          DDS=(DGSM*(2.0*BARK*D+DGSM)) * SCALE2
          DGSM=SQRT((D*BARK)**2.0+DDS)-BARK*D
          DGLT = DG(K)
          DG(K) = DGSM*(1.0-XDWT) + XDWT*DGLT
          IF(DEBUG)WRITE(JOSTND,*)
     &     'K,D,DG(K),BARK,DWT,DGSM,DGLT,SCALE2,YR,FNT, = ',
     &      K,D,DG(K),BARK,XDWT,DGSM,DGLT,SCALE2,YR,FNT
        ENDIF
        IF((DBH(K)+DG(K)).LT.DIAM(ISPC))THEN
          DG(K)=DIAM(ISPC)-DBH(K)
        ENDIF
      ENDIF
C----------
C  CHECK FOR TREE SIZE CAP COMPLIANCE
C----------
      CALL DGBND(ISPC,DBH(K),DG(K))
C
   23 CONTINUE
C----------
C  RETURN TO PROCESS NEXT TRIPLE IF TRIPLING.  OTHERWISE,
C  PRINT DEBUG AND RETURN TO PROCESS NEXT TREE.
C----------
      IF(LESTB .OR. .NOT.LTRIP .OR. L.GE.2) GO TO 22
      L=L+1
      K=ITRN+2*I-2+L
      GO TO 2
C----------
C  END OF GROWTH PREDICTION LOOP.  PRINT DEBUG INFO IF DESIRED.
C----------
   22 CONTINUE
      IF(DEBUG)THEN
      HTNEW=HT(I)+HTG(I)
      WRITE(JOSTND,9987) I,ISPC,HT(I),HTG(I),HTNEW,DBH(I),DG(I)
 9987 FORMAT('IN REGENT, I=',I4,',  ISPC=',I3,'  CUR HT=',F7.2,
     &       ',  HT INC=',F7.4,',  NEW HT=',F7.2,',  CUR DBH=',F10.5,
     &       ',  DBH INC=',F7.4)
      ENDIF
   25 CONTINUE
   30 CONTINUE
      GO TO 91
C----------
C  SMALL TREE HEIGHT CALIBRATION SECTION.
C----------
   40 CONTINUE
      DO 45 ISPC=1,MAXSP
      HCOR(ISPC)=0.0
      CORTEM(ISPC)=1.0
      NUMCAL(ISPC)=0
   45 CONTINUE
      IF (ITRN.LE.0) GO TO 91
      IF(IFINTH .EQ. 0)  GOTO 95
      SCALE3 = REGYR / FINTH
C----------
C  BEGIN PROCESSING TREE LIST IN SPECIES ORDER.  DO NOT CALCULATE
C  CORRECTION TERMS IF THERE ARE NO TREES FOR THIS SPECIES.
C----------
      DO 100 ISPC=1,MAXSP
      CORNEW=1.0
      I1=ISCT(ISPC,1)
      IF(I1.EQ.0 .OR. .NOT. LHTCAL(ISPC)) GO TO 100
      N=0
      SNP=0.0
      SNX=0.0
      SNY=0.0
      I2=ISCT(ISPC,2)
      IREFI=IREF(ISPC)
      RSI=SITEAR(ISPC)
      IF(RSI.LT.40.)RSI=40.
C----------
C  BEGIN TREE LOOP WITHIN SPECIES.  IF MEASURED HEIGHT INCREMENT IS
C  LESS THAN OR EQUAL TO ZERO, OR DBH IS GREATER THAN 1.0, THE RECORD
C  WILL BE EXCLUDED FROM THE CALIBRATION.
C----------
      DO 60 I3=I1,I2
      I=IND1(I3)
      H=HT(I)
      TBAL=(1.0 - (PCT(I)/100.)) * BA
      IICR=((ICR(I)-1)/10)+1
      IF(IICR.GT.9) IICR=9
      RCR=REAL(IICR)
      XSITE=SITEAR(ISPC)
      PBAL=PTBALT(I)
      CR=ICR(I)
C----------
C  DIA GT 3 INCHES INCLUDED IN OVERALL MEAN
C----------
      IF(IHTG.LT.2) H=H-HTG(I)
      IF(DBH(I).GE.3.0.OR.H.LT.0.01) GO TO 60
      EDH = 0.

C----------
C  CALCULATE SMALL TREE HEIGHT GROWTH
C  SET MODE ARGUMENT TO 0
C----------

C  INITIALIZE VARIABLES AND CALL HTCALC
      MODE0= 0
      AGET= 0.0
      HTGR= 0.0
      HTMAX=0.0
      CALL HTCALC(MODE0,ISPC,AGET,H,HTMAX,HTGR,JOSTND,DEBUG)

C----------
C  CALL HTCALC TO DETERMINE AN AGE
C  SET MODE ARGUMENT TO 2
C----------

C  IF INITIAL TREE HEIGHT IS >= MAX HEIGHT FOR SITE (HTMAX).
C  IF TRUE, THEN SET HTG= 0.1 FT.
      IF(HTMAX-H.LE.1.) THEN
        IF(DEBUG) WRITE(JOSTND,*) 'HTI>=HTMAX , HTMAX ',HTMAX
        HTGR = 0.10
      ELSE
C ELSE FIND HTGR
        MODE2= 2
        CALL HTCALC (MODE2,ISPC,AGET,H,HTMAX,HTGR,JOSTND,DEBUG)
      ENDIF

      EDH=HTGR

      P=PROB(I)
      IF(HTG(I).LT.0.001) GO TO 60
      TERM=HTG(I) * SCALE3
      SNP=SNP+P
      SNX=SNX+EDH*P
      SNY=SNY+TERM*P
      N=N+1
C----------
C  PRINT DEBUG INFO IF DESIRED.
C----------
      IF(DEBUG)WRITE(JOSTND,9991) NPLT,I,ISPC,H,DBH(I),ICR(I),
     & PCT(I),RELDM1,RHCON(ISPC),EDH,TERM
 9991 FORMAT('NPLT=',A26,',  I=',I5,',  ISPC=',I3,',  H=',F6.1,
     & ',  DBH=',F5.1,',  ICR',I5,',  PCT=',F6.1,',  RELDEN=',
     & F6.1 / 12X,'RHCON=',F10.3,',  EDH=',F10.3,', TERM=',F10.3)
C----------
C  END OF TREE LOOP WITHIN SPECIES.
C----------
   60 CONTINUE
      IF(DEBUG) WRITE(JOSTND,9992) ISPC,SNP,SNX,SNY
 9992 FORMAT(/'SUMS FOR SPECIES ',I2,':  SNP=',F10.2,
     & ';  SNX=',F10.2,';  SNY=',F10.2)
C----------
C  COMPUTE CALIBRATION TERMS.  CALIBRATION TERMS ARE NOT COMPUTED
C  IF THERE WERE FEWER THAN NCALHT (DEFAULT=5) HEIGHT INCREMENT
C  OBSERVATIONS FOR A SPECIES.
C----------
      IF(N.LT.NCALHT) GO TO 80
C----------
C  CALCULATE MEANS FOR THE POPULATION AND FOR THE SAMPLE ON THE
C  NATURAL SCALE.
C----------
      SNX=SNX/SNP
      SNY=SNY/SNP
C----------
C  CALCULATE RATIO ESTIMATOR.
C----------
      CORNEW = SNY/SNX
      IF(CORNEW.LE.0.0) CORNEW=1.0E-4
      HCOR(ISPC)=ALOG(CORNEW)
C----------
C  TRAP CALIBRATION VALUES OUTSIDE 2.5 STANDARD DEVIATIONS FROM THE 
C  MEAN. IF C IS THE CALIBRATION TERM, WITH A DEFAULT OF 1.0, THEN
C  LN(C) HAS A MEAN OF 0.  -2.5 < LN(C) < 2.5 IMPLIES 
C  0.0821 < C < 12.1825
C----------
      IF(CORNEW.LT.0.0821 .OR. CORNEW.GT.12.1825) THEN
        CALL ERRGRO(.TRUE.,27)
        WRITE(JOSTND,9194)ISPC,JSP(ISPC),CORNEW
 9194   FORMAT(T28,'SMALL TREE HTG: SPECIES = ',I2,' (',A3,
     &  ') CALCULATED CALIBRATION VALUE = ',F8.2)
        CORNEW=1.0
        HCOR(ISPC)=0.0
      ENDIF
   80 CONTINUE
      CORTEM(IREFI) = CORNEW
      NUMCAL(IREFI) = N
  100 CONTINUE
C----------
C  END OF CALIBRATION LOOP.  PRINT CALIBRATION STATISTICS AND RETURN
C----------
      WRITE(JOSTND,9993) (NUMCAL(I),I=1,NUMSP)
 9993 FORMAT(/'NUMBER OF RECORDS AVAILABLE FOR SCALING'/
     >       'THE SMALL TREE HEIGHT INCREMENT MODEL',
     >        ((T48,11(I4,2X)/)))
   95 CONTINUE
      WRITE(JOSTND,9994) (CORTEM(I),I=1,NUMSP)
 9994 FORMAT(/'INITIAL SCALE FACTORS FOR THE SMALL TREE'/
     >      'HEIGHT INCREMENT MODEL',
     >       ((T48,11(F5.2,1X)/)))
C----------
C OUTPUT CALIBRATION TO DATABASE.
C----------
      CALL DBSCALIB(2,CORTEM,NUMCAL,CORTEM) ! LAST ARG IGNORED
C----------
C OUTPUT CALIBRATION TERMS IF CALBSTAT KEYWORD WAS PRESENT.
C----------
      IF(JOCALB .GT. 0) THEN
        KOUT=0
        DO 207 K=1,MAXSP
        IF(CORTEM(K).NE.1.0 .OR. NUMCAL(K).GE.NCALHT) THEN
          SPEC=NSP(MAXSP,1)(1:2)
          ISPEC=MAXSP
          DO 203 KK=1,MAXSP
          IF(K .NE. IREF(KK)) GO TO 203
          ISPEC=KK
          SPEC=NSP(KK,1)(1:2)
          GO TO 2031
  203     CONTINUE
 2031     WRITE(JOCALB,204)ISPEC,SPEC,NUMCAL(K),CORTEM(K)
  204     FORMAT(' CAL: SH',1X,I2,1X,A2,1X,I4,1X,F6.3)
          KOUT = KOUT + 1
        ENDIF
  207   CONTINUE
        IF(KOUT .EQ. 0)WRITE(JOCALB,209)
  209   FORMAT(' NO SH VALUES COMPUTED')
        WRITE(JOCALB,210)
  210   FORMAT(' CALBSTAT END')
      ENDIF
   91 IF(DEBUG)WRITE(JOSTND,9995)ICYC
 9995 FORMAT('LEAVING SUBROUTINE REGENT  CYCLE =',I5)
      RETURN
      ENTRY REGCON
C----------
C  ENTRY POINT FOR LOADING OF REGENERATION GROWTH MODEL
C  CONSTANTS  THAT REQUIRE ONE-TIME RESOLUTION.
C---------
      DO 90 ISPC=1,MAXSP
      RHCON(ISPC) = 1.0
      IF(LRCOR2.AND.RCOR2(ISPC).GT.0.0)
     &RHCON(ISPC)=RCOR2(ISPC)
   90 CONTINUE
      RETURN
      END
