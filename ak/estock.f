      SUBROUTINE ESTOCK (ELEV,IFT,XBA,XTPA,PN,DEBUG)
      IMPLICIT NONE
C----------
C AK $Id$
C----------
C     CALCULATES PROBABILITY OF STOCKING FOR REGENERATION MODEL.
C     COEFFICIENTS FOR PLANTING ARE NOT INCLUDED IN EQUATIONS.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ESPARM.F77'
C
C
      INCLUDE 'ESCOMN.F77'
C
C
      INCLUDE 'ESCOM2.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
COMMONS
C
C----------
C  VARIABLE DEFINITIONS:
C----------
C
C  IFT      -- STAND FOREST TYPE CATEGORY WHERE:
C                1 = 122
C                2 = 125
C                3 = 270
C                4 = 271
C                5 = 281
C                6 = 301
C                7 = 304
C                8 = 305
C                9 = 703
C               10 = 901
C               11 = 902
C               12 = 904 (MAPPED TO 703)
C               13 = 911
C               14 = OTHER (MAPPED TO 122)
C  XBA      -- PLOT BASAL AREA PER ACRE >= REGNBK
C  XPTA     -- PLOT TREES PER ACRE >= REGNBK
C  XQMD     -- PLOT LEVEL QMD >= REGNBK
C  SLO      -- PLOT SLOPE AS PROPORTION (ESCOMN.F77)
C  ELEV     -- STAND ELEVATION (100S OF FEET)
C  ELEVSQ   -- STAND ELEVATION (100S OF FEET) SQUARED (ESCOMN.F77)
C  XCOS     -- COSINE(ASPECT IN RADIANS) * SLOPE (ESCOMN.F77)
C  PNFT     -- FOREST TYPE COEFFICIENT (B1)
C  PNSBA    -- BASAL AREA COEFFICIENT (B2)
C  PNQMD    -- QMD COEFFICIENT (B3)
C  PNSLO    -- SLOPE COEFFICIENT (B4)
C  PNEVEL   -- ELEVATION COEFFICIENT (B5)
C  PNELEVSQ -- ELEVATION SQUARED COEFFICIENT (B6)
C  PNXCOS   -- COS(ASPECT)*SLOPE COEFFICIENT (B7)
C  PNBAQMD  -- BASAL AREA AND QMD INTERACTION COEFFICIENT (B8)
C  PN       -- USED IN DETERMINING PROBABILITY OF STOCKING
C----------
C  VARIABLE DECLARATIONS:
C----------  
      REAL PNFT(14),PNELEV(14),PNXCOS(14)
      REAL PNSLO,PNELEVSQ,PNSBA,PNQMD,PNBAQMD
      REAL B1,B2,B3,B4,B5,B6,B7,B8,PN
      REAL ELEV,ADJSLO,ADJXC,XBA,XTPA,XQMD
      INTEGER IFT
      LOGICAL DEBUG

      DATA PNFT /
     & 1.525972, 3.479931, 2.230101, 3.457414, 2.884086,
     & 2.884086, 4.057971, 1.748115, 1.052537, 2.718818,
     & 1.538086, 1.525972, 1.728355, 1.525972 /

      DATA PNSBA / -0.002606/

      DATA PNQMD / -0.187452/

      DATA PNSLO / -0.001629 /

      DATA PNELEV / 
     &  0.014416, -0.051071, 0.021339, 0.003037, 0.062246,
     &  0.062246,  0.007363, 0.055765, 0.087327, 0.014415,
     &  0.024643,  0.014416, 0.026169, 0.014416/

      DATA PNELEVSQ / -0.000591 /

      DATA PNXCOS / 
     &  0.009289, -0.006078,  0.001235, -0.000253,  -0.002148,
     & -0.002148,  0.001664, -0.001415, -0.017164,   0.006085,
     & -0.010449,  0.009289,  0.001012,  0.009289/

      DATA PNBAQMD / 0.000348 /
C      
C      SET COEFFICIENTS FOR COMPUTING PN           
C      LP,YC,RC FOREST TYPES: USE WESTERN HEMLOCK FOREST TYPE 
C      COEFFICIENTS.
C
C      B1 = PNFT(IFT)
C      B2 = PNBA
C      B3 = PNQMD
C      B4 = PNSLO
C      B5 = PNELEV
C      B6 = PNELEVSQ
C      B7 = PNXCOS
C      B8 = PNBAQMD

C     SET COEFFICIENTS FOR COMPUTING PN           
      B1 = PNFT(IFT)
      B2 = PNSBA
      B3 = PNQMD
      B4 = PNSLO
      B5 = PNELEV(IFT)
      B6 = PNELEVSQ
      B7 = PNXCOS(IFT)
      B8 = PNBAQMD

C     CALCULATE PLOT QMD FROM PLOT BA AND TPA
C     IF BA IS LESS OR EQUAL TO 1 THEN SET QMD TO 0.1
      IF(XBA .LE. 1.0) THEN 
        XQMD = 0.1
      ELSE
        XQMD=SQRT((XBA/XTPA)/0.005454)
      ENDIF

C     ADJUST PLOT SLO-BASED VARIABLES TO PERCENT SLOPE.  
      ADJSLO = SLO*100
      ADJXC  = XCOS*100

C----------
C  PREDICT STOCKING PROBABILITY
C
C  PN = B1 + B2 * XBA + B3 * XQMD + B4 * ADJSLO + B5 * ELEV +
C  B6 * ELEVSQ + B7 * ADJXC + B8 * XQMD*XBA
C
C  VARIABLES DEFINED IN SECTION ABOVE
C  PN IS CONVERTED TO PROBABILITY OF STOCKING IN ESTAB.F BY
C  EXP(PN)/(1 + EXP(PN))
C----------

C     PREDICT STOCKING PROBABILITY      
      PN = B1 + B2 * XBA + B3 * XQMD + B4 * ADJSLO + B5 * ELEV + 
     &    B6 * ELEVSQ + B7 * ADJXC + B8 * XBA * XQMD

      IF(DEBUG)WRITE(JOSTND,*)'IN ESTOCK CHECKING VALUES',' XBA=',
     & XBA,' XTPA=',XTPA,' XQMD=',XQMD,' ADJSLO=',ADJSLO,' ELEV=',ELEV,
     & ' ELEVSQ=',ELEVSQ,' ADJXC=',ADJXC,' PN=',PN
C
      RETURN
      END
