      SUBROUTINE ESTOCK (ELEV,IFT,XBA,XTPA,PN,DEBUG)
      IMPLICIT NONE
C----------
C AK $Id$
C----------
C     CALCULATES PROBABILITY OF STOCKING FOR REGENERATION MODEL.
C     COEFFICIENTS FOR PLANTING ARE NOT INCLUDED IN EQUATIONS.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ESPARM.F77'
C
C
      INCLUDE 'ESCOMN.F77'
C
C
      INCLUDE 'ESCOM2.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
COMMONS
C
C----------
C  VARIABLE DEFINITIONS:
C----------
C
C  IFT      -- STAND FOREST TYPE CATEGORY WHERE:
C                1 = 122
C                2 = 125
C                3 = 270
C                4 = 271
C                5 = 281
C                6 = 301
C                7 = 304
C                8 = 305
C                9 = 703
C               10 = 901
C               11 = 902
C               12 = 904 (MAPPED TO 703)
C               13 = 911
C               14 = OTHER (MAPPED TO 122)
C  XBA      -- PLOT BASAL AREA PER ACRE >= REGNBK
C  XPTA     -- PLOT TREES PER ACRE >= REGNBK
C  XQMD     -- PLOT LEVEL QMD >= REGNBK
C  SLO      -- PLOT SLOPE AS PROPORTION (ESCOMN.F77)
C  ELEV     -- STAND ELEVATION (100S OF FEET)
C  ELEVSQ   -- STAND ELEVATION (100S OF FEET) SQUARED (ESCOMN.F77)
C  XCOS     -- COSINE(ASPECT IN RADIANS) * SLOPE (ESCOMN.F77)
C  PNFT     -- FOREST TYPE COEFFICIENT (B1)
C  PNSBA    -- BASAL AREA COEFFICIENT (B2)
C  PNQMD    -- QMD COEFFICIENT (B3)
C  PNSLO    -- SLOPE COEFFICIENT (B4)
C  PNEVEL   -- ELEVATION COEFFICIENT (B5)
C  PNELEVSQ -- ELEVATION SQUARED COEFFICIENT (B6)
C  PNXCOS   -- COS(ASPECT)*SLOPE COEFFICIENT (B7)
C  PNBAQMD  -- BASAL AREA AND QMD INTERACTION COEFFICIENT (B8)
C  PN       -- USED IN DETERMINING PROBABILITY OF STOCKING
C----------
C  VARIABLE DECLARATIONS:
C----------  
      REAL PNFT(14),PNELEV(14),PNXCOS(14)
      REAL PNSLO,PNELEVSQ,PNSBA,PNQMD,PNBAQMD
      REAL B1,B2,B3,B4,B5,B6,B7,B8,PN
      REAL ELEV,ADJSLO,ADJXC,XBA,XTPA,XQMD
      INTEGER IFT
      LOGICAL DEBUG

      DATA PNFT /
     & 1.525811, 3.496247, 2.237915, 3.44889, 2.889512,
     & 2.889512, 4.035941, 1.749776, 1.107324, 2.684195,
     & 1.544932, 1.525811, 1.700774, 1.525811 /

      DATA PNSBA / -0.002635/

      DATA PNQMD / -0.187522/

      DATA PNSLO / -0.001573 /

      DATA PNELEV / 
     &  0.014689, -0.046547, 0.020701, 0.004105, 0.061282,
     &  0.061282,  0.014742, 0.056203, 0.082727, 0.017547,
     &  0.024143,  0.014689, 0.032648, 0.014689/

      DATA PNELEVSQ / -0.00058 /

      DATA PNXCOS / 
     &  0.011315, -0.026856, -0.000405,  0.000183, -0.002708,
     & -0.002708,  0.002334,  0.001186, -0.01259,   0.006784,
     & -0.010329,  0.011315,  0.000311,  0.011315/

      DATA PNBAQMD / 0.000349 /
C      
C      SET COEFFICIENTS FOR COMPUTING PN           
C      LP,YC,RC FOREST TYPES: USE WESTERN HEMLOCK FOREST TYPE 
C      COEFFICIENTS.
C
C      B1 = PNFT(IFT)
C      B2 = PNBA
C      B3 = PNQMD
C      B4 = PNSLO
C      B5 = PNELEV
C      B6 = PNELEVSQ
C      B7 = PNXCOS
C      B8 = PNBAQMD

C     SET COEFFICIENTS FOR COMPUTING PN           
      B1 = PNFT(IFT)
      B2 = PNSBA
      B3 = PNQMD
      B4 = PNSLO
      B5 = PNELEV(IFT)
      B6 = PNELEVSQ
      B7 = PNXCOS(IFT)
      B8 = PNBAQMD

C     CALCULATE PLOT QMD FROM PLOT BA AND TPA
C     IF BA IS LESS OR EQUAL TO 1 THEN SET QMD TO 0.1
      IF(XBA .LE. 1.0) THEN 
        XQMD = 0.1
      ELSE
        XQMD=SQRT((XBA/XTPA)/0.005454)
      ENDIF

C     ADJUST PLOT SLO-BASED VARIABLES TO PERCENT SLOPE.  
      ADJSLO = SLO*100
      ADJXC  = XCOS*100

C----------
C  PREDICT STOCKING PROBABILITY
C
C  PN = B1 + B2 * XBA + B3 * XQMD + B4 * ADJSLO + B5 * ELEV +
C  B6 * ELEVSQ + B7 * ADJXC + B8 * XQMD*XBA
C
C  VARIABLES DEFINED IN SECTION ABOVE
C  PN IS CONVERTED TO PROBABILITY OF STOCKING IN ESTAB.F BY
C  EXP(PN)/(1 + EXP(PN))
C----------

C     PREDICT STOCKING PROBABILITY      
      PN = B1 + B2 * XBA + B3 * XQMD + B4 * ADJSLO + B5 * ELEV + 
     &    B6 * ELEVSQ + B7 * ADJXC + B8 * XBA * XQMD

      IF(DEBUG)WRITE(JOSTND,*)'IN ESTOCK CHECKING VALUES',' XBA=',
     & XBA,' XTPA=',XTPA,' XQMD=',XQMD,' ADJSLO=',ADJSLO,' ELEV=',ELEV,
     & ' ELEVSQ=',ELEVSQ,' ADJXC=',ADJXC,' PN=',PN
C
      RETURN
      END
