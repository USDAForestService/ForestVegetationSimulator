      SUBROUTINE ESTOCK (TLAT,ELEV,IFT,XPRD,XPTA,PN)
      IMPLICIT NONE
C----------
C AK $Id$
C----------
C     CALCULATES PROBABILITY OF STOCKING FOR REGENERATION MODEL.
C     COEFFICIENTS FOR PLANTING ARE NOT INCLUDED IN EQUATIONS.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ESPARM.F77'
C
C
      INCLUDE 'ESCOMN.F77'
C
C
      INCLUDE 'ESCOM2.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
COMMONS
C
C----------
C  VARIABLE DEFINITIONS:
C----------
C
C  IFT      -- STAND FOREST TYPE CATEGORY WHERE:
C                1 = 122
C                2 = 125
C                3 = 270
C                4 = 271
C                5 = 281
C                6 = 301
C                7 = 304
C                8 = 305
C                9 = 703
C               10 = 901
C               11 = 902
C               12 = 904 (MAPPED TO 703)
C               13 = 911
C               14 = OTHER (MAPPED TO 122)
C  XPRD     -- PLOT RELATIVE DENSITY >= REGNBK, (SDIZ/SDIMAX)
C  XPTA     -- PLOT TREES PER ACRE >= REGNBK
C  SLO      -- PLOT SLOPE AS PROPORTION (ESCOMN.F77)
C  ELEV     -- STAND ELEVATION (100S OF FEET)
C  ELEVSQ   -- STAND ELEVATION (100S OF FEET) SQUARED (ESCOMN.F77)
C  XCOS     -- COSINE(ASPECT IN RADIANS) * SLOPE (ESCOMN.F77)
C  TLAT     -- STAND LATITUDE
C  PNFT     -- FOREST TYPE COEFFICIENT (B1)
C  PNRDA    -- RELATIVE DENSITY COEFFICIENT (B2)
C  PNPTPAA  -- TPA COEFFICIENT (B3)
C  PNSLO    -- SLOPE COEFFICIENT (B4)
C  PNEVEL   -- ELEVATION COEFFICIENT (B5)
C  PNELEVSQ -- ELEVATION SQUARED COEFFICIENT (B6)
C  PNXCOS   -- COS(ASPECT)*SLOPE COEFFICIENT (B7)
C  PNTLAT   -- STAND LATTITUDE COEFFICIENT (B8)
C  PN       -- USED IN DETERMINING PROBABILITY OF STOCKING
C----------
C  VARIABLE DECLARATIONS:
C----------  
      REAL PN,TLAT,ELEV,XPRD,XPTA,PNFT(14),PNRDA,PNPTPAA,PNSLO,
     &     PNELEV,PNELEVSQ,PNXCOS,PNTLAT,B1,B2,B3,B4,B5,B6,B7,B8,
     &     ADJSLO,ADJXC
      INTEGER IFT
C
C----------
      DATA PNFT /
     & 1.91407,  3.059863, 2.409481, 3.477396, 3.42792,
     & 3.159591, 4.103265, 2.17215, 2.243518, 3.033035,
     &  2.08587, 2.243518, 2.074072, 1.913788 /
      DATA PNRDA / -1.118168 /
      DATA PNPTPAA / 0.001757 /
      DATA PNSLO / -0.001908 /
      DATA PNELEV / 0.044478 /
      DATA PNELEVSQ / -0.00127 /
      DATA PNXCOS / -0.001886 /
      DATA PNTLAT / -0.037725 /
      
C     SET COEFFICIENTS FOR COMPUTING PN           
      B1 = PNFT(IFT)
      B2 = PNRDA
      B3 = PNPTPAA
      B4 = PNSLO
      B5 = PNELEV
      B6 = PNELEVSQ
      B7 = PNXCOS
      B8 = PNTLAT

C     ADJUST PLOT SLO-BASED VARIABLES TO PERCENT SLOPE.  
      ADJSLO = SLO*100
      ADJXC  = XCOS*100   

C     PREDICT STOCKING PROBABILITY      
      PN = B1 + B2 * XPRD + B3 * XPTA + B4 * ADJSLO + B5 * ELEV + 
     &     B6 * ELEVSQ + B7 * ADJXC + B8 * TLAT
C
      RETURN
      END
