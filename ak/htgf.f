      SUBROUTINE HTGF
      IMPLICIT NONE
C----------
C AK $Id$
C----------
C   THIS SUBROUTINE COMPUTES THE PREDICTED PERIODIC HEIGHT
C   INCREMENT FOR EACH CYCLE AND LOADS IT INTO THE ARRAY HTG.
C   CALLED FROM **TREGRO** DURING REGULAR CYCLING.
C   ENTRY **HTCONS** IS CALLED FROM **RCON** TO LOAD SITE
C   DEPENDENT CONSTANTS THAT NEED ONLY BE RESOLVED ONCE.
C----------
C
C Number  V  Code  Common Name         FIA  PLANTS Scientific Name
C   1        SF   Pacific silver fir  011  ABAM   Abies amabilis
C   2        AF   subalpine fir       019  ABLA   Abies lasiocarpa
C   3        YC   Alaska cedar        042  CANO9  Callitropsis nootkatensis
C   4        TA   tamarack            071  LALA   Larix laricina
C   5     P  WS   white spruce        094  PIGL   Picea glauca
C   6     P  LS   Lutz’s spruce            PILU   Picea lutzii
C   7     P  BE   black spruce        095  PIMA   Picea mariana
C   8        SS   Sitka spruce        098  PISI   Picea sitchensis
C   9        LP   lodgepole pine      108  PICO   Pinus contorta
C  10        RC   western redcedar    242  THPL   Thuja plicata
C  11        WH   western hemlock     263  TSHE   Tsuga heterophylla
C  12        MH   mountain hemlock    264  TSME   Tsuga mertensiana
C  13     P  OS   other softwoods     298  2TE
C  14        AD   alder species       350  ALNUS  Alnus species
C  15        RA   red alder           351  ALRU2  Alnus rubra
C  16     P  PB   paper birch         375  BEPA   Betula papyrifera
C  17     P  AB   Alaska birch        376  BENE4  Betula neoalaskana
C  18     P  BA   balsam poplar       741  POBA2  Populus balsamifera
C  19     P  AS   quaking aspen       746  POTR5  Populus tremuloides
C  20     P  CW   black cottonwood    747  POBAT  Populus trichocarpa
C  21     P  WI   willow species      920  SALIX  Salix species
C  22     P  SU   Scouler’s willow    928  SASC   Salix scouleriana
C  23     P  OH   other hardwoods     998  2TD
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'HTCAL.F77'
C
C
      INCLUDE 'MULTCM.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
COMMONS
C
C------------
C  VARIABLE DECLARATIONS:
C----------
C
      LOGICAL DEBUG
C
      INTEGER I,I1,I2,I3,ISPC,ITFN
      INTEGER IWHO,PI2
C
      REAL AGMAX,AGP10,CNEWHT,D,D2,FAHMX
      REAL FAHMX2,H,MISHGF,POTHTG,RNEWHT,SCALE
      REAL SITAGE,SITHT,TEMHTG,XHT,XSITE
      REAL A,B,DHI,DLO,SDIC,SDIC2,XMAX,ZRD(MAXPLT)
      REAL B1,B2,B3,B4,B5,B6,DG10,ELEVATN
      REAL HGCAP(MAXSP),HGMLT(MAXSP)
      REAL PFNUMR,PFDENO,PFMOD
C----------
C  DATA STATEMENTS:
C----------

C ARRAY CONTAINING MAXIMUM HEIGHT VALUES THAT SPECIES ARE EXPECTED TO
C ACHIEVE. VALUES ARE USED TO CONSTRAIN HEIGHT GROWTH VALUES. **SUBJECT TO REMOVAL**.
      DATA HGCAP/
     &      150.0, 120.0, 150.0,  75.0,  150.0, 150.0,  75.0, 250.0,
     &      100.0, 150.0, 200.0, 200.0,  150.0, 130.0, 130.0,  90.0,
     &      90.0,  100.0,  90.0, 100.0,   50.0,  50.0, 100.0/

C ARRAY SUPPLYING HG MULTIPLIERS FOR SPECIES. THESE MULTIPLIERS ARE USED
C TO HELP CERTAIN SPECIES GET CLOSER TO SITE INDEX. MUTIPIERS HELP SPECIES
C HIT THE 95TH PERCENTILE SITE INDEX VALUE FOUND IN THE FITTING DATASET.
C THE MULTIPLIERS WERE CREATED BY TESTING WHAT MULTIPLICATION FACTOR WERE
C REQUIRED FOR A SPECIES TO ACHIEVE 95TH PERCENTILE SITE INDEX DURING
C A 110 YEAR PLANTING SIMULATION. **SUBJECT TO REMOVAL**.

      DATA HGMLT/
     &     1.00, 1.00, 1.00, 1.00, 1.00, 1.00, 1.00, 1.00, 1.00, 1.00,
     &     1.00, 1.00, 1.00, 1.00, 1.00, 1.00, 1.00, 1.00, 1.00, 1.00,
     &     1.00, 1.00, 1.00/

C-----------
C  SEE IF WE NEED TO DO SOME DEBUG.
C-----------
      CALL DBCHK (DEBUG,'HTGF',4,ICYC)
      IF(DEBUG) WRITE(JOSTND,3)ICYC
    3 FORMAT(' ENTERING SUBROUTINE HTGF  CYCLE =',I5)
C
      IF(DEBUG)WRITE(JOSTND,*) 'IN HTGF AT BEGINNING,HTCON=',
     *HTCON,'RMAI=',RMAI,'ELEV=',ELEV
C
      SCALE=FINT/YR
      AGP10=0.0
C----------
C  GET THE HEIGHT GROWTH MULTIPLIERS.
C----------
      CALL MULTS (2,IY(ICYC),XHMULT)

C----------
C  CALL SDICAL TO LOAD THE POINT MAX SDI WEIGHTED BY SPECIES ARRAY XMAXPT()
C  RECENT DEAD ARE NOT INCLUDED. IF THEY NEED TO BE, SDICAL.F NEEDS MODIFIED
C  TO DO SO. IWHO PARAMETER HAS NO AFFECT ON XMAXPT ARRAY VALUES.
C----------
      IWHO = 2
      CALL SDICAL (IWHO, XMAX)

C----------
C  COMPUTE RELATIVE DENSITY (ZEIDI) FOR INDIVIDUAL POINTS.
C  ALL SPECIES AND ALL SIZES INCLUDED FOR THIS CALCULATION.
C----------
      DLO = 0.0
      DHI = 500.0
      ISPC = 0
      IWHO = 1
      PI2=INT(PI)
      DO I1 = 1, PI2
         CALL SDICLS (ISPC,DLO,DHI,IWHO,SDIC,SDIC2,A,B,I1)
         ZRD(I1) = SDIC2
      END DO
C----------
C  SCALE ELEVATION TERM REQUIRED FOR HG CALCULATION
C----------
      ELEVATN=ELEV*100
C----------
C   BEGIN SPECIES LOOP:
C----------
      DO 40 ISPC=1,MAXSP
      I1 = ISCT(ISPC,1)
      IF (I1 .EQ. 0) GO TO 40
      I2 = ISCT(ISPC,2)
      XSITE = SITEAR(ISPC)
      XHT=XHMULT(ISPC)
C-----------
C   BEGIN TREE LOOP WITHIN SPECIES LOOP
C-----------
      DO 30 I3=I1,I2
      I=IND1(I3)
      H=HT(I)
      D=DBH(I)
      HTG(I)=0.
      DG10 = DG(I)
      IF(DG(I) .LE. 0.)DG(I)=0.0001
      IF (PROB(I).LE.0.) GO TO 161
C
      SITAGE = 0.0
      SITHT = 0.0
      AGMAX = 0.0
      FAHMX = 0.0
      FAHMX2 = 0.0
      D2 = 0.0
C      
cld      CALL FINDAG(I,ISPC,D,D2,H,SITAGE,SITHT,AGMAX,FAHMX,FAHMX2,DEBUG)
C      
      IF((HT(I) - 4.5) .LE. 0.)GO TO 161
      IF(DEBUG)WRITE(JOSTND,9050)I,ISP(I),DBH(I),HT(I),ICR(I),
     &AVH,XSITE,SCALE,XHT,DG(I)
 9050 FORMAT('IN HTGF 9050 I,ISP,DBH,HT,ICR,AVH,XSITE,SCALE,XHT,DG=',
     &2I5,2F10.2,I5,5F8.3)

C----------
C  CALCULATE HEIGHT GROWTH FOR ALL SPECIES
C
C  ANNUAL HEIGHT INCREMENT (FT/YEAR) IS CALCULATED USING THE FOLLOWING
C  FUNCTIONAL FORM:
C
C  HI = EXP(X)
C  X = B1 + B2*DBH^2 + B3*LN(DBH) + B4*ELEV + B5*XSITE + B6*DG10
C
C  WHERE 
C  HI: ANNUAL HEIGHT INCREMENT
C  DBH: DIAMETER AT BREAST HEIGHT
C  ELEV: ELEVATION OF THE STAND
C  XSITE: SITE INDEX
C  DG10: 10 YEAR OUTSIDE BARK DIAMETER GROWTH.
C
C  HI IS CONVERTED TO 10 YEAR HEIGHT INCREMENT BY HI * 10 (YR)
C----------

C  LOAD HG COEFFICIENTS FOR SPECIES
      B1=NOPERMH1(ISPC)
      B2=NOPERMH2(ISPC)
      B3=NOPERMH3(ISPC)
      B4=NOPERMH4(ISPC)
      B5=NOPERMH5(ISPC)
      B6=NOPERMH6(ISPC)
      POTHTG=EXP(B1 + B2*D**2 + B3*LOG(D) + B4*ELEVATN +
     &           B5*LOG(XSITE) + B6*LOG(DG10))*YR

C----------
C  CALCULATE PERMAFROST HEIGHT GROWTH MODIFIER IF LPERM IS TRUE
C
C  COMPUTE PERMAFROST MODIFIER BY COMPUTING THE NUMERATOR AND
C  DENOMINATOR WITH THE EQUATION BELOW. NUMERATOR INCLUDES PERM
C  FACTOR AND DENOMINATOR DOES NOT. THE RESULTING MODIFIER
C  SHOULD BE LESS THAN 1.0 IF PERMAFROST IS NOT PRESENT, PFMOD
C  WILL BE 1.0, NO AFFECT.
C
C  HI = EXP(X)
C  X = B1 + PERM(B2) + B3*DBH^2 + B4*LN(DBH) + B5*ELEV + B6*LOG(DG10)
C
C  WHERE 
C  HI: ANNUAL HEIGHT INCREMENT
C  PERM: PERMAFORST PRESENCE VARIABLE
C  DBH: DIAMETER AT BREAST HEIGHT
C  ELEV: ELEVATION OF THE STAND
C  DG10: 10 YEAR OUTSIDE BARK DIAMETER GROWTH.
C----------

      IF(LPERM) THEN
        SELECT CASE (ISPC)
        CASE (5:7, 13, 16:23)
          B1=PERMH1(ISPC)
          B2=PERMH2(ISPC)
          B3=PERMH3(ISPC)
          B4=PERMH4(ISPC)
          B5=PERMH5(ISPC)
          B6=PERMH6(ISPC)

C  NUMERATOR OF PFMOD
          PFNUMR=EXP(B1 + B2 + B3*(D)**2 + B4*LOG(D) + 
     &               B5*ELEVATN + B6*LOG(DG10))*YR

C  DENOMINATOR OF PFMOD
          PFDENO=EXP(B1 + B3*(D)**2 + B4*LOG(D) + B5*ELEVATN +
     &               B6*LOG(DG10))*YR

C  CALCULATE PFMOD
          PFMOD=PFNUMR/PFDENO
        CASE DEFAULT
          PFMOD = 1.0
        END SELECT
      ELSE
        PFMOD = 1.0
      ENDIF
      
C  ADJUST HEIGHT GROWTH BASED ON SPECIES
C  AD, WI, OH: ASSUME MULTIPLIER OF 0.45
C  SU: ASSUME MULTIPLIER OF 0.65
C  ALL OTHER SPECIES: ASSUME A MULTIPLIER OF 1.00
      SELECT CASE (ISPC)
        CASE(14,21,23)
          POTHTG = POTHTG * 0.45
        CASE(22)
          POTHTG = POTHTG * 0.65
        CASE DEFAULT
          POTHTG = POTHTG * 1.00
      END SELECT

      POTHTG = POTHTG * HGMLT(ISPC) * PFMOD

      IF(DEBUG) WRITE(JOSTND,9)
     & I, ISPC, DBH(I), HT(I), ELEVATN, XSITE, DG10, PFMOD, POTHTG
    9 FORMAT(' IN HTGF: I=',I5,' ISPC=',I5,' DBH=',F7.4,' HT=',F8.4,
     & ' ELEVATN=',F7.2,' XSITE=',F7.2, ' DG10=',F7.4, ' PFMOD=',F7.4,
     & ' POTHTG=',F8.4)

C ENFORCE MINIMUM HEIGHT GROWTH VALUE IF NECCESARY
      HTG(I)= POTHTG
      IF(HTG(I) .LE. 0.1) HTG(I) = 0.1
C----------
C IF SMALL DG THEN MINIMAL HTG
C----------
      IF(DG(I) .LE. 0.04) HTG(I) = 0.1
      IF(DEBUG) WRITE(JOSTND,120)
     & CNEWHT,RNEWHT,HT(I),HTG(I),DBH(I),DG(I)
  120 FORMAT(' HTGF 120F CNEWHT,RNEWHT,HT,HTG,DBH,DG =',6F10.4)
C
  161 CONTINUE
C-----------
C   HEIGHT GROWTH EQUATION, EVALUATED FOR EACH TREE EACH CYCLE
C   MULTIPLIED BY SCALE TO CHANGE FROM A YR. PERIOD TO FINT AND
C   MULTIPLIED BY XHT TO APPLY USER SUPPLIED GROWTH MULTIPLIERS.
C----------
      HTG(I)=SCALE*XHT*HTG(I)*EXP(HTCON(ISPC))
C----------
C    APPLY DWARF MISTLETOE HEIGHT GROWTH IMPACT HERE,
C    INSTEAD OF AT EACH FUNCTION IF SPECIAL CASES EXIST.
C----------
      HTG(I)=HTG(I)*MISHGF(I,ISPC)
      TEMHTG=HTG(I)
C----------
C CHECK FOR SIZE CAP COMPLIANCE.
C----------
      IF((HT(I)+HTG(I)).GT.SIZCAP(ISPC,4))THEN
        HTG(I)=SIZCAP(ISPC,4)-HT(I)
        IF(HTG(I) .LT. 0.1) HTG(I)=0.1
      ENDIF
C
      IF(.NOT.LTRIP) GO TO 30
      ITFN=ITRN+2*I-1
      HTG(ITFN)=TEMHTG
C----------
C CHECK FOR SIZE CAP COMPLIANCE.
C----------
      IF((HT(ITFN)+HTG(ITFN)).GT.SIZCAP(ISPC,4))THEN
        HTG(ITFN)=SIZCAP(ISPC,4)-HT(ITFN)
        IF(HTG(ITFN) .LT. 0.1) HTG(ITFN)=0.1
      ENDIF
C
      HTG(ITFN+1)=TEMHTG
C----------
C CHECK FOR SIZE CAP COMPLIANCE.
C----------
      IF((HT(ITFN+1)+HTG(ITFN+1)).GT.SIZCAP(ISPC,4))THEN
        HTG(ITFN+1)=SIZCAP(ISPC,4)-HT(ITFN+1)
        IF(HTG(ITFN+1) .LT. 0.1) HTG(ITFN+1)=0.1
      ENDIF
C
      IF(DEBUG) WRITE(JOSTND,9001) HTG(ITFN),HTG(ITFN+1)
 9001 FORMAT( ' UPPER HTG =',F8.4,' LOWER HTG =',F8.4)
C----------
C   END OF TREE LOOP
C----------
   30 CONTINUE
C----------
C   END OF SPECIES LOOP
C----------
   40 CONTINUE
      IF(DEBUG)WRITE(JOSTND,9002)ICYC
 9002 FORMAT(' LEAVING SUBROUTINE HTGF  CYCLE =',I5)
C
      RETURN
C
      ENTRY HTCONS
C----------
C  ENTRY POINT FOR LOADING HEIGHT INCREMENT MODEL COEFFICIENTS THAT
C  ARE SITE DEPENDENT AND REQUIRE ONE-TIME RESOLUTION.
C----------
C  LOAD OVERALL INTERCEPT FOR EACH SPECIES.
C----------
      DO 50 ISPC=1,MAXSP
      HTCON(ISPC)=0.0
      IF(LHCOR2 .AND. HCOR2(ISPC).GT.0.0) HTCON(ISPC)=
     &    HTCON(ISPC)+ALOG(HCOR2(ISPC))
   50 CONTINUE
C
      RETURN
      END
