      SUBROUTINE CCFCAL(ISPC,D,H,JCR,P,LTHIN,CCFT,CRWDTH,MODE)
      IMPLICIT NONE
C----------
C AK $Id$
C----------
C  THIS ROUTINE COMPUTES CROWN WIDTH AND CCF FOR INDIVIDUAL TREES.
C  CALLED FROM DENSE, PRTRLS, SSTAGE, AND CVCW.
C
C  REFERENCE PAINE AND HANN, ORE STATE UNIV RP 46
C  RA AND CW RELATIONSHIPS FROM PN VARIANT
C
C  ARGUMENT DEFINITIONS:
C    ISPC = NUMERIC SPECIES CODE
C       D = DIAMETER AT BREAST HEIGHT
C       H = TOTAL TREE HEIGHT
C     JCR = CROWN RATIO IN PERCENT (0-100)
C       P = TREES PER ACRE
C   LTHIN = .TRUE. IF THINNING HAS JUST OCCURRED
C         = .FALSE. OTHERWISE
C    CCFT = CCF REPRESENTED BY THIS TREE
C  CRWDTH = CROWN WIDTH OF THIS TREE
C    MODE = 1 IF ONLY NEED CCF RETURNED
C           2 IF ONLY NEED CRWDTH RETURNED
C
C  SPECIES ORDER:
C  1=WS  2=RC  3=SF  4=MH  5=WH  6=YC  7=LP  8=SS  9=AF 10=HD 11=OT
C
C  SOURCES OF COEFFICIENTS:
C    RD10 = SMITH TABLE 1; RITCHIE AND HANN TABLE 2:
C    RD11 = SMITH TABLE 1; RITCHIE AND HANN TABLE 2:
C    COEFFICIENTS USED FOR ALL OTHER SPECIES FIT BY WILBUR FARR (UNPUBLISHED)
C    PATTERNED OFF OF PAINE AND HANN FORMAT.
C
C      PAINE AND HANN, 1982. MAXIMUM CROWN WIDTH EQUATIONS FOR
C        SOUTHWESTERN OREGON TREE SPECIES. RES PAP 46, FOR RES LAB
C        SCH FOR, OSU, CORVALLIS. 20PP.
C      RITCHIE AND HANN, 1985. EQUATIONS FOR PREDICTING BASAL AREA
C        INCREMENT IN DOUGLAS-FIR AND GRAND FIR. RES PAP 51, FOR RES
C        LAB SCH FOR, OSU, CORVALLIS. 9PP. (TABLE 2 PG 8)
C      SMITH 1966. STUDIES OF CROWN DEVELOPMENT ARE IMPROVING CANADIAN
C        FOREST MANAGEMENT. PROCEEDINGS, SIXTH WORLD FORESTRY CONGRESS.
C        MADRID, SPAIN. VOL 2:2309-2315. (TABLES 1 & 2, PG 2310)
C----------
C  VARIABLE DECLARATIONS:
C----------
C
      LOGICAL LDANUW,LTHIN
C
      INTEGER IDANUW,ISPC,JCR,MODE
C
      REAL CCFT,CRWDTH,D,H,P
C
      REAL RD10(3),RD11(3)
C
C----------
C  DATA STATEMENTS:
C
C  SPECIES ORDER
C  1    2    3    4    5    6    7    8    9   10   11  12  13
C WS  WRC  PSF   MH   WH  AYC   LP   SS   SAF  RA   CW  OH  OS
C----------
      DATA RD10/ 1.15394E-1, 4.41381E-2, 4.22070E-3/
C
      DATA RD11/ 4.50757E-4, 2.92090E-3, 4.73186E-3/
C----------
C  DUMMY ARGUMENT NOT USED WARNING SUPPRESSION SECTION
C----------
      IDANUW = JCR
      LDANUW = LTHIN
C----------
C  INITIALIZE RETURN VARIABLES.
C----------
      CCFT = 0.
      CRWDTH = 0.
C----------
C  RED ALDER AND COTTONWOOD FROM WC VARIANT
C----------
      IF(ISPC.EQ.10 .OR. ISPC.EQ.11) THEN
        IF(MODE.EQ.1) THEN
          IF (D .LT. 1.0) THEN
            IF(ISPC .EQ. 10) CCFT = D * (RD10(1)+RD10(2)+RD10(3))
            IF(ISPC .EQ. 11) CCFT = D * (RD11(1)+RD11(2)+RD11(3))
          ELSE
            IF(ISPC .EQ. 10) CCFT=RD10(1)+RD10(2)*D+RD10(3)*D**2.0
            IF(ISPC .EQ. 11) CCFT=RD11(1)+RD11(2)*D+RD11(3)*D**2.0
          ENDIF
          CCFT = CCFT * P
        ENDIF
C----------
C  COMPUTE CROWN WIDTH
C----------
        IF(MODE.EQ.2) THEN
          CALL R6CRWD (ISPC,D,H,CRWDTH)
          IF(CRWDTH .GT. 99.9) CRWDTH=99.9
        ENDIF
      GO TO 100
      ENDIF
C----------
C  COMPUTE CCF.
C----------
      IF(D.GT.0.1) THEN
        CCFT= 0.02987 + 0.02928 * (D**.87) + 0.00717 * (D**1.74)
      ELSE
        CCFT = .001
      ENDIF
C----------
C  COMPUTE CROWN WIDTH.
C----------
      CRWDTH = SQRT(CCFT/0.001803)
      IF(CRWDTH .GT. 99.9) CRWDTH=99.9
C
      CCFT=CCFT*P
C
  100 CONTINUE
      RETURN
      END
