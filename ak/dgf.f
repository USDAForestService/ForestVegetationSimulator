      SUBROUTINE DGF(DIAM)
      IMPLICIT NONE
C----------
C AK $Id$
C----------
C  THIS SUBROUTINE COMPUTES THE VALUE OF DGPRED (CHANGE IN
C  DIAMETER) FOR EACH TREE RECORD, AND LOADS IT INTO THE ARRAY
C  WK2. THE SET OF TREE DIAMETERS TO BE USED IS PASSED AS THE
C  ARGUMENT DIAM.  THE PROGRAM THUS HAS THE FLEXIBILITY TO
C  PROCESS DIFFERENT CALIBRATION OPTIONS.  THIS ROUTINE IS CALLED
C  BY **DGDRIV** DURING CALIBRATION AND WHILE CYCLING FOR GROWTH
C  PREDICTION.  ENTRY **DGCONS** IS CALLED BY **RCON** TO LOAD SITE
C  DEPENDENT COEFFICIENTS THAT NEED ONLY BE RESOLVED ONCE.
C
C----------
C
COMMONS
C
      INCLUDE 'PRGPRM.F77'

      INCLUDE 'ARRAYS.F77'

      INCLUDE 'CALCOM.F77'

      INCLUDE 'COEFFS.F77'

      INCLUDE 'CONTRL.F77'

      INCLUDE 'OUTCOM.F77'

      INCLUDE 'PLOT.F77'

      INCLUDE 'VARCOM.F77'
C
COMMONS
C
C----------
C  VARIABLE DEFINITIONS:
C---------
C
C   DIAM   -- ARRAY LOADED WITH TREE DIAMETERS
C             (PASSED AS AN ARGUEMENT).
C
C  ** FOR THE FOLOWING, ARRAYS HOLD SPECIES SPECIFIC VALUES FOR THE  **
C  ** EQUATIONS AND THE SCALAR COEFFICIENTS ARE NOT SPECIES SPECIFIC.**
C
C   DGBAL  -- ARRAY, COEFFICIENTS FOR THE BASAL AREA IN
C             LARGER TREES TERM IN THE DIAMETER GROWTH EQUATION
C   DGDBAL -- ARRAY, COEFFICIENTS FOR THE INTERACTION
C             BETWEEN POINT BASAL AREA IN LARGER TREES AND LN(DBH)
C   DGDSQ  -- ARRAY, THE COEFFICIENTS FOR THE DIAMETER
C             SQUARED TERM IN THE DIAMETER GROWTH EQUATION
C   DGEL   -- ARRAY, COEFFICIENTS FOR THE ELEVATION TERM IN THE
C             DIAMETER GROWTH EQUATION.
C   DGLD   -- ARRAY, COEFFICIENTS FOR THE LOG(DIAMETER)
C             TERM IN THE DIAMETER GROWTH EQUATION
C   DGLNCR -- ARRAY, COEFFICIENTS FOR THE LOG(CROWN
C             RATIO) TERM IN THE DIAMETER GROWTH EQUATION
C   DGLNSI -- ARRAY, COEFFICIENTS FOR THE LOG(SITE
C             INDEX) TERM IN THE DIAMETER GROWTH EQUATION
C   DGRD   -- ARRAY, COEFFICIENTS FOR POINT RELATIVE DENSITY
C             (PLOT ZEIDE SDI / PLOT MAX SDI – DECIMAL)
C   DGSASP -- ARRAY, COEFFICIENT FOR SLOPE*COS(ASPECT) TERM
C   DGSLOP -- ARRAY, COEFFICIENT FOR SLOPE PERCENT TERM
C   OBSERV -- CONTAINS THE NUMBER OF OBSERVATIONS BY SPECIES FOR THE
C             GROWTH MODEL (THIS DATA IS ACTUALLY USED BY **DGDRIV** 
C             FOR CALIBRATION).
C   PFCON  -- ARRAY, PERMAFROST INTERCEPT COEFFICIENT (b1)
C   PFDBAL -- ARRAY, PERMAFROST POINT BASAL AREA LARGER COEFFIENT (b4)
C   PFDSQ  -- SCALAR, PERMAFROST DBH SQUARED COEFFIENT (b2)
C   PFEL   -- SCALAR, PERMAFROST ELEVATION COEFFIENT (b7)
C   PFLD   -- ARRAY, PERMAFROST NAT LOG OF DBH COEFFIENT (b3)
C   PFLNCR -- SCALAR, PERMAFROST NAT LOG OF CROWN RATIO COEFFIENT (b6)
C   PFPRES -- SCALAR, PERMAFROST PRESENCE/ABSENCE FACTOR (b01)
C   PFRD   -- ARRAY, PERMAFROST ZEIDI POINT RELATIVE DENSITY COEFFIENT (b5)
C   PFSASP -- SCALAR, PERMAFROST SLOPE*cos(ASPECT) COEFFIENT (b9)
C   PFSLOP -- SCALAR, PERMAFROST SLOPE COEFFIENT (b8)
C----------
C
C SPECIES LIST FOR ALASKA VARIANT.
C
C     PERMAFROST
C      AFFECTED
C         | 
C Number  V  Code  Common Name         FIA  PLANTS Scientific Name
C   1        SF   Pacific silver fir  011  ABAM   Abies amabilis
C   2        AF   subalpine fir       019  ABLA   Abies lasiocarpa
C   3        YC   Alaska cedar        042  CANO9  Callitropsis nootkatensis
C   4     P  TA   tamarack            071  LALA   Larix laricina
C   5     P  WS   white spruce        094  PIGL   Picea glauca
C   6     P  LS   Lutz’s spruce            PILU   Picea lutzii
C   7     P  BE   black spruce        095  PIMA   Picea mariana
C   8        SS   Sitka spruce        098  PISI   Picea sitchensis
C   9        LP   lodgepole pine      108  PICO   Pinus contorta
C  10        RC   western redcedar    242  THPL   Thuja plicata
C  11        WH   western hemlock     263  TSHE   Tsuga heterophylla
C  12        MH   mountain hemlock    264  TSME   Tsuga mertensiana
C  13     P  OS   other softwoods     298  2TE
C  14        AD   alder species       350  ALNUS  Alnus species
C  15        RA   red alder           351  ALRU2  Alnus rubra
C  16     P  PB   paper birch         375  BEPA   Betula papyrifera
C  17     P  AB   Alaska birch        376  BENE4  Betula neoalaskana
C  18     P  BA   balsam poplar       741  POBA2  Populus balsamifera
C  19     P  AS   quaking aspen       746  POTR5  Populus tremuloides
C  20     P  CW   black cottonwood    747  POBAT  Populus trichocarpa
C  21     P  WI   willow species      920  SALIX  Salix species
C  22     P  SU   Scouler’s willow    928  SASC   Salix scouleriana
C  23     P  OH   other hardwoods     998  2TD
C----------
C  VARIABLE DECLARATIONS:
C----------
      LOGICAL DEBUG
      INTEGER I ,I1, I2, I3, ISPC, IWHO

      REAL PBAL,BRAT,BRATIO,CONSPP,CR,D,XMAX

      REAL SSITE,TEMEL

      REAL DGCONB1(MAXSP), DGDBAL(MAXSP), DGDISQ(MAXSP), DGEL(MAXSP),
     &     DGLD(MAXSP), DGLNCR(MAXSP), DGLNSI(MAXSP), DGRD(MAXSP),
     &     DGSASP(MAXSP), DGSLOP(MAXSP)
      REAL A, B, D2, DGCOMP1, DGCOMP2, DGPRED, DHI, DLO, PRD,
     &     SDICS, SDICZ, TEMSLP, TEMSASP, ZRD(MAXPLT)

      REAL PFPRES, PFCON(MAXSP), PFDBAL(MAXSP), PFDSQ, PFEL, 
     &     PFLD(MAXSP), PFLNCR, PFRD(MAXSP), PFSASP, PFSLOP
      REAL PFCOMP1, PFCOMP2, PFMOD, BASEDG

      REAL DUP, TEMPD1, TEMPD2
      
      REAL DIAM(MAXTRE), OBSERV(MAXSP)
C     , DGRAT(MAXSP)
      
C----------
C  DATA STATEMENTS:
C----------
C *** BEGIN DIAMETER GROWTH EQUATION COEFFICIENTS ***
C DESCRIBED IN DOCUMENTATION AS
C INTERCEPT -- b1
      DATA DGCONB1 /
     & -8.580747, -8.580747, -9.485113, -6.585228, -6.315766,
     & -6.315766, -6.585228, -8.580747, -9.297786, -9.357119, 
     & -9.458059, -9.155381, -6.315766, -5.646338, -5.195889, 
     & -6.015696, -6.015696, -5.646338, -6.704925, -5.646338, 
     & -5.646338, -5.646338, -5.646338 /

C DESCRIBED IN DOCUMENTATION AS COEFFIENT FOR
C DBH^2 -- b2
      DATA DGDISQ /
     & -0.000439, -0.000439, -0.000439, -0.002279, -0.002279, 
     & -0.002279, -0.002279, -0.000439, -0.000439, -0.000439, 
     & -0.000439, -0.000439, -0.002279, -0.002279, -0.002279, 
     & -0.002279, -0.002279, -0.002279, -0.002279, -0.002279, 
     & -0.002279, -0.002279, -0.002279 /

C DESCRIBED IN DOCUMENTATION AS COEFFIENT FOR
C ln(DBH) -- b3
      DATA DGLD /
     &  0.033655,  0.033655,  0.081005,  0.152853,  0.480982, 
     &  0.480982,  0.152853,  0.033655, -0.000862,  0.243224, 
     &  0.175997,  0.044554,  0.480982,  0.212906,  0.425682, 
     &  0.353350,  0.353350,  0.212906,  0.669461,  0.212906, 
     &  0.212906,  0.212906,  0.212906 /

C DESCRIBED IN DOCUMENTATION AS COEFFIENT FOR
C PBAL -- b4
      DATA DGDBAL /
     & -0.002283, -0.002283, -0.000598, -0.001513, -0.000057, 
     & -0.000057, -0.001513, -0.002283, -0.000598, -0.000598, 
     & -0.000595, -0.000580, -0.000057, -0.000722, -0.001166, 
     & -0.007165, -0.007165, -0.000722, -0.000557, -0.000722, 
     & -0.000722, -0.000722, -0.000722 /

C DESCRIBED IN DOCUMENTATION AS COEFFIENT FOR
C PRD -- b5
      DATA DGRD /
     &  0.0,       0.0,       0.0,      -0.353197, -0.455502,
     & -0.455502, -0.353197,  0.0,       0.0,       0.0, 
     &  0.0,       0.0,      -0.455502, -0.763633, -0.623822, 
     & -0.353197, -0.353197, -0.763633, -0.480135, -0.763633, 
     & -0.763633, -0.763633, -0.763633 /

C DESCRIBED IN DOCUMENTATION AS COEFFIENT FOR
C ln(CR) -- b6
      DATA DGLNCR /
     & 0.819909, 0.819909, 0.819909, 0.727631, 0.727631, 
     & 0.727631, 0.727631, 0.819909, 0.819909, 0.819909, 
     & 0.819909, 0.819909, 0.727631, 0.727631, 0.727631, 
     & 0.727631, 0.727631, 0.727631, 0.727631, 0.727631, 
     & 0.727631, 0.727631, 0.727631 /

C DESCRIBED IN DOCUMENTATION AS COEFFIENT FOR
C ELEV-- b7
      DATA DGEL /
     &  0.0,       0.0,       0.0,      -0.000175, -0.000175, 
     & -0.000175, -0.000175,  0.0,       0.0,       0.0,      
     &  0.0,       0.0,      -0.000175, -0.000175, -0.000175, 
     & -0.000175, -0.000175, -0.000175, -0.000175, -0.000175, 
     & -0.000175, -0.000175, -0.000175 /

C DESCRIBED IN DOCUMENTATION AS COEFFIENT FOR
C SLOPE -- b8
      DATA DGSLOP /
     & 0.001604, 0.001604, 0.001604, 0.001225, 0.001225, 
     & 0.001225, 0.001225, 0.001604, 0.001604, 0.001604, 
     & 0.001604, 0.001604, 0.001225, 0.001225, 0.001225, 
     & 0.001225, 0.001225, 0.001225, 0.001225, 0.001225, 
     & 0.001225, 0.001225, 0.001225 /

C DESCRIBED IN DOCUMENTATION AS COEFFIENT FOR
C SLOPE*cos(ASPECT) -- b9
      DATA DGSASP /
     &  0.000020,  0.000020,  0.000020, -0.002424, -0.002424, 
     & -0.002424, -0.002424,  0.000020,  0.000020,  0.000020, 
     &  0.000020,  0.000020, -0.002424, -0.002424, -0.002424, 
     & -0.002424, -0.002424, -0.002424, -0.002424, -0.002424, 
     & -0.002424, -0.002424, -0.002424 /

C DESCRIBED IN DOCUMENTATION AS COEFFIENT FOR
C ln(SI) -- b10
      DATA DGLNSI /
     & 0.712498, 0.712498, 0.712498, 0.0,      0.0,      
     & 0.0,      0.0,      0.712498, 0.712498, 0.712498, 
     & 0.712498, 0.712498, 0.0,      0.0,      0.0,      
     & 0.0,      0.0,      0.0,      0.0,      0.0,      
     & 0.0,      0.0,      0.0 /

CC *** END DIAMETER GROWTH EQUATION COEFFICIENTS ***
C----------
C *** BEGIN PERMAFROST DIAMETER GROWTH MODIFIER COEFFICIENTS ***
C DESCRIBED IN DOCUMENTATION AS PERMAFROST PRESENCE/ABSENCE FACTOR
C IN THE CALCULATION OF THE MODIFIER.
C ADDITIVE FACTOR -- b01
      DATA PFPRES / -0.349215 /

C DESCRIBED IN DOCUMENTATION AS PERMAFROST COEFFICIENT FOR
C INTERCEPT -- b1
      DATA PFCON /
     &       0.0,       0.0,       0.0, -6.529717, -6.274336,
     & -6.274336, -6.529717,       0.0,       0.0,       0.0,
     &       0.0,       0.0, -6.274336,       0.0,       0.0,
     & -6.093543, -6.093543, -5.527782, -6.861172, -5.527782,
     & -5.527782, -5.527782, -5.527782/

C DESCRIBED IN DOCUMENTATION AS PERMAFROST COEFFIENT FOR
C DBH^2 -- b2
      DATA PFDSQ / -0.002711 /

C DESCRIBED IN DOCUMENTATION AS PERMAFROST COEFFIENT FOR
C ln(DBH) -- b3
      DATA PFLD /
     &      0.0,      0.0,      0.0, 0.161256, 0.476859,
     & 0.476859, 0.161256,      0.0,      0.0,      0.0,
     &      0.0,      0.0, 0.476859,      0.0,      0.0,
     & 0.373362, 0.373362, 0.082885, 0.712862, 0.082885,
     & 0.082885, 0.082885, 0.082885/

C DESCRIBED IN DOCUMENTATION AS PERMAFROST COEFFIENT FOR
C PBAL -- b4
      DATA PFDBAL /
     &       0.0,       0.0,       0.0, -0.001871, -0.000531,
     & -0.000531, -0.001871,       0.0,       0.0,       0.0,
     &       0.0,       0.0, -0.000531,       0.0,       0.0,
     & -0.007347, -0.007347, -0.001321, -0.000533, -0.001321,
     & -0.001321, -0.001321, -0.001321 /

C DESCRIBED IN DOCUMENTATION AS PERMAFROST COEFFIENT FOR
C PRD -- b5
      DATA PFRD /
     &       0.0,       0.0,       0.0,  -0.02879, -0.482094,
     & -0.482094, -0.02879,       0.0,        0.0,       0.0,
     &       0.0,       0.0, -0.482094,       0.0,       0.0,
     & -0.330313, -0.330313, -0.819852, -0.462575, -0.819852,
     & -0.819852, -0.819852, -0.819852 /

C DESCRIBED IN DOCUMENTATION AS PERMAFROST COEFFIENT FOR
C ln(CR) -- b6
      DATA PFLNCR / 0.736013 /

C DESCRIBED IN DOCUMENTATION AS PERMAFROST COEFFIENT FOR
C ELEV-- b7
      DATA PFEL / -0.000137 /

C DESCRIBED IN DOCUMENTATION AS PERMAFROST COEFFIENT FOR
C SLOPE -- b8
      DATA PFSLOP / 0.001702 /

C DESCRIBED IN DOCUMENTATION AS PERMAFROST COEFFIENT FOR
C SLOPE*cos(ASPECT) -- b9
      DATA PFSASP / -0.001819 /
C
C *** END PERMAFROST DIAMETER GROWTH MODIFIER COEFFICIENTS ***
C----------
C  NUMBER OF DATA OBSERVATIONS BY SPECIES FOR GROWTH MODEL
C
      DATA OBSERV /
     &  4346.0,  4346.0,  2781.0, 29501.0, 70550.0,
     & 70550.0, 29501.0,  4346.0,   879.0,  1546.0,
     & 10395.0,  5872.0, 70550.0,  4727.0,   202.0,
     & 32332.0, 32332.0,  4727.0, 21380.0,  4727.0,
     &  4727.0,  4727.0,  4727.0/
     
C  DATAFRAME OF DG MODIFIERS USED TO CORRECT FOR ANNUALIZED DG
C      DATA DGRAT/
C     &  0.99, 0.99, 1.00, 0.99, 1.01, 1.01, 0.99, 0.99, 1.00, 1.00,
C     &  1.00, 1.00, 1.01, 0.98, 0.98, 0.97, 0.97, 0.98, 1.00, 0.98,
C     &  0.98, 0.98, 0.98/

C-----------
C  SEE IF WE NEED TO DO SOME DEBUG.
C-----------
      CALL DBCHK (DEBUG,'DGF',3,ICYC)
      IF(DEBUG) WRITE(JOSTND,2)ICYC
    2 FORMAT(' ENTERING DGF  CYCLE =',I5)
C----------
C  DEBUG OUTPUT: MODEL COEFFICIENTS.
C----------
      IF(DEBUG)WRITE(JOSTND,*) 'IN DGF,HTCON=',HTCON
      IF(DEBUG)WRITE(JOSTND,*)'RMAI=',RMAI,'ELEV=',ELEV,'RELDEN=',RELDEN
      IF(DEBUG)
     & WRITE(JOSTND,9000) DGCON,DGDSQ,DGLD,DGLNCR,DGDBAL
 9000 FORMAT(/11(1X,F10.5))
C----------
C  DIAMETER GROWTH EQUATION
C
C  THIS IS OUTSIDE BARK ANNUAL DIAMETER INCREMENT THAT WILL BE
C  CONVERTED TO INSIDE BARK BASAL AREA (DDS) INCREMENT WHEN
C  LOADED INTO WK2 ARRAY AT END OF TREE LOOP.
C
C  ADI = exp(X)
C
C  X = b1 + b2 * DBH^2 + b3 * ln(DBH) + b4 * PBAL+ b5 * PRD
C    + b6 * ln(CR) + b7 * ELEV + b8 * SLOPE
C    + b9 * SLOPE * cos(ASPECT) + b10*ln(SI)
C
C  Example:
C  X = b1 + b2*(10.0)^2 + b3*ln(10.0) + b4*(100.0) + b5*(0.35)
C    + b6*ln(45) + b7*1500 + b8*50
C    + b9*50*cos(3.14159) + b10*ln(75)
C
C  WHERE:
C  ADI = Annual diameter increment (in year-1)
C  DBH = Diameter at breast height (in)
C  PBAL = Plot level basal area in larger trees (ft2 * acre-1)
C  PRD = Plot Relative density (plot Zeide SDI / plot Max SDI – decimal)
C  CR = Crown ratio (%)
C  ELEV = Elevation of plot (ft)
C  SLOPE = Slope of plot (%)
C  ASPECT = Aspect of plot (radians)
C  SI = site index (ft)
C
C----------
C
C  CALL SDICAL TO LOAD THE POINT MAX SDI WEIGHTED BY SPECIES ARRAY XMAXPT()
C  RECENT DEAD ARE NOT INCLUDED. IF THEY NEED TO BE, SDICAL.F NEEDS MODIFIED
C  TO DO SO. IWHO PARAMETER HAS NO AFFECT ON XMAXPT ARRAY VALUES.
C
      IWHO = 2
      CALL SDICAL (IWHO, XMAX)
C
C  LOAD RELATIVE DENSITY (ZEIDI) FOR INDIVIDUAL POINTS.
C  ALL SPECIES AND ALL SIZES INCLUDED FOR THIS CALCULATION.
C
      DLO = 0.0
      DHI = 500.0
      ISPC = 0
      IWHO = 1
      I2 = INT(PI)
      
      IF(DEBUG)WRITE(JOSTND,*) 'IN DGF, CALL SDICLS - POINT COUNT ',I2
      DO I1 = 1, I2
         CALL SDICLS (ISPC,DLO,DHI,IWHO,SDICS,SDICZ,A,B,I1)
         IF(DEBUG)WRITE(JOSTND,*) 'IN DGF, POINT ',I1,' SDICZ= ',SDICZ
         ZRD(I1) = SDICZ
      END DO
      
C     SET ELEVATION, SLOPE, AND ASPECT VALUES
      TEMEL = ELEV * 100.0
      TEMSLP = SLOPE * 100.0
      TEMSASP = TEMSLP * COS(ASPECT)

C----------
C  BEGIN SPECIES LOOP.  ASSIGN VARIABLES WHICH ARE SPECIES DEPENDENT
C----------
      DO ISPC=1,MAXSP
        I1=ISCT(ISPC,1)
        IF(DEBUG)WRITE(JOSTND,*)
     &    'IN DGF, TOP OF SPECIES LOOP, ISPC= ',ISPC,' ISCT= ',I1
        IF(I1.EQ.0) GO TO 20
        I2=ISCT(ISPC,2)
        SSITE = SITEAR(ISPC)
        CONSPP= DGCONB1(ISPC) + COR(ISPC)
C
C       ELEVATION, SLOPE, ASPECT AND SITE INDEX COMPONENT OF
C       DIAMETER GROWTH EQUATION
C       THESE ARE THE COMPONENTS OF THE EQUATION THAT ARE NOT
C       TREE SPECIFIC AND ARE LOADED IN DGCONS ENTRY AT END OF
C       THIS ROUTINE.
C
        DGCOMP1 = DGEL(ISPC) * TEMEL
     &          + DGSLOP(ISPC) * TEMSLP
     &          + DGSASP(ISPC) * TEMSASP
     &          + DGLNSI(ISPC) * LOG(SSITE)

C       IF PRESENCE OF PERMAFROST IS TRUE, SET UP FIRST COMPONENTS
c       OF MODIFIER VARIABLES FOR PERMAFROST AFFECTED SPECIES. 
C       THESE ARE NOT POINT OR TREE SPECIFIC: ELEVATION, SLOPE, ASPECT
C
        IF (LPERM) THEN
          SELECT CASE (ISPC)
            CASE (4:7, 13, 16:23)
       	      PFCOMP1 = PFEL * TEMEL
     &                + PFSLOP * TEMSLP
     &                + PFSASP * TEMSASP
            CASE DEFAULT
              PFCOMP1 = 0.0
          END SELECT
        ELSE
          PFCOMP1 = 0.0
        ENDIF

C----------
C  BEGIN TREE LOOP WITHIN SPECIES ISPC.
C----------
        DO I3=I1,I2
          I = IND1(I3)
          D = DIAM(I)
          IF (D.LE.0.0) GOTO 10
          D2 = D*D
          CR = REAL(ICR(I))

C         BASAL AREA IN LARGER TREES ON THE POINT
          PBAL = PTBALT(I)

C         RELATIVE DENSITY (ZEIDI) ON THE POINT
C         CONSTRAIN RD IF NEEDED
          IF (XMAXPT(ITRE(I)).LE.0.0) THEN
           PRD = 0.01
          ELSE
           PRD = ZRD(ITRE(I)) / XMAXPT(ITRE(I))
          ENDIF
C
C         DIAMETER, PBAL, RELATIVE DENSITY (ZEIDI) AND CROWN RATIO
C         COMPONENTS OF DIAMETER GROWTH EQUATION.
C         THESE COMPONENTS USE POINT OR TREE SPECIFIC VLUES.
          DGCOMP2 = DGDISQ(ISPC) * D2
     &            + DGLD(ISPC) * LOG(D)
     &            + DGDBAL(ISPC) * PBAL
     &            + DGRD(ISPC) * PRD
     &            + DGLNCR(ISPC) * LOG(CR)
C
C         ANNUAL DIAMETER GROWTH = exp(X)
C         X = b1 + b2 * DBH^2 + b3 * ln(DBH) + b4 * PBAL + b5 * PRD
C             + b6 * ln(CR) + b7 * ELEV + b8 * SLOPE
C             + b9 * SLOPE * cos(ASPECT) + b10*ln(SI)
C
C         DGCOMP2 INCLUDES: b2, b3, b4, b5, b6
C         DGCOMP1 INCLUDES: b7, b8, b9, b10 
          BASEDG = (EXP(DGCONB1(ISPC) + DGCOMP2 + DGCOMP1))
C
C         IF PRESENCE OF PERMAFROST IS TRUE, SET UP SECOND COMPONENTS
C         OF MODIFIER VARIABLES FOR PERMAFROST AFFECTED SPECIES. 
C         THESE ARE POINT AND TREE SPECIFIC:
C         DIAMETER, POINT BAL, POINT RELATIVE DENSITY (ZEIDI) AND
C         CROWN RATIO
C
C         COMPUTE PERMAFROST MODIFIER BY COMPUTING THE NUMERATOR WITH 
C         THE EQUATION BELOW. NUMERATOR INCLUDES PERM FACTOR WHEN 
C         PERMAFROST IS TURNED ON FOR AFFECTED SPECIES. NUMERATOR
C         DOES NOT INCLUDE PERM FACTOR WHEN TURNED OFF FOR AFFECTED 
C         SPECIES. 
C
C         ADI = exp(X)
C
C         X = b1 + PERM + b2 * DBH2 + b3 * ln(DBH) + b4 * PBAL
C             + b5 * PRD + b6 * ln(CR) + b7 * ELEV + b8 * SLOPE
C             + b9 * SLOPE * cos(ASPECT)
C         WHERE:
C         ADI = Annual diameter increment (in year-1)
C         PERM = permafrost presence variable
C         DBH = Diameter at breast height (in)
C         PBAL = Plot level basal area in larger trees (ft2 * acre-1)
C         PRD = Relative density (plot Zeide SDI / plot Max SDI - decimal)
C         CR = Crown ratio (%)
C         ELEV = Elevation of plot (ft)
C         SLOPE = Slope of plot (%)
C         ASPECT = Aspect of plot (radians)
          SELECT CASE (ISPC)
            CASE (4:7, 13, 16:23)
              PFCOMP2 = PFDSQ * D2
     &                  + PFLD(ISPC) * LOG(D)
     &                  + PFDBAL(ISPC) * PBAL
     &                  + PFRD(ISPC) * PRD
     &                  + PFLNCR * LOG(CR)
              IF (LPERM) THEN
                PFMOD = EXP(PFCON(ISPC) + PFPRES + PFCOMP2 + 
     &          PFCOMP1)/BASEDG
                IF (PFMOD.GT.1.0)PFMOD=1.0
              ELSE
                PFMOD = EXP(PFCON(ISPC) + PFCOMP2 + 
     &          PFCOMP1)/BASEDG
                IF (PFMOD.LT.1.0)PFMOD=1.0
              ENDIF
            CASE DEFAULT
              PFMOD = 1.0
          END SELECT
          IF(DEBUG)WRITE(JOSTND,*)
     &    'IN DGF, ISPC= ',ISPC,' ISCT= ',I1, ' BASEDG=', BASEDG,
     &    ' PFMOD= ', PFMOD
C
C         ANNUAL DIAMETER GROWTH WITH PERMAFROST MODIFIER
C         EXPANDED TO PERIOD.
          DGPRED = YR * BASEDG * PFMOD
     
C         ADJUST DIAMETER GROWTH BASED ON SPECIES
C         AD, WI, OH: ASSUME MULTIPLIER OF 0.45
C         SU: ASSUME MULTIPLIER OF 0.65
C         ALL OTHER SPECIES: ASSUME A MULTIPLIER OF 1.00
          SELECT CASE (ISPC)
            CASE(14,21,23)
              DGPRED = DGPRED * 0.45
            CASE(22)
              DGPRED = DGPRED * 0.65
            CASE DEFAULT
              DGPRED = DGPRED * 1.00
          END SELECT

C          IF(DEBUG) WRITE(JOSTND,8000) ISPC, D, DGPRED
          IF(DEBUG) WRITE(JOSTND,8000) ISPC, D, DGCONB1(ISPC), DGPRED,
     &    DGCOMP2, DGCOMP1, SSITE, FINT
C 8000     FORMAT('IN DGF ISPC, D, DGPRED= ',I3,2F12.5)
 8000     FORMAT('IN DGF ISPC, D, B1, DGPRED, DGCOMP2, ',
     &           'DGCOMP1, SSITE, FINT,= ',
     &           I3,7F15.5)

C         CONVERT OUTSIDE BARK DIAMETER INCREMENT TO INSIDE BARK
C         CHANGE IN SQUARED DIAMETERS AND LOAD INTO WK2 ARRAY.
C
          BRAT = BRATIO(ISPC,D,HT(I))
          TEMPD1 = D * BRAT                           ! CURRENT DIB
          DUP = D + DGPRED                            ! WITH GROWTH DOB
          TEMPD2 = DUP * BRAT                         ! WITH GROWTH DIB

C         LOG OF CHANGE IN DIB SQUARED FOR THE PERIOD
          WK2(I) = LOG(TEMPD2**2 - TEMPD1**2)         ! CHANGE IN DIB
C----------
C  END OF TREE LOOP.  PRINT DEBUG INFO IF DESIRED.
C----------
          IF(DEBUG) THEN
          WRITE(JOSTND,9001)
     &       I,ISPC,D,BRAT,PBAL,CR,ZRD(ITRE(I)),XMAXPT(ITRE(I)),PRD,
     &       PFMOD,DGPRED,WK2(I)
 9001     FORMAT(' IN DGF I=',I4,'  ISPC=',I3,'  D=',F7.2,' BRAT=',F7.4,
     &          '  PBAL=',F7.2,'  CR=',F7.4,
     &          '  ZSDI=',F9.3, '  XMAPT =',F9.3, '  PRD=',F9.3,
     &          '  PFMOD=',F7.5,'  DGPRED=',F7.4,'  DDS=',F7.4)
          ENDIF
   10     CONTINUE
        ENDDO
C----------
C  END OF SPECIES LOOP.
C----------
   20 CONTINUE
      ENDDO
      IF(DEBUG)WRITE(JOSTND,9002)ICYC
 9002 FORMAT(' LEAVING SUBROUTINE DGF  CYCLE =',I5)
      RETURN

C**********************************************************************
C**********************************************************************

      ENTRY DGCONS
C----------
C  ENTRY POINT FOR LOADING COEFFICIENTS OF THE DIAMETER INCREMENT
C  MODEL THAT ARE SITE SPECIFIC AND NEED ONLY BE RESOLVED ONCE.
C----------
C  LOAD ELEVATION, SLOPE AND ASPEC.

C  ENTER LOOP TO LOAD SPECIES DEPENDENT VECTORS.
C
C  SOME CALIBRATION RELATED VALUES MAY BE PROCESSED OR LOADED HERE,
C  BUT THAT HAS NOT BEEN WORKED OUT YET.
C
C----------
      CALL DBCHK (DEBUG,'DGCONS',6,ICYC)
      IF(DEBUG)WRITE(JOSTND,*) 'ENTER DGCONS'

      DO ISPC=1,MAXSP
        ATTEN(ISPC) = OBSERV(ISPC)
C----------
C  IF READCORD OR REUSCORD WAS SPECIFIED (LDCOR2 IS TRUE) ADD
C  LN(COR2) TO THE BAI MODEL CONSTANT TERM (DGCON).  COR2 IS
C  INITIALIZED TO 1.0 IN BLKDATA.
C  CHANGE TEMSASP BACK TO ASPECT
C----------
        IF (LDCOR2.AND.COR2(ISPC).GT.0.0) THEN
          DGCON(ISPC) = DGCON(ISPC) + ALOG(COR2(ISPC))
        ENDIF
C        IF(DEBUG) WRITE(JOSTND,40) TEMEL, TEMSASP, TEMSLP, ISPC,
C     &  SITEAR(ISPC), DGCON(ISPC), COR2(ISPC)
C   40   FORMAT(' IN DGF-DGCONS: TEMEL, TEMSASP, TEMSLP, ISPC, ',
C     &  'SITEAR(ISPC), DGCON(ISPC), COR2(ISPC): ',
C     &  3F7.1,I5,F7.1,2F10.3)

        IF(DEBUG) WRITE(JOSTND,40) ISPC,
     &  SITEAR(ISPC), DGCON(ISPC), COR2(ISPC)
   40   FORMAT(' IN DGF-DGCONS: ISPC, SITEAR(ISPC), DGCON(ISPC),',
     &         'COR2(ISPC): ', I5,F7.1,2F10.3)
      ENDDO
C
      RETURN
      END
