      SUBROUTINE SMHTGF(ISPC,I,HTGRTH,CR,TPCCF,LESTB,DEBUG)
      IMPLICIT NONE
C----------
C CI $Id: smhtgf.f 0000 2018-02-14 00:00:00Z gedixon $
C----------
C THIS SUBROUTINE COMPUTES THE HEIGHT GROWTH FOR SMALL TREES (HTGRTH)
C ONLY USED IN THE CI VARIANT FOR 11=WB, 12=PY, AND 16=LM
C CALLED FROM **REGENT.
C CALLS **FINDAG
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'CALCOM.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
COMMONS
C----------
      EXTERNAL RANN
      LOGICAL DEBUG,LESTB
      INTEGER I,ISPC
      REAL D1,D2,H,SITAGE,SITHT,AGMAX,HTMAX,HTMAX2
      REAL TPCCF,CR,HTGRTH,BACHLO,D,BETA1,BETA2,HTG1,STDDEV,HITE1
      REAL HITE2,AG2,HTGR
C----------
C     SPECIES LIST FOR CENTRAL IDAHO VARIANT.
C
C     1 = WESTERN WHITE PINE (WP)          PINUS MONTICOLA
C     2 = WESTERN LARCH (WL)               LARIX OCCIDENTALIS
C     3 = DOUGLAS-FIR (DF)                 PSEUDOTSUGA MENZIESII
C     4 = GRAND FIR (GF)                   ABIES GRANDIS
C     5 = WESTERN HEMLOCK (WH)             TSUGA HETEROPHYLLA
C     6 = WESTERN REDCEDAR (RC)            THUJA PLICATA
C     7 = LODGEPOLE PINE (LP)              PINUS CONTORTA
C     8 = ENGLEMANN SPRUCE (ES)            PICEA ENGELMANNII
C     9 = SUBALPINE FIR (AF)               ABIES LASIOCARPA
C    10 = PONDEROSA PINE (PP)              PINUS PONDEROSA
C    11 = WHITEBARK PINE (WB)              PINUS ALBICAULIS
C    12 = PACIFIC YEW (PY)                 TAXUS BREVIFOLIA
C    13 = QUAKING ASPEN (AS)               POPULUS TREMULOIDES
C    14 = WESTERN JUNIPER (WJ)             JUNIPERUS OCCIDENTALIS
C    15 = CURLLEAF MOUNTAIN-MAHOGANY (MC)  CERCOCARPUS LEDIFOLIUS
C    16 = LIMBER PINE (LM)                 PINUS FLEXILIS
C    17 = BLACK COTTONWOOD (CW)            POPULUS BALSAMIFERA VAR. TRICHOCARPA
C    18 = OTHER SOFTWOODS (OS)
C    19 = OTHER HARDWOODS (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C  FROM THE IE VARIANT:
C      USE 17(PY) FOR 12(PY)             (IE17 IS REALLY TT2=LM)
C      USE 18(AS) FOR 13(AS)             (IE18 IS REALLY UT6=AS)
C      USE 13(LM) FOR 11(WB) AND 16(LM)  (IE13 IS REALLY TT2=LM)
C      USE 19(CO) FOR 17(CW) AND 19(OH)  (IE19 IS REALLY CR38=OH)
C
C  FROM THE UT VARIANT:
C      USE 12(WJ) FOR 14(WJ)
C      USE 20(MC) FOR 15(MC)             (UT20 = SO30=MC, WHICH IS
C                                                  REALLY WC39=OT)
C----------
C  RESET ZRAND IF THIS IS THE START OF THE PROJECTION, THIS IS THE FIRST
C  PREDICTION FOR A NEW TRIPLE, OR IF GROWTH IN THE PREVIOUS CYCLE WAS
C  LESS THAN 0.1 FT.
C----------
      IF(ZRAND(I).NE.-999.) GO TO 20
   10 CONTINUE
      ZRAND(I)=BACHLO(0.0,1.0,RANN)
      IF(ZRAND(I).LT.-2.0 .OR. ZRAND(I).GT.2.0) GO TO 10
   20 CONTINUE
C
      D = DBH(I)
      IF (D.LE.0.0) THEN
        HTGRTH=0.0
        GO TO 900
      ENDIF
C
      SELECT CASE (ISPC)
C----------
C QUAKING ASPEN
C----------
      CASE(13)
        SITAGE = 0.0
        SITHT = 0.0
        AGMAX = 0.0
        HTMAX = 0.0
        HTMAX2 = 0.0
        D1 = DBH(I)
        H = HT(I)
        D2 = 0.0
        IF(DEBUG)WRITE(JOSTND,*)' IN HTGF, CALLING FINDAG I= ',I
        CALL FINDAG(I,ISPC,D1,D2,H,SITAGE,SITHT,AGMAX,HTMAX,HTMAX2,
     &              DEBUG)
C
C----------
C  COMPUTE HT GROWTH AND AGE FOR ASPEN.  EQUATION FORMULATED BY
C  WAYNE SHEPPERD, ROCKY MNT FOREST EXP STN, FORT COLLINS, CO.
C----------
        IF(LESTB)THEN
          SITAGE = ABIRTH(I)
        ENDIF
        HITE1=26.9825*SITAGE**1.1752
        AG2=SITAGE + 5.
        HITE2 = 26.9825*AG2**1.1752
        HTGR = (HITE2-HITE1)/(2.54*12.0)
        HTGRTH = HTGR+ZRAND(I)*.1
C----------
C GROWTH RATES APPEAR HIGH, REDUCE BY 25 PERCENT. DIXON 8-27-92.
C----------
        HTGRTH = HTGRTH * .75
C----------
C WHITEBARK PINE, PACIFIC YEW, LIMBER PINE
C----------
      CASE(11,12,16)
        BETA1=EXP(1.17527 - 0.42124*ALOG(TPCCF))
        BETA2=EXP(-2.56002 - 0.58642*ALOG(TPCCF))
        HTG1=BETA1 + BETA2*CR
        STDDEV=HTG1*(1.08720 - 0.00230*CR)
        HTGRTH=HTG1 + ZRAND(I)*STDDEV
C
      CASE DEFAULT
        HTGRTH=0.
      END SELECT
C
C----------
C  IF ESTIMATED HEIGHT INCREMENT IS CLOSE TO ZERO, SET FLAG TO DRAW
C  A NEW RANDOM ERROR ON NEXT PASS THROUGH.
C----------
      IF(HTGRTH .GT. 0.1) GOTO 900
      HTGRTH = 0.1
      ZRAND(I)=-999.
  900 CONTINUE
C
      IF(DEBUG)WRITE(JOSTND,*)' SMHTGF-I,ISPC,HTGRTH,D=',I,ISPC,HTGRTH,D
C
      RETURN
      END
