      SUBROUTINE BWEAGE (TOTR)
      IMPLICIT NONE
C----------
C  **BWEAGE                  DATE OF LAST REVISION:  07/14/10
C----------
C
C     AGE THE FOLIAGE ONE YEAR.
C
C     PART OF THE WESTERN SPRUCE BUDWORM MODEL/PROGNOSIS LINKAGE CODE.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--JANUARY 1984
c 
c     minor changes by K.Sheehan 7/96 to remove LBWDEB
C
C     CALLED FROM :
C
C       BWEDR - DRIVE THE BUDWORM MODEL FOR ONE STAND IN ONE YEAR.
C
C     FUNCTIONS AND SUBROUTINES CALLED :
C
C       BWESLP  - LINEAR INTERPOLATION FUNCTION.
C
C     PARAMETERS :
C
C     TOTR   - TOTAL REMAINING FOLIAGE, OUTPUT
C
C Revision History:
C  17-MAY-2005 Lance R. David (FHTET)
C     Added FVS parameter file PRGPRM.F77.
C  14-JUL-2010 Lance R. David (FMSC)
C     Added IMPLICIT NONE and declared variables as needed.
C-----------------------------------------------------------
C
COMMONS
C
      INCLUDE 'PRGPRM.F77'
      INCLUDE 'BWESTD.F77'
      INCLUDE 'BWECOM.F77'
C
COMMONS
C
      INTEGER I, ICMYR, ICROWN, IFAGE, IHOST, IREM, ISZI
      REAL BWESLP, CUM, DIF, DIV, FA, FR, PR, PRALL, PRB, XMULT
      REAL RECRX(4), RECRY(4), RECHST(6), TOTR(6,3)

      DATA RECRX/0.,.2,.8,1./,RECRY/.6,.6,0.,0./,
     >     RECHST/0.5,1.0,0.5,0.5,1.0,0.5/
C
C     ********************** EXECUTION BEGINS **************************
C
C     COMPUTE THE RETAINED PROPORTION OF BIOMASS ARRAY.
C
      DO 120 IHOST=1,6
      IF (IFHOST(IHOST).EQ.0) GOTO 120
      DO 100 ICROWN=1,9
      ISZI=(ICROWN+2)/3
C
      DIV=FOLADJ(IHOST,ICROWN,1)
      PRBIO(IHOST,ICROWN,1)=.0
      IF (FNEW (ICROWN,IHOST).LT..00001) FNEW (ICROWN,IHOST)=0.
      IF (DIV.GT..00001) PRBIO(IHOST,ICROWN,1)=FNEW (ICROWN,IHOST)/DIV
C
      DIV=FOLADJ(IHOST,ICROWN,2)
      PRBIO(IHOST,ICROWN,2)=.0
      IF (FOLD1(ICROWN,IHOST).LT..00001) FOLD1(ICROWN,IHOST)=0.
      IF (DIV.GT..00001) PRBIO(IHOST,ICROWN,2)=FOLD1(ICROWN,IHOST)/DIV
C
      DIV=FOLADJ(IHOST,ICROWN,3)
      PRBIO(IHOST,ICROWN,3)=.0
      IF (FOLD2(ICROWN,IHOST).LT..00001) FOLD2(ICROWN,IHOST)=0.
      IF (DIV.GT..00001) PRBIO(IHOST,ICROWN,3)=FOLD2(ICROWN,IHOST)/DIV
C
      DIV=FOLADJ(IHOST,ICROWN,4)
      PRBIO(IHOST,ICROWN,4)=.0
      IF (FREM (ICROWN,IHOST).LT..00001) FREM (ICROWN,IHOST)=0.
      IF (DIV.GT..00001) PRBIO(IHOST,ICROWN,4)=FREM (ICROWN,IHOST)/DIV
C
C     COMPUTE THE TOTAL REMAINING FOLIAGE (TOTR INITIALIZED IN BWDRIV).
C
      IF (BWTPHA(IHOST,ISZI).LE.0.0) GOTO 100
      TOTR(IHOST,ISZI)=TOTR(IHOST,ISZI)+FNEW(ICROWN,IHOST)
     >                +FOLD1(ICROWN,IHOST)+FOLD2(ICROWN,IHOST)
     >                +FREM (ICROWN,IHOST)
  100 CONTINUE
  120 CONTINUE
C
C     AGE THE FOLIAGE
C
      DO 230 IHOST=1,6
      IF (IFHOST(IHOST).EQ.0) GOTO 230
      DO 220 ICROWN=1,9
C
C     ADJUST THE REMAINING FOLIAGE.
C     IREM=1, DEFOLIATION HAS STOPED, REMAINING MOVES TOWARD THE
C             POTENTIAL.
C     IREM=2, DEFOLIATION HAS STARTED, REMAINING MOVES UP, OLD
C             NEEDLES ARE RETAINED.
C     IREM=3, DEFOLIATION IS CONTINOUS AND HEAVY, REMAINING MOVE.
C             DOWN, OLD NEEDLES START TO DIE.
C
      IREM=1
      PRB=PRBIO(IHOST,ICROWN,1)
      IF (PRB.GE..80) GOTO 150
      IREM=2
      ISZI=(ICROWN+2)/3
C
C     USE LAST YEARS TOTAL TREE DEFOLIATION AS A MEASURE OF
C     WHAT IS 'HEAVY' DEFOLIATION. IF NCUMYR = 0, THERE ISN'T AN
C     ESTIMATE OF LAST YEARS DEFOLIATION.
C
      IF (NCUMYR.LE.0) GOTO 150
      ICMYR=NCUMYR
      IF (ICUMYR.GT.0) ICMYR=ICUMYR
      CUM=0.
      IF (ICMYR.GT.0) CUM=CUMDEF(IHOST,ISZI,ICMYR)
      IF (CUM.GE. 40.) IREM=3
  150 CONTINUE
      FR=FREM(ICROWN,IHOST)
      PR=FOLPOT(IHOST,ICROWN,4)
      PRALL=PR
      DO 155 IFAGE=1,3
      PRALL=PRALL+FOLPOT(IHOST,ICROWN,IFAGE)
  155 CONTINUE
      XMULT=0.0
      DIV=FOLPOT(IHOST,ICROWN,3)
      IF (DIV.GT.0.000001) XMULT=FOLPOT(IHOST,ICROWN,4)/DIV
      FA=FOLD2(ICROWN,IHOST)*XMULT
C
C     BRANCH TO COMPUTE THE REMAINING FOLIAGE.
C
      GOTO (160,180,190),IREM
  160 CONTINUE
C
C     MOVE THE REMAINING TOWARD THE POTENTIAL.
C
      DIF=FR-PR
C
C     IF THE DIFFERENCE IS POSITIVE THERE IS MORE ACTUAL REMAINING
C     THAN POTENTIAL, LET SOME OF THE NEEDLES DROP.
C
      IF (DIF.GE. 0.0) THEN
         FA=PR
         IF (DIF/PR .GT. .10) FA=FR-(DIF*.40)
         FR=FA
      ELSE
C
C        THE DIFFERENCE IS NEGATIVE.  ADD NEEDLES FROM 2-YR GROUP.
C
         FR=FR+FA
         IF (FR.GT.PR) FR=PR
      ENDIF
      GOTO 200
  180 CONTINUE
C
C     ADD NEEDLES FROM 2-YR GROUP.
C
      FR=FR+FA*BWESLP(PRB,RECRX,RECRY,4)*RECHST(IHOST)
      IF (FR.GT.PRALL) FR=PRALL
      GOTO 200
  190 CONTINUE
C
C     ADD NEEDLES FROM 2-YR GROUP, THEN KILL SOME.
C
      FR=(FR+FA*BWESLP(PRB,RECRX,RECRY,4))*.85
      IF (FR.GT.PRALL) FR=PRALL
  200 CONTINUE
      FREM(ICROWN,IHOST)=FR
C
C     AGE THE 1-YEAR AND 2-YEAR OLD FOLIAGE.
C
      XMULT=0.0
      DIV=FOLPOT(IHOST,ICROWN,2)
      IF (DIV.GT..00001) XMULT=FOLPOT(IHOST,ICROWN,3)/DIV
      FOLD2(ICROWN,IHOST)=FOLD1(ICROWN,IHOST)*XMULT
      XMULT=0.0
      DIV=FOLPOT(IHOST,ICROWN,1)
      IF (DIV.GT..00001) XMULT=FOLPOT(IHOST,ICROWN,2)/DIV
      FOLD1(ICROWN,IHOST)=FNEW (ICROWN,IHOST)*XMULT
C
C     COMPUTE THE NEW BUD CROP.
C
      XMULT=0.0
      DIV=FOLPOT(IHOST,ICROWN,2)
      IF (DIV.GT..00001) XMULT=FOLD1(ICROWN,IHOST)/DIV
      FNEW (ICROWN,IHOST)=FOLPOT(IHOST,ICROWN,1) *
     >     BWESLP( ((PRBIO(IHOST,ICROWN,2)*.6) + (XMULT * .4)),
     >            RELFX(1,ICROWN),RELFY(1,ICROWN),4)
C
C     AGE ADJUSTED POTENTIAL FOLIAGE.
C
      FA=FOLADJ(IHOST,ICROWN,4)
      FA=FA+FOLADJ(IHOST,ICROWN,3)
      PR=FOLPOT(IHOST,ICROWN,4)
      IF (FA.GT.PR) FA=PR
      FR=FREM(ICROWN,IHOST)
      IF (FA.LT.FR) FA=FR
      FOLADJ(IHOST,ICROWN,4)=FA
      DO 210 I=1,2
      IFAGE=4-I
      XMULT=0.0
      DIV=FOLPOT(IHOST,ICROWN,IFAGE-1)
      IF (DIV.GT..00001) XMULT=FOLPOT(IHOST,ICROWN,IFAGE)/DIV
      FOLADJ(IHOST,ICROWN,IFAGE)=FOLADJ(IHOST,ICROWN,IFAGE-1)*XMULT
  210 CONTINUE
      FOLADJ(IHOST,ICROWN,1)=FNEW(ICROWN,IHOST)
  220 CONTINUE
  230 CONTINUE
C
      RETURN
      END
