      SUBROUTINE BWEPRB
      IMPLICIT NONE
C----------
C WSBWE $Id$
C----------
C
C     FINAL ADJUSTMENT TO PRBIO ARRAY FOR THE END OF THE CYCLE.
C
C     PART OF THE WESTERN SPRUCE BUDWORM MODEL/PROGNOSIS LINKAGE CODE.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--JANUARY 1984
c
c     minor changes by K.Sheehan 7/96 to remove LBWDEB
C
C     CALLED FROM :
C
C       BWECUP - SINGLE STAND BUDWORM MODEL LINK TO PROGNOSIS.
C
C     SUBROUTINES CALLED :
C
C       BWECRC - DETERMINE CROWN CLASS OF TREE.
C
C  Revision History:
C    14-JUL-2010 Lance R. David (FMSC)
C       Previouse noted revision 06/19/84
C       Added IMPLICIT NONE and declared variables as needed.
C----------
C
COMMONS
C
      INCLUDE 'PRGPRM.F77'
      INCLUDE 'ARRAYS.F77'
      INCLUDE 'CONTRL.F77'
      INCLUDE 'BWESTD.F77'
      INCLUDE 'BWECOM.F77'
C
COMMONS
C
      LOGICAL LTOLG,LTOMD
      INTEGER I, IC1NX, IC2NX, ICRC1, ICRC2, ICROWN, IFAGE,
     &        IHOST, ISPI, ISZI, ISZNX, ITCLS
      REAL BASWT, BIO, DIV, HTI, TRFBMS(MAXTRE), TRNSFL(6,9,4,3)


      EQUIVALENCE (WK6,TRNSFL),(WK4,TRFBMS(1))
C
C     SET DATE OF APPLICABILITY FOR THE RETAINED
C     PROPORTION OF BIOMASS ARRAY.
C
      IPRBYR=IBWYR2+1
C
C     RECOMPUTE THE RETAINED PROPORTION ARRAY AND COMPUTE THE PROPORTION
C     OF POTENTIAL FOLIAGE CURRENTLY HELD AS ADJUSTED POTENTIAL.
C
      DO 110 IHOST=1,6
      IF (IFHOST(IHOST).EQ.0) GOTO 110
      DO 105 ICROWN=1,9
C
C     RECOMPUTE THE RETAINED PROPORTION ARRAY
C
      DIV=FOLADJ(IHOST,ICROWN,1)
      PRBIO(IHOST,ICROWN,1)=.0
      IF (DIV.GT..00001) PRBIO(IHOST,ICROWN,1)=FNEW(ICROWN,IHOST)/DIV
      DIV=FOLADJ(IHOST,ICROWN,2)
      PRBIO(IHOST,ICROWN,2)=.0
      IF (DIV.GT..00001) PRBIO(IHOST,ICROWN,2)=FOLD1(ICROWN,IHOST)/DIV
      DIV=FOLADJ(IHOST,ICROWN,3)
      PRBIO(IHOST,ICROWN,3)=.0
      IF (DIV.GT..00001) PRBIO(IHOST,ICROWN,3)=FOLD2(ICROWN,IHOST)/DIV
      DIV=FOLADJ(IHOST,ICROWN,4)
      PRBIO(IHOST,ICROWN,4)=.0
      IF (DIV.GT..00001) PRBIO(IHOST,ICROWN,4)=FREM (ICROWN,IHOST)/DIV
C
C     COMPUTE PROPORTION OF POTENTIAL.
C
      DO 100 IFAGE=1,4
      POFPOT(IHOST,ICROWN,IFAGE)=1.0
      DIV=FOLPOT(IHOST,ICROWN,IFAGE)
      IF (DIV.GT..00001) POFPOT(IHOST,ICROWN,IFAGE)=
     >                   FOLADJ(IHOST,ICROWN,IFAGE)/DIV
  100 CONTINUE
  105 CONTINUE
  110 CONTINUE
C
C     ****** FINAL ADJUSTMENT TO PRBIO ******
C
C     ADJUST PRBIO SUCH THAT THE ESTIMATES OF DEFOLIATION WILL
C     APPLY DURING THE NEXT CYCLE EVEN THOUGH THE TREES WHICH MAKE UP
C     THE CURRENT CLASSES MAY MOVE INTO THE NEXT CLASS.  THIS ADJUSTMENT
C     IS MADE BY FIRST COMPUTING A FOLIAGE TRANSITION ARRAY WHICH
C     CONTAINS THE AMOUNT OF FOLIAGE EXPECTED TO MOVE INTO ANOTHER
C     TREE CLASS.  THEN, THE CURRENT VALUES OF PRBIO (PROPORTION OF
C     RETAINED BIOMASS) ARE RECOMPUTED FOR THE CROWNS CLASS MAKING UP
C     THE TWO LARGEST TREE CLASSES.  THE SMALL-TREE CROWN CLASSES ARE
C     LEFT AS IS.  THE COMPUTATION IS AN AVERAGE PRBIO FOR EACH LARGER
C     TREE CROWN CLASS.  THE AVERAGE IS A WEIGHTED AVERAGE WHERE THE
C     WEIGHTS ARE THE CURRENT BIOMASS IN THE CLASS AND THE BIOMASS
C     EXPECTED TO MOVE INTO THE CLASS.   THE PRBIO ESTIMATES FOR THE
C     BIOMASS WHICH IS LIKELY TO MOVE ARE THUS AVERAGED WITH THOSE
C     FOR THE BIOMASS WHICH WILL NOT MOVE.
C
C     INITIALIZE LOGICAL FLAGS, AND INITIALIZE THE TRANSITION ARRAY
C     WHICH IS EQUIVALENT TO WK6 IN MEMORY.
C
      LTOLG=.FALSE.
      LTOMD=.FALSE.
      DO 120 I=1,648
      WK6(I)=0.
  120 CONTINUE
C
C     DO FOR ALL TREES.
C
      IF (ITRN.LE.0) GO TO 160
      DO 150 I=1,ITRN
C
C     LOAD THE SPECIES CODE.
C
      ISPI=ISP(I)
      IHOST=IBWSPM(ISPI)
C
C     IF TREE IS NON-HOST, BYPASS THE CALCULATIONS.
C
      IF (IHOST.GE.7) GOTO 150
C
C     FIND THE TREE CLASS FOR THE TREE THIS DURING THIS CYCLE AND
C     THE ESTIMATE FOR THE TREE NEXT CYCLE.
C
      HTI=HT(I)
      CALL BWECRC (HTI,ISZI,ICRC1,ICRC2)
      CALL BWECRC (HTI+HTG(I),ISZNX,IC1NX,IC2NX)
C
C     IF ISZI EQUALS ISZNX, THE TREE WILL NOT MOVE CLASSES, THEN:
C     SKIP THE REMAINING CALCULATIONS FOR THE TREE.
C
      IF (ISZI.EQ.ISZNX) GOTO 150
C
C     SET THE TRANSITION CLASS INDEX, ISZI.
C     ITCLS=1 IF THE TREE MOVED FROM THE SMALL CLASS TO THE MID.
C           2 IF THE TREE MOVED FROM THE MID CLASS TO THE LARGE.
C           3 IF THE TREE MOVED FROM THE SMALL CLASS TO THE LARGE.
C
      ITCLS=1
      IF (ISZI.EQ.1 .AND. ISZNX.EQ.2) GOTO 130
      ITCLS=2
      IF (ISZI.EQ.2 .AND. ISZNX.EQ.3) GOTO 130
      ITCLS=3
  130 CONTINUE
C
C     SET LOGICAL FLAG INDECATING THAT AT LEAST SOME FOLIAGE MOVED.
C
      LTOLG=LTOLG .OR. ITCLS.GE.2
      LTOMD=LTOMD .OR. ITCLS.EQ.1
C
C     USE THE TOTAL TREE BIOMASS ESTIMATE FOR THE TREE AS THE BEGINNING
C     OF THE CYCLE IN COMPUTING THE WEIGHT.  NOTE THAT 1120.85921=
C     453.6*2.47103, WHICH IS G/HA = G/POUND  * ACRE/HA. THUS BIO IS IN
C     UNITS OF GRAMS PER HA.
C
      BIO=TRFBMS(I)*PROB(I)*1120.85921
C
C     IF THE TREE MOVED FROM THE MID TREE CLASS, REDUCE THE NUMBER OF
C     TREES PER HA IN THIS CLASS.  THIS STEP IS NOT NECESSARY FOR THE
C     LARGE TREES BECAUSE FOLIAGE DOES NOT MOVE FROM THE LARGE CLASS,
C     AND IT IS NOT NECESSARY FOR THE SMALL CLASS BECAUSE FOLIAGE DOES
C     NOT ENTER THE CLASS FROM ANOTHER CLASS.
C
      IF (ITCLS.EQ.2) BWTPHA(IHOST,ISZI)=BWTPHA(IHOST,ISZI)
     >                                  - (PROB(I)* 2.47103)
C
C     ACCUMULATE THE FOLIAGE TRANSFERING FROM CLASS TO CLASS.
C
      DO 140 IFAGE=1,4
      DO 140 ICROWN=1,9
      TRNSFL(IHOST,ICROWN,IFAGE,ITCLS)=TRNSFL(IHOST,ICROWN,IFAGE,ITCLS)
     >      + (THEOFL(IFAGE,ICROWN)*PRCRN3(ICROWN)*BIO)
  140 CONTINUE
  150 CONTINUE
  160 CONTINUE
C
C     IF THERE WERE NO TRANSITIONS, SKIP ALL THE REST OF THIS SECTION.
C
      IF (.NOT.(LTOLG .OR. LTOMD)) RETURN
C
C     WRITE THE FOLIAGE TRANSITION ARRAYS (DEBUG).
C
C     COMPUTE THE WEIGHTED AVERAGE DEFOLIATION IN EACH CROWN CLASS.
C
      DO 320 IHOST=1,6
      IF (IFHOST(IHOST).EQ.0) GOTO 320
      DO 310 IFAGE=1,4
C
C     FIRST DO CROWN CLASSES 7 TO 9 (LARGEST TREES).
C     IF THERE WERE NO TREES MOVING TO THE LARGE TREE CLASS, THEN:
C     SKIP DOWN TO THE MID CLASS.
C
      IF (.NOT.LTOLG) GOTO 260
      DO 230 ICROWN=7,9
      BASWT=FOLPOT(IHOST,ICROWN,IFAGE)*BWTPHA(IHOST,3)
      PRBIO(IHOST,ICROWN,IFAGE)=(PRBIO(IHOST,ICROWN,IFAGE)*BASWT)
     >    + (TRNSFL(IHOST,ICROWN,IFAGE,2)
     >    *  PRBIO (IHOST,ICROWN-3,IFAGE))
     >    + (TRNSFL(IHOST,ICROWN,IFAGE,3)
     >    *  PRBIO (IHOST,ICROWN-6,IFAGE))
      POFPOT(IHOST,ICROWN,IFAGE)=(POFPOT(IHOST,ICROWN,IFAGE)*BASWT)
     >    + (TRNSFL(IHOST,ICROWN,IFAGE,2)
     >    *  POFPOT (IHOST,ICROWN-3,IFAGE))
     >    + (TRNSFL(IHOST,ICROWN,IFAGE,3)
     >    *  POFPOT (IHOST,ICROWN-6,IFAGE))
C
C     LET TRNSFL HOLD THE SUM OF THE WEIGHTS.
C
      TRNSFL(IHOST,ICROWN,IFAGE,2)=TRNSFL(IHOST,ICROWN,IFAGE,2)+BASWT+
     >                             TRNSFL(IHOST,ICROWN,IFAGE,3)
  230 CONTINUE
C
C     NOW DIVIDE EACH CLASS BY THE SUM OF THE WEIGHTS.  IF THE SUM
C     IS NEAR ZERO, SET THE PORPORTION RETAINED BIOMASS EQUAL 1.0
C
      DO 250 ICROWN=7,9
      DIV=TRNSFL(IHOST,ICROWN,IFAGE,2)
      IF (DIV.LE. 0.00001) GOTO 240
      PRBIO(IHOST,ICROWN,IFAGE)=PRBIO(IHOST,ICROWN,IFAGE)/DIV
      POFPOT(IHOST,ICROWN,IFAGE)=POFPOT(IHOST,ICROWN,IFAGE)/DIV
      GOTO 250
  240 CONTINUE
      PRBIO(IHOST,ICROWN,IFAGE)=1.0
      POFPOT(IHOST,ICROWN,IFAGE)=1.0
  250 CONTINUE
  260 CONTINUE
C
C     IF THERE WAS NO TRANSITION TO MID-CLASS TREES, SKIP THESE
C     CALCULATIONS.
C
      IF (.NOT.LTOMD) GOTO 300
C
C     ELSE: DO THE MID-SIZED CLASS.
C
      DO 270 ICROWN=4,6
      BASWT=FOLPOT(IHOST,ICROWN,IFAGE)*BWTPHA(IHOST,2)
      PRBIO(IHOST,ICROWN,IFAGE)=(PRBIO(IHOST,ICROWN,IFAGE)*BASWT)
     >    + (TRNSFL(IHOST,ICROWN,IFAGE,1)
     >    *  PRBIO (IHOST,ICROWN-3,IFAGE))
      POFPOT(IHOST,ICROWN,IFAGE)=(POFPOT(IHOST,ICROWN,IFAGE)*BASWT)
     >    + (TRNSFL(IHOST,ICROWN,IFAGE,1)
     >    *  POFPOT (IHOST,ICROWN-3,IFAGE))
      TRNSFL(IHOST,ICROWN,IFAGE,1)=TRNSFL(IHOST,ICROWN,IFAGE,1)+BASWT
  270 CONTINUE
C
C     DIVIDE EACH CLASS BY THE SUM OF WEIGHTS.
C
      DO 290 ICROWN=4,6
      DIV=TRNSFL(IHOST,ICROWN,IFAGE,1)
      IF (DIV.LE. 0.00001) GOTO 280
      PRBIO(IHOST,ICROWN,IFAGE)=PRBIO(IHOST,ICROWN,IFAGE)/DIV
      POFPOT(IHOST,ICROWN,IFAGE)=POFPOT(IHOST,ICROWN,IFAGE)/DIV
      GOTO 290
  280 CONTINUE
      PRBIO(IHOST,ICROWN,IFAGE)=1.0
      POFPOT(IHOST,ICROWN,IFAGE)=1.0
  290 CONTINUE
  300 CONTINUE
  310 CONTINUE
  320 CONTINUE
C
      RETURN
      END
