      FUNCTION BWERNP (XMEAN,XMXVAR)
      IMPLICIT NONE
C----------
C  **BWERNP                 DATE OF LAST REVISION:  07/14/10
C----------
C
C     PART OF THE WESTERN SPRUCE BUDWORM MODEL/PROGNOSIS LINKAGE CODE.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--APRIL 1989
C
C     CALLED FROM :
C
C     BWEDAM - APPLY BUDWORM DAMAGE TO PROGNOSIS SAMPLE TREES.
C
C     SUBROUTINES CALLED :
C
C     BWEBET - BETA DISTRIBUTION RANDOM NUMBER GENERATOR.
C
C     PARAMETERS :
C
C     XMEAN = A MEAN PROPORTION...ON A 0 TO 1.0 RANGE.
C     XMXVAR= THE MAX VARIANCE AT XMEAN=.5
C
C     BWERNP  = A BETA RANDOM NUMBER WHEREBY THE VARIANCE IS A
C              SIMPLE FUNCTION OF THE MEAN.
C
C  Revision History:
C    14-JUL-2010 Lance R. David (FMSC)
C       Previous noted revision 12/05/90
C       Added IMPLICIT NONE and declared variables as needed.
C----------
      INTEGER KODE
      REAL BWERNP, C, SC, V, VAR, W, XM, XMEAN, XMXVAR, X(1)

      DATA C/2.7/,SC/0.9304/
      X(1)=0.0
C
C     IF THE MEAN IS CLOSE TO EITHER ZERO OR ONE, THEN SKIP THIS STUFF.
C
      IF (XMEAN.LT. 0.001 .OR. XMEAN.GT.  0.999) THEN
         BWERNP=XMEAN
      ELSE
C
C        COMPUTE THE VARIANCE AS A FUNCTION OF THE MEAN AND XMXVAR.
C        ASSUME THAT THE RELATIONSHIP IS SYMMETRICAL ABOUT .5
C
         IF (XMEAN.GT. 0.5) THEN
            XM=1.-XMEAN
         ELSE
            XM=XMEAN
         ENDIF
         VAR=(SC*XMXVAR)*(C*(XM*(C-1.))**(C-1.)*EXP(-((C-1.)*XM)**C))  
C
C        COMPUTE THE PARAMETERS OF THE BETA
C
         W=(XMEAN*(1.-XMEAN)/VAR)-1.
         V=XMEAN*W
         W=(1.-XMEAN)*W
C
C        GET ONE BETA VARIATE.
C
C        WRITE (16,*) 'IN BWERNP: bCALL BWEBET V=',V,' W=',W,' X=',X
         CALL BWEBET (V,W,X,1,KODE)
C        WRITE (16,*) 'IN BWERNP: aCALL BWEBET V=',V,' W=',W,' X=',X
         IF (KODE.LT.0) THEN
            BWERNP=XMEAN
         ELSE
            BWERNP=X(1)
         ENDIF
      ENDIF
      RETURN
      END
