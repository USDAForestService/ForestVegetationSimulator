      SUBROUTINE BWEDEF
      IMPLICIT NONE
C----------
C  **BWEDEF             DATE OF LAST REVISION:  07/14/10
C----------
C
C     APPLIES DEFOLIATION RATES AS SPECIFIED BY A USER USING DEFOL
C     OPTION.
C
C     PART OF THE WESTERN SPRUCE BUDWORM MODEL/PROGNOSIS LINKAGE CODE.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--JANUARY 1984
c
c     minor changes by K.Sheehan 7/96 to remove LBWDEB
C
C     CALLED FROM :
C
C       BWEDR - DRIVE THE BUDWORM MODEL FOR ONE STAND IN ONE YEAR.
C
C     SUBROUTINES CALLED :
C
C       OPGET  - GET NEXT OPTION.
C       OPDEL1 - DELETE OPTION.
C       OPDONE - SIGNAL THAT OPTION IS DONE.
C       BWESIN - SET DO LOOP INDICES FOR SETPRBIO AND DEFOL OPTIONS.
C
C REVISION HISTORY
C   05-OCT-2006 Lance R. David
C     Previous date noted was 2/25/97 by Kathy Sheehan.
C     Added debug handling.
C   14-JUL-2010 Lance R. David (FMSC)
C     Added IMPLICIT NONE and declared variables as needed.
C----------------------------------------------------------------------
C
COMMONS
C
      INCLUDE 'PRGPRM.F77'
      INCLUDE 'CONTRL.F77'
      INCLUDE 'BWESTD.F77'
      INCLUDE 'BWECOM.F77'
C
COMMONS
C
      INTEGER IACT, ICRC1, ICRC2, ICRC3, ICROWN, IDT, IHOST,
     &        IS1, IS2, NP
      REAL PARMS(6)
      LOGICAL DEBUG

C
C.... Check for DEBUG
C
      CALL DBCHK(DEBUG,'BWEDEF',5,ICYC)

      IF (DEBUG) WRITE (JOSTND,*) 'ENTER BWEDEF: ICYC=',ICYC,
     &  ' ITODO=',ITODO,' NTODO=',NTODO,' IYRCUR=',IYRCUR

   10 CONTINUE
C
C     IF ALL OF THE OPTIONS HAVE BEEN PROCESSED, THEN:  RETURN.
C
      IF (ITODO.GT.NTODO) GOTO 1000
      CALL OPGET (ITODO,6,IDT,IACT,NP,PARMS)
      IF (DEBUG) WRITE (JOSTND,*) 'IN BWEDEF: OPGET RETURN: ',
     &  'ITODO, IDT, IACT, PARMS= ', ITODO, IDT, IACT, PARMS

C
C     IF THE DATE OF THE OPTION IS GREATER OR EQUAL TO THE CURRENT YEAR,
C     THEN: KEEP THE OPTION.
C
      IF (IDT.GE.IYRCUR) GOTO 20
C
C     ELSE: THE OPTION IS OLDER THAN THE YEAR, DELETE IT AND GET THE
C     NEXT ONE.
C
      CALL OPDEL1 (ITODO)
      ITODO=ITODO+1
      GOTO 10
   20 CONTINUE
C
C     IF THE DATE OF THIS OPTION IS GREATER THAN THE CURRENT DATE, THEN:
C     RETURN.
C
      IF (IDT.GT.IYRCUR) GOTO 1000
C
C     SIGNAL THAT THE OPTION IS DONE.
C
      CALL OPDONE (ITODO,IYRCUR)
      ITODO=ITODO+1
C
C     LOAD INDEXING BOUNDS.
C
      CALL BWESIN (PARMS,IBWSPM,IS1,IS2,ICRC1,ICRC2,ICRC3)
      IF (DEBUG) WRITE (JOSTND,*)
     &  'IN BWEDEF: IBWSPM, IS1, IS2, ICRC1, ICRC2, ICRC3',
     &  IBWSPM, IS1, IS2, ICRC1, ICRC2, ICRC3

C
C     IF THE SPECIES MAPED TO NON-HOST, THEN DELETE OPTION.
C
      IF (IS1.LE.6) GOTO 30
      CALL OPDEL1 (ITODO-1)
      GOTO 10
   30 CONTINUE
C
C     APPLY MANUAL DEFOLIATION.
C
      DO 80 IHOST=IS1,IS2
      IF (IFHOST(IHOST).EQ.0) GOTO 80
      DO 70 ICROWN=ICRC1,ICRC2,ICRC3
      FNEW(ICROWN,IHOST)=FNEW(ICROWN,IHOST)*(1.-(PARMS(3)/100.))
      FOLD1(ICROWN,IHOST)=FOLD1(ICROWN,IHOST)*(1.-(PARMS(4)/100.))
      FOLD2(ICROWN,IHOST)=FOLD2(ICROWN,IHOST)*(1.-(PARMS(5)/100.))
      FREM (ICROWN,IHOST)=FREM (ICROWN,IHOST)*(1.-(PARMS(6)/100.))
   70 CONTINUE
   80 CONTINUE
C
C     GET ANOTHER OPTION.
C
      GOTO 10
 1000 CONTINUE
      IF (DEBUG) WRITE (JOSTND,*)'EXIT BWEDEF: ICYC=', ICYC
      RETURN
      END
