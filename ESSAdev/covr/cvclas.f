      SUBROUTINE CVCLAS (LTHIN)
      IMPLICIT NONE
C----------
C COVR $Id$
C----------
C  THIS SUBROUTINE IS CALLED BY **CVBROW** OR **CVCNOP** TO COMPUTE A
C  "SUCCESSIONAL STAGE" FOR THE STAND, AS DESCRIBED BY
C
C     S.R. PETERSON.  1982.  A PRELIMINARY SURVEY OF FOREST BIRD
C        COMMUNITIES IN NORTHERN IDAHO.  NORTHWEST SCIENCE 56(4):
C        287-298.
C
C     S.R. PETERSON. 1978.  FOREST MANAGEMENT AND BIRD COMMUNITIES
C        IN NORTHERN IDAHO.  PAC. DIV. AMER. ASSOC. ADV. SCI.,
C        59TH ANNUAL MEETING, JUNE 13-17, (ABSTRACT).
C
C  STAND CLASSIFICATION IS BASED ON:
C
C  -- SAGE -- TIME SINCE DISTURBANCE
C  -- RELDEN -- CROWN COMPETITION FACTOR
C  -- TOTLCV(41,2) -- PERCENT SHRUB COVER
C  -- AVH -- AVERAGE TREE HEIGHT
C  -- TALLSH -- AVERAGE "TALL SHRUB" HEIGHT
C  -- DIAM --"AVERAGE" TREE DIAMETER
C
C  THE LOGIC USED IN CLASSIFYING THE STAND INVOLVES A SELECTIVE
C  DEFINITION OF AVG. STAND DBH. ITS PURPOSE IS TO AVOID
C  USE OF THE RMSQD FOR THE STAND IN CASES WHERE THE STATISTIC
C  IS MISLEADING OR PROVIDES VERY LITTLE INFORMATION (SUCH
C  AS IN THE CASE OF A TWO-STORIED STAND.)
C
C  OTHER VARIABLE DEFINITIONS:
C
C  -- CRAX20(2) -- CROWN AREA IN TWO DENSEST 20' HEIGHT CLASSES
C  -- CRAREA -- TOTAL CROWN AREA
C  -- CRXHT(16) -- CROWN AREA BY HEIGHT CLASS, COMPUTED IN **CVSUM**
C  -- MTAG1, MTAG2 -- INDEX TO TWO DENSEST 20' HEIGHT CLASSES
C  -- ONE, TWO, THREE -- INDICES TO THREE DENSEST 10' HEIGHT CLASSES
C  -- INDX(16) -- INDEXES CROWN AREA SORTED BY DECREASING VALUE
C              WITHIN HEIGHT CLASSES
C  -- SD2XHT(16) -- SUM OF SQUARED DBH'S BY HEIGHT CLASS
C  -- TXHT(41,2,16) -- NUMBER OF TREES BY HEIGHT CLASS
C  -- ISTAGE(41,2) -- STAND SUCCESSIONAL STAGE CODE
C  -- STRUCT -- STAND STRUCTURE CODE
C
C  HEIGHT CLASS CODES: 1=0-10', 2=10-20',...   ...16=150' AND UP.
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'CVCOM.F77'
C
COMMONS
C
C----------
C DIMENSION AND TYPE STATEMENTS FOR INTERNAL VARIABLES.
C----------
      INTEGER STRUCT,ONE,TWO,THREE
      INTEGER INDX(16)
      LOGICAL LTHIN,DEBUG
      REAL CRAX20(2)
      INTEGER IP1,ITHN,I,ITEST,IPLUS1,MTAG1,MTAG2,MTAGP1
      REAL HTRNGE,DIAM,TEST,TOP2,TOP3,DENX20
C----------
C  CHECK FOR DEBUG.
C----------
      CALL DBCHK(DEBUG,'CVCLAS',6,ICYC)
C----------
C  DON'T COMPUTE SUCCESSIONAL STAGE IF SHRUB PREDICTIONS ARE
C  NOT AVAILABLE (BECAUSE TIME SINCE DISTURBANCE GT 40 YEARS).
C----------
      IF (SAGE .GT. 40.) RETURN
C----------
C SET CYCLE AND THIN SUBSCRIPTS.
C----------
      IP1 = ICYC + 1
      ITHN = 1
      IF (.NOT. LTHIN) GO TO 10
      IP1 = ICYC
      ITHN = 2
   10 CONTINUE
C
      IF (DEBUG) WRITE (JOSTND,8090) ICYC
 8090 FORMAT (/ '**CALLING CVCLAS, ICYC= ',I2)
      IF (DEBUG) WRITE (JOSTND,8091) IP1,ITHN,AVH,TALLSH
 8091 FORMAT (/ 'IP1= ',I2,'  ITHN= ',I2,'  AVH= ',F10.4,
     &          '  TALLSH= ',F10.4)
C======================================================================
C  ** COMPUTE SHRUB-DOMINATED SUCCESSIONAL STAGES **
C======================================================================
C----------
C   SUCCESSIONAL STAGE 1 -- RECENT DISTURBANCE
C----------
      ISTAGE(IP1,ITHN) = 0
      IF (SAGE .GT. 5.0) GO TO 250
      IF ((RELDEN .GT. 30.0) .AND. (AVH .GT. 1.0)) GO TO 280
      IF ((TALLSH .GT. 1.0) .OR. (TOTLCV(IP1,ITHN) .GT. 25.0)) GO TO 250
      ISTAGE(IP1,ITHN) = 1
      IF (DEBUG) WRITE (JOSTND,8092) ISTAGE(IP1,ITHN),SAGE,RELDEN,
     &     AVH,TALLSH,TOTLCV(IP1,ITHN)
 8092 FORMAT (/ 'ISTAGE(IP1,ITHN)= ',I2 /
     &'SAGE= ',F10.2,'  RELDEN= ',F10.2,'  AVH= ',F10.4,
     &'  TALLSH= ',F10.4,'  TOTLCV(IP1,ITHN)= ',F10.4)
      RETURN
  250 CONTINUE
C----------
C   SUCCESSIONAL STAGE 2 -- LOW SHRUB
C----------
      IF ((RELDEN .GT. 30.0) .AND. (AVH .GT. 1.0)) GO TO 280
      IF (TALLSH .GT. 2.5) GO TO 260
      ISTAGE(IP1,ITHN) = 2
      IF (DEBUG) WRITE (JOSTND,8092) ISTAGE(IP1,ITHN),SAGE,RELDEN,
     &     AVH,TALLSH,TOTLCV(IP1,ITHN)
      RETURN
  260 CONTINUE
C----------
C   SUCCESSIONAL STAGE 3 -- MEDIUM SHRUB
C----------
      IF (TALLSH .GT. 5.0) GO TO 270
      ISTAGE(IP1,ITHN) = 3
      IF (DEBUG) WRITE (JOSTND,8092) ISTAGE(IP1,ITHN),SAGE,RELDEN,
     &     AVH,TALLSH,TOTLCV(IP1,ITHN)
      RETURN
  270 CONTINUE
C----------
C   SUCCESSIONAL STAGE 4 -- TALL SHRUB WITH NO CONIFERS
C----------
      IF (TOTLCV(IP1,ITHN) .LT. 70.0) GO TO 280
      ISTAGE(IP1,ITHN) = 4
      IF (DEBUG) WRITE (JOSTND,8092) ISTAGE(IP1,ITHN),SAGE,RELDEN,
     &     AVH,TALLSH,TOTLCV(IP1,ITHN)
      RETURN
  280 CONTINUE
C----------
C   SUCCESSIONAL STAGE 5 -- TALL SHRUB WITH FEW CONIFERS
C----------
      IF ((TALLSH .LT. 5.0).AND.(TOTLCV(IP1,ITHN) .LT. 50.0)) GO TO 290
      IF (AVH .GT. 5.0) GO TO 290
      IF (RELDEN .GT. 50.0) GO TO 290
      ISTAGE(IP1,ITHN) = 5
      IF (DEBUG) WRITE (JOSTND,8092) ISTAGE(IP1,ITHN),SAGE,RELDEN,
     &     AVH,TALLSH,TOTLCV(IP1,ITHN)
      RETURN
  290 CONTINUE
C----------
C   SUCCESSIONAL STAGE 6 -- TALL SHRUB WITH MOSTLY CONIFERS
C----------
      IF ((TALLSH .LT. 5.0 ).AND.(TOTLCV(IP1,ITHN) .LT. 30.0)) GO TO 300
      IF (RELDEN .GT. 100.0) GO TO 300
      ISTAGE(IP1,ITHN) = 6
      IF (DEBUG) WRITE (JOSTND,8092) ISTAGE(IP1,ITHN),SAGE,RELDEN,
     &     AVH,TALLSH,TOTLCV(IP1,ITHN)
      RETURN
  300 CONTINUE
C======================================================================
C  ENTER CODE TO FIND STAND STRUCTURE AND DIAMETER FOR
C  TREE-DOMINATED STAGES.
C======================================================================
C------------------------------------------------------------
C  ** STRUCT = 1   :   STAND IS EVEN-AGED **
C------------------------------------------------------------
C  CALCULATE RANGE OF TREE HEIGHTS.
C  IF 30' OR LESS, STAND IS EVEN-AGED.
C  DIAM SET TO ROOT MEAN SQUARE OF STAND, OR TO OLD RMSQD
C  IF THINNING HAS JUST OCCURRED.
C----------
      HTRNGE = HTMAX - HTMIN
      IF (HTRNGE .GT. 30.0) GO TO 110
      STRUCT = 1
      DIAM = RMSQD
      IF (LTHIN) DIAM = ORMSQD
      GO TO 190
  110 CONTINUE
C----------
C  SORT CROWN AREA 'CRXHT(16)' BY HEIGHT CLASS.
C  INDX(1) = SUBSCRIPT OF HEIGHT CLASS WITH GREATEST CROWN AREA
C  INDX(16) = SUBSCRIPT OF HEIGHT CLASS WITH LEAST CROWN AREA
C----------
      CALL RDPSRT(16,CRXHT,INDX,.TRUE.)
      ONE = INDX(1)
      TWO = INDX(2)
      THREE = INDX(3)
      IF (DEBUG) WRITE (JOSTND,9000)ONE,TWO,THREE,(CRXHT(I),I=1,16)
 9000 FORMAT ('ONE=',I2,'   TWO=',I2,'   THREE=',I2 /
     &        'CRXHT(I)=',16F7.1)
C----------
C  IF THE DENSEST LAYER CONTAINS 50% OR MORE OF THE TOTAL CROWN AREA,
C  THE STAND IS EVEN-AGED.
C  SET DIAM TO THE RMSQD FOR THAT CLASS.
C----------
      TEST = CRXHT(ONE)/CRAREA
      IF (TEST .LT. 0.50) GO TO 120
      STRUCT = 1
      DIAM = SQRT(SD2XHT(ONE)/TXHT(IP1,ITHN,ONE))
      IF (DEBUG) WRITE (JOSTND,9001) STRUCT,DIAM,
     &     SD2XHT(ONE),TXHT(IP1,ITHN,ONE)
 9001 FORMAT ('STRUCT=',I5,'  DIAM=',F10.1,'  SD2XHT=',F10.1,
     &    '  TXHT=',F10.1/19X,F10.1,9X,F10.1/19X,F10.1,9X,F10.1)
      GO TO 190
  120 CONTINUE
C----------
C  IF THE TWO DENSEST LAYERS CONTAIN 70% OR MORE OF THE TOTAL
C  CROWN AREA, AND IF THIS IS SPREAD OVER 3 HEIGHT CLASSES OR LESS,
C  THE STAND IS EVEN-AGED.
C  SET DIAM TO THE RMSQD FOR THE TWO CLASSES.
C----------
      TOP2 = CRXHT(ONE) + CRXHT(TWO)
      TEST = TOP2/CRAREA
      ITEST = MAX0(ONE,TWO) - MIN0(ONE,TWO)
      IF ((TEST .LT. 0.70) .OR. (ITEST .GT. 2)) GO TO 130
      STRUCT = 1
      DIAM = SQRT((SD2XHT(ONE) + SD2XHT(TWO))/
     &     (TXHT(IP1,ITHN,ONE) + TXHT(IP1,ITHN,TWO)))
      IF (DEBUG) WRITE (JOSTND,9001) STRUCT,DIAM,
     &     SD2XHT(ONE),TXHT(IP1,ITHN,ONE),
     &     SD2XHT(TWO),TXHT(IP1,ITHN,TWO)
      GO TO 190
  130 CONTINUE
C----------
C  IF THE 3 DENSEST LAYERS CONTAIN 90% OR MORE OF THE TOTAL CROWN AREA,
C  AND IF THE 3 LAYERS ARE CONSECUTIVE, THE STAND IS EVEN-AGED.
C  SET DIAM EQUAL TO THE RMSQD OF THE THREE CLASSES.
C----------
      TOP3 = CRXHT(ONE) + CRXHT(TWO) + CRXHT(THREE)
      TEST = TOP3/CRAREA
      IF (TEST .LT. 0.90) GO TO 140
      ITEST = MAX0(ONE,TWO,THREE) - MIN0(ONE,TWO,THREE)
      IF (ITEST .GT. 2) GO TO 140
      STRUCT = 1
      DIAM = SQRT((SD2XHT(ONE) + SD2XHT(TWO) + SD2XHT(THREE))/
     &     (TXHT(IP1,ITHN,ONE)+TXHT(IP1,ITHN,TWO)+TXHT(IP1,ITHN,THREE)))
      IF (DEBUG) WRITE (JOSTND,9001) STRUCT,DIAM,
     &     SD2XHT(ONE),TXHT(IP1,ITHN,ONE),
     &     SD2XHT(TWO),TXHT(IP1,ITHN,TWO),
     &     SD2XHT(THREE),TXHT(IP1,ITHN,THREE)
      GO TO 190
  140 CONTINUE
C------------------------------------------------------------
C  ** STRUCT = 2   :   STAND IS TWO-STORIED **
C------------------------------------------------------------
C  STARTING AT THE BOTTOM, DETERMINE THE CROWN AREA CONTRIBUTED BY
C  EACH CONSECUTIVE 20' LAYER. SAVE THE MAXIMUM VALUE 'CRAX20(1)',
C  AND A TAG TO ITS LOCATION 'MTAG1'.
C----------
      CRAX20(1) = 0.0
      DO 150 I=1,15
      IPLUS1 = I + 1
      DENX20 = CRXHT(I) + CRXHT(IPLUS1)
      IF (DENX20 .LT. CRAX20(1)) GO TO 150
      CRAX20(1) = DENX20
      MTAG1 = I
  150 CONTINUE
C----------
C  FIND THE SECOND DENSEST 20' LAYER BY REPEATING THE ABOVE PROCESS.
C----------
      CRAX20(2) = 0.0
      ITEST = MTAG1 + 1
      DO 160 I=1,15
      IPLUS1 = I + 1
      IF ((IPLUS1.EQ.MTAG1).OR.(I.EQ.MTAG1).OR.(I.EQ.ITEST)) GO TO 160
      DENX20 = CRXHT(I) + CRXHT(IPLUS1)
      IF (DENX20 .LT. CRAX20(2)) GO TO 160
      CRAX20(2) = DENX20
      MTAG2 = I
  160 CONTINUE
C----------
C  IF THE TWO DENSEST 20' LAYERS ARE SEPARATED BY 20' OR MORE, THE
C  STAND IS TWO STORIED.
C  SET DIAM EQUAL TO THE RMSQD OF DENSEST LAYER ONLY.
C----------
      ITEST = IABS(MTAG1 - MTAG2)
      IF (ITEST .LT. 4) GO TO 170
      MTAGP1 = MTAG1 + 1
      STRUCT = 2
      DIAM = SQRT((SD2XHT(MTAG1) + SD2XHT(MTAGP1))/
     &     (TXHT(IP1,ITHN,MTAG1) + TXHT(IP1,ITHN,MTAGP1)))
      IF (DEBUG) WRITE(JOSTND,9004) STRUCT,DIAM,MTAG1,
     &     SD2XHT(MTAG1),TXHT(IP1,ITHN,MTAG1),
     &     STRUCT,DIAM,MTAGP1,SD2XHT(MTAGP1),TXHT(IP1,ITHN,MTAGP1)
 9004 FORMAT ('STRUCT=',I5,'   DIAM=',F10.1,'  MTAG=',I3,
     &      '   SD2XHT=',F10.1,'   TXHT=',F10.1)
      GO TO 190
  170 CONTINUE
C------------------------------------------------------------
C  ** STRUCT = 3   :   STAND IS ALL-AGED **
C------------------------------------------------------------
C  IF THE CROWN AREA IN 3 DENSEST LAYERS IS LESS THAN 50% OF TOTAL
C  CROWN AREA, THE STAND IS ALL-AGED.
C  SET DIAM EQUAL TO THE RMSQD OF 3 DENSEST 10' LAYERS.
C----------
      IF (TEST .GE. 0.50) GO TO 180
      STRUCT = 3
      DIAM = SQRT((SD2XHT(ONE) + SD2XHT(TWO) + SD2XHT(THREE))/
     &     (TXHT(IP1,ITHN,ONE)+TXHT(IP1,ITHN,TWO)+TXHT(IP1,ITHN,THREE)))
      IF (DEBUG) WRITE (JOSTND,9001) STRUCT,DIAM,
     &     SD2XHT(ONE),TXHT(IP1,ITHN,ONE),
     &     SD2XHT(TWO),TXHT(IP1,ITHN,TWO),
     &     SD2XHT(THREE),TXHT(IP1,ITHN,THREE)
      GO TO 190
  180 CONTINUE
C----------
C  IF THE DENSEST LAYER CONTAINS LESS THAN 20% OF THE TOTAL CROWN
C  AREA, THE STAND IS ALL-AGED.
C  SET DIAM EQUAL TO THE RMSQD OF 3 DENSEST 10' LAYERS.
C----------
      TEST = CRXHT(ONE)/CRAREA
      IF (TEST .LT. 0.20) GO TO 185
      STRUCT = 3
      DIAM = SQRT((SD2XHT(ONE) + SD2XHT(TWO) + SD2XHT(THREE))/
     &     (TXHT(IP1,ITHN,ONE)+TXHT(IP1,ITHN,TWO)+TXHT(IP1,ITHN,THREE)))
      IF (DEBUG) WRITE (JOSTND,9001) STRUCT,DIAM,
     &     SD2XHT(ONE),TXHT(IP1,ITHN,ONE),
     &     SD2XHT(TWO),TXHT(IP1,ITHN,TWO),
     &     SD2XHT(THREE),TXHT(IP1,ITHN,THREE)
      GO TO 190
  185 CONTINUE
  190 CONTINUE
C======================================================================
C  ** COMPUTE TREE-DOMINATED SUCCESSIONAL STAGES **
C======================================================================
C----------
C   SUCCESSIONAL STAGE 7 -- SAPLING TIMBER
C----------
      IF (DIAM .GT. 4.0) GO TO 310
      ISTAGE(IP1,ITHN) = 7
      IF (DEBUG) WRITE (JOSTND,8093) ISTAGE(IP1,ITHN),DIAM
 8093 FORMAT (/ 'ISTAGE(IP1,ITHN)= ',I2,'  DIAM= ',F10.3)
      RETURN
  310 CONTINUE
C----------
C   SUCCESSIONAL STAGE 8 -- POLE TIMBER
C----------
      IF (DIAM .GT. 11.0) GO TO 320
      ISTAGE(IP1,ITHN) = 8
      IF (DEBUG) WRITE (JOSTND,8093) ISTAGE(IP1,ITHN),DIAM
      RETURN
  320 CONTINUE
C----------
C   SUCCESSIONAL STAGE 9 -- MATURE TIMBER
C----------
      IF (DIAM .GT. 24.0) GO TO 330
      ISTAGE(IP1,ITHN) = 9
      IF (DEBUG) WRITE (JOSTND,8093) ISTAGE(IP1,ITHN),DIAM
      RETURN
  330 CONTINUE
C----------
C   SUCCESSIONAL STAGE 10 -- OLD-GROWTH TIMBER
C----------
      ISTAGE(IP1,ITHN) = 10
      IF (DEBUG) WRITE (JOSTND,8093) ISTAGE(IP1,ITHN),DIAM
      RETURN
      END
