        SUBROUTINE VARVOL
        IMPLICIT NONE
C----------
C  **VARVOL--SN & REGION 8  DATE OF LAST REVISION:   01/19/2011
C            REGION 9 CAPABILITIES ADDED FOR MARK TWAIN AND SHAWNEE
C----------
C
C  THIS SUBROUTINE CALLS THE APPROPRIATE VOLUME CALCULATION ROUTINE
C  FROM THE NATIONAL CRUISE SYSTEM VOLUME LIBRARY FOR METHB OR METHC
C  EQUAL TO 6.  IT ALSO CONTAINS ANY OTHER SPECIAL VOLUME CALCULATION
C  METHOD SPECIFIC TO A VARIANT (METHB OR METHC = 8)
C
C  FOR EASTERN VARIANTS:
C    VN = TOTAL MERCH CUBIC (PULPWOOD AND SAWLOG)
C    VM = MERCH SAWLOG CUBIC FOOT VOLUME
C    BBFV = MERCH SAWLOG BOARD FOOT VOLUME
C
C  THIS SUBROUTINE INCLUDES LOGIC FOR ALL REGION 8 VARIANTS -- SN, SE
C
C----------
C
COMMONS
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'VOLSTD.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'SNCOM.F77'
COMMONS
C
C----------
C  VARIABLE DICTIONARY
C
C  I__GA -- R8 GEOGRAPHIC AREA WHERE __ IS STATE ABBREVIATION,
C           USED IN R8VOL CUBIC FOOT EQUATIONS
C           (AR AND OZ ARE BOTH FOR ARKANSAS)
C  I__GR -- R8 GROUPING CODE WHERE __ IS STATE ABBREVIATION,
C           USED IN R8VOL SAWTIMBER BOARD FOOT EQUATIONS
C----------
      REAL XTOPD
      INTEGER IALGA(7),IALGR(7),IGAGA(8),IGAGR(8),
     &          ITNGA(6),ITNGR(6),IFLGA(6),IFLGR(6),IMSGA(17),
     &          IMSGR(17),INCGA(11),INCGR(11),ISCGA(5),ISCGR(5),
     &          IARGA(12),IARGR(12),IOZGA(7),IOZGR(7)
      INTEGER IDBGA(7),IDBGR(7),IKIGA(5),IKIGR(5),IGWGA(16),
     &          IGWGR(16),ITXGA(8),ITXGR(8)
C
      REAL VOL(15)
      REAL BRATIO,TDIBC,TDIBB,X01,TOTCU,BOLT1,BOLT2,BFTDOB,TOPDOB
      REAL SINDX,SAWBF,SAWCU,PULPCU,HTC1,HTC2,VM,VN,BBFV,D
      REAL VMAX,BARK,H,X02,X03,BOLTHT(21),LOGLEN(7,21)
      INTEGER IT,ITRNC,ISPC,I,ISPLIM,IERR,I01,I02,I0,I1,IHT1,IHT2
      LOGICAL TKILL,CTKFLG,BTKFLG,DEBUG,DONE,LCONE
      CHARACTER VLSP*3,VVER*7,SNSP(90)*3,SESP(118)*3,CTYPE*1
      CHARACTER SNCSVL(90)*3,HTTYP*1
      CHARACTER VOLEQN*10,PROD*2,IAREA*2,FORST*2,TEMEQN*10,EQN*10
      CHARACTER BFTOP*1,CFTOP*1
      INTEGER FOREST,BFPFLG,CUPFLG,SPFLG
      CHARACTER*10 EQNC,EQNB
C----------
C  DEFINE SPECIES DATA ARRAYS
C----------
      DATA SNCSVL/
     &'001','068','001','132','110','131','129','131','132','132',
     &'131','129','131','132','221','221','001','318','316','316',
     &'316','318','331','373','373','391','400','450','460','471',
     &'490','521','531','541','541','543','541','552','999','999',
     &'999','601','602','611','621','651','651','651','653','651',
     &'999','680','691','693','694','701','711','999','731','742',
     &'741','762','802','806','812','813','824','816','813','824',
     &'823','823','816','832','833','813','835','837','824','901',
     &'920','931','951','970','970','970','970','001','999','999'/
C
      DATA SNSP/
     &'261','100','115','132','110','111','115','121','126','126',
     &'128','129','131','132','221','222','261','500','500','316',
     &'300','500','330','370','370','370','400','300','460','300',
     &'300','500','531','541','541','300','544','500','300','300',
     &'300','500','500','611','621','652','300','652','653','300',
     &'300','300','300','693','694','500','300','300','731','300',
     &'300','300','802','806','812','813','800','800','822','800',
     &'800','800','827','832','833','800','835','800','835','901',
     &'300','300','300','970','970','970','970','132','300','300'/
C
      DATA SESP/
     &'100','100','110','111','115','121','100','126','128','129',
     &'131','132','221','222','261','300','300','500','316','300',
     &'500','330','370','370','370','370','370','400','400','400',
     &'400','404','400','400','400','400','300','460','500','300',
     &'300','500','500','531','541','541','300','544','300','300',
     &'500','300','500','300','500','500','611','621','500','300',
     &'652','653','300','300','300','300','693','693','694','500',
     &'300','300','731','300','300','300','300','300','300','802',
     &'800','806','800','800','812','813','800','800','800','822',
     &'800','800','800','800','827','828','800','831','832','833',
     &'800','835','835','837','800','835','901','300','300','300',
     &'300','970','970','970','970','970','970','300'/
C
      DATA IALGA/40,2*10,4*40/,IALGR/130,2*150,160,2*140,170/,
     &     IDBGA/7*30/,IDBGR/7*100/,
     &     IGAGA/7*30,20/,IGAGR/7*10,60/,
     &     ITNGA/6*30/,ITNGR/6*10/,
     &     IFLGA/6*10/,IFLGR/6*30/,
     &     IKIGA/5*50/,IKIGR/5*20/,
     &     IMSGA/5*50,70,11*40/,IMSGR/190,2*200,210,220,180,11*230/,
     &     IGWGA/16*30/,IGWGR/16*110/,
     &     IARGA/12*60/,IARGR/300,4*310,300,5*310,320/,
     &     IOZGA/2*60,10,3*60,70/,IOZGR/6*40,50/,
     &     INCGA/11*30/,INCGR/2*10,70,6*10,80,10/,
     &     ISCGA/20,30,20,20,10/,ISCGR/240,10,240,240,250/,
     &     ITXGA/8*50/,ITXGR/2*260,270,3*290,280,260/
C
C----------
C  NATIONAL CRUISE SYSTEM ROUTINES (METHOD = 6)
C----------
      ENTRY NATCRS (VN,VM,BBFV,ISPC,D,H,TKILL,BARK,ITRNC,VMAX,
     1              CTKFLG,BTKFLG,IT)
C----------
C  CHECK FOR DEBUG
C----------
      CALL DBCHK(DEBUG,'VARVOL',6,ICYC)
      IF(DEBUG)WRITE(JOSTND,*)'IN VARVOL, ICYC= ',ICYC,
     &'ISEFOR= ',ISEFOR
C----------
C  INITIALIZE VOLUME ARRAY TO ZERO
C----------
      DO  10 I=1,15
      VOL(I)= 0.
   10 CONTINUE
C----------
C  VOLUME SECTION FOR REGION 8 FORESTS
C----------
      IF(ISEFOR .NE. 0)THEN
        HTC1=0.
        HTC2=0.
C----------
C  MAP SPECIES CODES TO SPECIES THAT HAVE VOLUME EQUATIONS.  THIS IS
C  VARIANT SPECIFIC.
C----------
        CALL VARVER(VVER)
        IF (VVER(:2) .EQ. 'SN') THEN
          VLSP=SNSP(ISPC)
          ISPLIM=17
        ELSE
          VLSP=SESP(ISPC)
          ISPLIM=15
        ENDIF
C----------
C  COMPUTE PULPWOOD CUBIC, SAWLOG CUBIC, AND SAWLOG BOARDFOOT VOLUME
C
C  IF MINIMUM PIECE ISN'T MET OR DIAMETER IS TOO SMALL, DON'T COMPUTE
C  ANY VOLUMES
C----------
        IF(D .LT. DBHMIN(ISPC) .OR. (TOPD(ISPC)
     &  .NE. 0.0 .AND. TOPD(ISPC) .NE. 4.0) )THEN
          PULPCU=0.0
          GOTO 140
        ENDIF
C----------
C  CALL R8VOL TO CALCULATE PULPWOOD AND HEIGHT TO 4 IN TOP
C  SET EQUATION NUMBER FOR PULPWOOD TREE
C----------
         WRITE(CFTOP,'(I1)')NINT(TOPD(ISPC))
         VEQNNC(ISPC)(3:3)=CFTOP
C----------
C  SET CONSTANT ARGUMENTS IN CALL TO R8VOL
C
C  INTEGER CONSTANT ARGUMENTS - ALL PULPWOOD TREE
C----------
        XTOPD=0.
        PROD = '02'
        FORST='  '
        CTYPE='F'
        BFPFLG=0
        CUPFLG=1
        SPFLG=1
        IERR=0
        I01=0
        I02=0
C----------
C  REAL CONSTANT ARGUMENTS
C----------
C----------
C  CHARACTER CONSTANT ARGUMENTS
C----------
C
        CALL R8VOL(VEQNNC(ISPC),D,H,HTC1,HTC2,XTOPD,PROD,VOL,FORST,
     &             I01,I02,CTYPE,BFPFLG,CUPFLG,SPFLG,IERR)
C
        IF(IT.GT.0)HT2TD(IT,2)=HTC2
C
        IF(DEBUG)WRITE(JOSTND,*)' R8 VOLEQN=',VEQNNC(ISPC),'ISPC=',ISPC,
     &    ' D=',D,' H=',H,' HTC1=',HTC1,' HTC2=',HTC2,' PROD=',PROD,
     &    ' VOL4=',VOL(4),' VOL5=',VOL(5),' VOL7=',VOL(7),'PULPWOOD'
C
        PULPCU=VOL(4)
C----------
C  IF MINIMUM PIECE ISN'T MET OR DIAMETER IS TOO SMALL,ZERO
C  VOLUMES
C----------
        IF(HTC1 .LE. 0.0 .OR. D .LT. DBHMIN(ISPC) .OR. (TOPD(ISPC)
     &  .NE. 0.0 .AND. TOPD(ISPC) .NE. 4.0) )THEN
          PULPCU=0.0
          IF(IT.GT.0)HT2TD(IT,2)=0.
        ENDIF
C  SET SAWLOG SPECS TO COMPUTE MERCHANTABLE CF SAWLOG VOLUME OF SAWLOG
C  TREE (TOTAL MERCH = SAWCF + TOPWOOD)
C----------
  140   CONTINUE
        IF ((BFTOPD(ISPC).NE.7.0 .AND. ISPC.LE.ISPLIM)
     &  .OR. ((BFTOPD(ISPC).NE.9.0) .AND. (ISPC.GT.ISPLIM) .AND.
     &  (JSP(ISPC)(1:2).NE.'OS')) .OR. (BFTOPD(ISPC).NE.7.0
     &  .AND.JSP(ISPC)(1:2).EQ.'OS') .OR. (D.LT.BFMIND(ISPC)))THEN
          SAWCU=0.0
          SAWBF=0.0
          IF(IT.GT.0)HT2TD(IT,1)=0.
C          IF(IT.GT.0)HT2TD(IT,2)=0.
          GOTO 200
        ENDIF
C----------
C SAWLOG CUBIC SECTION

C
C----------
        WRITE(BFTOP,'(I1)')NINT(BFTOPD(ISPC))
        VEQNNC(ISPC)(3:3)=BFTOP
C----------
C  SET CONSTANT ARGUMENTS IN CALL TO R8VOL
C
C  INTEGER CONSTANT ARGUMENTS - SAWLOG CF
C----------
        XTOPD=0.
        PROD = '01'
        CTYPE='F'
        FORST='  '
        BFPFLG=0
        CUPFLG=1
        SPFLG=1
        IERR=0
        I01=0
        I02=0
        I0=0
        I1=1
C----------
C THE FIRST CALL TO R8V0L FOR CUBIC MERCH VOL CALCULATES HEIGHT TO
C 4 IN TOP AND CF FOR ALL PULPWOOD TREE. SECOND CALL TO R8VOL 
C CALCULATES SAWLOG CF (VOL(4)) AND SECONDARY PRODUCT (VOL7).
C THE SUM OF THESE IS TOTAL MERCH FOR A SAWLOG TREE = VOL(4)+VOL(7)
C----------
        HTC1=0.
        HTC2=0.
        CALL R8VOL(VEQNNC(ISPC),D,H,HTC1,HTC2,XTOPD,PROD,VOL,FORST,
     &             I01,I02,CTYPE,BFPFLG,CUPFLG,SPFLG,IERR)
        IF(IT.GT.0)HT2TD(IT,1)=HTC2
C
       IF(DEBUG)WRITE(JOSTND,*)' R8 VOLEQN=',VEQNNC(ISPC),'ISPC=',ISPC,
     &  ' D=',D,' H= ',H,' HTC1=',HTC1,' PROD=',PROD,' VOL1=',VOL(1),
     &  ' VOL2=',VOL(2),' VOL3=',VOL(3),' VOL4=',VOL(4),' VOL5=',
     &  VOL(5),' VOL6=',VOL(6),' VOL7=',VOL(7),' SAWCU'
C----------
C  IF MINIMUM PIECE ISN'T MET OR DIAMETER IS TOO SMALL, DON'T COMPUTE
C  ANY VOLUMES
C----------
        IF(HTC1 .LE. 0.0 .OR. D .LT. DBHMIN(ISPC) .OR. (TOPD(ISPC)
     &  .NE. 0.0 .AND. TOPD(ISPC) .NE. 4.0) )THEN
          PULPCU=0.0
        IF(IT.GT.0)HT2TD(IT,2)=0.
        ENDIF
C
        IF(HTC1 .LT. 10.0)THEN
          SAWCU=0.0
          SAWBF=0.0
          IF(IT.GT.0)HT2TD(IT,1)=0.
C          IF(IT.GT.0)HT2TD(IT,2)=0.
        ELSE
          SAWCU=VOL(4)
          PULPCU=VOL(4)+VOL(7)
C----------
C  COMPUTE BOARD FOOT VOLUME FOR SAWLOGS
C  USE MERCH HEIGHT (HTC1) CALCULATED FOR CF VOLUME
C  SET EQUATION NUMBER FOR SAWLOGS
C----------
C  SET CONSTANT ARGUMENTS IN CALL TO R8VOL
C
C  INTEGER CONSTANT ARGUMENTS - BF
C----------
          XTOPD=0.
          PROD = '01'
          CTYPE='F'
          FORST='  '
          BFPFLG=1
          CUPFLG=1
          SPFLG=1
          IERR=0
          I01=0
          I02=0
          I0=0
          I1=1
C
          CALL R8VOL(VEQNNB(ISPC),D,H,HTC1,HTC2,XTOPD,PROD,VOL,FORST,
     &               I01,I02,CTYPE,BFPFLG,CUPFLG,SPFLG,IERR)
C
          IF(IT.GT.0)HT2TD(IT,1)=HTC2
C
          IF(DEBUG)WRITE(JOSTND,*)' R8 VOLEQN=',VEQNNB(ISPC),'ISPC=',
     &  ISPC,' D=',D,' H=',H,'HTC1=',HTC1,' PROD=',PROD,' VOL2=',VOL(2),
     &    ' VOL4=',VOL(4),' VOL5=',VOL(5),' VOL7=',VOL(7),' SAWBD'
C
          SAWBF=VOL(2)
        ENDIF
  200   CONTINUE
C----------
C  SET RETURN VARIABLES.
C----------
        IF(DEBUG)WRITE(JOSTND,*)' R8 TOTCU=',PULPCU,' SAWCU=',SAWCU,
     &   ' SAWBF=',SAWBF
        VN=PULPCU
        VMAX=VN
        VM=SAWCU
        BBFV=SAWBF
        CTKFLG = .TRUE.
        BTKFLG = .TRUE.
        IF(VN.LE.0.)THEN
          CTKFLG = .FALSE.
          VN=0.
        ENDIF
        IF(VM.LE.0.)THEN
          BTKFLG = .FALSE.
          VM=0.
        ENDIF
        IF(BBFV.LE.0.)THEN
          BTKFLG = .FALSE.
          BBFV=0.
        ENDIF
        IF(DEBUG)WRITE(JOSTND,*)' R8 RETURNING VN,VMAX,VM,BBFV= ',
     &  VN,VMAX,VM,BBFV
C
        GOTO 900
      ELSE
C----------
C REGION 9 VOLUME SECTION
C
      DONE=.FALSE.
C----------
C  REAL CONSTANT ARGUMENTS
C----------
      IF(DEBUG)WRITE(JOSTND,*)' ENTERING R9 LGIC'
      X01=0.
      X02=2.
      X03=0.
C----------
C  INTEGER CONSTANT ARGUMENTS
C----------
      IERR=0
      I01=0
      I1=1
C----------
C  CHARACTER CONSTANT ARGUMENTS
C----------
      CTYPE='F'
      HTTYP='L'
C
C----------
C  TREE DBH IS LESS THAN PULPWOOD MIN DBH, OR NO PULPWOOD HT
C----------
      IF(D.LT.DBHMIN(ISPC))THEN
C
C  CASE 1: TREE DOES NOT MEET PULPWOOD SPECS
C
        TOTCU=0.
        X02=0.
        X03=0.
C----------
C  TREE DBH IS >PULPWOOD MIN DBH BUT <SAWTIMBER MIN DBH, OR NO SAW HT
C----------
      ELSEIF(D.LT.BFMIND(ISPC))THEN
C
C  CASE 2: TREE ONLY HAS PULPWOOD (PROD='02')
C
        IF(DEBUG)WRITE(JOSTND,*)' IT,CASE 2 PWVOL ISPC,VEQNNC,D,H= ',
     &  IT,ISPC,VEQNNC(ISPC),D,H
C
        PROD='02'
        X02=0.
        CALL R9CLARK(VEQNNC(ISPC),X01,BFTOPD(ISPC),TOPD(ISPC),D,X02,X03,
     &  H,LOGDIA,BOLTHT,LOGLEN,LOGVOL,VOL,I01,I01,I1,I01,I01,PROD,IERR)
C
        IF(DEBUG)WRITE(JOSTND,*)' AFTER CASE 2 PWVOL VOL= ',VOL
C
        TOTCU=VOL(4)
        SAWCU=0.0
        SAWBF=0.0
        DONE=.TRUE.
C----------
C  TREE DBH IS >SAWTIMBER MIN DBH, POSITIVE SAW HT AND PULP HT
C----------
      ELSE

C  CASE 3: TREE HAS SAWTIMBER AND PULPWOOD (PROD='01' AND TOPWOOD)
C
C  IF THE VOLEQNUM KEYWORD WAS USED TO SET DIFFERENT EQ. NOS. FOR CF AND
C  FOR BF THEN 2 CALLS TO R9CLARK ARE REQUIRED OTHERWISE ONE CALL IS
C  ENOUGH
C
        IF(DEBUG)WRITE(JOSTND,*)' IT,CASE 3 SBVOL VEQNNC(ISPC),H,D,',
     &  IT,VEQNNC(ISPC),H,D
C
        PROD='01'
        CALL R9CLARK(VEQNNC(ISPC),X01,BFTOPD(ISPC),TOPD(ISPC),D,X02,X03,
     &  H,LOGDIA,BOLTHT,LOGLEN,LOGVOL,VOL,I01,I1,I1,I01,I1,PROD,IERR)
        SAWBF=VOL(2)
        SAWCU=VOL(4)
        TOTCU=SAWCU+VOL(7)
        IF(DEBUG)WRITE(JOSTND,*)' AFTER CASE 3 (CALL 1) TCVOL VOL= ',VOL
        IF(VEQNNB(ISPC).EQ.VEQNNC(ISPC))THEN
          DONE=.TRUE.
        ELSE
          IF(DEBUG)WRITE(JOSTND,*)' IT,CASE 3 SBVOL VEQNNB(ISPC),H,D,',
     &    'FORST= ',IT,VEQNNB(ISPC),H,D
          CALL R9CLARK(VEQNNB(ISPC),X01,BFTOPD(ISPC),TOPD(ISPC),D,X02,
     &         X03,H,LOGDIA,BOLTHT,LOGLEN,LOGVOL,VOL,I01,I1,I1,I01,
     &         I1,PROD,IERR)
          SAWBF=VOL(2)
          DONE=.TRUE.
          IF(DEBUG)WRITE(JOSTND,*)' AFTER CASE 3 (CALL 2)TCVOL VOL= ',
     &VOL
        ENDIF       
        IF(DEBUG)WRITE(JOSTND,*)' IT,AFTER CASE 3 TCVOL VOL= ',VOL
      ENDIF
      IF(IT.GT.0)HT2TD(IT,1)=X02
      IF(IT.GT.0)HT2TD(IT,2)=X03
      IF (DONE)GOTO 50
C----------
C--TREE DID NOT MEET PULPWOOD SPECS
C----------
      IF(D .LT. BFMIND(ISPC))THEN
C
C  CASE 4: TREE DOES NOT MEET SAWTIMBER OR PULPWOOD SPECS
C
        SAWCU=0.0
        SAWBF=0.0
      ELSE
C
C  CASE 5: STRANGE CASE SPECS MET AND TREE ONLY HAS SAWTIMBER(PROD='01')
C  IF THE VOLEQNUM KEYWORD WAS USED TO SET DIFFERENT EQ. NOS. FOR CF AND
C  FOR BF THEN 2 CALLS TO R9CLARK ARE REQUIRED OTHERWISE ONE CALL IS
C  ENOUGH
C
        IF(DEBUG)WRITE(JOSTND,*)' IT,CASE 5 R9VOL VEQNNC(ISPC),D= ',
     &  VEQNNC(ISPC),D
C
        PROD='01'
        CALL R9CLARK(VEQNNC(ISPC),X01,BFTOPD(ISPC),TOPD(ISPC),D,X02,
     &  X03,H,LOGDIA,BOLTHT,LOGLEN,LOGVOL,VOL,I01,I1,I1,I01,I01,
     &  PROD,IERR)
C
        IF(DEBUG)WRITE(JOSTND,*)' AFTER CASE 5 (CALL 1) R9VOL VOL= ',VOL
C
        SAWCU=VOL(4)
        SAWBF=VOL(2)
        IF(VEQNNB(ISPC).NE.VEQNNC(ISPC))THEN
          IF(DEBUG)WRITE(JOSTND,*)' IT,CASE 5 R9VOL VEQNNB(ISPC),D= ',
     &    IT,VEQNNB(ISPC),D
          CALL R9CLARK(VEQNNB(ISPC),X01,BFTOPD(ISPC),TOPD(ISPC),D,X02,
     &    X03,H,LOGDIA,BOLTHT,LOGLEN,LOGVOL,VOL,I01,I1,I1,I01,I01,PROD,
     &    IERR)
          SAWBF=VOL(2)
        IF(DEBUG)WRITE(JOSTND,*)' AFTER CASE 5 (CALL 2) R9VOL VOL= ',VOL
        ENDIF
      IF(IT.GT.0)HT2TD(IT,1)=X02
      IF(IT.GT.0)HT2TD(IT,2)=X03
      ENDIF
C----------
C  SET RETURN VALUES.
C----------
   50 CONTINUE
      IF(DEBUG)WRITE(JOSTND,*)' TOTCU=',TOTCU,' SAWCU=',SAWCU,
     &   ' SAWBF=',SAWBF
      VN=TOTCU
      VMAX=VN
      VM=SAWCU
      BBFV=SAWBF
      CTKFLG = .TRUE.
      BTKFLG = .TRUE.
      IF(VN.LE.0.)THEN
        VN=0.
        CTKFLG = .FALSE.
      ENDIF
      IF(VM.LE.0.)THEN
        VM=0.
        BTKFLG = .FALSE.
      ENDIF
      IF(BBFV.LE.0.)THEN
        BBFV=0.
        BTKFLG = .FALSE.
      ENDIF
      IF(DEBUG)WRITE(JOSTND,*)' RETURNING VN,VMAX,VM,BBFV= ',
     &VN,VMAX,VM,BBFV
C
       GO TO 900
       ENDIF
C
C
C
C----------
C  ENTER ANY OTHER CUBIC HERE
C----------
      ENTRY OCFVOL (VN,VM,ISPC,D,H,TKILL,BARK,ITRNC,VMAX,LCONE,
     1              CTKFLG,IT)
      VN=0.
      VMAX=0.
      VM=0.
      CTKFLG = .FALSE.
      GOTO 900
C
C
C----------
      ENTRY OBFVOL (BBFV,ISPC,D,H,TKILL,BARK,ITRNC,VMAX,LCONE,
     1              BTKFLG,IT)
      BBFV=0.
      BTKFLG = .FALSE.
  900 CONTINUE
      RETURN
C
C
C----------
C  ENTRY POINT FOR SENDING VOLUME EQN NUMBER TO THE FVS-TO-NATCRZ ROUTINE
C----------
      ENTRY GETEQN(ISPC,D,H,EQNC,EQNB,TDIBC,TDIBB)
      EQNC='          '
      EQNB='          '
      TDIBC=TOPD(ISPC)*BRATIO(ISPC,D,H)
      TDIBB=BFTOPD(ISPC)*BRATIO(ISPC,D,H)
      RETURN
C
      END
