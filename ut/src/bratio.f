      FUNCTION BRATIO(IS,D,H)
      IMPLICIT NONE
C----------
C UT $Id: bratio.f 0000 2018-02-14 00:00:00Z gedixon $
C----------
C
C FUNCTION TO COMPUTE BARK RATIOS AS A FUNCTION OF DIAMETER AND SPECIES.
C REPLACES ARRAY BKRAT IN BASE MODEL.  
C----------
C  SPECIES ORDER:
C  1=WB,  2=LM,  3=DF,  4=WF,  5=BS,  6=AS,  7=LP,  8=ES,  9=AF, 10=PP,
C 11=PI, 12=WJ, 13=GO, 14=PM, 15=RM, 16=UJ, 17=GB, 18=NC, 19=FC, 20=MC,
C 21=BI, 22=BE, 23=OS, 24=OH
C
C VARIANT EXPANSION:
C GO AND OH USE OA (OAK SP.) EQUATIONS FROM UT
C PM USES PI (COMMON PINYON) EQUATIONS FROM UT
C RM AND UJ USE WJ (WESTERN JUNIPER) EQUATIONS FROM UT
C GB USES BC (BRISTLECONE PINE) EQUATIONS FROM CR
C NC, FC, AND BE USE NC (NARROWLEAF COTTONWOOD) EQUATIONS FROM CR
C MC USES MC (CURL-LEAF MTN-MAHOGANY) EQUATIONS FROM SO
C BI USES BM (BIGLEAF MAPLE) EQUATIONS FROM SO
C OS USES OT (OTHER SP.) EQUATIONS FROM UT
C
C----------
      REAL BARK1(24),BARK2(24),H,D,BRATIO,TEMD
      INTEGER IMAP(24),IS,IEQN
      REAL DANUW
C
      DATA BARK1/ 0.9625,  0.9625,    0.867,    0.890,  0.9502,
     &             0.950,  0.9625,   0.9502,    0.890,  0.8967,
     &                0.,      0.,  0.93789,       0.,      0.,
     &                0.,  0.9625,    0.892,    0.892,     0.9,
     &           0.94782,   0.892,   0.9625,  0.93789/
C
      DATA BARK2/ -.1141,  -.1141,       0.,       0., -0.2528,
     &                0.,  -.1141,   -.2528,       0.,  -.4448,
     &                0.,      0., -0.24096,       0.,      0.,
     &                0., -0.1141,   -0.086,   -0.086,      0.,
     &            0.0836,  -0.086,   -.1141, -0.24096/
C
      DATA IMAP/ 3, 3, 2, 2, 3, 2, 3, 3, 2, 3,
     &           1, 1, 3, 1, 1, 1, 3, 3, 3, 2,
     &           3, 3, 3, 3/
C----------
C  PI, JU, BS, AND OA USE EQUATIONS FROM CR MODEL TYPE 3-5
C  WB, LM, LP, OT USE EQUATIONS FROM CR VARIANT
C  ES USES THE EQUATION FROM THE CR VARIANT
C  PP USES THE EQUATION FROM THE CA VARIANT
C  WF AND AF USE THE CONSTANT RATIO FROM NI VARIANT
C----------
C  DUMMY ARGUMENT NOT USED WARNING SUPPRESSION SECTION
C----------
      DANUW = H
C
      IEQN=IMAP(IS)
      TEMD=D
      IF(TEMD.LT.1.)TEMD=1.
C
      IF(IEQN .EQ. 1) THEN
        IF(BARK1(IS).EQ.0.0 .AND. BARK2(IS).EQ.0.0)THEN
          IF(TEMD.GT.19.)TEMD=19.
          BRATIO = 0.9002 - 0.3089*(1/TEMD)
        ELSE
          BRATIO=BARK1(IS)+BARK2(IS)*D
          BRATIO=1.0/BRATIO
        ENDIF
C
      ELSEIF(IEQN .EQ. 2)THEN
        BRATIO=BARK1(IS)
C
      ELSEIF(IEQN .EQ. 3)THEN
        BRATIO=BARK1(IS)+BARK2(IS)*(1.0/TEMD)
      ENDIF
C
      IF(BRATIO .GT. 0.99) BRATIO=0.99
      IF(BRATIO .LT. 0.80) BRATIO=0.80
C
      RETURN
      END
