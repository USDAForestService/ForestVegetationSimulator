      SUBROUTINE SMHTGF(NSPC,D,CR,BA,BAL,SI,HTGR)
      IMPLICIT NONE
C----------
C WS $Id: smhtgf.f 0000 2018-02-14 00:00:00Z gedixon $
C----------
C THIS SUBROUTINE COMPUTES THE HEIGHT GROWTH FOR SMALL TREES (HTGR)
C IN REAL SCALE.  THIS ROUTINE IS CALLED FROM REGENT.
C----------
      INTEGER NSPC
      REAL HTGR,SI,BAL,BA,CR,D,TEMBAL,FACTOR
      REAL RDANUW
C----------
C     SPECIES LIST FOR WESTERN SIERRAS VARIANT.
C
C     1 = SUGAR PINE (SP)                   PINUS LAMBERTIANA
C     2 = DOUGLAS-FIR (DF)                  PSEUDOTSUGA MENZIESII
C     3 = WHITE FIR (WF)                    ABIES CONCOLOR
C     4 = GIANT SEQUOIA (GS)                SEQUOIADENDRON GIGANTEAUM
C     5 = INCENSE CEDAR (IC)                LIBOCEDRUS DECURRENS
C     6 = JEFFREY PINE (JP)                 PINUS JEFFREYI
C     7 = CALIFORNIA RED FIR (RF)           ABIES MAGNIFICA
C     8 = PONDEROSA PINE (PP)               PINUS PONDEROSA
C     9 = LODGEPOLE PINE (LP)               PINUS CONTORTA
C    10 = WHITEBARK PINE (WB)               PINUS ALBICAULIS
C    11 = WESTERN WHITE PINE (WP)           PINUS MONTICOLA
C    12 = SINGLELEAF PINYON (PM)            PINUS MONOPHYLLA
C    13 = PACIFIC SILVER FIR (SF)           ABIES AMABILIS
C    14 = KNOBCONE PINE (KP)                PINUS ATTENUATA
C    15 = FOXTAIL PINE (FP)                 PINUS BALFOURIANA
C    16 = COULTER PINE (CP)                 PINUS COULTERI
C    17 = LIMBER PINE (LM)                  PINUS FLEXILIS
C    18 = MONTEREY PINE (MP)                PINUS RADIATA
C    19 = GRAY PINE (GP)                    PINUS SABINIANA
C         (OR CALIFORNIA FOOTHILL PINE)
C    20 = WASHOE PINE (WE)                  PINUS WASHOENSIS
C    21 = GREAT BASIN BRISTLECONE PINE (GB) PINUS LONGAEVA
C    22 = BIGCONE DOUGLAS-FIR (BD)          PSEUDOTSUGA MACROCARPA
C    23 = REDWOOD (RW)                      SEQUOIA SEMPERVIRENS
C    24 = MOUNTAIN HEMLOCK (MH)             TSUGA MERTENSIANA
C    25 = WESTERN JUNIPER (WJ)              JUNIPERUS OCIDENTALIS
C    26 = UTAH JUNIPER (UJ)                 JUNIPERUS OSTEOSPERMA
C    27 = CALIFORNIA JUNIPER (CJ)           JUNIPERUS CALIFORNICA
C    28 = CALIFORNIA LIVE OAK (LO)          QUERCUS AGRIFOLIA
C    29 = CANYON LIVE OAK (CY)              QUERCUS CHRYSOLEPSIS
C    30 = BLUE OAK (BL)                     QUERCUS DOUGLASII
C    31 = CALIFORNIA BLACK OAK (BO)         QUERQUS KELLOGGII
C    32 = VALLEY OAK (VO)                   QUERCUS LOBATA
C         (OR CALIFORNIA WHITE OAK)
C    33 = INTERIOR LIVE OAK (IO)            QUERCUS WISLIZENI
C    34 = TANOAK (TO)                       LITHOCARPUS DENSIFLORUS
C    35 = GIANT CHINQUAPIN (GC)             CHRYSOLEPIS CHRYSOPHYLLA
C    36 = QUAKING ASPEN (AS)                POPULUS TREMULOIDES
C    37 = CALIFORNIA-LAUREL (CL)            UMBELLULARIA CALIFORNICA
C    38 = PACIFIC MADRONE (MA)              ARBUTUS MENZIESII
C    39 = PACIFIC DOGWOOD (DG)              CORNUS NUTTALLII
C    40 = BIGLEAF MAPLE (BM)                ACER MACROPHYLLUM
C    41 = CURLLEAF MOUNTAIN-MAHOGANY (MC)   CERCOCARPUS LEDIFOLIUS
C    42 = OTHER SOFTWOODS (OS)
C    43 = OTHER HARDWOODS (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C    FROM EXISTING WS EQUATIONS --
C      USE 1(SP) FOR 11(WP) AND 24(MH) 
C      USE 2(DF) FOR 22(BD)
C      USE 3(WF) FOR 13(SF)
C      USE 4(GS) FOR 23(RW)
C      USE 8(PP) FOR 18(MP)
C      USE 34(TO) FOR 35(GC), 36(AS), 37(CL), 38(MA), AND 39(DG)
C      USE 31(BO) FOR 28(LO), 29(CY), 30(BL), 32(VO), 33(IO), 40(BM), AND
C                     43(OH)
C
C    FROM CA VARIANT --
C      USE CA11(KP) FOR 12(PM), 14(KP), 15(FP), 16(CP), 17(LM), 19(GP), 20(WE), 
C                       25(WJ), 26(WJ), AND 27(CJ)
C      USE CA12(LP) FOR 9(LP) AND 10(WB)
C
C    FROM SO VARIANT --
C      USE SO30(MC) FOR 41(MC)
C
C    FROM UT VARIANT --
C      USE UT17(GB) FOR 21(GB)
C----------
C  DUMMY ARGUMENT NOT USED WARNING SUPPRESSION SECTION
C----------
      RDANUW = BA
      RDANUW = D
C
C----------
      TEMBAL=BAL
      IF(TEMBAL .LT. 5.0) TEMBAL=5.0
C
      SELECT CASE (NSPC)
C
C  NOTE: SPECIES 21=GB, AND 41=MC DO NOT USE THIS ROUTINE
C
C----------
C  SPECIES USING WS EQUATIONS: PINES
C  1=SP, 5=IC, 6=JP, 8=PP, 11=WP, 18=MP, 24=MH, 42=OS
C  TEST RUNS SHOW NEED FOR + & * CONSTANTS TO MATCH GROWTH
C  DATA.  GD  3-24.94
C----------
      CASE(1,5,6,8,11,18,24,42)
        HTGR = 0.7452 - 0.003271*BAL - 0.1632*CR + 0.0217*CR*CR
     &         + 0.00536*SI
        HTGR = EXP(HTGR)
        IF(NSPC.EQ.5 .OR. 
     &     NSPC.EQ.1 .OR. NSPC.EQ.11 .OR. NSPC.EQ.24) THEN
          HTGR = HTGR*1.75*(.80+0.004*(SI-50.))
        ELSE
          HTGR = HTGR*1.50*(.80+0.004*(SI-50.))
        ENDIF
C----------
C  SPECIES USING WS EQUATIONS: FIRS
C  2=DF, 3=WF, 4=GS, 7=RF, 13=SF, 22=BD, 23=RW
C  TEST RUNS SHOW NEED FOR + CONSTANT & * CONSTANT TO MATCH GROWTH
C  DATA.  GD 3-24-94
C----------
      CASE(2,3,4,7,13,22,23)
        HTGR = -0.2495 - 0.00111*BAL + 0.0100*CR*CR
        HTGR = EXP(HTGR)
        IF(NSPC.EQ.2 .OR. NSPC.EQ.22 .OR. 
     &     NSPC.EQ.4 .OR. NSPC.EQ.23)THEN
          HTGR = (HTGR+1.)*2.5*(.80+0.004*(SI-50.))
        ELSE
          HTGR = (HTGR+0.75)*2.0*(.80+0.004*(SI-50.))
        ENDIF
C----------
C  SPECIES USING WS EQUATIONS: BLACK OAK
C  28=LO, 29=CY, 30=BL, 31=BO, 32=VO, 33=IO, 40=BM, 43=OH
C----------
      CASE(28:33,40,43)
        HTGR = EXP(3.817 - 0.7829*ALOG(TEMBAL))
C----------
C  SPECIES USING WS EQUATIONS: TANOAK
C  34=TO, 35=GC, 36=AS, 37=CL, 38=MA, 39=DG
C----------
      CASE(34:39)
        HTGR = EXP(3.385 - 0.5898*ALOG(TEMBAL))
C----------
C  SPECIES USING CA EQUATIONS: LODGEPOLE PINE
C  9=LP, 10=WB  
C----------
      CASE(9,10)
        FACTOR = 0.80+0.004*(SI-50.)
        HTGR = EXP(0.7452 - 0.003271*BAL - 0.1632*CR + 0.0217*CR*CR
     &         + 0.00536*SI) * FACTOR * 1.75
C----------
C  SPECIES USING CA EQUATIONS: KNOBCONE PINE
C  12=PM, 14=KP, 15=FP, 16=CP, 17=LM, 19=GP, 20=WE, 25=WJ, 26=UJ, 27=CJ
C----------
      CASE(12,14:17,19:20,25:27)
        FACTOR = 0.80+0.004*(SI-50.)
        HTGR = EXP(0.7452 - 0.003271*BAL - 0.1632*CR + 0.0217*CR*CR
     &         + 0.00536*SI) * FACTOR * 1.75
C
      END SELECT
C----------
C  IF ESTIMATED HEIGHT INCREMENT IS CLOSE TO ZERO
C----------
      IF(HTGR .LT. 0.1) HTGR = 0.1
C
      RETURN
      END
