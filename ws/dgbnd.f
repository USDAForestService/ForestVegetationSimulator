      SUBROUTINE DGBND (ISPC,DBH,DDG)
      IMPLICIT NONE
C----------
C WS $Id$
C----------
C  THIS SUBROUTINE IS USED TO INSURE THAT A MAXIMUM VALUE FOR DG
C  IS NOT EXCEEDED.  EQUATION AND COEFFICIENTS ARE FROM DOLPH AND
C  DIXON (1993), WEST.J.APPL.FOR 8(1):24-27.
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
COMMONS
C----------
      EXTERNAL RANN
      INTEGER ISPC
      REAL DGAMAX(MAXSP),DGBMAX(MAXSP),DGCMAX(MAXSP),DDG,DBH,DGMAX
C----------
C     SPECIES LIST FOR WESTERN SIERRAS VARIANT.
C
C     1 = SUGAR PINE (SP)                   PINUS LAMBERTIANA
C     2 = DOUGLAS-FIR (DF)                  PSEUDOTSUGA MENZIESII
C     3 = WHITE FIR (WF)                    ABIES CONCOLOR
C     4 = GIANT SEQUOIA (GS)                SEQUOIADENDRON GIGANTEAUM
C     5 = INCENSE CEDAR (IC)                LIBOCEDRUS DECURRENS
C     6 = JEFFREY PINE (JP)                 PINUS JEFFREYI
C     7 = CALIFORNIA RED FIR (RF)           ABIES MAGNIFICA
C     8 = PONDEROSA PINE (PP)               PINUS PONDEROSA
C     9 = LODGEPOLE PINE (LP)               PINUS CONTORTA
C    10 = WHITEBARK PINE (WB)               PINUS ALBICAULIS
C    11 = WESTERN WHITE PINE (WP)           PINUS MONTICOLA
C    12 = SINGLELEAF PINYON (PM)            PINUS MONOPHYLLA
C    13 = PACIFIC SILVER FIR (SF)           ABIES AMABILIS
C    14 = KNOBCONE PINE (KP)                PINUS ATTENUATA
C    15 = FOXTAIL PINE (FP)                 PINUS BALFOURIANA
C    16 = COULTER PINE (CP)                 PINUS COULTERI
C    17 = LIMBER PINE (LM)                  PINUS FLEXILIS
C    18 = MONTEREY PINE (MP)                PINUS RADIATA
C    19 = GRAY PINE (GP)                    PINUS SABINIANA
C         (OR CALIFORNIA FOOTHILL PINE)
C    20 = WASHOE PINE (WE)                  PINUS WASHOENSIS
C    21 = GREAT BASIN BRISTLECONE PINE (GB) PINUS LONGAEVA
C    22 = BIGCONE DOUGLAS-FIR (BD)          PSEUDOTSUGA MACROCARPA
C    23 = REDWOOD (RW)                      SEQUOIA SEMPERVIRENS
C    24 = MOUNTAIN HEMLOCK (MH)             TSUGA MERTENSIANA
C    25 = WESTERN JUNIPER (WJ)              JUNIPERUS OCIDENTALIS
C    26 = UTAH JUNIPER (UJ)                 JUNIPERUS OSTEOSPERMA
C    27 = CALIFORNIA JUNIPER (CJ)           JUNIPERUS CALIFORNICA
C    28 = CALIFORNIA LIVE OAK (LO)          QUERCUS AGRIFOLIA
C    29 = CANYON LIVE OAK (CY)              QUERCUS CHRYSOLEPSIS
C    30 = BLUE OAK (BL)                     QUERCUS DOUGLASII
C    31 = CALIFORNIA BLACK OAK (BO)         QUERQUS KELLOGGII
C    32 = VALLEY OAK (VO)                   QUERCUS LOBATA
C         (OR CALIFORNIA WHITE OAK)
C    33 = INTERIOR LIVE OAK (IO)            QUERCUS WISLIZENI
C    34 = TANOAK (TO)                       LITHOCARPUS DENSIFLORUS
C    35 = GIANT CHINQUAPIN (GC)             CHRYSOLEPIS CHRYSOPHYLLA
C    36 = QUAKING ASPEN (AS)                POPULUS TREMULOIDES
C    37 = CALIFORNIA-LAUREL (CL)            UMBELLULARIA CALIFORNICA
C    38 = PACIFIC MADRONE (MA)              ARBUTUS MENZIESII
C    39 = PACIFIC DOGWOOD (DG)              CORNUS NUTTALLII
C    40 = BIGLEAF MAPLE (BM)                ACER MACROPHYLLUM
C    41 = CURLLEAF MOUNTAIN-MAHOGANY (MC)   CERCOCARPUS LEDIFOLIUS
C    42 = OTHER SOFTWOODS (OS)
C    43 = OTHER HARDWOODS (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C    FROM EXISTING WS EQUATIONS --
C      USE 1(SP) FOR 11(WP) AND 24(MH) 
C      USE 2(DF) FOR 22(BD)
C      USE 3(WF) FOR 13(SF)
C      USE 4(GS) FOR 23(RW)
C      USE 8(PP) FOR 18(MP)
C      USE 34(TO) FOR 35(GC), 36(AS), 37(CL), 38(MA), AND 39(DG)
C      USE 31(BO) FOR 28(LO), 29(CY), 30(BL), 32(VO), 33(IO), 40(BM), AND
C                     43(OH)
C
C    FROM CA VARIANT --
C      USE CA11(KP) FOR 12(PM), 14(KP), 15(FP), 16(CP), 17(LM), 19(GP), 20(WE), 
C                       25(WJ), 26(WJ), AND 27(CJ)
C      USE CA12(LP) FOR 9(LP) AND 10(WB)
C
C    FROM SO VARIANT --
C      USE SO30(MC) FOR 41(MC)
C
C    FROM UT VARIANT --
C      USE UT17(GB) FOR 21(GB)
C----------
      DATA DGAMAX/
     &   4.9094,   1.8388,   3.8681,   4.9094,   1.9557,
     &   3.7202,   3.8681,   3.7202,       0.,       0.,
     &   4.9094,       0.,   3.8681,       0.,       0.,
     &       0.,       0.,   3.7202,       0.,       0.,
     &       0.,   1.8388,   4.9094,   4.9094,       0.,
     &       0.,       0.,   1.9557,   1.9557,   1.9557,
     &   1.9557,   1.9557,   1.9557,   1.9557,   1.9557,
     &   1.9557,   1.9557,   1.9557,   1.9557,   1.9557,
     &       0.,   3.7202,   1.9557/
C
      DATA DGBMAX/
     &   0.4678,   0.7319,   0.5043,   0.4678,   0.4655,
     &   0.3860,   0.5043,   0.3860,       0.,       0.,
     &   0.4678,       0.,   0.5043,       0.,       0.,
     &       0.,       0.,   0.3860,       0.,       0.,
     &       0.,   0.7319,   0.4678,   0.4678,       0.,
     &       0.,       0.,   0.4655,   0.4655,   0.4655,
     &   0.4655,   0.4655,   0.4655,   0.4655,   0.4655,
     &   0.4655,   0.4655,   0.4655,   0.4655,   0.4655,
     &       0.,   0.3860,   0.4655/
C
      DATA DGCMAX/
     & -0.04365, -0.05194, -0.04492, -0.04365, -0.05613,
     & -0.03852, -0.04492, -0.03852,       0.,       0.,
     & -0.04365,       0., -0.04492,       0.,       0.,
     &       0.,       0., -0.03852,       0.,       0.,
     &       0., -0.05194, -0.04365, -0.04365,       0.,
     &       0.,       0., -0.05613, -0.05613, -0.05613,
     & -0.05613, -0.05613, -0.05613, -0.05613, -0.05613,
     & -0.05613, -0.05613, -0.05613, -0.05613, -0.05613,
     &       0., -0.03852, -0.05613/
C----------
C MAX DG CHECK (ONLY APPLIES TO SPECIES USING WS VARIANT EQUATIONS
C----------
      SELECT CASE (ISPC)
      CASE(1:8,11,13,18,22:24,28:40,42:43)
        IF(DBH.LT.3.)GO TO 900
        DGMAX = DGAMAX(ISPC) * (DBH**DGBMAX(ISPC)) *
     &          EXP(DGCMAX(ISPC)*DBH)
        IF(DDG .GT. DGMAX) DDG = DGMAX
        IF(DDG .LT. 0.0) DDG = 0.0
      END SELECT
C----------
C CHECK FOR SIZE CAP COMPLIANCE.
C----------
  900 CONTINUE
      IF((DBH+DDG).GT.SIZCAP(ISPC,1) .AND. SIZCAP(ISPC,3).LT.1.5)THEN
        DDG=SIZCAP(ISPC,1)-DBH
        IF(DDG .LT. 0.01) DDG=0.01
      ENDIF
C
      RETURN
      END
