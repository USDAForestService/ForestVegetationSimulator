      SUBROUTINE R5CRWD(ISPC,D,H,CRWDTH)
      IMPLICIT NONE
C----------
C WS $Id: r5crwd.f 0000 2018-06-14 00:00:00Z gedixon $
C----------
C  THIS ROUTINE COMPUTES CROWN WIDTH FOR INDIVIDUAL TREES.
C  CALLED FROM CCFCAL IN ALL REGION 5 VARIANTS.
C
C  CROWN WIDTH EQUATIONS FOR REGION 5:
C  EQUATIONS FOR TREES LARGER THAN THE THRESHOLD DBH ARE FROM
C  RALPH WARBINGTON AND JACK LEVITAN AS PRESENTED IN THE R5 FIA
C  USERS GUIDE.
C  EQUATIONS FOR TREES 4.5 FEET TALL UP TO THE THRESHOLD DBH WERE
C  DEVELOPED BY GARY DIXON.  THOSE FOR DOUGLAS-FIR, PONDEROSA PINE,
C  WHITE FIR, GIANT CHINQUAPIN, MADRONE, AND TANOAK WERE FIT FROM DATA
C  IN THE SIX RIVERS PLANTATION INVENTORY.  SLOPE VALUES WERE ADJUSTED
C  SLIGHTLY FROM THE REGRESSION VALUES TO INTERCECT THE WARBINGTON &
C  LEVITAN EQUATIONS AT THE THRESHOLD DBH.  FOR OTHER SPECIES, THE
C  GENERIC INTERCEPT OF 3.5 FOR CONIFERS, AND 2.5 FOR HARDWOODS, WAS
C  USED, AND SLOPE VALUES COMPUTED TO INTERSECT THE W/L EQUATIONS AT
C  THE THRESHOLD DBH.
C  EQUATIONS FOR TREES LESS THAN 4.5 FEET TALL ARE JUST A STRAIGHT LINE
C  FROM (0,0) UP TO THE GENERIC INTERCEPT VALUES.
C  EQUATIONS 29-35 ARE FROM ANN WITHERS 4/29/1996 INTERNAL MEMO TO
C  GARY DIXON.
C
C----------
C  DEFINITION OF VARIABLES:
C
C     CROWN WIDTH COEFFICIENTS FOR WARBINGTON/LEVITAN EQUATIONS
C       CRWDTH = WB1() + WB2()*D                   EQN TYPE 1
C       CRWDTH = WB1() * D**WB2()                  EQN TYPE 2
C       CRWDTH = WB1() + WB2()*D + WB3()*D*D       EQN TYPE 3
C       WB1() = CONSTANT OR INTERCEPT, SUBSCRIPTED BY SPECIES
C       WB2() = CONSTANT OR EXPONENT, SUBSCRIPTED BY SPECIES
C       WB3() = CONSTANT, SUBSCRIPTED BY SPECIES
C
C     CROWN WIDTH COEFFICIENTS FOR DIXON EQUATIONS
C       CRWDTH = DX1() + DX2()*D
C       DX1() = INTERCEPT, SUBSCRIPTED BY SPECIES
C       DX2() = DBH CONSTANT, SUBSCRIPTED BY SPECIES
C
C     CRWDTH = CROWN WIDTH FOR A TREE (FEET)
C     D      = DIAMETER AT BREAST HEIGHT (INCHES)
C     H      = TOTAL TREE HEIGHT (FEET)
C     ISPC   = NUMERIC FVS SPECIES CODE
C
C  TO CONVERT CROWN WIDTH TO CCF:
C  THE CONSTANT 0.001803 IS USED TO CONVERT CROWN WIDTH IN FEET
C  TO THE PERCENT OF AN ACRE COVERED BY THE CROWN
C     CCF = (PIE * (CRWDTH/2)**2 / 43560) * 100
C     CCF = 0.001803 * CRWDTH**2
C
C  ORDER OF COEFFICIENTS IN THIS SUBROUTINE:
C
C   1 = DOUGLAS FIR
C   2 = SUGAR PINE
C   3 = WESTERN WHITE PINE
C   4 = WHITE FIR
C   5 = RED FIR
C   6 = MOUNTAIN HEMLOCK
C   7 = INCENSE CEDAR
C   8 = BLACK OAK
C   9 = CANYON LIVE OAK
C  10 = TANOAK
C  11 = MADRONE
C  12 = GRAND FIR
C  13 = SPRUCE
C  14 = WESTERN HEMLOCK
C  15 = YEW
C  16 = WESTERN RED CEDAR
C  17 = RED ALDER
C  18 = ASH/COTTONWOOD/ASPEN
C  19 = OREGON WHITE OAK
C
C  20 = CHINQUAPIN
C
C  21 = PONDEROSA PINE
C  22 = JEFFREY PINE
C  23 = LODGEPOLE PINE
C  24 = WHITEBARK PINE
C  25 = WESTERN JUNIPER
C  26 = BLUE OAK
C
C  27 = OTHER SOFTWOODS
C  28 = OTHER HARDWOODS
C
C  29 = BIGCONE DOUGLAS-FIR
C  30 = CA BAY LAUREL
C  31 = REDWOOD
C  32 = COULTER PINE
C  33 = DIGGER (GRAY) PINE
C  34 = COAST LIVE OAK
C  35 = CA WHITE (VALLEY) OAK
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
COMMONS
C----------
      REAL WB1(35),WB2(35),WB3(35),DX1(35),DX2(35),SM(35)
      INTEGER IEQN(35)
      REAL SPLINE(35)
      INTEGER MAPSO(33),MAPNC(11),MAPWS(43),MAPCA(49)
      REAL CRWDTH,H,D,SPDBH
      INTEGER ISPC,INDX,ITYPE
C
      DATA WB1/
     &  6.81 , -1.476, -0.997,  5.82 ,  6.71 ,  4.72 ,  7.11 , 10.0  ,
     &  5.0  , 10.0  ,  1.0  ,  6.19 ,  6.50 ,  4.57 ,  4.2  ,  4.00 ,
     &  8.00 ,  0.50 ,  3.08 ,  2.98 ,  2.24 ,  1.52 ,  1.91 ,  2.37 ,
     &  4.31 ,  4.49 ,  6.0  ,  2.0  , 27.030, 12.733, 9.0684, 3.9347,
     & 3.8273, 5.3732, 4.5628/
      DATA WB2/
     & 0.732 , 1.01  , 0.92  , 0.591 , 0.421 , 0.608 , 0.470 , 1.20  ,
     & 1.69  , 1.05  , 1.43  , 1.01  , 1.80  , 1.41  , 1.42  , 1.60  ,
     & 1.53  , 1.62  , 1.92  , 1.55  , 0.763 , 0.891 , 0.784 , 0.736 ,
     & 0.628 , 0.688 , 0.6   , 1.5   , 0.8612, 2.249 , 0.4702, 0.7086,
     & 0.7624, 0.7707, 0.6925/
      DATA WB3/ 19*0., -0.014, 8*0., 7*0. /
C
      DATA DX1/
     &  3.62 ,  3.5  ,  3.5  ,  3.26 ,  3.5  ,  3.5  ,  3.5  ,  2.5  ,
     &  2.5  ,  2.23 ,  3.11 ,  3.5  ,  3.5  ,  3.5  ,  3.5  ,  3.5  ,
     &  2.5  ,  2.5  ,  2.5  ,  2.15 ,  3.77 ,  3.5  ,  3.5  ,  3.5  ,
     &  3.5  ,  2.5  ,  3.5  ,  2.5  ,  3.5  ,  2.5  ,  3.5  ,  3.5  ,
     &  3.5  ,  2.5  ,  2.5  /
      DATA DX2/
     &  1.370,  0.338,  0.329,  1.103,  1.063,  0.852,  1.192,  2.700,
     &  2.190,  1.630,  1.008,  1.548,  2.400,  1.624,  1.560,  1.700,
     &  2.630,  1.220,  2.036,  1.646, 0.7756, 0.5754, 0.6492, 0.8496,
     & 1.6684, 2.2175,  1.1  ,  1.4  , 5.5672, 4.2956, 3.1654, 1.7618,
     & 1.9108, 3.2150, 2.2816/
C
      DATA IEQN/ 19*1, 3, 6*2, 2*1, 2*1, 5*2 /
C
      DATA SPLINE/
     &  5.0  ,  7.4  ,  7.6  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,
     &  5.0  , 13.4  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,
     &  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,
     &  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,  5.0  ,
     &  5.0  ,  5.0  ,  5.0  /
C
      DATA SM/
     & 0.7778, 0.7778, 0.7778, 0.7778, 0.7778, 0.7778, 0.7778, 0.5556,
     & 0.5556, 0.5556, 0.5556, 0.7778, 0.7778, 0.7778, 0.7778, 0.7778,
     & 0.5556, 0.5556, 0.5556, 0.5556, 0.7778, 0.7778, 0.7778, 0.7778,
     & 0.7778, 0.5556, 0.7778, 0.5556, 0.7778, 0.5556, 0.7778, 0.7778,
     & 0.7778, 0.5556, 0.5556/
C----------
C MAPPING FUNCTION FOR SPECIES IN THE --SO-- VARIANT
C  1=WP,  2=SP,  3=DF,  4=WF,  5=MH,  6=IC,  7=LP,  8=ES,  9=SH, 10=PP,
C 11=WJ, 12=GF, 13=AF, 14=SF, 15=NF, 16=WB, 17=WL, 18=RC, 19=WH, 20=PY,
C 21=WA, 22=RA, 23=BM, 24=AS, 25=CW, 26=CH, 27=WO, 28=WI, 29=GC, 30=MC,
C 31=MB, 32=OS, 33=OH
C----------
      DATA MAPSO/
     &  3,  2,  1,  4,  6,  7, 23, 13,  5, 21,
     & 27, 12,  5,  4,  4, 24,  6, 16, 14, 15,
     & 17, 17, 28, 18, 18, 28, 19, 28, 20, 28,
     & 28, 27, 28/
C----------
C MAPPING FUNCTION FOR SPECIES IN THE --NC-- VARIANT
C 1=OC, 2=SP, 3=DF, 4=WF, 5=MA, 6=IC, 7=BO, 8=TO, 9=RF, 10=PP, 11=OH
C----------
      DATA MAPNC/
     & 27,  2,  1,  4, 11,  7,  8, 10,  5, 21, 28/
C----------
C MAPPING FUNCTION FOR SPECIES IN THE --WS-- VARIANT
C NOTE: 21=GB DOESN'T USE THIS ROUTINE; EQN 1 SET TO PREVENT INDEX OF 0
C             AS A PRECAUTION.
C
C     SPECIES LIST FOR WESTERN SIERRAS VARIANT.
C
C     1 = SUGAR PINE (SP)                   PINUS LAMBERTIANA
C     2 = DOUGLAS-FIR (DF)                  PSEUDOTSUGA MENZIESII
C     3 = WHITE FIR (WF)                    ABIES CONCOLOR
C     4 = GIANT SEQUOIA (GS)                SEQUOIADENDRON GIGANTEAUM
C     5 = INCENSE CEDAR (IC)                LIBOCEDRUS DECURRENS
C     6 = JEFFREY PINE (JP)                 PINUS JEFFREYI
C     7 = CALIFORNIA RED FIR (RF)           ABIES MAGNIFICA
C     8 = PONDEROSA PINE (PP)               PINUS PONDEROSA
C     9 = LODGEPOLE PINE (LP)               PINUS CONTORTA
C    10 = WHITEBARK PINE (WB)               PINUS ALBICAULIS
C    11 = WESTERN WHITE PINE (WP)           PINUS MONTICOLA
C    12 = SINGLELEAF PINYON (PM)            PINUS MONOPHYLLA
C    13 = PACIFIC SILVER FIR (SF)           ABIES AMABILIS
C    14 = KNOBCONE PINE (KP)                PINUS ATTENUATA
C    15 = FOXTAIL PINE (FP)                 PINUS BALFOURIANA
C    16 = COULTER PINE (CP)                 PINUS COULTERI
C    17 = LIMBER PINE (LM)                  PINUS FLEXILIS
C    18 = MONTEREY PINE (MP)                PINUS RADIATA
C    19 = GRAY PINE (GP)                    PINUS SABINIANA
C         (OR CALIFORNIA FOOTHILL PINE)
C    20 = WASHOE PINE (WE)                  PINUS WASHOENSIS
C    21 = GREAT BASIN BRISTLECONE PINE (GB) PINUS LONGAEVA
C    22 = BIGCONE DOUGLAS-FIR (BD)          PSEUDOTSUGA MACROCARPA
C    23 = REDWOOD (RW)                      SEQUOIA SEMPERVIRENS
C    24 = MOUNTAIN HEMLOCK (MH)             TSUGA MERTENSIANA
C    25 = WESTERN JUNIPER (WJ)              JUNIPERUS OCIDENTALIS
C    26 = UTAH JUNIPER (UJ)                 JUNIPERUS OSTEOSPERMA
C    27 = CALIFORNIA JUNIPER (CJ)           JUNIPERUS CALIFORNICA
C    28 = CALIFORNIA LIVE OAK (LO)          QUERCUS AGRIFOLIA
C    29 = CANYON LIVE OAK (CY)              QUERCUS CHRYSOLEPSIS
C    30 = BLUE OAK (BL)                     QUERCUS DOUGLASII
C    31 = CALIFORNIA BLACK OAK (BO)         QUERQUS KELLOGGII
C    32 = VALLEY OAK (VO)                   QUERCUS LOBATA
C         (OR CALIFORNIA WHITE OAK)
C    33 = INTERIOR LIVE OAK (IO)            QUERCUS WISLIZENI
C    34 = TANOAK (TO)                       LITHOCARPUS DENSIFLORUS
C    35 = GIANT CHINQUAPIN (GC)             CHRYSOLEPIS CHRYSOPHYLLA
C    36 = QUAKING ASPEN (AS)                POPULUS TREMULOIDES
C    37 = CALIFORNIA-LAUREL (CL)            UMBELLULARIA CALIFORNICA
C    38 = PACIFIC MADRONE (MA)              ARBUTUS MENZIESII
C    39 = PACIFIC DOGWOOD (DG)              CORNUS NUTTALLII
C    40 = BIGLEAF MAPLE (BM)                ACER MACROPHYLLUM
C    41 = CURLLEAF MOUNTAIN-MAHOGANY (MC)   CERCOCARPUS LEDIFOLIUS
C    42 = OTHER SOFTWOODS (OS)
C    43 = OTHER HARDWOODS (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C    FROM EXISTING WS EQUATIONS --
C      USE 1(SP) FOR 11(WP) AND 24(MH) 
C      USE 3(WF) FOR 13(SF)
C      USE 8(PP) FOR 18(MP)
C      USE 34(TO) FOR 35(GC), 36(AS), 38(MA), AND 39(DG)
C      USE 31(BO) FOR 29(CY), 30(BL), 33(IO), 40(BM), AND
C                     43(OH)
C
C    FROM CA VARIANT --
C      USE CA11(KP) FOR 12(PM), 14(KP), 15(FP), 17(LM), 20(WE), 25(WJ),
C                     26(WJ), AND 27(CJ)
C      USE CA12(LP) FOR 9(LP) AND 10(WB)
C
C    FROM SO VARIANT --
C      USE SO30(MC) FOR 41(MC)
C
C    FROM UT VARIANT --
C      USE UT17(GB) FOR 21(GB)
C----------
      DATA MAPWS/
     &  2,  1,  4, 27,  7, 22,  5, 21, 23, 24,
     &  3, 27,  4, 27, 27, 32, 27, 27, 33, 27,
     & 27, 29, 31,  6, 25, 25, 25, 34,  9, 26,
     &  8, 35, 28, 10, 20, 18, 30, 11, 28, 28,
     & 28, 27, 28/
C----------
C MAPPING FUNCTION FOR SPECIES IN THE --CA-- VARIANT
C  1=PC  2=IC  3=RC  4=WF  5=RF  6=SF  7=DF  8=WH  9=MH 10=WB
C 11=KP 12=LP 13=CP 14=LM 15=JP 16=SP 17=WP 18=PP 19=MP 20=GP
C 21=JU 22=BS 23=GS 24=PY 25=OS 26=LO 27=CY 28=DO 29=EO 30=WO
C 31=BO 32=VO 33=IO 34=BM 35=BU 36=RA 37=MA 38=GC 39=DG 40=OA
C 41=WA 42=TO 43=SY 44=AS 45=CO 46=WI 47=CN 48=BL 49=OH
C----------
      DATA MAPCA/
     & 27,  7, 16,  4,  5,  5,  1, 14,  6, 24,
     & 27, 23, 32, 27, 22,  2,  3, 21, 27, 33,
     & 25, 13, 27, 15, 27, 34,  9, 26, 28, 19,
     &  8, 35, 28, 28, 28, 17, 11, 20, 28, 18,
     & 28, 10, 28, 18, 18, 28, 28, 28, 28/
C----------
C ASSIGN THE APPROPRIATE EQUATION NUMBER BASED ON THE VARIANT.
C----------
      SELECT CASE (VARACD)
C
        CASE ('CA','OC')
          INDX=MAPCA(ISPC)
C
        CASE ('NC')
          INDX=MAPNC(ISPC)
C
        CASE ('SO')
          INDX=MAPSO(ISPC)
C
        CASE ('WS')
          INDX=MAPWS(ISPC)
C
        CASE DEFAULT
          INDX=99
C
      END SELECT
C----------
C  SET EQUATION TYPE AND SPLINE DBH
C----------
      ITYPE = IEQN(INDX)
      SPDBH = SPLINE(INDX)
C----------
C COMPUTE CROWN WIDTH
C----------
      IF(INDX .EQ. 99) THEN
        CRWDTH=0.
        GO TO 100
      ENDIF
C
      IF(D .GE. SPDBH) THEN
        IF(ITYPE .EQ. 1) THEN
          CRWDTH = WB1(INDX) + WB2(INDX)*D
        ELSE IF(ITYPE .EQ. 2) THEN
          CRWDTH = WB1(INDX) * D**WB2(INDX)
        ELSE IF(ITYPE .EQ. 3) THEN
          CRWDTH = WB1(INDX) + WB2(INDX)*D + WB3(INDX)*D*D
        ELSE
          CRWDTH = 0.
        ENDIF
      ELSE IF(H .GE. 4.5) THEN
        CRWDTH = DX1(INDX) + DX2(INDX)*D
      ELSE
        CRWDTH = SM(INDX)*H
      ENDIF
C
  100 CONTINUE
      RETURN
      END
