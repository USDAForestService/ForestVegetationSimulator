      SUBROUTINE CCFCAL(ISPC,D,H,JCR,P,LTHIN,CCFT,CRWDTH,MODE)
      IMPLICIT NONE
C----------
C  **CCFCAL--WS   DATE OF LAST REVISION:  05/09/11
C----------
C  THIS ROUTINE COMPUTES CROWN WIDTH AND CCF FOR INDIVIDUAL TREES.
C  CALLED FROM DENSE, PRTRLS, SSTAGE, AND CVCW.
C
C  ARGUMENT DEFINITIONS:
C    ISPC = NUMERIC SPECIES CODE
C       D = DIAMETER AT BREAST HEIGHT
C       H = TOTAL TREE HEIGHT
C     JCR = CROWN RATIO IN PERCENT (0-100)
C       P = TREES PER ACRE
C   LTHIN = .TRUE. IF THINNING HAS JUST OCCURRED
C         = .FALSE. OTHERWISE
C    CCFT = CCF REPRESENTED BY THIS TREE
C  CRWDTH = CROWN WIDTH OF THIS TREE
C    MODE = 1 IF ONLY NEED CCF RETURNED
C           2 IF ONLY NEED CRWDTH RETURNED
C
C  CROWN WIDTH EQUATIONS FOR REGION 5:
C  FROM WARBINGTON/LEVITAN & DIXON --- SEE DOCUMENTATION IN
C                                      SUBROUTINE **R5CRWD**
C----------
COMMONS
C
C
      INCLUDE  'PRGPRM.F77'
C
C
COMMONS
C----------
C     CONVERTING CROWN WIDTH TO CCF:
C     THE CONSTANT 0.001803 CAN BE USED TO CONVERT CROWN WIDTH
C     IN FEET TO THE PERCENT OF AN ACRE COVERED BY THE CROWN
C     CCF = (PIE * (CRWDTH/2)**2 / 43560) * 100
C     CCF = 0.001803 * CRWDTH**2
C
C
C  CCF EQUATIONS:
C     CCF COEFFICIENTS FOR TREES THAT ARE GREATER THAN 10.0 IN. DBH:
C      RD1 -- CONSTANT TERM IN CROWN COMPETITION FACTOR EQUATION,
C             SUBSCRIPTED BY SPECIES
C      RD2 -- COEFFICIENT FOR SUM OF DIAMETERS TERM IN CROWN
C             COMPETITION FACTOR EQUATION,SUBSCRIPTED BY SPECIES
C
C     CCF COEFFICIENTS FOR TREES THAT ARE LESS THAN 10.0 IN. DBH:
C      RDA -- MULTIPLIER.
C      RDB -- EXPONENT.  CCF(I) = RDA*DBH**RDB
C
C     SPECIES LIST FOR WESTERN SIERRAS VARIANT.
C
C     1 = SUGAR PINE (SP)                   PINUS LAMBERTIANA
C     2 = DOUGLAS-FIR (DF)                  PSEUDOTSUGA MENZIESII
C     3 = WHITE FIR (WF)                    ABIES CONCOLOR
C     4 = GIANT SEQUOIA (GS)                SEQUOIADENDRON GIGANTEAUM
C     5 = INCENSE CEDAR (IC)                LIBOCEDRUS DECURRENS
C     6 = JEFFREY PINE (JP)                 PINUS JEFFREYI
C     7 = CALIFORNIA RED FIR (RF)           ABIES MAGNIFICA
C     8 = PONDEROSA PINE (PP)               PINUS PONDEROSA
C     9 = LODGEPOLE PINE (LP)               PINUS CONTORTA
C    10 = WHITEBARK PINE (WB)               PINUS ALBICAULIS
C    11 = WESTERN WHITE PINE (WP)           PINUS MONTICOLA
C    12 = SINGLELEAF PINYON (PM)            PINUS MONOPHYLLA
C    13 = PACIFIC SILVER FIR (SF)           ABIES AMABILIS
C    14 = KNOBCONE PINE (KP)                PINUS ATTENUATA
C    15 = FOXTAIL PINE (FP)                 PINUS BALFOURIANA
C    16 = COULTER PINE (CP)                 PINUS COULTERI
C    17 = LIMBER PINE (LM)                  PINUS FLEXILIS
C    18 = MONTEREY PINE (MP)                PINUS RADIATA
C    19 = GRAY PINE (GP)                    PINUS SABINIANA
C         (OR CALIFORNIA FOOTHILL PINE)
C    20 = WASHOE PINE (WE)                  PINUS WASHOENSIS
C    21 = GREAT BASIN BRISTLECONE PINE (GB) PINUS LONGAEVA
C    22 = BIGCONE DOUGLAS-FIR (BD)          PSEUDOTSUGA MACROCARPA
C    23 = REDWOOD (RW)                      SEQUOIA SEMPERVIRENS
C    24 = MOUNTAIN HEMLOCK (MH)             TSUGA MERTENSIANA
C    25 = WESTERN JUNIPER (WJ)              JUNIPERUS OCIDENTALIS
C    26 = UTAH JUNIPER (UJ)                 JUNIPERUS OSTEOSPERMA
C    27 = CALIFORNIA JUNIPER (CJ)           JUNIPERUS CALIFORNICA
C    28 = CALIFORNIA LIVE OAK (LO)          QUERCUS AGRIFOLIA
C    29 = CANYON LIVE OAK (CY)              QUERCUS CHRYSOLEPSIS
C    30 = BLUE OAK (BL)                     QUERCUS DOUGLASII
C    31 = CALIFORNIA BLACK OAK (BO)         QUERQUS KELLOGGII
C    32 = VALLEY OAK (VO)                   QUERCUS LOBATA
C         (OR CALIFORNIA WHITE OAK)
C    33 = INTERIOR LIVE OAK (IO)            QUERCUS WISLIZENI
C    34 = TANOAK (TO)                       LITHOCARPUS DENSIFLORUS
C    35 = GIANT CHINKAPIN (GC)              CHRYSOLEPIS CHRYSOPHYLLA
C    36 = QUAKING ASPEN (AS)                POPULUS TREMULOIDES
C    37 = CALIFORNIA-LAUREL (CL)            UMBELLULARIA CALIFORNICA
C    38 = PACIFIC MADRONE (MA)              ARBUTUS MENZIESII
C    39 = PACIFIC DOGWOOD (DG)              CORNUS NUTTALLII
C    40 = BIGLEAF MAPLE (BM)                ACER MACROPHYLLUM
C    41 = CURLLEAF MOUNTAIN-MAHOGANY (MC)   CERCOCARPUS LEDIFOLIUS
C    42 = OTHER SOFTWOODS (OS)
C    43 = OTHER HARDWOODS (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C    FROM EXISTING WS EQUATIONS --
C      USE 1(SP) FOR 11(WP) AND 24(MH) 
C      USE 2(DF) FOR 22(BD)
C      USE 3(WF) FOR 13(SF)
C      USE 4(GS) FOR 23(RW)
C      USE 8(PP) FOR 18(MP)
C      USE 34(TO) FOR 35(GC), 36(AS), 37(CL), 38(MA), AND 39(DG)
C      USE 31(BO) FOR 28(LO), 29(CY), 30(BL), 32(VO), 33(IO), 40(BM), AND
C                     43(OH)
C
C    FROM CA VARIANT --
C      USE CA11(KP) FOR 12(PM), 14(KP), 15(FP), 16(CP), 17(LM), 19(GP), 20(WE), 
C                       25(WJ), 26(WJ), AND 27(CJ)
C      USE CA12(LP) FOR 9(LP) AND 10(WB)
C
C    FROM SO VARIANT --
C      USE SO30(MC) FOR 41(MC)
C
C    FROM UT VARIANT --
C      USE UT17(GB) FOR 21(GB)
C
C  SOURCES OF CCF EQUATION COEFFICIENTS:
C     ALL COEFFICIENTS ARE FROM THE R5 FIAS SYSTEM AT THE TIME THE
C     VARIANT WAS FIT. THESE WERE GENERALLY BASED ON MAPPING PROJECTS
C     FROM THE TAHOE, STANISLAUS, AND PLUMAS NATIONAL FORESTS.
C     SOME COEFFICIENTS WERE UPDATED IN 1995 WHEN THE MAPPING PROJECTS
C     ON THE MODOC AND LASSEN WERE COMPLETED. THIS IS WHY SOME OF THESE
C     COEFFICIENTS ARE DIFFERENT FROM THE CROWN WIDTH COEFFICIENTS IN
C     SUBROUTINE R5CRWD.
C     
C     1 SP = OLDER VERSION OF THE R5 FIAS USER'S GUIDE EQUATION
C     2 DF = R5 FIAS USER'S GUIDE (SEE R5CRWD): DOUGLAS-FIR
C     3 WF = R5 FIAS USER'S GUIDE (SEE R5CRWD): WHITE FIR
C     4 GS = R5 FIAS USER'S GUIDE (SEE R5CRWD): WHITE FIR
C     5 IC = R5 FIAS USER'S GUIDE (SEE R5CRWD): INCENSE CEDAR
C     6 JP = OLDER VERSION OF THE R5 FIAS USER'S GUIDE EQUATION
C     7 RF = R5 FIAS USER'S GUIDE (SEE R5CRWD): RED FIR
C     8 PP = OLDER VERSION OF THE R5 FIAS USER'S GUIDE EQUATION
C    21 GB = NORTHERN IDAHO VARIANT INT-133 TABLE 8: LODGEPOLE PINE
C    31 BO = R5 FIAS USER'S GUIDE (SEE R5CRWD): BLACK OAK
C    34 TO = R5 FIAS USER'S GUIDE (SEE R5CRWD): TANOAK
C    41 MC = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    42 OS = OLDER VERSION OF THE R5 FIAS USER'S GUIDE EQUATION
C----------
      LOGICAL LTHIN
      REAL RD1(MAXSP),RD2(MAXSP),RDA(MAXSP),RDB(MAXSP)
      REAL CRWDTH,CCFT,P,H,D
      INTEGER MODE,JCR,ISPC
C
      DATA RD1/
     & 6.74, 6.81, 5.82, 5.82, 7.11,  
     & 3.10, 6.71, 5.13, 0.00, 0.00,  
     & 6.74, 0.00, 5.82, 0.00, 0.00,  
     & 0.00, 0.00, 5.13, 0.00, 0.00,  
     & 0.00, 6.81, 5.82, 6.74, 0.00,  
     & 0.00, 0.00, 10.0, 10.0, 10.0,  
     & 10.0, 10.0, 10.0, 10.0, 10.0,  
     & 10.0, 10.0, 10.0, 10.0, 10.0,  
     & 0.00, 7.07, 10.0/  
C
      DATA RD2/
     & 0.623, 0.732, 0.591, 0.591, 0.470,  
     & 0.839, 0.421, 0.658, 0.000, 0.000,  
     & 0.623, 0.000, 0.591, 0.000, 0.000,  
     & 0.000, 0.000, 0.658, 0.000, 0.000,  
     & 0.000, 0.732, 0.591, 0.623, 0.000,  
     & 0.000, 0.000, 1.200, 1.200, 1.200,  
     & 1.200, 1.200, 1.200, 1.050, 1.050,  
     & 1.050, 1.050, 1.050, 1.050, 1.200,  
     & 0.000, 0.551, 1.200/  
C
      DATA RDA/
     & 0.007244, 0.017299, 0.015248, 0.011109, 0.008915,  
     & 0.007875, 0.011402, 0.007813, 0.000000, 0.000000,  
     & 0.007244, 0.000000, 0.015248, 0.000000, 0.000000,  
     & 0.000000, 0.000000, 0.007813, 0.000000, 0.000000,  
     & 0.000000, 0.017299, 0.011109, 0.007244, 0.000000,  
     & 0.000000, 0.000000, 0.009187, 0.009187, 0.009187,  
     & 0.009187, 0.009187, 0.009187, 0.011109, 0.011109,  
     & 0.011109, 0.011109, 0.011109, 0.011109, 0.009187,  
     &      0.0, 0.009884, 0.009187/  
C
      DATA RDB/
     & 1.8182, 1.5571, 1.7333, 1.7250, 1.7800,  
     & 1.7360, 1.7560, 1.7780, 0.0000, 0.0000,  
     & 1.8182, 0.0000, 1.7333, 0.0000, 0.0000,  
     & 0.0000, 0.0000, 1.7780, 0.0000, 0.0000,  
     & 0.0000, 1.5571, 1.7250, 1.8182, 0.0000,  
     & 0.0000, 0.0000, 1.7600, 1.7600, 1.7600,  
     & 1.7600, 1.7600, 1.7600, 1.7250, 1.7250,  
     & 1.7250, 1.7250, 1.7250, 1.7250, 1.7600,  
     &    0.0, 1.6667, 1.7600/  
C----------
C  INITIALIZE RETURN VARIABLES.
C----------
      CCFT = 0.
      CRWDTH = 0.
C----------
C  COMPUTE CCF
C----------
      IF(MODE.EQ.1) THEN
        SELECT CASE (ISPC)
C----------
C  SPECIES USING SURROGATE EQNS FROM THE CA VARIANT
C----------
          CASE(9,10,12,14:17,19,20,25:27)
            CALL R5CRWD (ISPC,D,H,CRWDTH)
            CCFT= CRWDTH  * CRWDTH  * 0.001803
C----------
C  SPECIES USING SURROGATE EQNS FROM THE SO VARIANT
C----------
        CASE(41)
          IF (D .LT. 1.0) THEN
            CCFT = D * (0.0204 + 0.0246 + 0.0074)
          ELSE
            CCFT = 0.0204 + 0.0246*D + 0.0074*D**2.0
          ENDIF
C----------
C  SPECIES USING SURROGATE EQNS FROM THE UT VARIANT
C----------
        CASE(21)
          IF (D .GE. 10.0) THEN
            CCFT = 0.01925 + 0.01676*D + 0.00365*D*D
          ELSEIF (D .GT. 0.1) THEN
            CCFT = 0.009187 * (D**1.7600)
          ELSE
            CCFT = 0.001
          ENDIF
C----------
C  SPECIES USING ORIGINAL WS EQNS
C----------
          CASE DEFAULT
            IF (D .GE. 1.0) THEN
              CCFT = ((RD1(ISPC)+D*RD2(ISPC))**2.0)*0.001803
            ELSE IF(D.GT.0.1) THEN
              CCFT = RDA(ISPC) * (D**RDB(ISPC))
            ELSE
              CCFT=0.001
            ENDIF
C
        END SELECT
C
        IF(P .GT. 0.)CCFT = CCFT * P
      ENDIF
C----------
C COMPUTE CROWN WIDTH
C----------
      IF(MODE.EQ.2) THEN
C
        SELECT CASE (ISPC)
C
          CASE (21)
            IF (D .GE. 10.0) THEN
              CCFT = 0.01925 + 0.01676*D + 0.00365*D*D
            ELSEIF (D .GT. 0.1) THEN
              CCFT = 0.009187 * (D**1.7600)
            ELSE
              CCFT = 0.001
            ENDIF
            CRWDTH = SQRT(CCFT/0.001803)
C
          CASE DEFAULT
            CALL R5CRWD (ISPC,D,H,CRWDTH)
C
        END SELECT
C----------
C  LIMIT CROWN WIDTH FOR PRINTING ON TREELIST.
C----------
        IF(CRWDTH .GT. 99.9) CRWDTH=99.9
      ENDIF
C
      RETURN
      END
