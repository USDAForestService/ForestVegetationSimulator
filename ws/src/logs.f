      SUBROUTINE LOGS(DBH,HT,IBCD,BDMIN,ISP,STMP,BV)
      IMPLICIT NONE
C----------
C  **LOGS--WS   DATE OF LAST REVISION:  05/09/11
C----------
C  REGION 5 BOARD FOOT VOLUME MODELS.
C  TAPER EQUATIONS BY G.S.BIGING
C  BY K.STUMPF, ADAPTED BY B.KRUMLAND, P.J.DAUGHERTY
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
COMMONS
C----------
      INTEGER IMAP(MAXSP)
      INTEGER ISP,IBCD,JSP,N,NLOGS,IDU,ITEM,J
      REAL XPS(6),TPCF1(6),TPCF2(6),BV,STMP,BDMIN,HT,DBH,TRIM,SLN,X,D
      REAL SF(4,2),B2,B1,HI,DIBAHI,BVL,DTOP,Z1,HTM,S1,AH,DU,FRAC,TBV
      REAL QTLN
C----------
C     SPECIES LIST FOR WESTERN SIERRAS VARIANT.
C
C     1 = SUGAR PINE (SP)                   PINUS LAMBERTIANA
C     2 = DOUGLAS-FIR (DF)                  PSEUDOTSUGA MENZIESII
C     3 = WHITE FIR (WF)                    ABIES CONCOLOR
C     4 = GIANT SEQUOIA (GS)                SEQUOIADENDRON GIGANTEAUM
C     5 = INCENSE CEDAR (IC)                LIBOCEDRUS DECURRENS
C     6 = JEFFREY PINE (JP)                 PINUS JEFFREYI
C     7 = CALIFORNIA RED FIR (RF)           ABIES MAGNIFICA
C     8 = PONDEROSA PINE (PP)               PINUS PONDEROSA
C     9 = LODGEPOLE PINE (LP)               PINUS CONTORTA
C    10 = WHITEBARK PINE (WB)               PINUS ALBICAULIS
C    11 = WESTERN WHITE PINE (WP)           PINUS MONTICOLA
C    12 = SINGLELEAF PINYON (PM)            PINUS MONOPHYLLA
C    13 = PACIFIC SILVER FIR (SF)           ABIES AMABILIS
C    14 = KNOBCONE PINE (KP)                PINUS ATTENUATA
C    15 = FOXTAIL PINE (FP)                 PINUS BALFOURIANA
C    16 = COULTER PINE (CP)                 PINUS COULTERI
C    17 = LIMBER PINE (LM)                  PINUS FLEXILIS
C    18 = MONTEREY PINE (MP)                PINUS RADIATA
C    19 = GRAY PINE (GP)                    PINUS SABINIANA
C         (OR CALIFORNIA FOOTHILL PINE)
C    20 = WASHOE PINE (WE)                  PINUS WASHOENSIS
C    21 = GREAT BASIN BRISTLECONE PINE (GB) PINUS LONGAEVA
C    22 = BIGCONE DOUGLAS-FIR (BD)          PSEUDOTSUGA MACROCARPA
C    23 = REDWOOD (RW)                      SEQUOIA SEMPERVIRENS
C    24 = MOUNTAIN HEMLOCK (MH)             TSUGA MERTENSIANA
C    25 = WESTERN JUNIPER (WJ)              JUNIPERUS OCIDENTALIS
C    26 = UTAH JUNIPER (UJ)                 JUNIPERUS OSTEOSPERMA
C    27 = CALIFORNIA JUNIPER (CJ)           JUNIPERUS CALIFORNICA
C    28 = CALIFORNIA LIVE OAK (LO)          QUERCUS AGRIFOLIA
C    29 = CANYON LIVE OAK (CY)              QUERCUS CHRYSOLEPSIS
C    30 = BLUE OAK (BL)                     QUERCUS DOUGLASII
C    31 = CALIFORNIA BLACK OAK (BO)         QUERQUS KELLOGGII
C    32 = VALLEY OAK (VO)                   QUERCUS LOBATA
C         (OR CALIFORNIA WHITE OAK)
C    33 = INTERIOR LIVE OAK (IO)            QUERCUS WISLIZENI
C    34 = TANOAK (TO)                       LITHOCARPUS DENSIFLORUS
C    35 = GIANT CHINKAPIN (GC)              CHRYSOLEPIS CHRYSOPHYLLA
C    36 = QUAKING ASPEN (AS)                POPULUS TREMULOIDES
C    37 = CALIFORNIA-LAUREL (CL)            UMBELLULARIA CALIFORNICA
C    38 = PACIFIC MADRONE (MA)              ARBUTUS MENZIESII
C    39 = PACIFIC DOGWOOD (DG)              CORNUS NUTTALLII
C    40 = BIGLEAF MAPLE (BM)                ACER MACROPHYLLUM
C    41 = CURLLEAF MOUNTAIN-MAHOGANY (MC)   CERCOCARPUS LEDIFOLIUS
C    42 = OTHER SOFTWOODS (OS)
C    43 = OTHER HARDWOODS (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C    FROM EXISTING WS EQUATIONS --
C      USE 1(SP) FOR 11(WP) AND 24(MH) 
C      USE 2(DF) FOR 22(BD)
C      USE 3(WF) FOR 13(SF)
C      USE 4(GS) FOR 23(RW)
C      USE 8(PP) FOR 18(MP)
C      USE 34(TO) FOR 35(GC), 36(AS), 37(CL), 38(MA), AND 39(DG)
C      USE 31(BO) FOR 28(LO), 29(CY), 30(BL), 32(VO), 33(IO), 40(BM), AND
C                     43(OH)
C
C    FROM CA VARIANT --
C      USE CA11(KP) FOR 12(PM), 14(KP), 15(FP), 16(CP), 17(LM), 19(GP), 20(WE), 
C                       25(WJ), 26(WJ), AND 27(CJ)
C      USE CA12(LP) FOR 9(LP) AND 10(WB)
C
C    FROM SO VARIANT --
C      USE SO30(MC) FOR 41(MC)
C
C    FROM UT VARIANT --
C      USE UT17(GB) FOR 21(GB)
C----------
C  DATA STATEMENTS
C----------
      DATA TPCF1/
     & 1.01959, 1.06932, 1.07134, 1.02929, 1.09262, 1.07588/
C
      DATA TPCF2/
     & 0.33567, 0.41563, 0.47216, 0.33401, 0.36530, 0.35378/
C
      DATA XPS/
     & 0.95204, 0.92368, 0.89659, 0.95411, 0.94976, 0.95222/
C
      DATA IMAP/
     &  2,  4,  5,  1,  3,  1,  6,  1,  1,  1, 
     &  2,  1,  5,  1,  1,  1,  1,  1,  1,  1, 
     &  1,  4,  1,  2,  1,  1,  1,  1,  1,  1, 
     &  1,  1,  1,  3,  3,  3,  3,  3,  3,  1, 
     &  3,  1,  1/
C
C  DEFINE NOMINAL SCALING STANDARDS: SLN=STANDARD LOG LENGTH;
C  TRIM=KERF.  
C
      DATA SLN,TRIM/16.3,0.3/
C
C  SCRIBNER VOL. FACTORS FOR SMALL LOGS
C
      DATA SF/1.249,1.608,1.854,2.410,1.160,1.400,1.501,2.084/
C----------
C  STATEMENT FUNCTION TO CALC. DIB AT H(I)
C----------
      DIBAHI(HI,HT,B1,B2,D,X)=D*(B1+B2*ALOG(1.-X*(HI/HT)**(1./3.)))
C----------
C  INITIALIZE VOLUME ESTIMATE.
C----------
      BV=0.0
C----------
C   VOLUMES FOR SOME SPECIES ARE CALCULATED FROM FORMULA.
C----------
      SELECT CASE (ISP)
      CASE(4)
C----------
C       GIANT SEQUOIA FROM WENSEL AND SCHOENHEIDE
C----------
        BVL=-5.66831 + 2.21035*ALOG(DBH) + 0.91921*ALOG(HT)
        BV=EXP(BVL)
C
      CASE(28:33,40,43)
C----------
C       BLACK OAK EQ FROM PNW 414
C       EQN IS IN CUBIC FEET, APPLY BF/CF RATIO   R.JOHNSON
C----------
        BV = .00124 * DBH**2.68 * HT**0.424
        BV = BV * 5.0
C
      CASE DEFAULT 
C----------
C  VOLUME FOR OTHER SPECIES IS COMPUTED FROM LOG RULE.  JSP MAPS 
C  SPECIES ONTO A TAPER MODEL.
C----------
        JSP=IMAP(ISP)
        DTOP=IBCD
C----------
C  CHECK FOR MERCHANTABILITY LIMITS.
C---------- 
        IF(DBH.LE.DTOP .OR. DBH.LT.BDMIN) GO TO 100
        Z1= EXP((DTOP/DBH - TPCF1(JSP))/TPCF2(JSP)) - 1.
C----------
C  HTM IS TOTAL MERCHANTABLE LENGTH ADJUSTED FOR STUMP HEIGHT.
C  RETURN IF LESS THAN 1/2 LOG.
C----------
        HTM = (Z1/(-XPS(JSP)))**3*HT - STMP
        IF(HTM.LE.(.5*SLN)) GO TO 100
C----------
C  NLOGS IS THE TOTAL NUMBER OF LOGS EXCLUDING THE TOP FULL LOG AND ANY 
C  PARTIAL LOG ABOVE IT.
C----------
        NLOGS=HTM/SLN -1
        IF(NLOGS.LE.0) GO TO 80
        S1=SLN
        AH=0.0
        BV=0.0
        N=0
        IDU=0
        ITEM=1
   60   CONTINUE  
        DO 70 J=1,NLOGS
        HI = FLOAT(J)*S1+AH
        DU = DIBAHI(HI,HT,TPCF1(JSP),TPCF2(JSP),DBH,XPS(JSP))
        FRAC=((S1-TRIM)/16.0)
        IF(DU.LE.9.0) THEN
          IDU = DU+.5 -5
          IF(IDU.LE.0) IDU=1
          N = 1
          IF(S1.LT.SLN)N=2
          TBV = SF(IDU,N) * S1
        ELSE
          TBV=((.79*DU -2.0)*DU -4.)*FRAC
        ENDIF
        TBV = INT(TBV/10. + .5) * 10.
        BV=BV+TBV
   70   CONTINUE
        IF(ITEM.EQ.2) GO TO 100
C----------
C  RETURN TO STATEMENT 60 TO COMPUTE VOLUME FOR TOP LOGS.
C----------
   80   ITEM=2
        AH=NLOGS*SLN
        IF(AH.LE.0)AH=0.0
        QTLN= HTM-AH
        NLOGS=2
        IF(QTLN.LT.(1.5*SLN))NLOGS=1
        S1=QTLN/FLOAT(NLOGS)
        GO TO 60
  100   CONTINUE
      END SELECT
C
      RETURN
      END
