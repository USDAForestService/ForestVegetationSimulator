      SUBROUTINE CUTSTK
      IMPLICIT NONE
C----------
C  **CUTSTK--ON   DATE OF LAST REVISION:  07/11/08
C----------
C  THIS SUBROUTINE CONTAINS ENTRY POINTS FOR CALCULATING STOCKING
C  LEVELS FOR VARIOUS THINNING OPTIONS.
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
COMMONS
C----------
      LOGICAL LINCL

      INTEGER I,IISPG,JPNUM,JSPCUT,JTYP,IC,IGRP,IULIM,IG
      INTEGER JJSP(MAXSP)
      REAL BASP(6),A1(6),A2(6),A3(6),TPRED(6)      
      REAL STTPA,HU,HL,CSTOCK,D,H,TPA,FSTOCK,TOTBA,DU,DL
      
      DATA A1/-.1433,-.3430,-.5453,-.0935,-.2996,-.1869/
      DATA A2/7.1389,10.7172,16.222,5.6206,14.5479,10.5087/
      DATA A3/34.3018,43.0417,46.0012,80.2369,75.4377,103.2119/
C----------
C  PUT SPECIES IN ONE OF 6 STOCKING CATEGORIES
C
C  SPECIES GROUP 1 - NORTHERN HARDWOODS
C  SPECIES GROUP 2 - UPLAND AND RED OAK
C  SPECIES GROUP 3 - ASPEN AND PAPER BIRCH
C  SPECIES GROUP 4 - JACK PINE
C  SPECIES GROUP 5 - WHITE AND BLACK SPRUCE
C  SPECIES GROUP 6 - RED AND WHITE PINE
C----------
      DATA JJSP/2*4,3*6,6*5,1,2*6,2*1,3,12*1,7*2,3*1,4*3,5*2,20*3,
     &          4,6,2*5/
C
C----------
C  ENTRY AUTSTK CALCULATES NORMAL STOCKING FOR AUTOMATIC THINS.
C
C  VARIABLE DEFINITIONS:
C  A1,A2,A3  COEFFICIENTS TO PREDICT TPA BY STOCKING CATEGORY
C  TPRED     TREES PER ACRE FOR THE STAND AT 100% STOCKING-
C            BY STOCKING CATEGORY.
C  STTPA     NUMBER OF TREES PER ACRE PREDICTED FOR THE NEW STAND AT
C            100% STOCKING--INCLUDES ALL SIX STOCKING CATEGORIES
C-----------
      ENTRY AUTSTK (FSTOCK)
      DO 10 I=1,6
      BASP(I) = 0.0
      TPRED(I) = 0.0
   10 CONTINUE
      TOTBA = 0.0
C----------
C  DETERMINE TOTAL NUMBER OF TREES PER ACRE IN EACH SPECIES GROUP
C----------
      DO 20 I=1,ITRN
      IISPG = JJSP(ISP(I))
      BASP(IISPG) = BASP(IISPG) + .0054542*DBH(I)*DBH(I)*PROB(I)
   20 CONTINUE
C----------
C  RECALCULATE STOCKING PERCENT FOR LIVE TREES ONLY (ISTATUS = 1).
C----------
      DO 30 I=1,6
      IF(BASP(I) .EQ. 0.0) THEN
        TPRED(I)= 1.0
        GO TO 30
      ENDIF
      TOTBA=BASP(I)+TOTBA
C----------
C  CALCULATE PREDICTED TREES PER ACRE FOR EACH SPECIES GROUP
C----------
      TPRED(I) =(A1(I)*RMSQD*RMSQD+A2(I)*RMSQD+A3(I))/
     &                (.0054542*RMSQD*RMSQD)
   30 CONTINUE
C----------
C  WEIGHT PREDICTED TPA FOR EACH STOCKING CATEGORY BY PERCENT BA IN
C  EACH STOCKING CATEGORY
C----------
      STTPA = (BASP(1)*TPRED(1)+BASP(2)*TPRED(2)+BASP(3)*TPRED(3)
     &           +BASP(4)*TPRED(4)+BASP(5)*TPRED(5)+
     &            BASP(6)*TPRED(6))/TOTBA
      FSTOCK = STTPA
      RETURN
C
C-----------
C  ENTRY CLSSTK COMPUTES BA OR TPA STOCKING IN A SPECIFIED
C  DBH/HT/SPECIES CLASS.
C
C  CSTOCK = CLASS STOCKING
C  JTYP   = 1 IF STOCKING IS IN TPA,  2 IF STOCKING IS IN BA
C  JSPCUT = SPECIES WHICH WILL BE CUT
C  LINCL  = LOGICAL VARIABLE USED TO INCLUDE SPECIES OR NOT.
C  DL,DU  = LOWER AND UPPER DIAMETER LIMITS RESPECTIVELY (GE,LT)
C  HL,HU  = LOWER AND UPPER HEIGHT LIMITS RESPECTIVELY (GE,LT)
C  JPNUM = POINT NUMBER (IN FVS SEQUENCE FORMAT), 0 = ALL POINTS
C----------
      ENTRY CLSSTK (CSTOCK,JTYP,JSPCUT,DL,DU,HL,HU,JPNUM)
      CSTOCK=0.0
      DO 100 IC=1,ITRN
      IF(JPNUM.GT.0 .AND. ITRE(IC).NE.JPNUM)GO TO 100
C
      LINCL = .FALSE.
      IF(JSPCUT.EQ.0 .OR. JSPCUT.EQ.ISP(IC))THEN
        LINCL = .TRUE.
      ELSEIF(JSPCUT.LT.0)THEN
        IGRP = -JSPCUT
        IULIM = ISPGRP(IGRP,1)+1
        DO 60 IG=2,IULIM
        IF(ISP(IC) .EQ. ISPGRP(IGRP,IG))THEN
          LINCL = .TRUE.
          GO TO 61
        ENDIF
   60   CONTINUE
      ENDIF
   61 CONTINUE
C
      IF (LINCL) THEN
        D=DBH(IC)
        H=HT(IC)
        TPA=WK4(IC)
        IF(JPNUM.GT.0)TPA=TPA*(PI-FLOAT(NONSTK))
        IF(D.LT.DL .OR. D.GE.DU) GO TO 100
        IF(H.LT.HL .OR. H.GE.HU) GO TO 100
        IF(JTYP.EQ.1) THEN
          CSTOCK=CSTOCK + TPA
        ELSE
          CSTOCK=CSTOCK + TPA*(D*D*0.005454154)
        ENDIF
	ENDIF
  100 CONTINUE
      RETURN
C
      END
