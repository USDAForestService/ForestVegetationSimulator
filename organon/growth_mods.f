C----------
C ORGANON $Id$
C----------
      SUBROUTINE GG_MODS(TAGE,GWDG,GWHG,DGMOD,HGMOD)
      IMPLICIT NONE
      REAL*4 TAGE,GWDG,GWHG,DGMOD,HGMOD,XGWHG,XGWDG,A1,A2,B1,B2,A,B
      XGWHG=GWHG
      IF(GWHG .GT. 20.0) XGWHG=20.0
      IF(GWHG. LT. 0.0) XGWHG=0.0
      XGWDG=GWDG
      IF(GWDG .GT. 20.0) XGWDG=20.0
      IF(GWDG. LT. 0.0) XGWDG=0.0
C
C     SET THE PARAMETERS FOR THE DIAMETER GROWTH MODIFIER
C
      A1=0.0101054                     ! VALUE FOR TAGE = 5
      A2=0.0031                         ! VALUE FOR TAGE => 10
      IF(TAGE .LE. 5.0) THEN
         A=A1
      ELSEIF(TAGE .GT. 5.0 .AND. TAGE .LT. 10.0) THEN
         A=A1-(A1-A2)*((TAGE-5.0)/5.0)
      ELSE
         A=A2
      ENDIF
C
C     SET THE PARAMETERS FOR THE HEIGHT GROWTH MODIFIER
C
      B1=0.0062770                     ! VALUE FOR TAGE = 5
      B2=0.0036                        ! VALUE FOR TAGE => 10
      IF(TAGE .LE. 5.0) THEN
         B=B1
      ELSEIF(TAGE .GT. 5.0 .AND. TAGE .LT. 10.0) THEN
         B=B1-(B1-B2)*((TAGE-5.0)/5.0)
      ELSE
         B=B2
      ENDIF
C
C     GENETIC GAIN DIAMETER GROWTH RATE MODIFIER
C
      DGMOD=1.0+A*XGWDG
C
C     GENETIC GAIN HEIGHT GROWTH RATE MODIFIER
C
      HGMOD=1.0+B*XGWHG
      RETURN
      END
********************************************************************************
      SUBROUTINE SNC_MODS(FR,DGMOD,HGMOD)
      IMPLICIT NONE
      REAL*4 FR,DGMOD,HGMOD,XFR,A0,A1,A2,B0,B1,B2
      XFR=FR
      IF(FR .GT. 4.0) XFR=4.0
      IF(FR. LT. 0.85) XFR=0.85
C
C     SET THE PARAMETERS FOR THE DIAMETER GROWTH MODIFIER
C
      A0=-1.02300676
      A1=-0.11705018
      A2=3.0
C
C     SET THE PARAMETERS FOR THE HEIGHT GROWTH MODIFIER
C
      B0=-1.80656927
      B1=-0.08555130
      B2=3.0
C
C     SWISS NEEDLE CAST DIAMETER GROWTH RATE MODIFIER
C
      DGMOD=1.0-EXP(A0+A1*XFR**A2)
C
C     SWISS NEEDLE CAST HEIGHT GROWTH RATE MODIFIER
C
      HGMOD=1.0-EXP(B0+B1*XFR**B2)
      RETURN
      END
