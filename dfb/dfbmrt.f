      SUBROUTINE DFBMRT
      IMPLICIT NONE
C----------
C  **DFBMRT  DATE OF LAST REVISION:  06/30/10
C----------
C
C  MORTALITY ROUTINE FOR DFB MODEL.  THIS ROUTINE LOADS WK2 (PROGNOSIS
C  MORTALITY ARRAY) WITH THE MORTALITY CAUSED BY DOUGLAS-FIR
C  BEETLE.
C
C  CALLED BY :
C     DFBDRV  [DFB]
C
C  CALLS :
C     DFBIND  (SUBROUTINE)   [DFB]
C     DFBTAB  (SUBROUTINE)   [DFB]
C
C  LOCAL VARIABLES :
C     BA     - BASAL AREA OF THE CURRENT TREE RECORD.
C     CFTVOL - ARRAY THAT HOLDS THE CUBIC FOOT VOLUME LOST DUE TO DFB
C              FOR EACH DBH CLASS.
C     D      - DBH OF THE CURRENT TREE RECORD.
C     I      - COUNTER INDEX
C     I1,I2  - INDEXES FOR THE ARRAY IND1.  I1 IS THE STARTING
C              INDEX FOR A SPECIES IN ARRAY IND1 (DF = IDFSPC).
C              I2 IS THE ENDING INDEX FOR A SPECIES IN ARRAY IND1
C              (DF = IDFSPC).
C     DCLAS  - DIAMETER CLASS INDEX.
C     J      - INDEX TO DOUGLAS-FIR TREES.
C     P      - TREES/ACRE FOR THE CURRENT TREE RECORD (TEMPORARY
C              PROB(I)).
C     SUMDBH - THE SUM OF THE DBH MULTIPLIED BY THE TREES/ACRE FOR ALL
C              DF TREE RECORDS >= 9 INCHES DBH.
C     TAMORT - THE NUMBER OF TREES/ACRE KILLED IN THE CURRENT TREE
C              RECORD.
C
C  COMMON BLOCK VARIABLES USED :
C     BADF9  - (DFBCOM)  INPUT
C     CFV    - (ARRAYS)  INPUT
C     DBH    - (ARRAYS)  INPUT
C     DEADDF - (DFBCOM)  OUTPUT
C     DEBUIN - (DFBCOM)  INPUT
C     DFKILL - (DFBCOM)  INPUT
C     IDFSPC - (DFBCOM)  INPUT
C     IND1   - (ARRAYS)  INPUT
C     ISCT   - (CONTRL)  INPUT
C     JODFB  - (DFBCOM)  INPUT
C     LBAMOD - (DFBCOM)  INPUT
C     LIVEDF - (DFBCOM)  OUTPUT
C     OKILL  - (DFBCOM)  INPUT/OUTPUT
C     PROB   - (ARRAYS)  INPUT
C     START  - (DFBCOM)  INPUT
C     WK2    - (ARRAYS)  OUTPUT
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'DFBCOM.F77'
C
C
COMMONS
C

      INTEGER I, I1, I2, DCLAS, J

      REAL  CFTVOL(20), TAMORT, P, D, BA
      REAL  SUMDBH

      IF (DEBUIN) WRITE (JODFB,*) ' ** ENTERING SUBROUTINE DFBMRT'

C.... INITIALIZE LIVEDF AND DEADDF.

      DO 100 I = 1,20
         LIVEDF(I) = START(I)
         DEADDF(I) = 0.0
         CFTVOL(I) = 0.0
  100 CONTINUE

C.... SET UP INDEXES INTO THE DOUGLAS-FIR TREE RECORDS.

      I1 = ISCT(IDFSPC,1)
      IF (I1 .EQ. 0) GOTO 1000
      I2 = ISCT(IDFSPC,2)

C.... IF THE DBH METHOD IS TO BE USED TO CALCULATE MORTALITY THEN
C.... CALCULATE THE SUM OF TREES/ACRE MULTIPLIED TO DBH FOR TREES WITH
C.... A DBH >= 9".

      IF (.NOT. LBAMOD) THEN
         SUMDBH = 0.0
         DO 150 I = I1,I2
            J = IND1(I)
            IF (DBH(J) .GE. 9.0) THEN
               SUMDBH = SUMDBH + PROB(J) * DBH(J)
            ENDIF
  150    CONTINUE
      ENDIF

C.... START LOOP TO CALCULATE THE TREES/ACRE THAT ARE KILLED BY THE
C.... DOUGLAS-FIR BEETLE IN EACH TREE RECORD.

      DO 200 I = I1,I2

C....    SETUP TREE RECORD INDEX AND CALCULATE THE BASAL AREA FOR THE
C....    TREE RECORD.

         J = IND1(I)
         P = PROB(J)
         D = DBH(J)

C....    IF THE BASAL AREA METHOD IS TO BE USED TO CALCULATE MORTALITY
C....    THEN CALCULATE THE BASAL AREA FOR THE CURRENT TREE RECORD.

         IF (LBAMOD) THEN
            BA = 0.005454154 * D * D * P
         ENDIF

C....    IF THE TREE RECORD HAS A DBH >= 9 INCHES (DBH CLASS >= 5)
C....    THEN CALCULATE THE NUMBER OF TREES/ACRE TO KILL FOR THE TREE
C....    RECORD.  IF LBAMOD IS .TRUE. THEN USE THE BASAL AREA MORTALITY
C....    DISTRIBUTION METHOD.  OTHERWISE USE THE DBH MORTALITY
C....    DISTRIBUTION METHOD (THE DEFAULT).  SUM UP THE LIVE DOUGLAS-FIR
C....    AND DEAD DOUGLAS-FIR FOR THE DBH CLASS.

         CALL DFBIND (DBH(J),DCLAS)

         IF (DCLAS .GE. 5) THEN
            IF (LBAMOD) THEN
               TAMORT = DFKILL * BA / BADF9
            ELSE
               TAMORT = DFKILL * (D * P / SUMDBH)
            ENDIF

            IF (TAMORT .GT. P) TAMORT = P
            LIVEDF(DCLAS) = LIVEDF(DCLAS) - TAMORT
            DEADDF(DCLAS) = DEADDF(DCLAS) + TAMORT
         ELSE
            TAMORT = 0.0
         ENDIF

C....    CALCULATE THE CUBIC FOOT VOLUME LOST DUE TO THE DOUGLAS-FIR
C....    BEETLE.

         CFTVOL(DCLAS) = CFTVOL(DCLAS) + TAMORT * CFV(J)

         IF (OKILL .GT. 0.0) THEN

C....       IF WINDTHROW OCCURED THEN ADD DFB MORTALITY TO WINDTHROW
C....       MORTALITY.

            WK2(J) = WK2(J) + TAMORT
         ELSE

C....       IF NO WINDTHROW THEN COMPARE THE DFB MORTALITY TO THE
C....       BACKGROUND MORTALITY AND TAKE THE GREATER OF THE TWO.

            IF (TAMORT .GT. WK2(J)) WK2(J) = TAMORT
         ENDIF

C....    MAKE SURE THAT THE NUMBER OF TREES/ACRE TO KILL FOR THE CURRENT
C....    TREE RECORD IS NOT GREATER THAN THE NUMBER THAT EXIST.

         IF (WK2(J) .GT. PROB(J)) WK2(J) = PROB(J)

  200 CONTINUE

C.... CLEAR OKILL

      OKILL = 0.0

C.... PRINT OUT TABLE OF DFB MORALITY INFORMATION.

      CALL DFBTAB(CFTVOL)

 1000 CONTINUE
      IF (DEBUIN) WRITE (JODFB,*) ' ** LEAVING SUBROUTINE DFBMRT'

      RETURN
      END
