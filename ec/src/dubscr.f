      SUBROUTINE DUBSCR(ISPC,D,H,CR,TPCT,TPCCF)
C----------
C  **DUBSCR--EC   DATE OF LAST REVISION:  07/09/10
C----------
C  THIS SUBROUTINE CALCULATES CROWN RATIOS FOR TREES INSERTED BY
C  THE REGENERATION ESTABLISHMENT MODEL.  IT ALSO DUBS CROWN RATIOS
C  FOR TREES IN THE INVENTORY THAT ARE MISSING CROWN RATIO
C  MEASUREMENTS AND ARE LESS THAN 5.0 INCHES DBH.  FINALLY, IT IS
C  USED TO REPLACE CROWN RATIO ESTIMATES FOR ALL TREES THAT
C  CROSS THE THRESHOLD BETWEEN THE SMALL AND LARGE TREE MODELS.
C  THIS ROUTINE WAS MOVED FROM THE SO11 SOURCE DIRECTORY TO EC
C  WHEN THE SO11 MODEL WAS RETIRED IN JUNE 2010. IT IS USED BY
C  THE EC, NC, AND WS VARIANTS
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
COMMONS
C
      EXTERNAL RANN
      DIMENSION BCR0(MAXSP),BCR1(MAXSP),BCR2(MAXSP),BCR3(MAXSP),
     & CRSD(MAXSP),BCR5(MAXSP),BCR6(MAXSP),
     & BCR8(MAXSP),BCR9(MAXSP),BCR10(MAXSP)
      DATA BCR2/
     &   .000000,   .000000,   .022409,   .022409,   .022409,   .022409,
     &   .000000,   .022409,   .022409,   .000000,   .000000/
      DATA BCR1/
     &  -.209765,  -.209765,  -.093105,  -.093105,  -.093105,  -.093105,
     &  -.209765,  -.093105,  -.093105,  -.209765,   .000000/
      DATA BCR5/
     &   .011032,   .011032,   .000000,   .000000,   .000000,   .000000,
     &   .011032,   .000000,   .000000,   .011032,   .000000/
      DATA BCR6/
     &   .000000,   .000000,  -.045532,  -.045532,  -.045532,  -.045532,
     &   .000000,  -.045532,  -.045532,   .000000,   .000000/
      DATA BCR8/
     &   .017727,   .017727,   .000000,   .000000,   .000000,   .000000,
     &   .017727,   .000000,   .000000,   .017727,   .000000/
      DATA BCR3/
     &   .003359,   .003359,   .002633,   .002633,   .002633,   .002633,
     &   .003359,   .002633,   .002633,   .003359,   .000000/
      DATA BCR9/
     &  -.000053,  -.000053,   .000022,   .000022,   .000022,   .000022,
     &  -.000053,   .000022,   .000022,  -.000053,   .000000/
      DATA BCR10/
     &   .014098,   .014098,  -.013115,  -.013115,  -.013115,  -.013115,
     &   .014098,  -.013115,  -.013115,   .014098,   .000000/
      DATA BCR0/
     & -1.669490, -1.669490,  -.426688,  -.426688,  -.426688,  -.426688,
     & -1.669490,  -.426688,  -.426688, -1.669490,  -2.19723/
      DATA CRSD/
     &  .5000,.5000,.6957,.6957,.6957,.9310,
     &  .6124,.6957,.6957,.4942,0.200/
C-----------
C  CHECK FOR DEBUG.
C-----------
C     CALL DBCHK (DEBUG,'DUBSCR',6,ICYC)
C----------
C  EXPECTED CROWN RATIO IS A FUNCTION OF SPECIES, DBH, BASAL AREA, BAL,
C  AND PCCF.  THE MODEL IS BASED ON THE LOGISTIC FUNCTION,
C  AND RETURNS A VALUE BETWEEN ZERO AND ONE.
C----------
      CR = BCR2(ISPC)*H
     *   + BCR1(ISPC)*D
     *   + BCR5(ISPC)*TPCCF
     *   + BCR6(ISPC)*(AVH/H)
     *   + BCR8(ISPC)*AVH
     *   + BCR3(ISPC)*BA
     *   + BCR9(ISPC)*(BA*TPCCF)
     *   + BCR10(ISPC)*RMAI
     *   + BCR0(ISPC)
C----------
C  A RANDOM ERROR IS ASSIGNED TO THE CROWN RATIO PREDICTION
C  PRIOR TO THE LOGISTIC TRANSFORMATION.  LINEAR REGRESSION
C  WAS USED TO FIT THE MODELS AND THE ELEMENTS OF CRSD
C  ARE THE STANDARD ERRORS FOR THE LINEARIZED MODELS BY SPECIES.
C----------
      SD=CRSD(ISPC)
   10 FCR=BACHLO(0.0,SD,RANN)
      IF(ABS(FCR).GT.SD) GO TO 10
      IF(ABS(CR+FCR).GE.86.)CR=86.
      CR=1.0/(1.0+EXP(CR+FCR))
      IF(CR .GT. .95) CR = .950
      IF(CR .LT. .05) CR=.05
C     IF(DEBUG)WRITE(JOSTND,600)ISPC,D,H,TBA,TPCCF,CR,FCR,RMAI,TAVH
C 600 FORMAT(' IN DUBSCR, ISPC=',I2,' DBH=',F4.1,' H=',F5.1,
C    & ' TBA=',F7.3,' TPCCF=',F8.4,' CR=',F4.3,
C    &   ' RAN ERR = ',F6.4,' RMAI= ',F9.4,' TAVH=',F9.4)
      RETURN
      END
