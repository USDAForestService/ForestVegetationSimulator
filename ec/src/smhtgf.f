      SUBROUTINE SMHTGF (MODE,ICYC,ISPC,H,DTIME,HHT,JOSTND,DEBUG)
      IMPLICIT NONE
C----------
C  **SMHTGF--EC  DATE OF LAST REVISION:  03/08/12
C----------
C  THIS ROUTINE CALCULATES THE HEIGHT GROWTH OF SMALL TREES (D<3.0 IN).
C  WHEN CALLED FROM ESSUBH (MODE=0), DTIME= TOTAL TREE AGE.
C  IF CALLED FROM REGENT (MODE=1), DTIME= FINT AND THE  
C  RETURN VARIABLE (HHT) IS THE HEIGHT INCREMENT.
C  CALLED FROM **ESSUBH
C  CALLED FROM **REGENT
C----------
C  INPUT VARIABLES
C  ISPC   - SPECIES SEQUENCE NUMBER
C  H      - BEGINING HEIGHT
C  SI     - SITE INDEX FOR SPECIES
C  MODE   - = 0 IF CALL IS FROM ESSUBH, = 1 IF CALL IS FROM REGENT 
C  DTIME  - DTIME=TOTAL SEEDLING AGE (MODE= 0), DTIME=FINT (MODE= 1)
C
C  RETURN VARIABLES
C  HHT    - HEIGHT GROWTH OVER TIME INCREMENT DTIME (REGENT)
C  HHT    - HEIGHT 5 YEARS INTO CYCLE, OR END OF CYCLE FINT<5 (ESSUBH)
C----------
COMMONS
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
COMMONS
C----------
      LOGICAL DEBUG
      INTEGER ISPC,ICYC,JOSTND,MODE
      REAL    AGEPDT,C1,C2,C3,C4,EFFAGE,DTIME,H,HHT,SI,HHT1,HHT2
C----------
C  SPECIES LIST FOR EAST CASCADES VARIANT.
C
C   1 = WESTERN WHITE PINE      (WP)    PINUS MONTICOLA
C   2 = WESTERN LARCH           (WL)    LARIX OCCIDENTALIS
C   3 = DOUGLAS-FIR             (DF)    PSEUDOTSUGA MENZIESII
C   4 = PACIFIC SILVER FIR      (SF)    ABIES AMABILIS
C   5 = WESTERN REDCEDAR        (RC)    THUJA PLICATA
C   6 = GRAND FIR               (GF)    ABIES GRANDIS
C   7 = LODGEPOLE PINE          (LP)    PINUS CONTORTA
C   8 = ENGELMANN SPRUCE        (ES)    PICEA ENGELMANNII
C   9 = SUBALPINE FIR           (AF)    ABIES LASIOCARPA
C  10 = PONDEROSA PINE          (PP)    PINUS PONDEROSA
C  11 = WESTERN HEMLOCK         (WH)    TSUGA HETEROPHYLLA
C  12 = MOUNTAIN HEMLOCK        (MH)    TSUGA MERTENSIANA
C  13 = PACIFIC YEW             (PY)    TAXUS BREVIFOLIA
C  14 = WHITEBARK PINE          (WB)    PINUS ALBICAULIS
C  15 = NOBLE FIR               (NF)    ABIES PROCERA
C  16 = WHITE FIR               (WF)    ABIES CONCOLOR
C  17 = SUBALPINE LARCH         (LL)    LARIX LYALLII
C  18 = ALASKA CEDAR            (YC)    CHAMAECYPARIS NOOTKATENSIS
C  19 = WESTERN JUNIPER         (WJ)    JUNIPERUS OCCIDENTALIS
C  20 = BIGLEAF MAPLE           (BM)    ACER MACROPHYLLUM
C  21 = VINE MAPLE              (VN)    ACER CIRCINATUM
C  22 = RED ALDER               (RA)    ALNUS RUBRA
C  23 = PAPER BIRCH             (PB)    BETULA PAPYRIFERA
C  24 = GOLDEN CHINKAPIN        (GC)    CHRYSOLEPIS CHRYSOPHYLLA
C  25 = PACIFIC DOGWOOD         (DG)    CORNUS NUTTALLII
C  26 = QUAKING ASPEN           (AS)    POPULUS TREMULOIDES
C  27 = BLACK COTTONWOOD        (CW)    POPULUS BALSAMIFERA var. TRICHOCARPA
C  28 = OREGON WHITE OAK        (WO)    QUERCUS GARRYANA
C  29 = CHERRY AND PLUM SPECIES (PL)    PRUNUS sp.
C  30 = WILLOW SPECIES          (WI)    SALIX sp.
C  31 = OTHER SOFTWOODS         (OS)
C  32 = OTHER HARDWOODS         (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C  FROM THE EC VARIANT:
C      USE 6(GF) FOR 16(WF)
C      USE OLD 11(OT) FOR NEW 12(MH) AND 31(OS)
C
C  FROM THE WC VARIANT:
C      USE 19(WH) FOR 11(WH)
C      USE 33(PY) FOR 13(PY)
C      USE 31(WB) FOR 14(WB)
C      USE  7(NF) FOR 15(NF)
C      USE 30(LL) FOR 17(LL)
C      USE  8(YC) FOR 18(YC)
C      USE 29(WJ) FOR 19(WJ)
C      USE 21(BM) FOR 20(BM) AND 21(VN)
C      USE 22(RA) FOR 22(RA)
C      USE 24(PB) FOR 23(PB)
C      USE 25(GC) FOR 24(GC)
C      USE 34(DG) FOR 25(DG)
C      USE 26(AS) FOR 26(AS) AND 32(OH)
C      USE 27(CW) FOR 27(CW)
C      USE 28(WO) FOR 28(WO)
C      USE 36(CH) FOR 29(PL)
C      USE 37(WI) FOR 30(WI)
C----------
C  IF NO GROWTH IS TO BE CALCULATED ON ESTAB TREES, I.E. PLANTING
C  IS MORE THAN 5 YEARS INTO THE SIMULATION, THEN RETURN AND
C  LEAVE THE SMALL TREE GROWTH, DURING THE PLANTING CYCLE, TO
C  THE REGENT ROUTINE.
C----------
      IF(DTIME .LE. 0.0) GO TO 120
C
      SI= SITEAR(ISPC)
C
      SELECT CASE (ISPC)
C
C----------
C  HEIGHT OF TALLEST SUBSEQUENT SPECIES 1 (WP) **********
C----------
      CASE(1)
C----------
C       FOR NON-LINEAR HEIGHT GROWTH MODEL SOLVE FIRST FOR EFFECTIVE
C       AGE BASED ON INPUT HEIGHT.
C----------
        C1= 0.375045
        C2= 0.92503
        C3= -0.020796
        C4= 2.48811
C
        IF(MODE .EQ. 0) THEN
          EFFAGE= 0.
          AGEPDT= EFFAGE + DTIME
        ELSEIF(MODE .EQ. 1) THEN
          EFFAGE= ALOG( (1.0 - (C1/SI * H)**(1/C4))/C2 ) / C3
          AGEPDT= EFFAGE + DTIME
        ENDIF
C
        HHT1 = (SI/C1)*(1.0-C2*EXP(C3*EFFAGE))**C4
        HHT2 = (SI/C1)*(1.0-C2*EXP(C3*AGEPDT))**C4
        HHT= HHT2 - HHT1
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 2 (WL) **********
C----------
      CASE(2,17)
        HHT = ((-3.97245 + 0.50995*SI)/(28.11668-0.05661*SI))*DTIME
C----------
C  HT OF TALLEST SUBS. SPECIES 3 (DF) **********
C----------
      CASE(3)
        HHT = ((2.0 + 0.420*SI)/(28.5 - 0.05*SI))*DTIME
C----------
C  HT OF TALLEST SUBS. SPECIES 4 (SF) **********
C----------
      CASE(4)
        HHT = ((-0.6667 + 0.4333*SI)/(28.5 - 0.05*SI))*DTIME
C----------
C  HT OF TALLEST SUBS. SPECIES 5 (RC) **********
C----------
      CASE(5)
        C1= 0.752842
        C2= 1.0
        C3= -0.0174
        C4= 1.4711
        IF(MODE .EQ. 0) THEN
          EFFAGE= 0.
          AGEPDT= EFFAGE + DTIME
        ELSEIF(MODE .EQ. 1) THEN
          EFFAGE= ALOG( (1.0 - (C1/SI * H)**(1/C4))/C2 ) / C3
          AGEPDT= EFFAGE + DTIME
        ENDIF
C
        HHT1 = (SI/C1)*(1.0-C2*EXP(C3*EFFAGE))**C4
        HHT2 = (SI/C1)*(1.0-C2*EXP(C3*AGEPDT))**C4
        HHT= HHT2 - HHT1
C----------
C  HT OF TALLEST SUBS. SPECIES  6 (GF) **********
C  HT OF TALLEST SUBS. SPECIES 16 (WF) **********
C----------
      CASE(6,16)
        HHT = ((-1.0470 + 0.4220*SI)/(28.7739 - 0.0597*SI))*DTIME
C----------
C  HT OF TALLEST SUBS. SPECIES 7 (LP) **********
C----------
      CASE(7)
        HHT = (0.3277 + 0.01296*SI)*DTIME
C----------
C  HT OF TALLEST SUBS. SPECIES 8 (ES) **********
C----------
      CASE(8)
        HHT = ((-8.0 + 0.35*SI)/(53.72545 - 0.274509*SI))*DTIME
C----------
C  HT OF TALLEST SUBS. SPECIES 9 (AF) **********
C----------
      CASE(9)
        HHT = ((6.0 + 0.14*SI)/(33.882 - 0.06588*SI))*DTIME
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 10 (PP) **********
C----------
      CASE(10)
        HHT = ((-1.0 + 0.32857*SI)/(28.0 - 0.042857*SI))*DTIME
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 11 (WH) **********
C----------
      CASE(11)
        HHT = ((-5.74874 + 0.54576*SI)/(26.15767 - 0.03596*SI))*DTIME
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 12 (MH) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 31 (OS) **********
C  IN METERS CONVERTED TO FEET
C----------
      CASE(12,31)
        HHT = ((0.965758 + 0.082969*SI)/(55.249612 - 1.288852*SI))*DTIME
     &        *3.280833
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 13 (PY) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 14 (WB) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 18 (YC) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 19 (WJ) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 20 (BM) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 21 (VN) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 23 (PB) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 24 (GC) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 25 (DG) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 26 (AS) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 27 (CW) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 29 (PL) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 30 (WI) **********
C  HEIGHT OF TALLEST SUBS. SPECIES 32 (OH) **********
C----------
      CASE(13,14,18:21,23:27,29,30,32)
        HHT = ((1.47043 + 0.23317*SI)/(31.56252 - 0.05586*SI))*DTIME
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 15 (NF) **********
C----------
      CASE(15)
        HHT = ((11.26677 + 0.12027*SI)/(27.93806 - 0.02873*SI))*DTIME
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 22 (RA) **********
C----------
      CASE(22)
        HHT = (-0.007025 + 0.056794*SI)*DTIME
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 28 (WO) **********
C----------
      CASE(28)
        HHT = (((-37.60812*ALOG(1.0-(SI/114.24569)**0.44444))*0.01)-0.1)
     &        *DTIME     
C
      END SELECT
C
  120 CONTINUE
      IF (DEBUG)
     & WRITE(JOSTND,*)' LEAVING SMHTGF, ICYC,ISPC,HHT,DTIME,SI= ',
     &ICYC,ISPC,HHT,DTIME,SI
C
      RETURN
      END
