      SUBROUTINE CCFCAL(ISPC,D,H,JCR,P,LTHIN,CCFT,CRWDTH,MODE)
      IMPLICIT NONE
C----------
C  **CCFCAL--EC   DATE OF LAST REVISION:  05/09/12
C----------
C  THIS ROUTINE COMPUTES CROWN WIDTH AND CCF FOR INDIVIDUAL TREES.
C  CALLED FROM DENSE, PRTRLS, SSTAGE, AND CVCW.
C
C  ARGUMENT DEFINITIONS:
C    ISPC = NUMERIC SPECIES CODE
C       D = DIAMETER AT BREAST HEIGHT
C       H = TOTAL TREE HEIGHT
C     JCR = CROWN RATIO IN PERCENT (0-100)
C       P = TREES PER ACRE
C   LTHIN = .TRUE. IF THINNING HAS JUST OCCURRED
C         = .FALSE. OTHERWISE
C    CCFT = CCF REPRESENTED BY THIS TREE
C  CRWDTH = CROWN WIDTH OF THIS TREE
C    MODE = 1 IF ONLY NEED CCF RETURNED
C           2 IF ONLY NEED CRWDTH RETURNED
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
COMMONS
C----------
C  DIMENSION AND DATA STATEMENTS FOR INTERNAL VARIABLES.
C
C     CCF COEFFICIENTS FOR TREES THAT ARE GREATER THAN 10.0 IN. DBH:
C      RD1 -- CONSTANT TERM IN CROWN COMPETITION FACTOR EQUATION,
C             SUBSCRIPTED BY SPECIES
C      RD2 -- COEFFICIENT FOR SUM OF DIAMETERS TERM IN CROWN
C             COMPETITION FACTOR EQUATION,SUBSCRIPTED BY
C             SPECIES
C      RD3 -- COEFFICIENT FOR SUM OF DIAMETER SQUARED TERM IN
C             CROWN COMPETITION EQUATION, SUBSCRIPTED BY SPECIES
C
C     CCF COEFFICIENTS FOR TREES THAT ARE LESS THAN 10.0 IN. DBH:
C      RDA -- MULTIPLIER.
C      RDB -- EXPONENT.  CCF(I) = RDA * (DBH**RDB)
C
C
C  SPECIES LIST FOR EAST CASCADES VARIANT.
C
C   1 = WESTERN WHITE PINE      (WP)    PINUS MONTICOLA
C   2 = WESTERN LARCH           (WL)    LARIX OCCIDENTALIS
C   3 = DOUGLAS-FIR             (DF)    PSEUDOTSUGA MENZIESII
C   4 = PACIFIC SILVER FIR      (SF)    ABIES AMABILIS
C   5 = WESTERN REDCEDAR        (RC)    THUJA PLICATA
C   6 = GRAND FIR               (GF)    ABIES GRANDIS
C   7 = LODGEPOLE PINE          (LP)    PINUS CONTORTA
C   8 = ENGELMANN SPRUCE        (ES)    PICEA ENGELMANNII
C   9 = SUBALPINE FIR           (AF)    ABIES LASIOCARPA
C  10 = PONDEROSA PINE          (PP)    PINUS PONDEROSA
C  11 = WESTERN HEMLOCK         (WH)    TSUGA HETEROPHYLLA
C  12 = MOUNTAIN HEMLOCK        (MH)    TSUGA MERTENSIANA
C  13 = PACIFIC YEW             (PY)    TAXUS BREVIFOLIA
C  14 = WHITEBARK PINE          (WB)    PINUS ALBICAULIS
C  15 = NOBLE FIR               (NF)    ABIES PROCERA
C  16 = WHITE FIR               (WF)    ABIES CONCOLOR
C  17 = SUBALPINE LARCH         (LL)    LARIX LYALLII
C  18 = ALASKA CEDAR            (YC)    CALLITROPSIS NOOTKATENSIS
C  19 = WESTERN JUNIPER         (WJ)    JUNIPERUS OCCIDENTALIS
C  20 = BIGLEAF MAPLE           (BM)    ACER MACROPHYLLUM
C  21 = VINE MAPLE              (VN)    ACER CIRCINATUM
C  22 = RED ALDER               (RA)    ALNUS RUBRA
C  23 = PAPER BIRCH             (PB)    BETULA PAPYRIFERA
C  24 = GIANT CHINQUAPIN        (GC)    CHRYSOLEPIS CHRYSOPHYLLA
C  25 = PACIFIC DOGWOOD         (DG)    CORNUS NUTTALLII
C  26 = QUAKING ASPEN           (AS)    POPULUS TREMULOIDES
C  27 = BLACK COTTONWOOD        (CW)    POPULUS BALSAMIFERA var. TRICHOCARPA
C  28 = OREGON WHITE OAK        (WO)    QUERCUS GARRYANA
C  29 = CHERRY AND PLUM SPECIES (PL)    PRUNUS sp.
C  30 = WILLOW SPECIES          (WI)    SALIX sp.
C  31 = OTHER SOFTWOODS         (OS)
C  32 = OTHER HARDWOODS         (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C  FROM THE EC VARIANT:
C      USE 6(GF) FOR 16(WF)
C      USE OLD 11(OT) FOR NEW 12(MH) AND 31(OS)
C
C  FROM THE WC VARIANT:
C      USE 19(WH) FOR 11(WH)
C      USE 33(PY) FOR 13(PY)
C      USE 31(WB) FOR 14(WB)
C      USE  7(NF) FOR 15(NF)
C      USE 30(LL) FOR 17(LL)
C      USE  8(YC) FOR 18(YC)
C      USE 29(WJ) FOR 19(WJ)
C      USE 21(BM) FOR 20(BM) AND 21(VN)
C      USE 22(RA) FOR 22(RA)
C      USE 24(PB) FOR 23(PB)
C      USE 25(GC) FOR 24(GC)
C      USE 34(DG) FOR 25(DG)
C      USE 26(AS) FOR 26(AS) AND 32(OH)
C      USE 27(CW) FOR 27(CW)
C      USE 28(WO) FOR 28(WO)
C      USE 36(CH) FOR 29(PL)
C      USE 37(WI) FOR 30(WI)
C
C  SOURCES OF CCF COEFFICIENTS:
C     1 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: WESTERN WHITE PINE
C     2 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: WESTERN LARCH
C     3 = PAINE AND HANN TABLE 2: DOUGLAS-FIR
C     4 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: GRAND FIR
C     5 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: WESTERN RED CEDAR
C     6 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: GRAND FIR
C     7 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: LODGEPOLE PINE 
C     8 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: ENGELMANN SPRUCE
C     9 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: SUBALPINE FIR
C    10 = PAINE AND HANN TABLE 2: PONDEROSA PINE
C    11 = PAINE AND HANN TABLE 2: WESTERN HEMLOCK
C    12 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: WESTERN HEMLOCK
C    13 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    14 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: LODGEPOLE PINE
C    15 = PAINE AND HANN TABLE 2: NOBLE FIR
C    16 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: GRAND FIR
C    17 = PAINE AND HANN TABLE 2: INCENSE CEDAR
C    18 = PAINE AND HANN TABLE 2: INCENSE CEDAR
C    19 = PAINE AND HANN TABLE 2: INCENSE CEDAR
C    20 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    21 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    22 = PAINE AND HANN TABLE 2: TANOAK
C    23 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    24 = PAINE AND HANN TABLE 2: CHINQUAPIN
C    25 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    26 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    27 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    28 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    29 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    30 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C    31 = NORTHERN IDAHO VARIANT INT-133 TABLE 8: WESTERN HEMLOCK
C    32 = PAINE AND HANN TABLE 2: CALIFORNIA BLACK OAK
C
C      PAINE AND HANN, 1982. MAXIMUM CROWN WIDTH EQUATIONS FOR
C        SOUTHWESTERN OREGON TREE SPECIES. RES PAP 46, FOR RES LAB
C        SCH FOR, OSU, CORVALLIS. 20PP.
C
C      WYKOFF, CROOKSTON, STAGE, 1982. USER'S GUIDE TO THE STAND
C        PROGNOSIS MODEL. GEN TECH REP INT-133. OGDEN, UT:
C        INTERMOUNTAIN FOREST AND RANGE EXP STN. 112P.
C
C----------
      LOGICAL LTHIN
      REAL CRWDTH,CCFT,P,H,D
      INTEGER MODE,JCR,ISPC
      REAL RD1(MAXSP),RD2(MAXSP),RD3(MAXSP),RDA(MAXSP),RDB(MAXSP)
C
      DATA RD1/
     &      .03,     0.02,    .0388,     0.04,     0.03,
     &     0.04,   .01925,      .03,      .03,    .0219, 
     &  0.03758,     0.03,   0.0204,  0.01925,  0.02453,
     &     0.04,   0.0194,   0.0194,   0.0194,   0.0204,
     &   0.0204,  0.03561,   0.0204,   0.0160,   0.0204,
     &   0.0204,   0.0204,   0.0204,   0.0204,   0.0204,
     &     0.03,   0.0204/
C
      DATA RD2/
     &   0.0167,   0.0148,   0.0269,   0.0270,   0.0238,
     &    0.027,   .01676,    .0173,    .0216,    .0169,
     &   0.0233,   0.0215,   0.0246,   0.0168,   0.0115,
     &    0.027,   0.0142,   0.0142,   0.0142,   0.0246,
     &   0.0246,  0.02731,   0.0246,   0.0167,   0.0246,
     &   0.0246,   0.0246,   0.0246,   0.0246,   0.0246,
     &   0.0215,   0.0246/ 
C
      DATA RD3/
     &  0.00230,  0.00338,  0.00466,  0.00405,  0.00490,
     &  0.00405,   .00365,   .00259,   .00405,   .00325,
     &  0.00361,  0.00363,   0.0074,  0.00365,  0.00134,
     &  0.00405,  0.00261,  0.00261,  0.00261,   0.0074,
     &   0.0074,  0.00524,   0.0074,  0.00434,   0.0074,
     &   0.0074,   0.0074,   0.0074,   0.0074,   0.0074,
     &  0.00363,   0.0074/
C
      DATA RDA/
     & 0.009884, 0.007244, 0.017299, 0.015248, 0.008915,
     & 0.015248, 0.009187, 0.007875, 0.011402, 0.007813,
     &      0.0, 0.011109,      0.0,      0.0,      0.0,
     & 0.015248,      0.0,      0.0,      0.0,      0.0,
     &      0.0,      0.0,      0.0,      0.0,      0.0,
     &      0.0,      0.0,      0.0,      0.0,      0.0,
     & 0.011109,      0.0/
C
      DATA RDB/
     &   1.6667,   1.8182,   1.5571,   1.7333,   1.7800,
     &   1.7333,   1.7600,   1.7360,   1.7560,   1.7780,
     &      0.0,   1.7250,      0.0,      0.0,      0.0,
     &   1.7333,      0.0,      0.0,      0.0,      0.0,
     &      0.0,      0.0,      0.0,      0.0,      0.0,
     &      0.0,      0.0,      0.0,      0.0,      0.0,
     &   1.7250,      0.0/
C----------
C  INITIALIZE RETURN VARIABLES.
C----------
      CCFT = 0.
      CRWDTH = 0.
C----------
C  COMPUTE CCF
C----------
      IF(MODE.EQ.1) THEN
        SELECT CASE (ISPC)
C
C  SPECIES USING EC VARIANT EQUATIONS
C
        CASE(1:10,12,16,31)
          IF(D .GE. 1.0) THEN
            CCFT = RD1(ISPC) + D*RD2(ISPC) + D*D*RD3(ISPC)
          ELSEIF (D .GT. 0.1) THEN
            CCFT = RDA(ISPC) * (D**RDB(ISPC))
          ELSE
            CCFT=.001
          ENDIF
C
C  SPECIES USING WC VARIANT EQUATIONS
C
        CASE(11,13:15,17:30,32)
          IF(D .GE. 1.0) THEN
            CCFT = RD1(ISPC) + D*RD2(ISPC) + D*D*RD3(ISPC)
          ELSE
            CCFT = D * (RD1(ISPC)+RD2(ISPC)+RD3(ISPC))
          ENDIF
C
        END SELECT
        CCFT = CCFT * P
      ENDIF
C----------
C  COMPUTE CROWN WIDTH; ALL SPECIES USE THE SAME LOGIC.
C----------
      IF(MODE.EQ.2) THEN
        CALL R6CRWD (ISPC,D,H,CRWDTH)
        IF(CRWDTH .GT. 99.9) CRWDTH=99.9
      ENDIF
C
      RETURN
      END
