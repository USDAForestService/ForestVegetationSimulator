      SUBROUTINE SICHG(ISISP,SSITE,SIAGE)
      IMPLICIT NONE
C----------
C  **SICHG--EC  DATE OF LAST REVISION:  05/09/12
C----------
C  THIS ROUTINE TAKES THE SITE FROM THE STDINFO CARD AND/OR
C  THE DEFAULT VALUE FROM SITSET, AND DETERMINES IF IT IS A 
C  BHA OR TTA SITE CURVE.  THE RETURN VARIABLE SIAGE REPRESENTS THE
C  SPECIES BASE AGE + AGE2BH IF THE SITE SPECIES SITE CURVE
C  IS A TTA CURVE AND SPECIES CURVE A BHA CURVE, AND SO ON.
C  THEN, THE SITE FOR EACH SPECIES MAY BE ESTIMATED USING
C  THE SITE SPECIES CURVE IN HTCALC.
C  DATA STATEMENTS FOR A AND B CONTAIN COEFFICIENTS FOR A LINEAR
C  RELATION OF AGE TO BREAST HEIGHT AS A FUNCTION OF SITE INDEX.
C  THE LINEAR RELATION IS AN ESTIMATE FROM THE LOWER END POINT OF
C  A HEIGHT GROWTH CURVE TO THE ORIGIN (0 AGE; 0 TOT TREE HT.)
C  THESE COEFFICIENTS SHOULD BE COMPATIBLE WITH SIMILAR VALUES USED
C  IN THE SMHTGF SUBROUTINE.
C
C  CALLED FROM **SITSET
C----------
COMMONS
C
      INCLUDE 'PRGPRM.F77'
C
COMMONS
C----------
      CHARACTER*1 ISILOC,REFLOC(MAXSP)
      INTEGER DIFF,ISISP,I
      INTEGER REFAGE(MAXSP)
      REAL SSITE,AGE2BH
      REAL A(MAXSP),B(MAXSP),SIAGE(MAXSP)
C----------
C  SPECIES LIST FOR EAST CASCADES VARIANT.
C
C   1 = WESTERN WHITE PINE      (WP)    PINUS MONTICOLA
C   2 = WESTERN LARCH           (WL)    LARIX OCCIDENTALIS
C   3 = DOUGLAS-FIR             (DF)    PSEUDOTSUGA MENZIESII
C   4 = PACIFIC SILVER FIR      (SF)    ABIES AMABILIS
C   5 = WESTERN REDCEDAR        (RC)    THUJA PLICATA
C   6 = GRAND FIR               (GF)    ABIES GRANDIS
C   7 = LODGEPOLE PINE          (LP)    PINUS CONTORTA
C   8 = ENGELMANN SPRUCE        (ES)    PICEA ENGELMANNII
C   9 = SUBALPINE FIR           (AF)    ABIES LASIOCARPA
C  10 = PONDEROSA PINE          (PP)    PINUS PONDEROSA
C  11 = WESTERN HEMLOCK         (WH)    TSUGA HETEROPHYLLA
C  12 = MOUNTAIN HEMLOCK        (MH)    TSUGA MERTENSIANA
C  13 = PACIFIC YEW             (PY)    TAXUS BREVIFOLIA
C  14 = WHITEBARK PINE          (WB)    PINUS ALBICAULIS
C  15 = NOBLE FIR               (NF)    ABIES PROCERA
C  16 = WHITE FIR               (WF)    ABIES CONCOLOR
C  17 = SUBALPINE LARCH         (LL)    LARIX LYALLII
C  18 = ALASKA CEDAR            (YC)    CALLITROPSIS NOOTKATENSIS
C  19 = WESTERN JUNIPER         (WJ)    JUNIPERUS OCCIDENTALIS
C  20 = BIGLEAF MAPLE           (BM)    ACER MACROPHYLLUM
C  21 = VINE MAPLE              (VN)    ACER CIRCINATUM
C  22 = RED ALDER               (RA)    ALNUS RUBRA
C  23 = PAPER BIRCH             (PB)    BETULA PAPYRIFERA
C  24 = GIANT CHINQUAPIN        (GC)    CHRYSOLEPIS CHRYSOPHYLLA
C  25 = PACIFIC DOGWOOD         (DG)    CORNUS NUTTALLII
C  26 = QUAKING ASPEN           (AS)    POPULUS TREMULOIDES
C  27 = BLACK COTTONWOOD        (CW)    POPULUS BALSAMIFERA var. TRICHOCARPA
C  28 = OREGON WHITE OAK        (WO)    QUERCUS GARRYANA
C  29 = CHERRY AND PLUM SPECIES (PL)    PRUNUS sp.
C  30 = WILLOW SPECIES          (WI)    SALIX sp.
C  31 = OTHER SOFTWOODS         (OS)
C  32 = OTHER HARDWOODS         (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C  FROM THE EC VARIANT:
C      USE 6(GF) FOR 16(WF)
C      USE OLD 11(OT) FOR NEW 12(MH) AND 31(OS)
C
C  FROM THE WC VARIANT:
C      USE 19(WH) FOR 11(WH)
C      USE 33(PY) FOR 13(PY)
C      USE 31(WB) FOR 14(WB)
C      USE  7(NF) FOR 15(NF)
C      USE 30(LL) FOR 17(LL)
C      USE  8(YC) FOR 18(YC)
C      USE 29(WJ) FOR 19(WJ)
C      USE 21(BM) FOR 20(BM) AND 21(VN)
C      USE 22(RA) FOR 22(RA)
C      USE 24(PB) FOR 23(PB)
C      USE 25(GC) FOR 24(GC)
C      USE 34(DG) FOR 25(DG)
C      USE 26(AS) FOR 26(AS) AND 32(OH)
C      USE 27(CW) FOR 27(CW)
C      USE 28(WO) FOR 28(WO)
C      USE 36(CH) FOR 29(PL)
C      USE 37(WI) FOR 30(WI)
C----------
C  DATA STATEMENTS
C----------
      DATA A/
     & 18.43316,  8.11680,  8.50000,  9.01840, 24.62857,
     &  9.01840,  7.78447, 33.72545, 13.88200,  8.00000,
     &  6.15767, 45.24969, 11.56252, 11.56252, 7.938059,
     &  9.01840,  8.11668, 11.56252, 11.56252, 11.56252,
     & 11.56252,  3.28241, 11.56252, 11.56252, 11.56252,
     & 11.56252, 11.56252,  4.94166, 11.56252, 11.56252,
     & 45.24960, 11.56252/
C
      DATA B/
     & -0.13399, -0.05661, -0.05000, -0.05700, -0.20465,
     & -0.05700, -0.05013,-0.274509, -0.06590, -0.04286,
     & -0.03596, -1.28885, -0.05586, -0.05586, -0.02873,
     & -0.05700, -0.05661, -0.05586, -0.05586, -0.05586,
     & -0.05586, -0.02987, -0.05586, -0.05586, -0.05586,
     & -0.05586, -0.05586, -0.02419, -0.05586, -0.05586,
     & -1.28885, -0.05586/ 
C
      DATA REFLOC/
     &  'T', 'B', 'B', 'B', 'T', 'B', 'T', 'B', 'B', 'B',
     &  'B', 'B', 'B', 'B', 'B', 'B', 'B', 'B', 'B', 'B',
     &  'B', 'T', 'B', 'B', 'B', 'B', 'B', 'B', 'B', 'B',
     &  'B', 'B'/
C
      DATA REFAGE/
     &   50,  50,  50,  50, 100,  50, 100, 100, 100, 100,
     &   50, 100, 100, 100, 100,  50,  50, 100, 100, 100,
     &  100,  20, 100, 100, 100, 100, 100,  50, 100, 100,
     &  100, 100/
C----------
C ISILOC IS THE PLACE THE AGE FOR THE SITE SPECIES.
C----------
      ISILOC = REFLOC(ISISP)
      DO 100 I = 1,MAXSP
C----------
C  SET UP THE ARRAY TO TELL WHETHER YOU NEED TO SLIDE UP OR DOWN THE SIT
C  LINE TO ADJUST FOR TOTAL AGE OR BREAST HIGH AGE
C----------
      IF(ISILOC .EQ. 'T' .AND. REFLOC(I) .EQ. 'B')DIFF=-1
      IF(ISILOC .EQ. REFLOC(I))DIFF=0
      IF(ISILOC .EQ. 'B' .AND. REFLOC(I) .EQ. 'T')DIFF=1
      AGE2BH=0.0
      IF(DIFF .LT. 0 .OR. DIFF .GT. 0)THEN
        AGE2BH=A(I) + B(I)*SSITE
        IF((ISISP .NE. 31) .AND. (I .EQ. 31))
     &    AGE2BH=A(I) + B(I)*(SSITE/3.281)
        IF((ISISP .EQ. 31) .AND. (I .NE. 31))
     &    AGE2BH=A(I) + B(I)*(SSITE*3.281)
        IF((ISISP .NE. 12) .AND. (I .EQ. 12))
     &    AGE2BH=A(I) + B(I)*(SSITE/3.281)
        IF((ISISP .EQ. 12) .AND. (I .NE. 12))
     &    AGE2BH=A(I) + B(I)*(SSITE*3.281)
      END IF
      SIAGE(I) = REFAGE(I) + AGE2BH*DIFF
C
  100 CONTINUE
C
      RETURN
      END
