      SUBROUTINE HTCALC (SINDX,ISPC,AG,HGUESS,JOSTND,DEBUG)
      IMPLICIT NONE
C----------
C  **HTCALC--EC   DATE OF LAST REVISION:  05/09/12
C----------
C THIS ROUTINE CALCULATES A POTENTIAL HT GIVEN A SPECIES SITE AND AGE
C IT IS USED TO CAL POTHTG AND SITE.
C GO TO A DIFFERENT POTENTIAL HEIGHT CURVE DEPENDING ON THE SPECIES
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
C
COMMONS
C----------
      LOGICAL DEBUG
      INTEGER ISPC,JOSTND
      REAL HGUESS,AG,SINDX,B0,B1,B2,B3,B4,B5,B6,B7,B8,B9,B10,B11
      REAL B12,B13,X1,X2,X3,Z
C----------
C  SPECIES LIST FOR EAST CASCADES VARIANT.
C
C   1 = WESTERN WHITE PINE      (WP)    PINUS MONTICOLA
C   2 = WESTERN LARCH           (WL)    LARIX OCCIDENTALIS
C   3 = DOUGLAS-FIR             (DF)    PSEUDOTSUGA MENZIESII
C   4 = PACIFIC SILVER FIR      (SF)    ABIES AMABILIS
C   5 = WESTERN REDCEDAR        (RC)    THUJA PLICATA
C   6 = GRAND FIR               (GF)    ABIES GRANDIS
C   7 = LODGEPOLE PINE          (LP)    PINUS CONTORTA
C   8 = ENGELMANN SPRUCE        (ES)    PICEA ENGELMANNII
C   9 = SUBALPINE FIR           (AF)    ABIES LASIOCARPA
C  10 = PONDEROSA PINE          (PP)    PINUS PONDEROSA
C  11 = WESTERN HEMLOCK         (WH)    TSUGA HETEROPHYLLA
C  12 = MOUNTAIN HEMLOCK        (MH)    TSUGA MERTENSIANA
C  13 = PACIFIC YEW             (PY)    TAXUS BREVIFOLIA
C  14 = WHITEBARK PINE          (WB)    PINUS ALBICAULIS
C  15 = NOBLE FIR               (NF)    ABIES PROCERA
C  16 = WHITE FIR               (WF)    ABIES CONCOLOR
C  17 = SUBALPINE LARCH         (LL)    LARIX LYALLII
C  18 = ALASKA CEDAR            (YC)    CALLITROPSIS NOOTKATENSIS
C  19 = WESTERN JUNIPER         (WJ)    JUNIPERUS OCCIDENTALIS
C  20 = BIGLEAF MAPLE           (BM)    ACER MACROPHYLLUM
C  21 = VINE MAPLE              (VN)    ACER CIRCINATUM
C  22 = RED ALDER               (RA)    ALNUS RUBRA
C  23 = PAPER BIRCH             (PB)    BETULA PAPYRIFERA
C  24 = GIANT CHINQUAPIN        (GC)    CHRYSOLEPIS CHRYSOPHYLLA
C  25 = PACIFIC DOGWOOD         (DG)    CORNUS NUTTALLII
C  26 = QUAKING ASPEN           (AS)    POPULUS TREMULOIDES
C  27 = BLACK COTTONWOOD        (CW)    POPULUS BALSAMIFERA var. TRICHOCARPA
C  28 = OREGON WHITE OAK        (WO)    QUERCUS GARRYANA
C  29 = CHERRY AND PLUM SPECIES (PL)    PRUNUS sp.
C  30 = WILLOW SPECIES          (WI)    SALIX sp.
C  31 = OTHER SOFTWOODS         (OS)
C  32 = OTHER HARDWOODS         (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C  FROM THE EC VARIANT:
C      USE 6(GF) FOR 16(WF)
C      USE OLD 11(OT) FOR NEW 12(MH) AND 31(OS)
C
C  FROM THE WC VARIANT:
C      USE 19(WH) FOR 11(WH)
C      USE 33(PY) FOR 13(PY)
C      USE 31(WB) FOR 14(WB)
C      USE  7(NF) FOR 15(NF)
C      USE 30(LL) FOR 17(LL)
C      USE  8(YC) FOR 18(YC)
C      USE 29(WJ) FOR 19(WJ)
C      USE 21(BM) FOR 20(BM) AND 21(VN)
C      USE 22(RA) FOR 22(RA)
C      USE 24(PB) FOR 23(PB)
C      USE 25(GC) FOR 24(GC)
C      USE 34(DG) FOR 25(DG)
C      USE 26(AS) FOR 26(AS) AND 32(OH)
C      USE 27(CW) FOR 27(CW)
C      USE 28(WO) FOR 28(WO)
C      USE 36(CH) FOR 29(PL)
C      USE 37(WI) FOR 30(WI)
C----------
C  SEE IF WE NEED TO DO SOME DEBUG.
C----------
      IF(DEBUG) WRITE(JOSTND,10)SINDX,ISPC,AG
   10 FORMAT(' ENTERING HTCALC, SINDX,ISPC,AG= ',F7.2,I5,F7.2)
C----------
C LOAD THE BETA COEFFICIENTS
C----------
      B0=BB0(ISPC)
      B1=BB1(ISPC)
      B2=BB2(ISPC)
      B3=BB3(ISPC)
      B4=BB4(ISPC)
      B5=BB5(ISPC)
      B6=BB6(ISPC)
      B7=BB7(ISPC)
      B8=BB8(ISPC)
      B9=BB9(ISPC)
      B10=BB10(ISPC)
      B11=BB11(ISPC)
      B12=BB12(ISPC)
      B13=BB13(ISPC)
C
      SELECT CASE (ISPC)
C----------
C WHITE PINE USE BRICKELL, INT-75
C----------
      CASE(1)
        HGUESS=SINDX/(B0*(1.0-B1*EXP(B2*AG))**B3)
C----------
C WESTERN LARCH USE COCHRAN, PNW 424
C----------
      CASE(2,17)
        HGUESS=4.5 + B1*AG + B2*AG*AG + B3*AG**3  +
     &         B4*AG**4 + (SINDX -4.5)*(B5 + B6*AG
     &         + B7*AG*AG + B8*AG**3)-
     &         B9*(B10 + B11*AG + B12*AG*AG + B13*AG**3)
C----------
C DOUGLAS FIR USE COCHRAN, PNW 251
C----------
      CASE(3)
        IF(DEBUG)WRITE(JOSTND,30)B1,B2,B3,B4,B5,B6,B7,B8,AG,SINDX
   30   FORMAT(' IN HTCALC 30F B1-8,AB,SINDX =',/,1H ,10F13.8)
        HGUESS=4.5 + EXP(B1 + B2*ALOG(AG) + B3*(ALOG(AG))**4)
     &         + B4*(B5 + B6*(1.0 - EXP(B7*AG))**B8)
     &         + (SINDX - 4.5)*(B5 + B6*(1.0 - EXP(B7*AG)**B8))
C----------
C SILVER FIR, SURROGATE IS WHITE FIR USE COCHRAN PNW 252
C GRAND FIR ---- USE SILVER FIR
C----------
      CASE(4,6,16)
        X2= B0 + B1*ALOG(AG) + B2*(ALOG(AG))**4
     &      + B3*(ALOG(AG))**9 + B4*(ALOG(AG))**11
     &      + B5*(ALOG(AG))**18
        X3= B6 + B7*ALOG(AG) + B8*(ALOG(AG))**2
     &      + B9*(ALOG(AG))**7 + B10*(ALOG(AG))**16
     &      + B11*(ALOG(AG))**24
        HGUESS=EXP(X2) + B12*EXP(X3) + (SINDX - 4.5)*EXP(X3)
     &       + 4.5
        IF(DEBUG)WRITE(JOSTND,9500)X2,X3,SINDX,AG,HGUESS
 9500   FORMAT(' X2,X3,SINDX,AG',5E12.4)
C----------
C RED CEDAR ---- USE HEGYI, JELINEK, VISZLAI,
C & CARPENTER 1981 FOR SITE REFERENCE+
C----------
      CASE(5)
        IF(DEBUG)WRITE(JOSTND,9501)ISPC,AG,SINDX,B0,B1,B2,B3,B4,B5
 9501   FORMAT(' HTCALC RED CEDAR ISPC,AG,SI,B0-B5= ',I5,8E10.4)
        HGUESS = B1*SINDX * ((1.0 - EXP(B2*AG))**B3)
C----------
C LODGEPOLE PINE USE ALEXANDER TACKLE AND DAHMS FOR CCF 100, RM-29
C----------
      CASE(7)
        HGUESS = B0 + B1*AG + B2*AG*AG + B3*  0.0*SINDX
     &    + B4*AG*SINDX + B5*AG*AG*SINDX
        IF(DEBUG)WRITE(JOSTND,9701)AG,SINDX,HGUESS
 9701   FORMAT(' HTCALC LPP AG SI HGUESS ',3F13.8)
C----------
C ENGLEMANN SPRUCE USE ALEXANDER, RM-32
C----------
      CASE(8)
        HGUESS = 4.5+((B0*SINDX**B1)*(1.0-EXP(-B2*AG))**(B3*SINDX**B4))
        IF(DEBUG)WRITE(JOSTND,*)B0,B1,B2,B3,B4,AG,SINDX,HGUESS
C----------
C SUB ALPINE FIR AND NOBLE FIR USE JOHNSON'S EQUIV OF DEMARS, PNW-119
C----------
      CASE(9)
        HGUESS=SINDX*(B0 + B1*AG + B2*AG*AG)
C----------
C PONDEROSA PINE USE BARRETT, PNW-232
C----------
      CASE(10)
        IF(DEBUG)WRITE(JOSTND,20)B0,B1,AG,B2,B3,B4,B5,B6,B7,SINDX
   20   FORMAT(' HTCALC 20F B0-1,AG,B2-7,SINDX = ',/,1H ,10F13.8)
        HGUESS =( B0*(1.0 -EXP(B1*AG))**B2)
     &           - ((B3 + B4*(1.0-EXP(B5*AG))**B6)*B7)
     &           + ((B3 + B4*(1.0 - EXP(B5*AG))**B6)*(SINDX - 4.5))
     &           + 4.5
C----------
C WESTERN HEMLOCK - WILEY, 1978
C----------
      CASE(11)
        IF(DEBUG)WRITE(JOSTND,*)' WESTERN HEMLOCK EQUATION'
        Z = 2500./(SINDX-4.5)
        HGUESS = (AG*AG/(B0 + B1*Z + (B2 + B3*Z)*AG +
     &           (B4 + B5*Z)*(AG*AG))) + 4.5
C----------
C MOUNTAIN HEMLOCK AND OTHER SOFTWOODS. USE MEANS (UNPUBL) METRIC
C SINCE MEANS USES HEIGHT ABOVE B.H., ADD IN 1.37 METERS BEFORE
C CONVERTING TO ENGLISH UNITS.
C----------
      CASE(12,31)
        IF(DEBUG)WRITE(JOSTND,9502)ISPC,AG,SINDX,B0,B1,B2,B4,B5
 9502   FORMAT(' HTCALC HEMLOC SP AG,SI B S ',I5,7E12.4)
        HGUESS = (B0 + B1*SINDX)*(1.0 - EXP(B2*SQRT(SINDX)*AG))
     &           ** (B4 + B5/SINDX)
        HGUESS=(HGUESS+1.37)*3.281
C----------
C PACIFIC YEW, WHITEBARK PINE, ALASKA CEDAR, WESTERN JUNIPER
C BIGLEAF MAPLE, VINE MAPLE, PAPER BIRCH, GIANT CHINQUAPIN,
C PACIFIC DOGWOOD, QUAKING ASPEN, BLACK COTTONWOOD,
C CHERRY AND PLUM SPECIES, WILLOW SPECIES, OTHER HARDWOODS
C
C USE CURTIS, FOR. SCI. 20:307-316.  
C CURTIS CURVES ARE PRESENTED IN METRIC (3.2808 ?)
C----------
      CASE(13,14,18:21,23:27,29:30,32)
        HGUESS = (SINDX - 4.5) / ( B0 + B1/(SINDX - 4.5)
     &           + B2 * AG**(-1.4) +(B3/(SINDX - 4.5))*AG**(-1.4))
C
C EXCESSIVE HT GROWTH -- APPROX 30-40 FT/CYCLE, TAKE OUT METRIC MULT
C DIXON 11-05-92
C
C       HGUESS = (HGUESS + 4.5)*3.2808
        HGUESS = HGUESS + 4.5
C----------
C NOBLE FIR - HERMAN, PNW-243
C----------
      CASE(15)
        X1 = B0 + B1*(SINDX - 4.5) - B2*(SINDX - 4.5)**2
        X2 = B3 + B4*(SINDX - 4.5)**(-1) + B5*(SINDX - 4.5)**(-2)
        HGUESS = 4.5 + (SINDX - 4.5)/(X1*(1.0/AG)**2 +
     &   X2*(1.0/AG) + 1.0 - 0.0001*X1 - 0.01*X2)
C----------
C RED ALDER - HARRINGTON, PNW-358
C----------
      CASE(22)
        HGUESS = SINDX
     &           + (B0 + B1*SINDX)*(1.0-EXP((B2 + B3*SINDX)*AG))**B4
     &           - (B0 + B1*SINDX)*(1.0-EXP((B2 + B3*SINDX)*20.0))**B4
C----------
C OREGON WHITE OAK - KING, DF, WEYERHAEUSER FORESTRY PAPER NO. 8
C----------
      CASE(28)
        Z = 2500./(SINDX-4.5)
        HGUESS = (AG*AG/(B0 + B1*Z + (B2 + B3*Z)*AG +
     &            (B4 + B5*Z)*(AG*AG))) + 4.5
C
      END SELECT
C
      RETURN
      END
