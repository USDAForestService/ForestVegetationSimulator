      SUBROUTINE HTCALC (SINDX,ISPC,AG,HGUESS,JOSTND,DEBUG)
      IMPLICIT NONE
C----------
C EC $Id$
C----------
C THIS ROUTINE CALCULATES A POTENTIAL HT GIVEN A SPECIES SITE AND AGE
C IT IS USED TO CAL POTHTG AND SITE.
C GO TO A DIFFERENT POTENTIAL HEIGHT CURVE DEPENDING ON THE SPECIES
C----------
      LOGICAL DEBUG
      INTEGER ISPC,JOSTND
      REAL HGUESS,AG,SINDX
      REAL X1,X2,X3,Z
C----------
C  SPECIES LIST FOR EAST CASCADES VARIANT.
C
C   1 = WESTERN WHITE PINE      (WP)    PINUS MONTICOLA
C   2 = WESTERN LARCH           (WL)    LARIX OCCIDENTALIS
C   3 = DOUGLAS-FIR             (DF)    PSEUDOTSUGA MENZIESII
C   4 = PACIFIC SILVER FIR      (SF)    ABIES AMABILIS
C   5 = WESTERN REDCEDAR        (RC)    THUJA PLICATA
C   6 = GRAND FIR               (GF)    ABIES GRANDIS
C   7 = LODGEPOLE PINE          (LP)    PINUS CONTORTA
C   8 = ENGELMANN SPRUCE        (ES)    PICEA ENGELMANNII
C   9 = SUBALPINE FIR           (AF)    ABIES LASIOCARPA
C  10 = PONDEROSA PINE          (PP)    PINUS PONDEROSA
C  11 = WESTERN HEMLOCK         (WH)    TSUGA HETEROPHYLLA
C  12 = MOUNTAIN HEMLOCK        (MH)    TSUGA MERTENSIANA
C  13 = PACIFIC YEW             (PY)    TAXUS BREVIFOLIA
C  14 = WHITEBARK PINE          (WB)    PINUS ALBICAULIS
C  15 = NOBLE FIR               (NF)    ABIES PROCERA
C  16 = WHITE FIR               (WF)    ABIES CONCOLOR
C  17 = SUBALPINE LARCH         (LL)    LARIX LYALLII
C  18 = ALASKA CEDAR            (YC)    CALLITROPSIS NOOTKATENSIS
C  19 = WESTERN JUNIPER         (WJ)    JUNIPERUS OCCIDENTALIS
C  20 = BIGLEAF MAPLE           (BM)    ACER MACROPHYLLUM
C  21 = VINE MAPLE              (VN)    ACER CIRCINATUM
C  22 = RED ALDER               (RA)    ALNUS RUBRA
C  23 = PAPER BIRCH             (PB)    BETULA PAPYRIFERA
C  24 = GIANT CHINQUAPIN        (GC)    CHRYSOLEPIS CHRYSOPHYLLA
C  25 = PACIFIC DOGWOOD         (DG)    CORNUS NUTTALLII
C  26 = QUAKING ASPEN           (AS)    POPULUS TREMULOIDES
C  27 = BLACK COTTONWOOD        (CW)    POPULUS BALSAMIFERA var. TRICHOCARPA
C  28 = OREGON WHITE OAK        (WO)    QUERCUS GARRYANA
C  29 = CHERRY AND PLUM SPECIES (PL)    PRUNUS sp.
C  30 = WILLOW SPECIES          (WI)    SALIX sp.
C  31 = OTHER SOFTWOODS         (OS)
C  32 = OTHER HARDWOODS         (OH)
C
C  SURROGATE EQUATION ASSIGNMENT:
C
C  FROM THE EC VARIANT:
C      USE 6(GF) FOR 16(WF)
C      USE OLD 11(OT) FOR NEW 12(MH) AND 31(OS)
C
C  FROM THE WC VARIANT:
C      USE 19(WH) FOR 11(WH)
C      USE 33(PY) FOR 13(PY)
C      USE 31(WB) FOR 14(WB)
C      USE  7(NF) FOR 15(NF)
C      USE 30(LL) FOR 17(LL)
C      USE  8(YC) FOR 18(YC)
C      USE 29(WJ) FOR 19(WJ)
C      USE 21(BM) FOR 20(BM) AND 21(VN)
C      USE 22(RA) FOR 22(RA)
C      USE 24(PB) FOR 23(PB)
C      USE 25(GC) FOR 24(GC)
C      USE 34(DG) FOR 25(DG)
C      USE 26(AS) FOR 26(AS) AND 32(OH)
C      USE 27(CW) FOR 27(CW)
C      USE 28(WO) FOR 28(WO)
C      USE 36(CH) FOR 29(PL)
C      USE 37(WI) FOR 30(WI)
C----------
C  SEE IF WE NEED TO DO SOME DEBUG.
C----------
      IF(DEBUG) WRITE(JOSTND,10)SINDX,ISPC,AG
   10 FORMAT(' ENTERING HTCALC, SINDX,ISPC,AG= ',F7.2,I5,F7.2)
C
      SELECT CASE (ISPC)
C----------
C WHITE PINE USE BRICKELL, INT-75
C SPECIES: WP
C----------
      CASE(1)
        HGUESS=SINDX/(0.37504453*
     &         (1.0-0.92503*EXP(-0.0207959*AG))**(-2.4881068))
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC WP HGUESS = ',HGUESS
C----------
C WESTERN LARCH USE COCHRAN, PNW 424
C SPECIES: WL, LL
C----------
      CASE(2,17)
        HGUESS=4.5 + 1.46897*AG + 0.0092466*AG*AG - 0.00023957*AG**3  
     &         + 1.1122E-6*AG**4 + (SINDX -4.5)*(-0.12528 + 0.039636*AG
     &         - 0.0004278*AG*AG + 1.7039E-6*AG**3) 
     &         - 73.57*(-0.12528 + 0.039636*AG - 0.0004278*AG*AG 
     &         + 1.7039E-6*AG**3)
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC WL/LL HGUESS = ',HGUESS
C----------
C DOUGLAS FIR USE COCHRAN, PNW 251
C SPECIES: DF
C----------
      CASE(3)
        HGUESS=4.5 + EXP(-0.37496 + 1.36164*ALOG(AG) - 0.00243434*
     &         (ALOG(AG))**4) - 79.97*(-0.2828 + 1.87947*(1.0 - 
     &         EXP(-0.022399*AG))**0.966998) + (SINDX - 4.5)*
     &         (-0.2828 + 1.87947*(1.0 - EXP(-0.022399*AG)**0.966998))
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC DF HGUESS = ',HGUESS
C----------
C SILVER FIR, SURROGATE IS WHITE FIR USE COCHRAN PNW 252
C GRAND FIR ---- USE SILVER FIR
C SPECIES: SF, GF, WF
C----------
      CASE(4,6,16)
        X2= -0.30935 + 1.2383*ALOG(AG) + 0.001762*(ALOG(AG))**4
     &      - 5.4E-6*(ALOG(AG))**9 + 2.046E-7*(ALOG(AG))**11
     &      - 4.04E-13*(ALOG(AG))**18
        X3= -6.2056 + 2.097*ALOG(AG) - 0.09411*(ALOG(AG))**2
     &      - 0.00004382*(ALOG(AG))**7 + 2.007E-11*(ALOG(AG))**16
     &      - 2.054E-17*(ALOG(AG))**24
        HGUESS=EXP(X2) - 84.73*EXP(X3) + (SINDX - 4.5)*EXP(X3)
     &       + 4.5
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC WP X2,X3,HGUESS = ',
     &  X2,X3,HGUESS
C----------
C RED CEDAR ---- USE HEGYI, JELINEK, VISZLAI,
C & CARPENTER 1981 FOR SITE REFERENCE+
C SPECIES: RC
C----------
      CASE(5)
        HGUESS = 1.3283*SINDX * ((1.0 - EXP(-0.0174*AG))**1.4711)
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC RC HGUESS = ',HGUESS
C----------
C LODGEPOLE PINE USE ALEXANDER TACKLE AND DAHMS FOR CCF 100, RM-29
C SPECIES: LP
C----------
      CASE(7)
        HGUESS = 9.89331 - 0.19177*AG + 0.00124*AG*AG
     &         - 0.00082*0.0*SINDX
     &         + 0.01387*AG*SINDX - 0.0000455*AG*AG*SINDX
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC LP HGUESS = ',HGUESS
C----------
C ENGLEMANN SPRUCE USE ALEXANDER, RM-32
C SPECIES: ES
C----------
      CASE(8)
        HGUESS = 4.5+((2.75780*SINDX**0.83312)*
     &         (1.0-EXP(-0.015701*AG))**(22.71944*SINDX**(-0.63557)))
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC ES HGUESS = ',HGUESS
C----------
C SUB ALPINE FIR AND NOBLE FIR USE JOHNSON'S EQUIV OF DEMARS, PNW-119
C SPECIES: AF
C----------
      CASE(9)
        HGUESS=SINDX*(-0.07831 + 0.0149*AG - 4.0818E-5*AG*AG)
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC AF HGUESS = ',HGUESS
C----------
C PONDEROSA PINE USE BARRETT, PNW-232
C SPECIES: PP
C----------
      CASE(10)
        HGUESS =( 128.8952205*(1.0 -EXP(-0.016959*AG))**1.23114)
     &           - ((-0.7864 + 2.49717*
     &           (1.0-EXP(-0.004504*AG))**0.33022)*100.43)
     &           + ((-0.7864 + 2.49717*(1.0-EXP(-0.004504*AG))**0.33022)
     &           *(SINDX-4.5)) + 4.5
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC PP HGUESS = ',HGUESS
C----------
C WESTERN HEMLOCK - WILEY, 1978
C SPECIES: WH
C----------
      CASE(11)
        Z = 2500./(SINDX-4.5)
        HGUESS = (AG*AG/(-1.7307 + 0.1394*Z + (-0.0616 + 0.0137*Z)*AG +
     &           (0.00192 + 0.00007*Z)*(AG*AG))) + 4.5
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC WH Z,HGUESS = ',Z,HGUESS
C----------
C MOUNTAIN HEMLOCK AND OTHER SOFTWOODS. USE MEANS (UNPUBL) METRIC
C SINCE MEANS USES HEIGHT ABOVE B.H., ADD IN 1.37 METERS BEFORE
C CONVERTING TO ENGLISH UNITS.
C SPECIES: MH, OS
C----------
      CASE(12,31)
        HGUESS = (22.8741 + 0.950234*SINDX)*
     &          (1.0 - EXP(-0.00206465*SQRT(SINDX)*AG))
     &          ** (1.365566 + 2.045963/SINDX)
        HGUESS=(HGUESS+1.37)*3.281
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC MH/OS HGUESS = ',HGUESS
C----------
C PACIFIC YEW, WHITEBARK PINE, ALASKA CEDAR, WESTERN JUNIPER
C BIGLEAF MAPLE, VINE MAPLE, PAPER BIRCH, GIANT CHINQUAPIN,
C PACIFIC DOGWOOD, QUAKING ASPEN, BLACK COTTONWOOD,
C CHERRY AND PLUM SPECIES, WILLOW SPECIES, OTHER HARDWOODS
C
C USE CURTIS, FOR. SCI. 20:307-316.  
C CURTIS CURVES ARE PRESENTED IN METRIC (3.2808 ?)
C BECAUSE OF EXCESSIVE HT GROWTH -- APPROX 30-40 FT/CYCLE, TOOK OUT 
C THE METRIC MULTIPLIER DIXON 11-05-92
C SPECIES: PY, WB, YC, WJ, BM, VN, PB, GC, DG, AS, CW, PL, WI, OH
C----------
      CASE(13,14,18:21,23:27,29:30,32)
        HGUESS = (SINDX - 4.5) / (0.6192 - 5.3394/(SINDX - 4.5)
     &           + 240.29*AG**(-1.4) +(3368.9/(SINDX-4.5))*AG**(-1.4))
        HGUESS = HGUESS + 4.5
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC PY/OTHERS HGUESS = ',HGUESS
C----------
C NOBLE FIR - HERMAN, PNW-243
C SPECIES: NF
C----------
      CASE(15)
        X1 = -564.38 + 22.25*(SINDX - 4.5) - 0.04995*(SINDX - 4.5)**2
        X2 = 6.80 + 2843.21*(SINDX - 4.5)**(-1) 
     &       + 34735.54*(SINDX - 4.5)**(-2)
        HGUESS = 4.5 + (SINDX - 4.5)/(X1*(1.0/AG)**2 +
     &   X2*(1.0/AG) + 1.0 - 0.0001*X1 - 0.01*X2)
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC NF X1,X2,HGUESS= ',X1,X2,HGUESS
C----------
C RED ALDER - HARRINGTON, PNW-358
C SPECIES: RA
C----------
      CASE(22)
        HGUESS = SINDX
     &           + (59.5864 + 0.7953*SINDX)*(1.0-EXP((0.00194 
     &           - 0.00074*SINDX)*AG))**0.9198
     &           - (59.5864 + 0.7953*SINDX)*(1.0-EXP((0.00194
     &           - 0.00074*SINDX)*20.0))**0.9198
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC RA HGUESS = ',HGUESS
C----------
C OREGON WHITE OAK - KING, DF, WEYERHAEUSER FORESTRY PAPER NO. 8
C SPECIES: WO
C----------
      CASE(28)
        Z = 2500./(SINDX-4.5)
        HGUESS = (AG*AG/(-0.954038 + 0.109757*Z + (5.58178E-2 + 
     &           7.92236E-3*Z)*AG + (-7.33819E-4 + 1.97693E-4*Z)*
     &           (AG*AG))) + 4.5
        IF(DEBUG)WRITE(JOSTND,*)' HTCALC WO HGUESS = ',HGUESS
C----------
C FUTURE SPECIES
C----------
      CASE DEFAULT
        HGUESS = 0.
C
      END SELECT
C
      RETURN
      END
