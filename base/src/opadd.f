      SUBROUTINE OPADD (IDT,IACTK,ISTUS,NPRMS,PRMS,KODE)
      IMPLICIT NONE
C----------
C  **OPADD  DATE OF LAST REVISION:  07/23/08
C----------
C
C     OPTION PROCESSING ROUTINE - NL CROOKSTON - JUNE 1981 - MOSCOW
C     FROM 1991 TO 2003 THERE WERE NO CHANGES TO THIS ROUTINE!
C     IN MAY 2003 CROOKSTON ADDED PROVISIONS FOR CHARACTER ARGUMENTS
C
C     OPADD IS USED TO DYNAMICALLY ADD AN ACTIVITY TO THE ACTIVITY
C     SCHEDULE.  WHEN ISTUS=0, YOU MUST CALL OPINCR (SEE BELOW)
C     AFTER ONE OR MORE CALLS TO OPADD.  THE ARGUMENTS:
C
C     KODE  = 0, ALL WENT WELL
C             1, ACTIVITY COULD NOT BE ADDED BECAUSE OPTION ARRAYS
C                ARE FULL.  A WARNING MSG IS ISSUED VIA ERRGRO.
C             2, IDT IS .LE. 0, YOU MUST SPECIFY A DATE (IDT MUST
C                BE OVER 0).
C     IDT   = THE DATE.
C     ISTUS = THE ACTIVITY STATUS CODE, 0=NEEDS TO BE DONE,
C             >0 ACCOMPLISHED ON DATE ISTUS, <0, SIGNAL DELETED.
C     IACTK = THE ACTIVITY CODE
C     NPRMS = THE NUMBER OF PARMETERS
C     PRMS  = THE PARMETER ARRAY.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'OPCOM.F77'
C
C
COMMONS
C
C     ISPACT= AN ARRAY OF ACTIVITIY CODES WHICH CORRESPOND TO
C             ACTIVITIES WHICH THE DATE AN EVENT OCCURES IS PLACED IN
C             POSITION 1 OF THE PARAMETER LIST.
C     NSPACT= THE LENGHT OF ISPACT.
C
      INTEGER KODE,NPRMS,ISTUS,IACTK,IDT,NSPACT,ID,NREDTS,IYR1,IYR2
      INTEGER I2,I,NCOPYS,II,NSCHDS,IP1,IP2,N,IA,I1,J,NCYC,ICYC
      PARAMETER (NSPACT=4)
      REAL PRMS(1)
      INTEGER IY(1),ISPACT(NSPACT)
      DATA ISPACT/33,427,428,429/
C
C     CHECK THE RANGE OF IDT
C
      IF (IDT .GT. 0) GOTO 10
      KODE=2
      RETURN
   10 CONTINUE
C
C     DEFINE 'ID' AS THE DATE CODE.
C
      ID=IDT
      ISEQDN=ISEQDN+1
      ISEQ(IMGL)=ISEQDN
      CALL OPNEW (KODE,ID,IACTK,NPRMS,PRMS)
C
C     SET THE OPTION STATUS TO ISTUS (IF LOPEVN IS TRUE, LEAVE THE
C     STATUS CODE UNCHANGED).
C
      IF (.NOT.LOPEVN) IACT(IMGL-1,4)=ISTUS
      RETURN
C
C
      ENTRY OPREDT (IACTK,IYR1,IYR2,NREDTS)
C
C     OPREDT IS USED TO REDATE AND RESCHEDULE AN ACTIVITY TO OCCUR
C     A DIFFERENT DATE.  ACTIVITIES ALREADY ACCOMPLISHED OR DELETED
C     WILL NOT BE REDATED NOR RESCHEDULED.  YOU MUST CALL OPINCR
C     AFTER ONE OR MORE CALLS TO OPREDT (SEE OPINCR, BELOW).
C
C     IACTK = THE ACTIVITY CODE.
C     IYR1  = THE YEAR THE ACTIVITY IS SCHEDULED.
C     IYR2  = THE NEW YEAR YOU WISH THE ACTIVITY TO BE SCHEDULED.
C     NREDTS= THE NUMBER OF REDATES WHICH OCCURED.
C
      NREDTS=0
      I2=IMGL-1
      DO 20 I=1,I2
      ID=IDATE(I)
      IF (ID.NE.IYR1 .OR. IACTK.NE.IACT(I,1) .OR. IACT(I,4).NE.0)
     >   GOTO 20
      NREDTS=NREDTS+1
      IDATE(I)=IYR2
   20 CONTINUE
      RETURN
C
C
      ENTRY OPCOPY (IACTK,IYR1,IYR2,NCOPYS,KODE)
C
C     OPCOPY IS USED TO COPY AN ACTIVITY (WHEATHER OR NOT IT HAS BEEN
C     ACCOMPLISHED, BUT NOT IF IT HAS BEEN DELETED) FROM ONE DATE TO
C     ANOTHER.  YOU MUST CALL OPINCR AFTER ONE OR MORE CALLS TO
C     OPREDT (SEE OPINCR, BELOW).  THE ARGUMENTS:
C
C     IACTK = THE ACTIVITY CODE.
C     IYR1  = THE YEAR THE ACTIVITY IS SCHEDULED.
C     IYR2  = THE NEW YEAR THE ACTIVITY IS TO BE COPIED TO.
C     NCOPYS= THE NUMBER OF ACTIVITIES COPIED.
C     KODE  = 0 IMPLIES NO ERRORS.
C             1 IMPLIES ONE OR MORE OF THE OCCURANCES OF IACTK IN
C               YEAR IYR1 WERE NOT COPIED BECAUSE THERE WAS NOT
C               ENOUGH ROOM IN THE ACTIVITY ARRAYS.  THE NUMBER
C               COPIED (NCOPYS) IS CORRECTLY REPORTED.  A WARNING
C               MSG IS ISSUED VIA ERRGRO.
C
      KODE=0
      NCOPYS=0
      I2=IMGL-1
      DO 40 II=1,I2
      I=IOPSRT(II)
      IF (IACTK.NE.IACT(I,1)) GOTO 40
      ID=IDATE(I)
      IF (ID.NE.IYR1.OR.IACT(I,4).LT.0) GOTO 40
      IF (IMGL .LE. IEPT) GOTO 30
      KODE=1
      CALL ERRGRO (.TRUE.,10)
      RETURN
   30 CONTINUE
      NCOPYS=NCOPYS+1
      IOPSRT(IMGL)=IMGL
      IDATE(IMGL)=IYR2
      ISEQ (IMGL)=IMGL
      IACT(IMGL,1)=IACT(I,1)
      IACT(IMGL,2)=IACT(I,2)
      IACT(IMGL,3)=IACT(I,3)
      IACT(IMGL,4)=0
      IACT(IMGL,5)=IACT(I,5)
      IMGL=IMGL+1
   40 CONTINUE
      RETURN
C
C
      ENTRY OPSCHD (IDT,IP1,IP2,NSCHDS,KODE)
C
C     OPSCHD IS USED BY THE EVENT MONITORING SYSTEM TO ACTUALLY
C     SCHEDULE ACTIVITIES WHICH HAVE BEEN REQUESTED TO OCCUR
C     FOLLOWING A GIVEN EVENT.  YOU MUST CALL OPINCR AFTER ONE OR
C     MORE CALLS TO OPSCHD (SEE OPINCR, BELOW).  THE ARGUMENTS:
C
C     IDT   = THE DATE THE EVENT OCCURED.
C     IP1   = POINTS TO THE FIRST ACTIVITY TO BE SCHEDULED.
C     IP2   = POINTS TO THE LAST ACTIVITY TO BE SCHEDULED.
C     NSCHDS= THE NUMBER OF ACTIVITIES ACTUALLY SCHEDULED.
C     KODE  = RETURN CODE, WHERE:
C             0 IMPLIES NO ERRORS
C             1 IMPLIES THAT IP1<IP2, OR IP1 OR IP2 = 0; THEREFORE
C               NO ACTIVITIES ARE SCHEDULED.
C             2 IMPLIES THAT NONE OR SOME OF THE ACTIVITIES WERE
C               SCHEDULED BECAUSE THERE WAS NOT ENOUGH ROOM IN THE
C               ACTIVITY ARRAYS.  AN ERROR MSG. IS ISSUED VIA ERRGRO.
C
C     CHECK RANGE OF IP1 & IP2 AND SPACE IN ACTIVITY ARRAYS.
C
      NSCHDS=0
      KODE=0
      N=IP1-IP2+1
      IF (IP1*IP2.NE.0 .AND. N.GT.0) GOTO 50
      KODE=1
      RETURN
   50 CONTINUE
      IF (N+IMGL.LE.IEPT+1) GOTO 60
      KODE=2
      CALL ERRGRO (.TRUE.,10)
      RETURN
   60 CONTINUE
C
C     MOVE THE ACTIVITIES
C
      DO 120 I=1,N
      II=IP1-I+1
C
C     SKIP DELETED ACTIVITIES.
C
      IF (IACT(II,4).LT.0) GOTO 120
      IOPSRT(IMGL)=IMGL
      IDATE(IMGL)=(IDT+IDATE(II))
      ISEQDN=ISEQDN+1
      ISEQ(IMGL)=ISEQDN
      IACT(IMGL,1)=IACT(II,1)
      IACT(IMGL,2)=IACT(II,2)
      IACT(IMGL,3)=IACT(II,3)
      IACT(IMGL,4)=0
      IACT(IMGL,5)=IACT(II,5)
C
C     FIND OUT IF THE SCHEDULED ACTIVITY IS ONE OF THE ONES WHICH
C     REQUIRES THE DATE THE EVENT OCCURED BE ENTERED IN THE PARMS.
C
      IA=IACT(IMGL,1)
      DO 70 II=1,NSPACT
      IF (IA.EQ.ISPACT(II)) GOTO 80
   70 CONTINUE
      GOTO 110
   80 CONTINUE
C
C     ADD THE DATE THE EVENT OCCURED TO THE PARMAETER LIST.
C     CASE 1: THE PARMETER LIST CONTAINS AT LEAST 1 ENTRY; COPY THE
C     PARAMETER LIST AND SET THE FIRST PARAMETER TO THE DATE.
C
      I1=IACT(IMGL,2)
      IF (I1.GT.0) THEN
         I2=IACT(IMGL,3)
         IF (IMPL+I2-I1.GT.ITOPRM) THEN
            KODE=2
            CALL ERRGRO (.TRUE.,10)
            RETURN
         ENDIF
         IACT(IMGL,2)=IMPL
         DO J=I1,I2
            PARMS(IMPL)=PARMS(J)
            IMPL=IMPL+1
         ENDDO
         IACT(IMGL,3)=IMPL-1
         PARMS(IACT(IMGL,2))=FLOAT(IDT)
      ELSE
C
C     CASE 2: THE PARAMETER LIST IS EMPTY, MAKE AN ENTRY AND STORE
C     THE VALUE.
C
         IF (IMPL.GT.ITOPRM) THEN
            KODE=2
            CALL ERRGRO (.TRUE.,10)
            RETURN
         ENDIF
         PARMS(IMPL)=FLOAT(IDT)
         IACT(IMGL,2)=IMPL
         IACT(IMGL,3)=IMPL
         IMPL=IMPL+1
      ENDIF
  110 CONTINUE
      IMGL=IMGL+1
      NSCHDS=NSCHDS+1
  120 CONTINUE
      RETURN
C
C
      ENTRY OPINCR (IY,ICYC,NCYC)
C
C     OPINCR IS USED TO INCORPORATE THE RESULTS OF PREVIOUS CALLS
C     TO OPADD, OPREDT, OPCOPY, OR OPSCHD INTO THE ACTIVITY
C     SCHEDULE.  THE REASON YOU MUST CALL OPINCR IS THAT CALLING
C     OPADD, OPREDT, OPCOPY, OR OPSCHD DOES NOT COMPLETE THE JOB
C     REQUIRED TO RESET THE POINTERS USED TO ACCESS THE ACTIVITIES
C     VIA CALLS TO MOST OF THE OTHER ROUTINES.  SUBROUTINE OPINCR
C     IS SEPERATE FROM THE ROUTINES IT FOLLOWS BECAUSE IT SAVES
C     CPU TIME TO EXECUTE SEVERAL ADDS, COPIES, ETC PRIOR TO
C     REALIGNING THE POINTER ARRAYS.  NOTE THAT OPINCR IS NOT
C     NEEDED FOLLOWING CALLS TO OPDEL1, OPDEL2, OR OPDEL3 NOR
C     FOLLOWING OPADD WHERE ISTUS > 0.
C     THE AGRUMENTS:
C     IY   =THE PROGNOSIS MODEL'S CYCLE DATE ARRAY.
C     ICYC =THE CURRENT CYCLE NUMBER.
C     NCYC =THE NUMBER OF CYCLES.
C
      CALL OPSORT (IMGL-1,IDATE,ISEQ,IOPSRT,.FALSE.)
      CALL OPCYCL (NCYC,IY(1))
      CALL OPCSET (ICYC)
      RETURN
      END
