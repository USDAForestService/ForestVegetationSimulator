      SUBROUTINE DENSE
      IMPLICIT NONE
C----------
C  $Id$
C----------
C   THIS SUBROUTINE CALCULATES THE AVERAGE HEIGHT OF THE STAND,
C   THE CROWN COMPETITION FACTOR BACKDATED TO THE START OF THE
C   PERIOD OF OBSERVED GROWTH, THE RELATIVE DENSITY BY SPECIES,
C   MAINTAINS AN INGROWTH ACCUMULATOR, MAKES THE CORRECTION
C   TO RELATIVE DENSITY FOR NON-STOCKABLE POINTS, AND LOADS PCT
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'CALCOM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'HTCAL.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
COMMONS
C
C----------
      INTEGER I,IS,IP,ISPC,I1,I2,I3,II
      REAL SSUMN,TEMP1,TEMP2,RAT,TOTAL,CCFT,CW,BATREE,DP,P,BAT,RELDT
      REAL TSUMD2,R,BRATIO,D,G,SN,BAGR
      REAL SUMDR0
      LOGICAL LREDO
      LOGICAL DEBUG
C-----------
C  SEE IF WE NEED TO DO SOME DEBUG.
C-----------
      CALL DBCHK (DEBUG,'DENSE',5,ICYC)
C----------
C  WK3 IS LOADED WITH DBH TO FACILITATE BACKDATING DURING
C  CALIBRATION.  WHEN CALIBRATING, BA AND RELDEN ARE DENSITY
C  ESTIMATES FOR THE START OF THE DIAMETER INCREMENT MEASUREMENT
C  PERIOD AND RELDM1 AND OLDBA ARE DENSITY ESTIMATES FOR THE START
C  OF THE HEIGHT INCREMENT MEASUREMENT PERIOD.
C----------
      LREDO=.FALSE.
      IF (ITRN.LE.0) GOTO 6
      DO 1 I=1,IREC1
      WK3(I)=DBH(I)
    1 CONTINUE
C----------
C  IF DIAMETERS NEED TO BE BACK DATED (LBKDEN=TRUE) PROCEED WITH
C  BACKDATING:  CALCULATE THE AVERAGE RATIO
C  OF DIAMETER SQUARED AT START OF GROWTH PERIOD TO DIAMETER
C  SQUARED AT THE END OF THE GROWTH PERIOD (BASAL AREA GROWTH
C  RATIO), FOR ALL GROWTH SAMPLE TREES.
C----------
      IF(DEBUG) WRITE(JOSTND,9006) IDG,LBKDEN
 9006 FORMAT(' IN DENSE;  IDG = ',I2,';  LBKDEN = ',L1)
      IF (.NOT.LBKDEN) GO TO 6
      LREDO=.TRUE.
C----------
C  NOW LOAD WK3 WITH DBH FOR THOSE TREES THAT DIED WITHIN
C  THE LAST FIVE YEARS (TREE HISTORY 6,7 = IMC() OF 7)
C  OLDER DEAD (TREE HISTORY 8,9 = IMC() OF 9) LOAD DBH OF 0.
C----------
      IF(IREC2.GT.MAXTRE) GO TO 12
      DO 11 I=IREC2,MAXTRE
      WK3(I)=DBH(I)
      IF(IMC(I) .EQ. 9) WK3(I)=0.
   11 CONTINUE
   12 CONTINUE
      BAGR=0.0
      SN=0.0
      DO 3 I=1,IREC1
      G=DG(I)
      D=DBH(I)
C----------
C  DG IS SET TO -1.0 IN **CRATET** WHEN THE INPUT VALUE IS ZERO.
C  THESE OBSERVATIONS ARE NOT INCLUDED IN THE CALCULATION OF THE
C  BASAL AREA GROWTH RATIO.  WHEN GROWTH IS GREATER THAN THE CURRENT 
C  DIAMETER, THE RECORD IS ALSO EXCLUDED FROM COMPUTING BAGR.
C----------
      IF(G.LT.0.0) GO TO 3
      IF(IDG.EQ.1) GO TO 2
      IS=ISP(I)
      G=G/BRATIO(IS,D,HT(I))
    2 CONTINUE
      IF(G.GT.D) GO TO 3
      BAGR=BAGR+(1.0-(2.0*D*G-G*G)/(D*D))
      SN=SN+1.0
    3 CONTINUE
C----------
C  IF THERE ARE NO GROWTH SAMPLE TREES, BACKDATING CANNOT BE DONE.
C----------
      IF(SN.LE.0.0) GO TO 6
C----------
C  COMPUTE AVERAGE BASAL AREA GROWTH RATIO, AND BACKDATE DIAMETERS,
C  LOADING THEM INTO WK3.
C----------
      BAGR=BAGR/SN
      DO 5 I=1,IREC1
      IS=ISP(I)
      G=DG(I)
      D=DBH(I)
      IF(IDG.EQ.1) GO TO 4
      G=G/BRATIO(IS,DBH(I),HT(I))
      IF(G.GT.D)G=D
    4 CONTINUE
      R=1.0-(2.0*D*G-G*G)/(D*D)
      IF(G.LT.0.0.OR.R.LE.0.0) R=BAGR
      WK3(I)=SQRT(D*D*R)
      IF(DEBUG) WRITE(JOSTND,9005) I,IS,G,R,D,WK3(I)
 9005 FORMAT(' IN DENSE: I = ',I4,';  IS = ',I2,';  G = ',
     &  F6.2,';  R = ',F8.5,';  D = ',F6.2,';  WK3 = ',F6.2)
    5 CONTINUE
    6 CONTINUE
C----------
C  INITIALIZE.  IF BACKDATING DENSITY AND THIS IS THE SECOND PASS,
C  DO NOT WIPE OUT THE POINT DENSITY STATS.
C----------
      IF(LBKDEN.AND..NOT.LREDO) GO TO 15
      DO 14 IP=1,MAXPLT
      BAAA(IP)=0.0
      PCCF(IP)=0.0
      PTPA(IP)=0.0
      DO 13 IS=1,MAXSP
      OVER(IS,IP)=0.0
   13 CONTINUE
   14 CONTINUE
   15 CONTINUE
      DO 16 IS=1,MAXSP
      RELDSP(IS)=0.0
   16 CONTINUE
      TSUMD2=0.0
      RELDT=0.
      TPROB=0.
      BAT= 0.
C----------
C   CALL MBACAL TO IDENTIFY THE SITE SPECIES IE SPECIES WITH MOST
C   BASAL AREA.
C----------
      CALL MBACAL
C----------
C   BEGIN SPECIES LOOP.   ACCUMULATE STAND SUMS.  LOAD WK5.
C----------
      SUMDR0=0.
      DR016=0.
      DO  50  ISPC= 1, MAXSP
      I2 = ISCT(ISPC,2)
      IF( I2 .LE. 0 ) GO TO  50
      I1 = ISCT(ISPC,1)
C----------
C  CCF FOR TREES GREATER THAN 10.0 IN. DBH IS CALCULATED WITH A
C  QUADRATIC CROWN AREA - DBH RELATIONSHIP.  FOR SMALLER TREES,
C  AN EXPONENTIAL CROWN AREA - DBH RELATIONSHIP IS USED.  IN
C  BOTH CASES, THERE IS A LINEAR RELATIONSHIP ON THE LOG - LOG
C  SCALE.
C----------
      DO 10 I3=I1,I2
      I=IND1(I3)
      P=PROB(I)
      TPROB=TPROB+P
      D=DBH(I)
      IF(LBKDEN.AND.LREDO)  D =WK3(I)
      IF((D.GE.DBHSDI).AND.LZEIDE)SUMDR0=SUMDR0+P*(D)**1.605    ! ADD ZEIDE SUMS IF DIA. >=DBHSDI
      DP=D*P
      WK5(I)=D*DP
      TSUMD2=TSUMD2+WK5(I)
      IP=ITRE(I)
      BATREE=0.005454154*WK5(I)
      BAT=BAT+BATREE
C----------
C    CALL CCFCAL TO CALCULATE CROWN COMPETITION
C----------
      CALL CCFCAL(ISPC,D,HT(I),ICR(I),P,.FALSE.,CCFT,CW,1)
C
      IF (DEBUG) WRITE(JOSTND,501) CCFT,P
 501  FORMAT(' DENSE: CCFT PROB =',2F10.2)
      RELDSP(ISPC)=RELDSP(ISPC)+CCFT
C----------
C  IF BACKDATING DENSITY AND THIS IS THE SECOND PASS, DO NOT
C  RECOMPUTE THE POINT DENSITY STATISTICS.
C----------
      IF (DEBUG) WRITE (JOSTND,500) LBKDEN,LREDO,D,REGNBK
  500 FORMAT (' IN DENSE: LBKDEN,LREDO,D,REGNBK = ',2L2,2E15.7)
      IF(LBKDEN.AND..NOT.LREDO) GO TO 10
      PCCF(IP)=PCCF(IP)+CCFT*PI/GROSPC
      PTPA(IP)=PTPA(IP)+P*PI/GROSPC
C----------
C  REGNBK IS THE MINIMUM DBH OF AN OVERSTORY TREE.
C----------
      IF(D.LT.REGNBK) GO TO 10
      BAAA(IP)=BAAA(IP)+BATREE*PI/GROSPC
      OVER(ISPC,IP)=OVER(ISPC,IP)+BATREE*PI/GROSPC
   10 CONTINUE
C----------
C   CALCULATE RELATIVE DENSITY FOR THE STAND.
C----------
      RELDT=RELDT + RELDSP(ISPC)
C----------
C   END OF CROWN COMPETITION FACTOR LOOP.  PRINT DEBUG INFO IF
C      DESIRED.
C----------
      IF (.NOT.DEBUG) GO TO 50
      WRITE (JOSTND,9000) ISPC,RELDSP(ISPC)
 9000 FORMAT(' CCF FOR SPECIES',I3,'=',F10.3)
   50 CONTINUE
      IF(.NOT.LREDO) GO TO 53
      LREDO=.FALSE.
      IF (IREC2.GT.MAXTRE) GOTO 49
      DO 52 I=IREC2,MAXTRE
      WK3(I)=0.01
   52 CONTINUE
   49 CONTINUE
      RELDM1=RELDT
      OLDBA=BAT
C----------
C  WHEN BACKDATING, PCT DISTRIBUTION MUST BE COMPUTED HERE.
C----------
      CALL PCTILE(ITRN,IND,WK5,PCT,TOTAL)
      GO TO 6
   53 CONTINUE
      RELDEN=RELDT
      RMSQD=0.
      IF (ITRN.GT.0.AND.TPROB.GT.0.0)THEN
        RMSQD = SQRT (TSUMD2 / TPROB)
        IF(LZEIDE)DR016=(SUMDR0/TPROB)**(1./1.605)
      ENDIF
      BA = BAT
C----------
C  IF PREPARING DENSITY ESTIMATES FOR CALIBRATION, FINISH THE JOB.
C----------
      IF(.NOT.LBKDEN) GO TO 54
      RAT=FINTH/FINT
      TEMP1=(RELDEN-RELDM1)*RAT+RELDM1
      TEMP2=(BA-OLDBA)*RAT+OLDBA
      RELDEN=RELDM1
      RELDM1=TEMP1
      BA=OLDBA
      OLDBA=TEMP2
      IF(DEBUG) WRITE(JOSTND,9003) BA,OLDBA,RELDEN,RELDM1
 9003 FORMAT(' IN DENSE:  BA = ',F8.2,', OLDBA = ',F8.2,
     &   ', RELDEN = ',F8.2,', RELDM1 = ',F8.2/)
   54 CONTINUE
C----------
C   IF NOT BACKDATING, COMPUTE BASAL AREA PCT DISTRIBUTION.
C----------
      IF(LBKDEN) GO TO 55
      CALL PCTILE(ITRN,IND,WK5,PCT,TOTAL)
      IF(LSTART) RELDM1=RELDEN
   55 CONTINUE
C----------
C   COMPUTE THE POINT BASAL AREA IN LARGER TREES...PTBAL USES WK5
C   AS IT IS COMPUTED ABOVE: D*D*P, AND WK6 FOR A SCRATCH VECTOR.
C----------
      CALL PTBAL
C----------
C     CALCULATE TOP HEIGHT: AVERAGE HEIGHT OF THE 40 TREES PER ACRE
C     WITH THE LARGEST DBH'S.
C----------
      AVH=0.
      IF (ITRN.LE.0) GOTO 70
      SSUMN=0.
      DO 60 I=1,ITRN
      II=IND(I)
      P=PROB(II)
      IF(SSUMN+P.GT.40.0) P=40.0-SSUMN
      SSUMN=SSUMN+P
      AVH=AVH+HT(II)*P
      IF(SSUMN.GE.40.0) GO TO 65
   60 CONTINUE
   65 CONTINUE
      IF (SSUMN .GT. 0.) AVH = AVH/SSUMN
   70 CONTINUE
      IF(DEBUG) WRITE(JOSTND,9002) GROSPC,TPROB,TSUMD2,RELDEN,BA,
     &          RMSQD,AVH,DR016
 9002 FORMAT(' IN DENSE, GROSPC = ',F10.4,', TPROB = ',F10.4,
     &      ', TSUMD2 = ',F10.2,', RELDEN = ',F10.4,', BA = ',
     &      F10.4/'     RMSQD = ',F10.4,', AVH = ',F10.4,
     &      ' DR016= ',F10.4)
      RETURN
      END
