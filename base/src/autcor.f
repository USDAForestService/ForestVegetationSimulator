      SUBROUTINE AUTCOR (COV,VRNEXT)
      IMPLICIT NONE
C----------
C  $Id$
C----------
C  PROCEDURE FOR CALCULATING EFFECT OF CYCLE LENGTH ON RANDOM COMPONENT OF
C  DIAMETER INCREMENT AND ITS AUTO-REGRESSIVE PROPERTIES.
C  PROCEDURE ASSUMES ANNUAL TREE RINGS ARE CHARACTERIZED BY ARMA(1,1)
C  TIME SERIES MODEL.  PERIODIC INCREMENT IS CONSIDERED TO BE DISTRIBUTED
C  AS THE SUM OF IFINT RANDOM VALUES WITH VARIANCE-COVARIANCE MATRIX
C  DEFINED BY THE SERIAL CORRELATION FUNCTION OF THE ARMA(1,1) MODEL.
C----------
COMMON
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
COMMON
C
      INTEGER I,NEW,NOLD,NLIM,L,NBIG,NSML
      REAL DT,T,COVAR,VAR,COV,VRNEXT
C----------
      IF ( .NOT.LSTART ) GO TO 100
C----------
C  INITIALIZE SERIAL CORRELATION FUNCTION FROM ARMA(1,1) PARAMETERS.
C----------
      BJRHO(1) = ( 1.-BJPHI*BJTHET)*(BJPHI-BJTHET)/
     &       (1.+BJTHET*(BJTHET-2.*BJPHI))
      DO 50 I = 2, 40
        BJRHO(I) = BJRHO(I-1)*BJPHI
   50 CONTINUE
C----------
C  PRECOMPUTE VARIANCE AND COVARIANCE MULTIPLIERS FOR "YR" YEARS.
C----------
      NEW = YR
      NOLD = YR
      GO TO 105
  100 CONTINUE
      NEW = IFINT
      NOLD = OLDFNT
C----------
C  CALCULATE VARIANCE MULTIPLIER FOR CURRENT CYCLE.
C----------
  105 CONTINUE
      NLIM = NEW - 1
      VAR = 0.0
      DO 110 I = 1,NLIM
        VAR = VAR + BJRHO(I)*( NEW - I )
  110 CONTINUE
      VAR = NEW + 2.*VAR
      VRNEXT = VAR
C----------
C  CALCULATE MULTIPLIER FOR COVARIANCE BETWEEN CURRENT AND PRECEDING
C  CYCLES.
C----------
      COVAR = 0.0
      L = NEW + NOLD -1
      IF ( L .GT. 40 )  L = 40
C----------
C  PREPARE TO LOOP OVER LARGER DIMENSION OF COVARIANCE MATRIX.
C----------
      NBIG = NEW
      NSML = NOLD
      IF ( NEW .GT. NOLD ) GO  TO 120
      NBIG = NOLD
      NSML = NEW
  120 CONTINUE
      T = 0.
      DT = 1.
      DO 130 I = 1,L
      T = T + DT
      COVAR = COVAR + BJRHO(I)*T
      IF ( I .EQ. NSML ) DT=0.0
      IF ( I .EQ. NBIG ) DT = -1.
  130 CONTINUE
  135 CONTINUE
      COV = COVAR
      RETURN
      END
