      SUBROUTINE LB1MEM (IP1,IP2,SETIN,LENONE,ONEMEM)
      IMPLICIT NONE
C----------
C  **LB1MEM DATE OF LAST REVISION:  07/23/08
C----------
C
C     GET THE FIRST MEMBER OF SETIN FOUND BETWEEN LOCATION IP1
C     AND IP2 AND RETURN IT AS STRING ONEMEM WITH LENGTH LENONE.
C
C     PART OF THE LABEL PROCESSING COMPONENT OF THE PROGNOSIS SYSTEM
C     N.L. CROOKSTON -- INTERMOUNTAIN RESEARCH STATION -- JAN 1987
C
C     IP1   = THE STARTING POSITION FOR THE SET MEMBER
C     IP2   = THE LAST POSITION THAT WILL BE CHECKED FOR SET MEMBERSHIP
C     SETIN = THE STRING HOLDING THE FIRST SET
C     LENONE= THE LENGTH OF THE MEMBER, ZERO IF NO MEMBER EXISTS.
C     ONEMEM= THE STRING HOLDING THE FIRST MEMBER
C
      INTEGER LENONE,IP1,IP2,LCOMMA,IEND
      CHARACTER*250 SETIN,ONEMEM
C
C     INITIALIZE THE LENGTH OF THE MEMBER TO ZERO.
C
      LENONE=0
C
C     IF THE NUMBER OF CHARACTERS BETWEEN IP1 AND IP2 IS LESS THAN
C     OR EQUAL TO ZERO, NO MEMBER EXISTS.
C
      IF (IP1.LE.0 .OR. IP2.LE.0 .OR. IP2-IP1.LT.0) GOTO 100
C
C     FIND THE LOCATION OF THE FIRST COMMA BETWEEN IP1 AND IP2
C
      LCOMMA=INDEX(SETIN(IP1:IP2), ',')
C
C     IF THERE IS NO COMMA, THEN THE MEMBER IS THE ENTIRE STRING.
C     IF THERE IS A COMMA, THEN THE MEMBER IS THE STRING BETWEEN IP1
C     AND THE LOCATION OF THE COMMA MINUS 1 (ASSUMING THAT THAT STRING
C     WILL ALSO BE GREATER THAN ZERO).
C
      IF (LCOMMA.EQ.0) THEN
         LENONE=IP2-IP1+1
         ONEMEM=SETIN(IP1:IP2)
      ELSE
         LENONE=LCOMMA-1
         IEND=LENONE+IP1-1
         IF (LENONE.GT.0) THEN
            ONEMEM=SETIN(IP1:IEND)
         ELSE
            LENONE=0
            ONEMEM=' '
         ENDIF
      ENDIF
  100 CONTINUE
      RETURN
      END
