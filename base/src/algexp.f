      SUBROUTINE ALGEXP (CEXPRS,LENCEX,MXEXPR,CLEFT,LCLFT,RECORD,
     >                   IRECNT,IREAD,JOSTND,IRC)
      IMPLICIT NONE
C----------
C  $Id$
C----------
C
C     CALLED FROM EVUSRV, HVIN, AND PPUSRV (AND PERHAPS OTHER PLACES).
C     READS USER DEFINED VARIABLES.
C
C     EVENT MONITOR AND PPE ROUTINE - N.L. CROOKSTON  - APRIL 1987
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
C     CEXPRS= THE EXPRESSION CHARACTER ARRAY.
C     LENCEX= THE LENGTH OF THE EXPRESSSION.
C     MXEXPR= THE MAX LENGTH OF CEXPRS.
C     CLEFT = THE PORTION OF THE EXPRESSION ON THE LEFT HAND SIDE OF
C             THE EQUAL SIGN. MAX SIZE IS 20 CHARS...RETURNED SIZE IS
C             UPTO 8 NONBLANK CHARS ALL CHARS.
C     LCLFT = NONBLANK LENGTH OF CLEFT.
C     RECORD= CHARACTER STRING
C     IREAD = READER DATA SET REFERENCE NUMBER.
C     JOSTND= OUTPUT DATA SET REFERENCE NUMBER.
C     IRC   = RETURN CODES: 0=OK,1='END  ' WAS FOUND.
C
      INTEGER IRC,JOSTND,IREAD,IRECNT,LCLFT,LENCEX,MXEXPR,ISTLNB
      INTEGER IAMP,IEQU,I,J,I1,I2,IRTNCD
      CHARACTER CEXPRS(MXEXPR)
      CHARACTER CLEFT*20,RECORD*(*),END*5
      INTEGER RECLEN
      RECLEN=LEN(RECORD)
C
C     READ THE EXPRESSION AND PLACE IT INTO THE CEXPRS ARRAY.
C
    1 CONTINUE
      CLEFT=' '
      LENCEX=0
      LCLFT =0
    5 CONTINUE
      READ (IREAD,'(A)',END=200) RECORD
      IRECNT=IRECNT+1
C
C     IF THIS RECORD DOES NOT FOLLOW AN &, THEN IT CAN BE A
C     COMMENT IF THE FIRST CHARACTER IS * or !
C
      IF (LENCEX.EQ.0 .AND.(RECORD(1:1).EQ.'*' .OR.
     >                      RECORD(1:1).EQ.'!')) THEN
         WRITE (JOSTND,'(T12,A)') RECORD(1:ISTLNB(RECORD))
         GOTO 5
      ENDIF
C
C     CONVERT THE TO UPPER CASE...AND LOOK FOR AN AMPERSAND AND AN
C     EQUAL SIGN.
C
      IAMP=0
      IEQU=0
      DO 10 I=1,RECLEN
      IF (RECORD(I:I).EQ.'&') THEN
         IAMP=I
         GOTO 15
      ENDIF
      IF (RECORD(I:I).EQ.'=') IEQU=I
   10 CONTINUE
   15 CONTINUE
C
C     IF THE END OPTION HAS BEEN REACHED...RETURN.
C
      END=RECORD(1:5)
      DO I=1,5
         CALL UPCASE(END(I:I))
      ENDDO
      IF (END.EQ.'END  ') THEN
         WRITE (JOSTND,'(/''END'')')
         GOTO 100
      ENDIF
      WRITE (JOSTND,'(T12,A)') RECORD(1:ISTLNB(RECORD))
C
C     IF AN EQUAL SIGN WAS FOUND, OR IF CLEFT IS BLANK AND AN
C     AMPERSAND WAS FOUND, THEN LOAD CLEFT.
C
      IF (IEQU.GT.0 .OR. (LCLFT.EQ.0.AND.IAMP.GT.0)) THEN
         J=IEQU-1
         IF (J.LE.0) J=IAMP-1
         DO 20 I=1,J
         IF (RECORD(I:I).NE.' ') THEN
            LCLFT=LCLFT+1
            IF (LCLFT.LE.20) THEN
               CLEFT(LCLFT:LCLFT)=RECORD(I:I)
               CALL UPCASE(CLEFT(LCLFT:LCLFT))
            ENDIF
         ENDIF
   20    CONTINUE
C
C        IF THE USER DEFINED VARIABLE NAME IS OVER 8 CHARS, TRUNCATE IT
C
         IF (LCLFT.GT.8) THEN
            LCLFT=8
            CLEFT(9:)=' '
            WRITE (JOSTND,25) RECORD(1:IEQU-1),CLEFT(1:8)
   25       FORMAT (/T12,'"',A,'" WILL BE SHORTENED TO "',A,'"')
         ENDIF
         IF (IEQU.EQ.0 .AND. IAMP.GT.0) GOTO 5
      ENDIF
C
C     PROCESS THE RIGHT HAND SIDE...IF LCLFT IS 0, THERE IS NO LEFT HAND
C     SIDE.  GOTO 1 TO FIND ANOTHER EXPRESSION.
C
      IF (LCLFT.LE.0) GOTO 1
C
C     FIND THE FIRST NONBLANK CHAR.
C
      J=RECLEN
      IF (IAMP.GT.0) J=IAMP-1
      I2=IEQU+1
      IF (I2.GT.J) I2=J
      DO 30 I=I2,J
      IF (RECORD(I:I).EQ.' ') GOTO 30
      I1=I
      GOTO 40
   30 CONTINUE
C
C     ALL CHARS ARE BLANK...READ ANOTHER RECORD
C
      GOTO 5
   40 CONTINUE
C
C     FIND THE LAST NONBLANK CHAR.
C
      DO 50 I=1,J
      I2=J-I+1
      IF (RECORD(I2:I2).NE.' ') GOTO 60
   50 CONTINUE
   60 CONTINUE
C
C     LOAD CEXPRS
C
      IF (LENCEX+I2+1-I1.GT.MXEXPR) GOTO 300
      DO 80 I=I1,I2
      LENCEX=LENCEX+1
      CEXPRS(LENCEX)=RECORD(I:I)
      CALL UPCASE(CEXPRS(LENCEX))
   80 CONTINUE
C
C     IF AN AMPERSAND WAS REACHED, ADD ONE BLANK TO THE EXPRESSION
C     AND BRANCH TO READ MORE OF THE EXPRESSION.
C
      IF (IAMP.GT.0) THEN
         IF (LENCEX.LT.MXEXPR) THEN
            LENCEX=LENCEX+1
            CEXPRS(LENCEX)=' '
         ENDIF
         GOTO 5
      ENDIF
      IRC=0
      RETURN
  100 CONTINUE
      IRC=1
      RETURN
  200 CONTINUE
      CALL ERRGRO(.FALSE.,2)
      CALL fvsGetRtnCode(IRTNCD)
      IF (IRTNCD.NE.0) RETURN
  300 CONTINUE
      CALL ERRGRO (.TRUE.,5)
      GOTO 1
      END



