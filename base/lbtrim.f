      SUBROUTINE LBTRIM (MAE,MAELNK)
      IMPLICIT NONE
C----------
C  $Id$
C----------
C
C     CALLED FROM PPEVMI, DELETE ACTIVITY GROUPS THAT MIGHT CREATE
C     BRANCHES IF THE INTERSECTION OF THE STAND LABEL SET AND THE
C     ACTIVITY GROUP LABEL SET IS NULL.
C
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--JAN 1987.
C
C     MAE   = LENGTH OF MAELNK.
C     MAELNK= LIST OF ACTIVITY GROUPS THAT ARE ASSOCIATED WITH
C             AN EVENT THAT HAS MORE THAN ONE ACTIVITY GROUP
C             ASSOCIATED WITH IT.
C             (1,.)= POINTS TO THE ENTRY IN IEVACT, AGLSET, & LENAGL
C             (2,.)= POINTS TO THE ENTRY IN IEVNTS
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'OPCOM.F77'
C
C
COMMONS
C
      INTEGER MAELNK(2,50),MAE,IDEL,I,J,KODE,LEN
C
C     IF THE LENGTH OF MAELNK IS ZERO, BRANCH TO RETURN
C
      IF (MAE.LE.0) GOTO 100
C
C     DO THROUGH THE ACTIVITY GROUPS, AND FLAG MEMBERS OF MEMLNK
C     FOR ACTIVITY GROUPS THAT SHOULD BE DROPPED.
C
      IDEL=0
      DO 10 I=1,MAE
C
      J=MAELNK(1,I)
C
C     IF THE LENGTH OF THE AGLSET IS -1, THEN KEEP THE ACTIVIY GROUP
C
      IF (LENAGL(J).GE.0) THEN
         CALL LBINTR (LENSLS,SLSET,LENAGL(J),
     >                AGLSET(J),LEN,WKSTR1,KODE)
         IF (LEN.EQ.0) THEN
            MAELNK(1,I)=0
            IDEL=IDEL+1
         ENDIF
      ENDIF
   10 CONTINUE
C
C     IF SOME ACTIVITY GROUPS ARE DELETED, THEN
C
      IF (IDEL.GT.0) THEN
C
C        IF ALL OF THE ACTIVIY GROUPS HAVE BEEN DELETED, THEN:
C
         IF (IDEL.EQ.MAE) THEN
            MAE=0
         ELSE
C
C           ELSE SOME HAVE BEEN DELETED, COMPRESS THE LIST MAELNK.
C
            LEN=0
            DO 20 I=1,MAE
            IF (MAELNK(1,I).LE.0) GOTO 20
            LEN=LEN+1
            MAELNK(1,LEN)=MAELNK(1,I)
            MAELNK(2,LEN)=MAELNK(2,I)
   20       CONTINUE
            MAE=LEN
         ENDIF
      ENDIF
C
  100 CONTINUE
      RETURN
      END
