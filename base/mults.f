      SUBROUTINE MULTS (KIND,IDT,XMULT)
      IMPLICIT NONE
C----------
C  $Id$
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
COMMONS
C
C     GETS THE MULTIPLIERS - NL CROOKSTON - JAN 1981 - MOSCOW.
C
C     KIND  = THE TYPE OF MULTIPLIER, WHERE:
C             1 IS BAIMULT    (FROM **DGDRIV** )
C             2 IS HTGMULT    (FROM **HTGF**   )
C             3 IS REGHMULT   (FROM **REGENT** )
C             4 IS MORTMULT
C             5 IS ESTBMULT
C             6 IS REGDMULT   (FROM **REGENT** )
C             7 IS MANAGED    (FROM **DGDRIV** )
C
C     IDT   = THE CURRENT YEAR=IY(ICYC).
C     XMULT = SPECIES SPECIFIC MULTIPLIERS.
C
      REAL XMULT(MAXSP)
      INTEGER MYACTS(7)
      REAL PRM(2)
      INTEGER IDT,KIND,NTODO,I,NP,IACTK,IDATE,ISP,IGRP,IULIM,IG,ISPG
      DATA MYACTS/91,92,93,94,95,96,82/
C
C     IF KIND IS OUT-OF-RANGE; RETURN
C
      IF (KIND.GT.7 .OR. KIND.LT.1) RETURN
C
C     FIND OUT IF THERE ARE ANY MULTIPLIERS.
C
      CALL OPFIND (1,MYACTS(KIND),NTODO)
C
C     IF THERE ARE NONE;
C     THEN: RETURN
C
      IF (NTODO .EQ. 0) RETURN
C
C     ELSE: GET THE MULTIPLIERS AND LOAD THEM INTO THE XMULT ARRAY.
C
      DO 50 I=1,NTODO
      CALL OPGET(I,2,IDATE,IACTK,NP,PRM)
      IF (IACTK.LT.0) GOTO 50
C
C     SET THE STATUS OF THE OPTION AS ACCOMPLISHED IN THE YEAR 'IDT'
C
      CALL OPDONE(I,IDT)
C----------
C  PROCESS MANAGED KEYWORD
C----------
      IF(KIND .EQ. 7) THEN
        XMULT(1)=PRM(1)
        GO TO 50
      ENDIF
C
C     IF SPECIES CODE IS:
C        < 0 THEN IT APPLIES TO SPECIES IN A GROUP
C        = 0 THEN IT APPLIES TO ALL SPECIES
C        > 0 THEN IT APPLIES TO JUST THIS SPECIES
C
      ISP=IFIX(PRM(1))
      IF(ISP .LT. 0)THEN
        IGRP = -ISP
        IULIM = ISPGRP(IGRP,1)+1
        DO 30 IG=2,IULIM
        ISPG = ISPGRP(IGRP,IG)
        XMULT(ISPG)=PRM(2)
   30   CONTINUE
      ELSEIF(ISP.EQ.0)THEN
        DO 40 ISP=1,MAXSP
        XMULT(ISP)=PRM(2)
   40   CONTINUE
      ELSE
        XMULT(ISP)=PRM(2)
      ENDIF
   50 CONTINUE      
      RETURN
      END
