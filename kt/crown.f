      SUBROUTINE CROWN
      IMPLICIT NONE
C----------
C KT $Id$
C----------
C  THIS SUBROUTINE IS USED TO DUB MISSING CROWN RATIOS AND COMPUTE CROWN
C  RATIO CHANGES FOR TREES THAT ARE GREATER THAN 3 INCHES DBH.  CROWN
C  RATIO IS PREDICTED FROM HABITAT TYPE, BASAL AREA, CROWN COMPETITION
C  FACTOR, DBH, TREE HEIGHT, AND PERCENTILE IN THE BASAL AREA
C  DISTRIBUTION.  WHEN THE EQUATION IS USED TO PREDICT CROWN RATIO
C  CHANGE, VALUES OF THE PREDICTOR VARIABLES FROM THE START OF THE CYCLE
C  ARE USED TO PREDICT OLD CROWN RATIO, VALUES FROM THE END OF THE CYCLE
C  ARE USED TO PREDICT NEW CROWN RATIO, AND THE CHANGE IS COMPUTED BY
C  SUBTRACTION.  THE CHANGE IS APPLIED TO ACTUAL CROWN RATIO.  THIS
C   ROUTINE IS CALLED FROM **CRATET** TO DUB MISSING VALUES, AND BY
C  **TREGRO** TO COMPUTE CHANGE DURING REGULAR CYCLING.  ENTRY
C  **CRCONS** IS CALLED BY **RCON** TO LOAD MODEL CONSTANTS THAT ARE
C  SITE DEPENDENT AND NEED ONLY BE RESOLVED ONCE.  A CALL TO **DUBSCR**
C  IS ISSUED TO DUB CROWN RATIO WHEN DBH IS LESS THAN 3 INCHES.
C  PROCESSING OF CROWN CHANGE FOR SMALL TREES IS CONTROLLED BY
C  **REGENT**.
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CALCOM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
COMMONS
C----------
C  DECLARATIONS AND DIMENSIONS FOR INTERNAL VARIABLES:
C
C  DCRCON -- CONSTANT TERM FOR THE CROWN RATIO MODEL BASED ON
C            PAST TREE STAND ATTRIBUTES
C  XCRCON -- CONSTANT TERM FOR THE CROWN RATIO MODEL BASED ON
C            CURRENT TREE STAND ATTRIBUTES
C    PARM -- ARRAY OF COEFFICIENTS FOR TERMS INVOLVING TREE
C            AND STAND ATTRIBUTES
C----------
      EXTERNAL RANN
      LOGICAL DEBUG
      REAL PARM(MAXSP,14)
      INTEGER MAPHAB(30,MAXSP)
      REAL CRHAB(14,MAXSP)
      REAL CRNMLT(MAXSP),DLOW(MAXSP),DHI(MAXSP),PRM(5),CHG,PDIFPY
      INTEGER MYACTS(1),ICFLG(MAXSP)
      INTEGER J,I,NTODO,NP,IACTK,IDATE,IDT,ISPCC,IGRP,IULIM,IG,IGSP,ISPC
      INTEGER I1,I2,I3,ICRI
      REAL HD,HN,BACHLO,XCR,DCR,EXPDCR,EXPPCR,PCR,P,BRATIO,BARK,H,D
      REAL B7,B8,B9,B10,B11,B12,B13,B14,DCRCON,XCRCON,X1,X2,OBA,RDM1
      REAL CRSD,CL,CR
C----------
C  DATA STATEMENTS.
C----------
      DATA MYACTS/81/
      DATA PARM/
     & 0.0,-0.00204,0.0,-0.00183,3*0.0,-0.00203,-0.00190,-0.002165,
     & -0.00264,
     & 4*0.0,-0.000001902,6*0.0,
     & -0.34566,4*0.0,0.17479,5*0.0,
     & 5*0.0,-0.00183,5*0.0,
     & 10*0.0,0.000005116,
     & 2*0.0,-0.15334,3*0.0,-0.18555,4*0.0,
     & 0.03882,3*0.0,0.03027,-0.0056,5*0.0,
     & -0.0007,3*0.0,-0.00055,6*0.0,
     & .0,0.30066,0.33840,0.24293,2*0.0,0.53172,0.29699,0.23372,
     & 0.26558,0.0,
     & 6*0.0,-0.02989,4*0.0,
     & 6*0.0,0.00011,4*0.0,
     & -0.21217,-0.59302,-0.59685,-0.25601,-0.25776,2*0.0,-0.38334,
     & -0.28433,-0.31555,-0.25138,
     & 0.00301,5*0.0,0.0042,0.0,0.001903,2*0.0,
     & 0.0,0.19558,0.16488,0.07260,0.06887,0.1105,0.0,0.09918,0.0,
     & 0.16072,0.05140/
      DATA ((MAPHAB(I,J),I=1,30), J=1,5)/
     & 12*2,3,3*4,3*5,6,6,1,6,1,7,1,4*6,
     & 7*2,3,2,4,4,5,6,3*7,8,8,4,1,10,9,10,1,11,4*1,2,
     & 3*2,3*4,6,7,4,8,8,5,9,3*10,2*11,8,1,2*12,13,1,3,1,3,3*1,
     & 9*2,1,1,2,3,3*4,5,5,6,1,3*7,1,8,1,7,3*1,
     & 30*1/
      DATA ((MAPHAB(I,J),I=1,30), J=6,11)/
     & 16*1,3*2,11*1,
     & 6*2,4,5,5,2,2,6,7,3*8,9,9,10,2*11,12,11,1,13,1,14,3,3,11,
     & 7*2,3,2,1,1,2,4,3*5,6,6,7,8,8,9,8,10,11,1,1,2*12,8,
     & 13*2,3,4*4,5,6,6,7,6,1,8,1,9,2*10,6,
     & 2,2,4,3*1,5,6,3*1,8,7,3*9,3*3,11*1,
     & 12*1,4*2,3,3,4,3*1,5,1,6,5*1/
      DATA CRHAB/
     & 0.8884, 0.7309, 0.9347, 0.9888, 0.9945, 1.1126, 1.0263, 7*0.0,
     & 0.06533, 0.03441, 0.2307, 0.1661, -0.1253, -0.05018, 0.11005,
     & 0.08113, 0.1782, 0.03919, 0.2107, 3*0.0,
     & 0.8643, 0.7271, 0.9840, 0.8127, 0.8874, 0.7055, 0.7708, 0.7849,
     & 0.8038, 0.8742, 0.8232, 0.8415, 0.9759, 0.0,
     &-0.2304,-0.5421,-0.4343,-0.3759,-0.4129,-0.4879,-0.2674,
     &-0.1941,6*0.0,
     & -0.2413,13*0.0,
     & -1.6053, -1.7128, 12*0.0,
     & -0.3785, -0.4142, -0.3985, -0.2987, -0.3810, -0.4087, -0.3577,
     & -0.2994, -0.2486,-0.2863,-0.1968,-0.4931,-0.2676,-0.5625,
     & 0.05351, -0.05031, 0.1075, -0.1872, 0.01729, 0.03667, 0.01885,
     & 0.09102, 0.1371, 0.08368, 0.1230, -0.02365, 2*0.0,
     & 0.09453, -0.07740, 0.07113, 0.2039, 0.06176, 0.1513, 0.09086,
     & 0.1580, 0.09229, 0.01551, 4*0.0,
     & -0.9436, -0.8654, -0.8849, -0.9067, -0.8783, -1.0103, -1.0268,
     & -1.0050, -1.0301, 5*0.0,
     & 0.4649, 0.3211, 0.1970, 0.2295, 0.3383, 0.3450, 8*0.0/
C---------
C     DATA CRSD/12.69/
C  THIS IS A POPULATION ESTIMATE AND TOO LARGE FOR WITHIN STAND VARIATION.
C  CUT IT IN HALF UNTIL SOMETHING BETTER COMES ALONG.   DIXON  4/19/99
C----------
      DATA CRSD/6.35/
C-----------
C  CHECK FOR DEBUG.
C-----------
      CALL DBCHK (DEBUG,'CROWN',5,ICYC)
C----------
C  WRITE DEBUG INFO (STAND DENSITY TERMS) IF DESIRED
C----------
      IF(DEBUG) WRITE(JOSTND,9000) BA, RELDEN, RELDM1
 9000 FORMAT(' IN CROWN, BA=',F7.3,'  RELDEN=',F7.3,'  RELDM1=',F7.3)
C----------
C  DUB CROWNS ON DEAD TREES IF NO LIVE TREES IN INVENTORY
C----------
      IF((ITRN.LE.0).AND.(IREC2.LT.MAXTP1))GO TO 74
C---------
C  IF THERE ARE NO TREE RECORDS, THEN RETURN.
C---------
      IF(ITRN.EQ.0)THEN
        RETURN
      ELSEIF(TPROB.LE.0.0)THEN
        DO I=1,ITRN
        ICR(I)=ABS(ICR(I))
        ENDDO
        RETURN
      ENDIF
C-----------
C  PROCESS CRNMULT KEYWORD.
C-----------
      CALL OPFIND(1,MYACTS,NTODO)
      IF(NTODO .EQ. 0)GO TO 25
      DO 24 I=1,NTODO
      CALL OPGET(I,5,IDATE,IACTK,NP,PRM)
      IDT=IDATE
      CALL OPDONE(I,IDT)
      ISPCC=INT(PRM(1))
      IF(DEBUG)WRITE(JOSTND,*)' PRM1= ',PRM
C----------
C  ISPCC<0 CHANGE FOR ALL SPECIES IN THE SPECIES GROUP
C  ISPCC=0 CHANGE FOR ALL SPEICES
C  ISPCC>0 CHANGE THE INDICATED SPECIES
C----------
      IF(ISPCC .LT. 0)THEN
        IGRP = -ISPCC
        IULIM = ISPGRP(IGRP,1)+1
        DO 15 IG=2,IULIM
        IGSP = ISPGRP(IGRP,IG)
        IF(PRM(2) .GE. 0.0)CRNMLT(IGSP)=PRM(2)
        IF(PRM(3) .GT. 0.0)DLOW(IGSP)=PRM(3)
        IF(PRM(4) .GT. 0.0)DHI(IGSP)=PRM(4)
        IF(PRM(5) .GT. 0.0)ICFLG(IGSP)=1
   15   CONTINUE
      ELSEIF(ISPCC .EQ. 0)THEN
        DO 22 ISPCC=1,MAXSP
        IF(PRM(2) .GE. 0.0)CRNMLT(ISPCC)=PRM(2)
        IF(PRM(3) .GT. 0.0)DLOW(ISPCC)=PRM(3)
        IF(PRM(4) .GT. 0.0)DHI(ISPCC)=PRM(4)
        IF(PRM(5) .GT. 0.0)ICFLG(ISPCC)=1
   22   CONTINUE
      ELSE
        IF(PRM(2) .GE. 0.0)CRNMLT(ISPCC)=PRM(2)
        IF(PRM(3) .GT. 0.0)DLOW(ISPCC)=PRM(3)
        IF(PRM(4) .GT. 0.0)DHI(ISPCC)=PRM(4)
        IF(PRM(5) .GT. 0.0)ICFLG(ISPCC)=1
      ENDIF
   24 CONTINUE
      IF(DEBUG)WRITE(JOSTND,*)' PRM2= ',PRM
   25 CONTINUE
      IF(DEBUG)WRITE(JOSTND,9024)ICYC,CRNMLT
 9024 FORMAT(/' IN CROWN 9024 ICYC,CRNMLT= ',
     & I5/((1X,11F6.2)/))
C----------
C  IF CCF LAST CYCLE WAS LESS THAN 100.0, THERE IS ASSUMED TO BE
C  NO DENSITY IMPACT ON CROWN RATIOS.
C----------
      RDM1=RELDM1
      OBA=OLDBA
      IF(RELDM1.GE.100.0) GO TO 20
      OBA=BA
      RDM1=RELDEN
   20 CONTINUE
      IF(LSTART) GOTO 21
      X1=ALOG(OBA)
      X2=ALOG(RDM1)
   21 CONTINUE
C----------
C  ENTER THE LOOP FOR SPECIES DEPENDENT VARIABLES
C----------
      DO 70 ISPC=1,MAXSP
      I1 = ISCT(ISPC,1)
      IF(I1 .EQ. 0)GOTO 70
      I2 = ISCT(ISPC,2)
       XCRCON = CRCON(ISPC) + PARM(ISPC,1)*BA + PARM(ISPC,2)*BA*BA +
     & PARM(ISPC,3)*ALOG(BA) + PARM(ISPC,4)*RELDEN + PARM(ISPC,5)
     & *RELDEN*RELDEN + PARM(ISPC,6)*ALOG(RELDEN)
      DCRCON=0.0
C----------
C  IF DUBBING MISSING CROWN RATIOS, BYPASS ASSIGNMENT OF DCRCON.
C----------
      IF(LSTART) GO TO 30
      DCRCON = CRCON(ISPC) + PARM(ISPC,1)*OBA + PARM(ISPC,2)*OBA*OBA +
     & PARM(ISPC,3)*X1 + PARM(ISPC,4)*RDM1 + PARM(ISPC,5)*RDM1
     & *RDM1 + PARM(ISPC,6)*X2
C----------
C  WRITE DEBUG INFO (CONSTANT TERMS) IF DESIRED
C----------
   30 CONTINUE
      IF(DEBUG) WRITE(JOSTND,9001) ISPC, XCRCON, DCRCON
 9001 FORMAT(' SPECIES: ',I2,' - XCRCON = ',F10.3,', DCRCON = ',F10.3)
C----------
C  VARIABLES B1, B2, ..., B14 ARE SET UP TO ELIMINATE THE
C  2-DIMENSIONAL ARRAY ACCESSES WHEN PROCESSING RECORDS OF THE SAME
C  SPECIES.
C----------
      B7 = PARM(ISPC,7)
      B8 = PARM(ISPC,8)
      B9 = PARM(ISPC,9)
      B10 = PARM(ISPC,10)
      B11 = PARM(ISPC,11)
      B12 = PARM(ISPC,12)
      B13 = PARM(ISPC,13)
      B14 = PARM(ISPC,14)
C----------
C  ENTER LOOP FOR TREE DEPENDENT VARIABLES
C----------
      DO 60 I3=I1,I2
      I = IND1(I3)
C----------
C  IF THIS IS THE INITIAL ENTRY TO 'CROWN' AND THE TREE IN QUESTION
C  HAS A CROWN RATIO ASCRIBED TO IT, THE WHOLE PROCESS IS BYPASSED.
C----------
      IF(LSTART .AND. ICR(I).GT.0)GOTO 60
C----------
C  IF ICR(I) IS NEGATIVE, CROWN RATIO CHANGE WAS COMPUTED IN A
C  PEST DYNAMICS EXTENSION.  SWITCH THE SIGN ON ICR(I) AND BYPASS
C  CHANGE CALCULATIONS.
C----------
      IF (LSTART) GOTO 40
      IF (ICR(I).GT.0) GO TO 40
      ICR(I)=-ICR(I)
      IF (DEBUG) WRITE (JOSTND,35) I,ICR(I)
   35 FORMAT (' ICR(',I4,') WAS CALCULATED ELSEWHERE AND IS ',I4)
      GOTO 60
   40 CONTINUE
      D=DBH(I)
      H=HT(I)
      BARK=BRATIO(ISPC,D,H)
C----------
C  BRANCH TO STATEMENT 58 TO HANDLE TREES WITH DBH LESS THAN 3 IN.
C----------
      IF(D.LT.3.0) GO TO 58
C----------
C  BYPASS CROWN CHANGE CALCULATION IF CROWN RATIO WAS ASSIGNED
C  THIS CYCLE IN REGENT.
C----------
      IF(.NOT.LSTART .AND. D-DG(I)/BARK .LT.3.0) GO TO 60
      P=PCT(I)
      IF(P.LT.0.01)P=0.01
C----------
C  CALCULATE THE PREDICTED CURRENT CROWN RATIO
C----------
      PCR = XCRCON + B7*D + B8*D*D + B9*ALOG(D) + B10*H + B11*H*H +
     & B12*ALOG(H) + B13*P + B14*ALOG(P)
      EXPPCR= EXP(PCR)
C----------
C  SET DIFFERENTIAL TO 0.0 AND BRANCH TO 50 IF DUBBING
C----------
      EXPDCR=0.0
      IF(LSTART) GO TO 50
C----------
C  BACKDATE TREE ATTRIBUTES SO THAT CROWN RATIO AT THE BEGINNING
C  OF THE CYCLE CAN BE PREDICTED.
C----------
      D=D-(DG(I)/BARK)
      IF(D.LE.0.0) D=DBH(I)
      H=H-HTG(I)
      IF(H.LE.0.0) H=HT(I)
      IF((OLDPCT(I).GT.PCT(I).AND.ONTREM(7).GT.0.0).OR.OLDPCT(I).LE.0.0)
     &  OLDPCT(I)=PCT(I)
      P=OLDPCT(I)
      IF(P.LT.0.01)P=0.01
C----------
C  CALCULATE THE PREDICTED CROWN RATIO AT THE START OF THE CYCLE.
C----------
      DCR = DCRCON + B7*D + B8*D*D + B9*ALOG(D) + B10*H + B11*H*H +
     & B12*ALOG(H) + B13*P + B14*ALOG(P)
      EXPDCR=EXP(DCR)
C----------
C  WRITE DEBUG INFO IF DESIRED
C----------
   50 CONTINUE
      IF(DEBUG) WRITE(JOSTND,9002) I,ICR(I),EXPPCR,EXPDCR,D,H,PCT(I),
     & OLDPCT(I)
 9002 FORMAT(' ICR(',I4,')=',I3,'  EXPPCR=',F10.3,' EXPDCR=',F10.3,
     &  ' D=',F7.3,' H=',F7.3,' P=',F7.3,' OLDP=',F7.3)
C----------
C  COMPUTE THE PREDICTED CROWN RATIO AND 
C  BOUND CROWN CHANGE TO 1% PER YEAR
C----------
      CHG=EXPPCR-EXPDCR
      IF(.NOT.LSTART.OR.(ICR(I).GT.0))THEN
        PDIFPY=CHG/REAL(ICR(I))/FINT*100.
        IF(PDIFPY.GT.0.01)CHG=REAL(ICR(I))*(0.01)*FINT/100.
        IF(PDIFPY.LT.-0.01)CHG=REAL(ICR(I))*(-0.01)*FINT/100.
      ENDIF
C----------
C  APPLY CRNMULT KEYWORD ADJUSTMENTS
C----------
      IF(DBH(I).GE.DLOW(ISPC) .AND. DBH(I).LT.DHI(ISPC))THEN
        ICRI=INT(REAL(ICR(I))+CRNMLT(ISPC)*CHG*100.0+0.50005)
      ELSE
        ICRI=INT(REAL(ICR(I))+CHG*100.0+0.50005)
      ENDIF
      IF ((DGSD.LT.1.0).OR.(.NOT.LSTART)) GO TO 51
      XCR=ICRI
      ICRI=INT(BACHLO(XCR,CRSD,RANN))
   51 CONTINUE

C----------
C  REDUCE CROWNS OF TREES  FLAGGED AS TOP-KILLED ON INVENTORY
C----------
      IF (.NOT.LSTART .OR. ITRUNC(I).EQ.0) GO TO 59
      HN=REAL(NORMHT(I))/100.0
      HD=HN-REAL(ITRUNC(I))/100.0
      CL=(REAL(ICRI)/100.)*HN-HD
      ICRI=INT((CL*100./HN)+.5)
      GO TO 59
C----------
C  CROWNS FOR TREES WITH DBH LT 3.0 IN ARE DUBBED HERE.  NO CHANGE
C  IS CALCULATED UNTIL THE TREE ATTAINS A DBH OF 3 INCHES.
C----------
   58 CONTINUE
      IF(.NOT.LSTART) GO TO 60
      CALL DUBSCR(ISPC,D,H,BA,CR)
      ICRI=INT(CR*100.0+0.5)
      IF(DEBUG)WRITE(JOSTND,*)' AFTER DUBSCR I,ISPC,D,H,BA,CR,ICRI= ',
     &I,ISPC,D,H,BA,CR,ICRI
C---------
C  ADDED FOR CRNMLT ADJUSTMENT OF DUBBED VALUES
C---------
      IF(DBH(I).GE.DLOW(ISPC) .AND. DBH(I).LT.DHI(ISPC))
     &   ICRI = INT(REAL(ICRI) * CRNMLT(ISPC))
C----------
C  END OF CROWN RATIO CALCULATION LOOP.  BOUND CR ESTIMATE AND FILL
C  THE ICR VECTOR.
C----------
   59 CONTINUE
      IF(ICRI.GT.95) ICRI=95
      IF ((ICRI.LT.5).AND.(CRNMLT(ISPC).EQ.1)) ICRI=5
      ICR(I)= ICRI
   60 CONTINUE
      IF(LSTART .AND. ICFLG(ISPC).EQ.1)THEN
        CRNMLT(ISPC)=1.0
        ICFLG(ISPC)=0
      ENDIF
   70 CONTINUE
   74 CONTINUE
C----------
C  DUB MISSING CROWNS ON CYCLE 0 DEAD TREES.
C----------
      IF(IREC2 .GT. MAXTRE) GO TO 75
      DO 79 I=IREC2,MAXTRE
      IF(ICR(I) .GT. 0) GO TO 79
      ISPC=ISP(I)
      D=DBH(I)
      H=HT(I)
      CALL DUBSCR (ISPC,D,H,BA,CR)
      ICRI=INT(CR*100.0 + 0.5)
      IF(ITRUNC(I).EQ.0) GO TO 78
      HN=REAL(NORMHT(I))/100.0
      HD=HN-REAL(ITRUNC(I))/100.0
      CL=(REAL(ICRI)/100.)*HN-HD
      ICRI=INT((CL*100./HN)+.5)
   78 CONTINUE
      IF(ICRI.GT.95) ICRI=95
      IF (ICRI .LT. 10) ICRI=10
      ICR(I)= ICRI
   79 CONTINUE
C
   75 CONTINUE
      RETURN
      ENTRY CRCONS
C----------
C  ENTRY POINT FOR LOADING CROWN RATIO MODEL COEFFICIENTS THAT ARE
C  SITE DEPENDENT AND REQUIRE ONE TIME RESOLUTION.  ITYPE INDEXES
C  HABITAT TYPE (CARRIED IN /PLOT/ COMMON AREA), CRHAB CONTAINS HABITAT
C  INTERCEPTS BY HABITAT TYPE BY SPECIES, AND MAPCR MAPS HABITAT TYPE
C  ONTO HABITAT CLASS FOR EACH SPECIES.
C----------
      DO 80 ISPC=1,MAXSP
      ICRHAB=MAPHAB(ITYPE,ISPC)
      CRCON(ISPC)=CRHAB(ICRHAB,ISPC)
   80 CONTINUE
      DATA CRNMLT/MAXSP*1.0/
      DATA ICFLG/MAXSP*0/
      DATA DLOW/MAXSP*0.0/
      DATA DHI/MAXSP*99.0/
      RETURN
      END
