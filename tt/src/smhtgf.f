      SUBROUTINE SMHTGF(ISPC,I,HTGRTH,CR,TPCCF,LESTB,DEBUG)
      IMPLICIT NONE
C----------
C  **SMHTGF--TT   DATE OF LAST REVISION:  02/08/13
C----------
C THIS SUBROUTINE COMPUTES THE HEIGHT GROWTH FOR SMALL TREES (HTGRTH)
C IN REAL SCALE.
C CALLED FROM **REGENT.
C CALLS **FINDAG
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'CALCOM.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
C
COMMONS
C----------
      EXTERNAL RANN
      LOGICAL DEBUG,LESTB
      INTEGER I,ISPC,J
      REAL D1,D2,H,SITAGE,SITHT,AGMAX,HTMAX,HTMAX2
      REAL TPCCF,CR,HTGRTH,BACHLO,D,BETA1,BETA2,HTG1,STDDEV,HITE1
      REAL HITE2,AG2,HTGR
C----------
C SPECIES ORDER FOR TETONS VARIANT:
C
C  1=WB,  2=LM,  3=DF,  4=PM,  5=BS,  6=AS,  7=LP,  8=ES,  9=AF, 10=PP,
C 11=UJ, 12=RM, 13=BI, 14=MM, 15=NC, 16=MC, 17=OS, 18=OH
C
C VARIANT EXPANSION:
C BS USES ES EQUATIONS FROM TT
C PM USES PI (COMMON PINYON) EQUATIONS FROM UT
C PP USES PP EQUATIONS FROM CI
C UJ AND RM USE WJ (WESTERN JUNIPER) EQUATIONS FROM UT
C BI USES BM (BIGLEAF MAPLE) EQUATIONS FROM SO
C MM USES MM EQUATIONS FROM IE
C NC AND OH USE NC (NARROWLEAF COTTONWOOD) EQUATIONS FROM CR
C MC USES MC (CURL-LEAF MTN-MAHOGANY) EQUATIONS FROM SO
C OS USES OT (OTHER SP.) EQUATIONS FROM TT
C----------
C  RESET ZRAND IF THIS IS THE START OF THE PROJECTION, THIS IS THE FIRST
C  PREDICTION FOR A NEW TRIPLE, OR IF GROWTH IN THE PREVIOUS CYCLE WAS
C  LESS THAN 0.1 FT.
C----------
      IF(ZRAND(I).NE.-999.) GO TO 20
   10 CONTINUE
      ZRAND(I)=BACHLO(0.0,1.0,RANN)
      IF(ZRAND(I).LT.-2.0 .OR. ZRAND(I).GT.2.0) GO TO 10
   20 CONTINUE
C
      D = DBH(I)
      IF (D.LE.0.0) THEN
        HTGRTH=0.0
        GO TO 900
      ENDIF
C
      SELECT CASE (ISPC)
C----------
C QUAKING ASPEN
C----------
      CASE(6)
        SITAGE = 0.0
        SITHT = 0.0
        AGMAX = 0.0
        HTMAX = 0.0
        HTMAX2 = 0.0
        D1 = DBH(I)
        H = HT(I)
        D2 = 0.0
        IF(DEBUG)WRITE(JOSTND,*)' IN HTGF, CALLING FINDAG I= ',I
        CALL FINDAG(I,ISPC,D1,D2,H,SITAGE,SITHT,AGMAX,HTMAX,HTMAX2,
     &              DEBUG)
C
C----------
C  COMPUTE HT GROWTH AND AGE FOR ASPEN.  EQUATION FORMULATED BY
C  WAYNE SHEPPERD, ROCKY MNT FOREST EXP STN, FORT COLLINS, CO.
C----------
        IF(LESTB)THEN
          SITAGE = ABIRTH(I)
        ENDIF
        HITE1=26.9825*SITAGE**1.1752
        AG2=SITAGE + 5.
        HITE2 = 26.9825*AG2**1.1752
        HTGR = (HITE2-HITE1)/(2.54*12.0)
        HTGRTH = HTGR+ZRAND(I)*.1
C----------
C GROWTH RATES APPEAR HIGH, REDUCE BY 25 PERCENT. DIXON 8-27-92.
C----------
        HTGRTH = HTGRTH * .75
C----------
C ALL OTHER TT SPECIES
C----------
      CASE DEFAULT
         BETA1=EXP(B0ACCF(ISPC) + B1ACCF(ISPC)*ALOG(TPCCF))
         BETA2=EXP(B0BCCF(ISPC) + B1BCCF(ISPC)*ALOG(TPCCF))
         HTG1=BETA1 + BETA2*CR
         STDDEV=HTG1*(B0ASTD(ISPC) + B1BSTD(ISPC)*CR)
         HTGRTH=HTG1 + ZRAND(I)*STDDEV
C
      END SELECT
C
C----------
C  IF ESTIMATED HEIGHT INCREMENT IS CLOSE TO ZERO, SET FLAG TO DRAW
C  A NEW RANDOM ERROR ON NEXT PASS THROUGH.
C----------
      IF(HTGRTH .GT. 0.1) GOTO 900
      HTGRTH = 0.1
      ZRAND(I)=-999.
  900 CONTINUE
C
      IF(DEBUG)WRITE(JOSTND,*)' SMHTGF-I,ISPC,HTGRTH,D,ZRAND=',
     - I,ISPC,HTGRTH,D,ZRAND(I)
C
      RETURN
      END
